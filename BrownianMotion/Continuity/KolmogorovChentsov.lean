/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import BrownianMotion.Continuity.KolmogorovChentsovInequality

/-!
# Kolmogorov-Chentsov theorem

-/

open MeasureTheory Filter
open scoped ENNReal NNReal Topology Asymptotics

section aux

lemma UniformContinuousOn.exists_tendsto {Î± Î² : Type*} [UniformSpace Î±] [FirstCountableTopology Î±]
    [UniformSpace Î²] [CompleteSpace Î²] {s t : Set Î±} (hs : Dense s) (ht : IsOpen t)
    {f : s â†’ Î²} (hf : UniformContinuousOn f {x | â†‘x âˆˆ t}) (a : Î±) (ha : a âˆˆ t) :
    âˆƒ c, Tendsto f (comap Subtype.val (ğ“ a)) (ğ“ c) := by
  have (u : â„• â†’ s) (hu : Tendsto (fun n â†¦ (u n : Î±)) atTop (ğ“ a)) :
      âˆƒ c, Tendsto (f âˆ˜ u) atTop (ğ“ c) := by
    refine cauchySeq_tendsto_of_complete ?_
    rw [cauchySeq_iff_tendsto]
    rw [UniformContinuousOn] at hf
    change Tendsto ((fun p â†¦ (f p.1, f p.2)) âˆ˜ (fun p : â„• Ã— â„• â†¦ (u p.1, u p.2))) atTop
      (uniformity Î²)
    refine hf.comp ?_
    rw [tendsto_inf]
    constructor
    Â· suffices hu_cauchy : CauchySeq u by rwa [cauchySeq_iff_tendsto] at hu_cauchy
      have h_cauchy := hu.cauchySeq
      rw [cauchySeq_iff] at h_cauchy
      rw [cauchySeq_iff, uniformity_subtype]
      simp only [mem_comap, ge_iff_le, forall_exists_index, and_imp] at h_cauchy âŠ¢
      intro V s hs hsV
      obtain âŸ¨N, hNâŸ© := h_cauchy s hs
      exact âŸ¨N, fun k hNk l hNl â†¦ hsV (hN k hNk l hNl)âŸ©
    Â· rw [tendsto_principal]
      have hut : âˆ€á¶  n in atTop, (u n : Î±) âˆˆ t := hu.eventually_mem (ht.mem_nhds ha)
      simp only [eventually_atTop, ge_iff_le, Set.mem_prod, Set.mem_setOf_eq, Prod.forall,
        Prod.exists, Prod.mk_le_mk, and_imp] at hut âŠ¢
      obtain âŸ¨N, hNâŸ© := hut
      exact âŸ¨N, N, fun a b hNa hNb â†¦ âŸ¨hN a hNa, hN b hNbâŸ©âŸ©
  choose c hc using this
  obtain âŸ¨u, huâŸ© : âˆƒ u : â„• â†’ s, Tendsto (fun n â†¦ (u n : Î±)) atTop (ğ“ a) := by
    have has : a âˆˆ closure s := by simp [hs.closure_eq]
    rw [mem_closure_iff_seq_limit] at has
    obtain âŸ¨u, hu_mem, huâŸ© := has
    exact âŸ¨fun n â†¦ âŸ¨u n, hu_mem nâŸ©, huâŸ©
  refine âŸ¨c u hu, ?_âŸ©
  refine tendsto_of_seq_tendsto fun v hv' â†¦ ?_
  have hv : Tendsto (fun n â†¦ (v n : Î±)) atTop (ğ“ a) := by rwa [tendsto_comap_iff] at hv'
  refine (hc u hu).congr_uniformity ?_
  change Tendsto ((fun p â†¦ (f p.1, f p.2)) âˆ˜ (fun n â†¦ (u n, v n))) atTop (uniformity Î²)
  rw [UniformContinuousOn] at hf
  refine hf.comp ?_
  have hu' : Tendsto u atTop (comap Subtype.val (ğ“ a)) := by rwa [tendsto_comap_iff]
  have hv' : Tendsto v atTop (comap Subtype.val (ğ“ a)) := by rwa [tendsto_comap_iff]
  refine Tendsto.mono_right (hu'.prodMk hv') ?_
  rw [le_inf_iff]
  constructor
  Â· rw [â† Filter.comap_prodMap_prod, â† nhds_prod_eq, uniformity_subtype]
    refine comap_mono ?_
    exact nhds_le_uniformity a
  Â· simp only [le_principal_iff]
    rw [mem_prod_iff]
    simp_rw [Set.prod_subset_prod_iff]
    simp only [mem_comap]
    refine âŸ¨Subtype.val â»Â¹' t, âŸ¨t, ?_, subset_rflâŸ©, Subtype.val â»Â¹' t, âŸ¨t, ?_, subset_rflâŸ©, ?_âŸ©
    Â· exact ht.mem_nhds ha
    Â· exact ht.mem_nhds ha
    Â· grind

lemma Dense.holderOnWith_extend {X Y : Type*} [PseudoEMetricSpace X] [PseudoEMetricSpace Y]
    [CompleteSpace Y] {C r : â„â‰¥0} {s : Set X} {f : s â†’ Y} {U : Set X} (hU : IsOpen U) (hs : Dense s)
    (hf : HolderOnWith C r f {x | â†‘x âˆˆ U}) (hr : 0 < r) :
    HolderOnWith C r (hs.extend f) U := by
  intro x hx y hy
  have := neBot_comap_nhds hs
  have h_mem x (hx : x âˆˆ U) : âˆ€á¶  z in (ğ“ x).comap ((â†‘) : s â†’ X), â†‘z âˆˆ U := by
    simp only [eventually_comap, Subtype.forall]
    suffices âˆ€á¶  z in ğ“ x, z âˆˆ U by
      filter_upwards [this] with z hz
      rintro _ h rfl
      exact hz
    rw [eventually_mem_set]
    exact hU.mem_nhds hx
  have h_prod_mem : âˆ€á¶  z : s Ã— s in
      ((ğ“ x).comap ((â†‘) : s â†’ X)) Ã—Ë¢ ((ğ“ y).comap ((â†‘) : s â†’ X)),
      â†‘z.1 âˆˆ U âˆ§ â†‘z.2 âˆˆ U := by
    simp only [eventually_and]
    exact âŸ¨(h_mem x hx).prod_inl _, (h_mem y hy).prod_inr _âŸ©
  have hfx : Tendsto f ((ğ“ x).comap ((â†‘) : s â†’ X)) (ğ“ (hs.extend f x)) := by
    refine tendsto_nhds_limUnder ?_
    exact UniformContinuousOn.exists_tendsto hs hU (hf.uniformContinuousOn hr) _ hx
  have hfy : Tendsto f ((ğ“ y).comap ((â†‘) : s â†’ X)) (ğ“ (hs.extend f y)) := by
    refine tendsto_nhds_limUnder ?_
    exact UniformContinuousOn.exists_tendsto hs hU (hf.uniformContinuousOn hr) _ hy
  classical
  let f' : s Ã— s â†’ â„â‰¥0âˆ := fun z â†¦ if â†‘z.1 âˆˆ U âˆ§ â†‘z.2 âˆˆ U then edist (f z.1) (f z.2) else 0
  let g' : s Ã— s â†’ â„â‰¥0âˆ := fun z â†¦ C * edist z.1 z.2 ^ (r : â„)
  have hf'_eq : f' =á¶ [((ğ“ x).comap ((â†‘) : s â†’ X)) Ã—Ë¢ ((ğ“ y).comap ((â†‘) : s â†’ X))]
      fun z â†¦ edist (f z.1) (f z.2) := by
    filter_upwards [h_prod_mem] with z hz
    simp [f', hz]
  refine le_of_tendsto_of_tendsto (f := f') (g := g')
    (b := ((ğ“ x).comap ((â†‘) : s â†’ X)) Ã—Ë¢ ((ğ“ y).comap ((â†‘) : s â†’ X))) ?_ ?_ ?_
  Â· refine Tendsto.congr' hf'_eq.symm ?_
    change Tendsto (edist.uncurry âˆ˜ (fun p : s Ã— s â†¦ (f p.1, f p.2))) _ _
    refine (Continuous.tendsto (by fun_prop) (hs.extend f x, hs.extend f y)).comp ?_
    refine Tendsto.prodMk_nhds ?_ ?_
    Â· exact hfx.comp tendsto_fst
    Â· exact hfy.comp tendsto_snd
  Â· simp_rw [g', Subtype.edist_eq]
    change Tendsto ((fun z â†¦ C * edist z.1 z.2 ^ r.toReal) âˆ˜ (fun z : s Ã— s â†¦ (z.1.1, z.2.1))) _ _
    refine (Continuous.tendsto ?_ (x, y)).comp ?_
    Â· fun_prop (disch := exact ENNReal.coe_ne_top)
    exact Tendsto.prodMk_nhds (tendsto_comap.comp tendsto_fst) (tendsto_comap.comp tendsto_snd)
  Â· filter_upwards [h_prod_mem] with z âŸ¨hzâ‚, hzâ‚‚âŸ©
    simp only [hzâ‚, hzâ‚‚, and_self, â†“reduceIte, ge_iff_le, f', g']
    exact hf z.1 hzâ‚ z.2 hzâ‚‚

lemma UniformContinuous.exists_tendsto {Î± Î² : Type*} [UniformSpace Î±] [FirstCountableTopology Î±]
    [UniformSpace Î²] [CompleteSpace Î²] {s : Set Î±}
    {f : s â†’ Î²} (hf : UniformContinuous f) (a : Î±) (has : a âˆˆ closure s) :
    âˆƒ c, Tendsto f (comap Subtype.val (ğ“ a)) (ğ“ c) := by
  have (u : â„• â†’ s) (hu : Tendsto (fun n â†¦ (u n : Î±)) atTop (ğ“ a)) :
      âˆƒ c, Tendsto (f âˆ˜ u) atTop (ğ“ c) := by
    refine cauchySeq_tendsto_of_complete ?_
    refine hf.comp_cauchySeq ?_
    have h_cauchy := hu.cauchySeq
    rw [cauchySeq_iff] at h_cauchy
    rw [cauchySeq_iff, uniformity_subtype]
    simp only [mem_comap, ge_iff_le, forall_exists_index, and_imp] at h_cauchy âŠ¢
    intro V s hs hsV
    obtain âŸ¨N, hNâŸ© := h_cauchy s hs
    exact âŸ¨N, fun k hNk l hNl â†¦ hsV (hN k hNk l hNl)âŸ©
  choose c hc using this
  obtain âŸ¨u, huâŸ© : âˆƒ u : â„• â†’ s, Tendsto (fun n â†¦ (u n : Î±)) atTop (ğ“ a) := by
    rw [mem_closure_iff_seq_limit] at has
    obtain âŸ¨u, huâŸ© := has
    exact âŸ¨fun n â†¦ âŸ¨u n, hu.1 nâŸ©, hu.2âŸ©
  refine âŸ¨c u hu, ?_âŸ©
  refine tendsto_of_seq_tendsto fun v hv' â†¦ ?_
  have hv : Tendsto (fun n â†¦ (v n : Î±)) atTop (ğ“ a) := by rwa [tendsto_comap_iff] at hv'
  refine (hc u hu).congr_uniformity ?_
  change Tendsto ((fun p â†¦ (f p.1, f p.2)) âˆ˜ (fun n â†¦ (u n, v n))) atTop (uniformity Î²)
  rw [UniformContinuous] at hf
  refine hf.comp ?_
  have hu' : Tendsto u atTop (comap Subtype.val (ğ“ a)) := by rwa [tendsto_comap_iff]
  have hv' : Tendsto v atTop (comap Subtype.val (ğ“ a)) := by rwa [tendsto_comap_iff]
  refine Tendsto.mono_right (hu'.prodMk hv') ?_
  rw [â† Filter.comap_prodMap_prod, â† nhds_prod_eq, uniformity_subtype]
  refine comap_mono ?_
  exact nhds_le_uniformity a

theorem measurable_limUnder_of_exists_tendsto {Î¹ X E : Type*}
    {mX : MeasurableSpace X} [TopologicalSpace E] [TopologicalSpace.PseudoMetrizableSpace E]
    [MeasurableSpace E] [BorelSpace E] {l : Filter Î¹}
    [l.IsCountablyGenerated] {f : Î¹ â†’ X â†’ E} [hE : Nonempty E]
    (h_conv : âˆ€ x, âˆƒ c, Tendsto (f Â· x) l (ğ“ c)) (hf : âˆ€ i, Measurable (f i)) :
    Measurable (fun x â†¦ limUnder l (f Â· x)) := by
  obtain rfl | hl := eq_or_neBot l
  Â· simp [limUnder, Filter.map_bot]
  refine measurable_of_tendsto_metrizable' l hf (tendsto_pi_nhds.mpr fun x â†¦ ?_)
  exact tendsto_nhds_limUnder (h_conv x)

theorem measurable_limUnder {Î¹ X E : Type*} [MeasurableSpace X] [TopologicalSpace E] [PolishSpace E]
    [MeasurableSpace E] [BorelSpace E] [Countable Î¹] {l : Filter Î¹}
    [l.IsCountablyGenerated] {f : Î¹ â†’ X â†’ E} [hE : Nonempty E] (hf : âˆ€ i, Measurable (f i)) :
    Measurable (fun x â†¦ limUnder l (f Â· x)) := by
  let conv := {x | âˆƒ c, Tendsto (f Â· x) l (ğ“ c)}
  have mconv : MeasurableSet conv := measurableSet_exists_tendsto hf
  have : (fun x â†¦ _root_.limUnder l (f Â· x)) = ((â†‘) : conv â†’ X).extend
      (fun x â†¦ _root_.limUnder l (f Â· x)) (fun _ â†¦ hE.some) := by
    ext x
    by_cases hx : x âˆˆ conv
    Â· rw [Function.extend_val_apply hx]
    Â· rw [Function.extend_val_apply' hx, limUnder_of_not_tendsto hx]
  rw [this]
  refine (MeasurableEmbedding.subtype_coe mconv).measurable_extend ?_ measurable_const
  exact measurable_limUnder_of_exists_tendsto (fun x â†¦ x.2)
    (fun i â†¦ (hf i).comp measurable_subtype_coe)

lemma limUnder_prod {Î± Î² X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [Nonempty X] [Nonempty Y] [T2Space X] [T2Space Y] {f : Filter Î±} {f' : Filter Î²}
    [NeBot f] [NeBot f'] {g : Î± â†’ X} {g' : Î² â†’ Y}
    (hâ‚ : âˆƒ x, Tendsto g f (ğ“ x)) (hâ‚‚ : âˆƒ x', Tendsto g' f' (ğ“ x')) :
    limUnder (f Ã—Ë¢ f') (fun x â†¦ (g x.1, g' x.2)) = (limUnder f g, limUnder f' g') := by
  rw [Filter.Tendsto.limUnder_eq]
  rw [nhds_prod_eq]
  apply Filter.Tendsto.prodMk
  Â· exact (tendsto_nhds_limUnder hâ‚).comp tendsto_fst
  Â· exact (tendsto_nhds_limUnder hâ‚‚).comp tendsto_snd

end aux

namespace ProbabilityTheory

variable {T Î© E : Type*} {mÎ© : MeasurableSpace Î©}
  {X : T â†’ Î© â†’ E} {c : â„â‰¥0âˆ} {d p q : â„} {M Î² : â„â‰¥0} {P : Measure Î©}
  {U : Set T}

section PseudoEMetricSpace

variable [PseudoEMetricSpace T] [PseudoEMetricSpace E]

section HolderSet

/-- A set on which the process is HÃ¶lder continuous. -/
def holderSet (X : T â†’ Î© â†’ E) (T' : Set T) (p Î² : â„) (U : Set T) : Set Î© :=
  {Ï‰ | â¨† (s : (T' âˆ© U : Set T)) (t : (T' âˆ© U : Set T)),
      edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p) < âˆ
      âˆ§ âˆ€ (s t : (T' âˆ© U : Set T)), edist s t = 0 â†’ edist (X s Ï‰) (X t Ï‰) = 0}

lemma IsKolmogorovProcess.measurableSet_holderSet
    (hX : IsKolmogorovProcess X P p q M) {T' : Set T} (hT' : T'.Countable) :
    MeasurableSet (holderSet X T' p Î² U) := by
  have : Countable T' := hT'
  have : Countable (T' âˆ© U : Set T) := hT'.mono Set.inter_subset_left
  let C Ï‰ := â¨† (s : (T' âˆ© U : Set T)) (t : (T' âˆ© U : Set T)),
    edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p)
  refine MeasurableSet.inter ?_ ?_
  Â· change MeasurableSet {Ï‰ | C Ï‰ < âˆ}
    refine measurableSet_lt ?_ (by fun_prop)
    refine Measurable.iSup (fun s â†¦ Measurable.iSup (fun t â†¦ ?_))
    exact Measurable.div (hX.measurable_edist.pow_const _) (by fun_prop)
  Â· have h_eq (A : Set T) : {Ï‰ | âˆ€ (s t : A), edist s t = 0 â†’ edist (X s Ï‰) (X t Ï‰) = 0}
        = â‹‚ (s : A) (t : A),
          ({Ï‰ | edist (X s Ï‰) (X t Ï‰) = 0} âˆª {Ï‰ | edist s t â‰  0}) := by
      ext; simp [imp_iff_or_not]
    change MeasurableSet {Ï‰ | âˆ€ (s t : (T' âˆ© U : Set T)),
      edist s t = 0 â†’ edist (X s Ï‰) (X t Ï‰) = 0}
    rw [h_eq]
    refine MeasurableSet.iInter (fun s â†¦ MeasurableSet.iInter (fun t â†¦ ?_))
    refine MeasurableSet.union ?_ ?_
    Â· exact MeasurableSet.preimage (measurableSet_singleton 0) hX.measurable_edist
    Â· exact (MeasurableSet.preimage (measurableSet_singleton 0) (by fun_prop)).compl

lemma holderOnWith_of_mem_holderSet (hT : HasBoundedInternalCoveringNumber U c d)
    (hd_pos : 0 < d) (hp_pos : 0 < p) (hÎ²_pos : 0 < Î²)
    {T' : Set T} {Ï‰ : Î©} (hÏ‰ : Ï‰ âˆˆ holderSet X T' p Î² U) :
    letI C Ï‰ := â¨† (s : (T' âˆ© U : Set T)) (t : (T' âˆ© U : Set T)),
      edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p)
    HolderOnWith (C Ï‰ ^ pâ»Â¹).toNNReal Î² (fun (t : T') â†¦ X t Ï‰) {t' | (t' : T) âˆˆ U} := by
  intro s hs t ht
  have h_edist_lt_top : edist s t < âˆ := by
    calc edist s t â‰¤ EMetric.diam U := EMetric.edist_le_diam_of_mem hs ht
    _ < âˆ := hT.diam_lt_top hd_pos
  have h_dist_top : edist s t ^ (Î² : â„) â‰  âˆ
  Â· simp only [ne_eq, ENNReal.rpow_eq_top_iff, NNReal.coe_pos, not_or, not_and, not_lt,
      NNReal.zero_le_coe, implies_true, nonpos_iff_eq_zero, true_and]
    exact fun h_eq_top â†¦ absurd h_eq_top h_edist_lt_top.ne
  by_cases h_dist_zero : edist s t = 0
  Â· simpa [h_dist_zero, hÎ²_pos] using hÏ‰.2 âŸ¨s, âŸ¨s.2, hsâŸ©âŸ© âŸ¨t, âŸ¨t.2, htâŸ©âŸ© h_dist_zero
  rw [â† ENNReal.div_le_iff (by simp [h_dist_zero]) h_dist_top]
  rw [ENNReal.coe_toNNReal]
  swap; Â· exact ENNReal.rpow_ne_top_of_nonneg (by positivity) hÏ‰.1.ne
  rw [ENNReal.le_rpow_inv_iff hp_pos, ENNReal.div_rpow_of_nonneg _ _ hp_pos.le,
    â† ENNReal.rpow_mul]
  exact le_iSupâ‚‚ âŸ¨s, âŸ¨s.2, hsâŸ©âŸ© âŸ¨t, âŸ¨t.2, htâŸ©âŸ© (f := fun (s t : (T' âˆ© U : Set T)) â†¦
    edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p))

lemma uniformContinuousOn_of_mem_holderSet
    (hT : HasBoundedInternalCoveringNumber U c d)
    (hd_pos : 0 < d) (hp_pos : 0 < p) (hÎ²_pos : 0 < Î²)
    {T' : Set T} {Ï‰ : Î©} (hÏ‰ : Ï‰ âˆˆ holderSet X T' p Î² U) :
    UniformContinuousOn (fun (t : T') â†¦ X t Ï‰) {x : T' | â†‘x âˆˆ U} :=
      (holderOnWith_of_mem_holderSet hT hd_pos hp_pos hÎ²_pos hÏ‰).uniformContinuousOn hÎ²_pos

lemma continuousOn_of_mem_holderSet (hT : HasBoundedInternalCoveringNumber U c d)
    (hd_pos : 0 < d) (hp_pos : 0 < p) (hÎ²_pos : 0 < Î²)
    {T' : Set T} {Ï‰ : Î©} (hÏ‰ : Ï‰ âˆˆ holderSet X T' p Î² U) :
    ContinuousOn (fun (t : T') â†¦ X t Ï‰) {x : T' | â†‘x âˆˆ U} :=
  (holderOnWith_of_mem_holderSet hT hd_pos hp_pos hÎ²_pos hÏ‰).continuousOn hÎ²_pos

lemma exists_tendsto_of_mem_holderSet [CompleteSpace E]
    (hT : HasBoundedInternalCoveringNumber U c d) (hU : IsOpen U)
    (hd_pos : 0 < d) (hp_pos : 0 < p) (hÎ²_pos : 0 < Î²)
    {T' : Set T} (hT'_dense : Dense T') {Ï‰ : Î©} (hÏ‰ : Ï‰ âˆˆ holderSet X T' p Î² U)
    (t : T) (htU : t âˆˆ U) :
    âˆƒ c, Tendsto (fun t' : T' â†¦ X t' Ï‰) (comap Subtype.val (ğ“ t)) (ğ“ c) :=
  (uniformContinuousOn_of_mem_holderSet hT hd_pos hp_pos hÎ²_pos hÏ‰).exists_tendsto hT'_dense hU t
    htU

lemma ae_mem_holderSet [MeasurableSpace E] [BorelSpace E]
    (hT : HasBoundedInternalCoveringNumber U c d) (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q) (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < (q - d) / p)
    {T' : Set T} (hT'_countable : T'.Countable) :
    âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ holderSet X T' p Î² U := by
  have : Countable T' := hT'_countable
  have hT'U : (T' âˆ© U).Countable := hT'_countable.mono Set.inter_subset_left
  have : Countable (T' âˆ© U : Set T) := hT'U
  have h_ae_zero : âˆ€áµ Ï‰ âˆ‚P, âˆ€ (s t : (T' âˆ© U : Set T)),
      edist s t = 0 â†’ edist (X s Ï‰) (X t Ï‰) = 0 := by
    simp_rw [ae_all_iff]
    exact fun s t hst â†¦ hX.IsAEKolmogorovProcess.edist_eq_zero hst
  let C Ï‰ := â¨† (s : (T' âˆ© U : Set T)) (t : (T' âˆ© U : Set T)),
    edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p)
  have hC_lt_top : âˆ€áµ Ï‰ âˆ‚P, C Ï‰ < âˆ :=
    hX.ae_iSup_rpow_edist_div_lt_top hT hc hd_pos hdq_lt hÎ²_pos hÎ²_lt hT'U Set.inter_subset_right
  filter_upwards [hC_lt_top, h_ae_zero] with Ï‰ hÏ‰â‚ hÏ‰â‚‚ using âŸ¨hÏ‰â‚, hÏ‰â‚‚âŸ©

end HolderSet

lemma IsKolmogorovProcess.tendstoInMeasure (hX : IsKolmogorovProcess X P p q M)
    (hq_pos : 0 < q) {T' : Set T} {u : â„• â†’ T'} {t : T}
    (hu : Tendsto (fun n â†¦ (u n : T)) atTop (ğ“ t)) :
    TendstoInMeasure P (fun n â†¦ X (u n)) atTop (X t) := by
  refine tendstoInMeasure_of_ne_top fun Îµ hÎµ hÎµ_top â†¦ ?_
  have h_tendsto : Tendsto (fun n â†¦ âˆ«â» Ï‰, edist (X (u n) Ï‰) (X t Ï‰) ^ p âˆ‚P) atTop (ğ“ 0) := by
    refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds ?_ (fun _ â†¦ zero_le')
      (fun n â†¦ hX.kolmogorovCondition (u n) t)
    have : Tendsto (fun n â†¦ edist (u n).1 t) atTop (ğ“ 0) := by
      rwa [â† tendsto_iff_edist_tendsto_0]
    rw [â† mul_zero (M : â„â‰¥0âˆ)]
    refine ENNReal.Tendsto.const_mul ?_ (by simp)
    change Tendsto ((fun x : â„â‰¥0âˆ â†¦ x ^ q) âˆ˜ (fun n â†¦ edist (u n).1 t)) atTop (ğ“ 0)
    refine Tendsto.comp ?_ this
    convert ENNReal.continuous_rpow_const.tendsto 0
    simp [hq_pos]
  suffices Tendsto (fun n â†¦ P {Ï‰ | Îµ ^ p â‰¤ edist (X (u n) Ï‰) (X t Ï‰) ^ p}) atTop (ğ“ 0) by
    convert this using 3 with n
    ext Ï‰
    simp only [Set.mem_setOf_eq]
    rw [ENNReal.rpow_le_rpow_iff hX.p_pos]
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds ?_ (fun _ â†¦ zero_le') ?_
    (h := fun n â†¦ (Îµ ^ p)â»Â¹ * âˆ«â» Ï‰, edist (X (u n) Ï‰) (X t Ï‰) ^ p âˆ‚P)
  Â· rw [â† mul_zero (Îµ ^ p)â»Â¹]
    exact ENNReal.Tendsto.const_mul h_tendsto (by simp [hÎµ_top, hÎµ.ne'])
  Â· refine fun n â†¦ (meas_ge_le_lintegral_div ?_ ?_ ?_).trans_eq ?_
    Â· exact (hX.measurable_edist.pow_const _).aemeasurable
    Â· simp [hÎµ.ne', hX.p_pos.le]
    Â· simp [hÎµ.ne', hÎµ_top]
    Â· rw [ENNReal.div_eq_inv_mul]

section IndicatorProcess

variable {A : Set Î©} [hE : Nonempty E]

/-- Process where we replace the values for `Ï‰` outside of `A` with a constant value. -/
noncomputable
def indicatorProcess (X : T â†’ Î© â†’ E) (A : Set Î©) : T â†’ Î© â†’ E :=
  haveI := Classical.decPred (Â· âˆˆ A)
  fun t Ï‰ â†¦ if Ï‰ âˆˆ A then X t Ï‰ else hE.some

omit [PseudoEMetricSpace T] [PseudoEMetricSpace E] in
@[simp]
lemma indicatorProcess_apply (X : T â†’ Î© â†’ E) (A : Set Î©) (t : T) (Ï‰ : Î©)
    [Decidable (Ï‰ âˆˆ A)] :
    indicatorProcess X A t Ï‰ = if Ï‰ âˆˆ A then X t Ï‰ else hE.some := by
  simp [indicatorProcess]

omit [PseudoEMetricSpace T] [PseudoEMetricSpace E] in
lemma measurable_indicatorProcess [MeasurableSpace E]
    (hA : MeasurableSet A) (hX : âˆ€ t, Measurable (X t)) (t : T) :
    Measurable (indicatorProcess X A t) :=
  Measurable.ite hA (hX t) measurable_const

lemma measurable_pair_indicatorProcess {Tâ‚ Tâ‚‚ : Type*}
    [MeasurableSpace E] [BorelSpace E] {Xâ‚ : Tâ‚ â†’ Î© â†’ E} {Xâ‚‚ : Tâ‚‚ â†’ Î© â†’ E}
    (hXâ‚ : âˆ€ t, Measurable (Xâ‚ t)) (hXâ‚‚ : âˆ€ t, Measurable (Xâ‚‚ t))
    (hXâ‚â‚‚ : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (Xâ‚ i Ï‰, Xâ‚‚ j Ï‰))
    {Aâ‚ Aâ‚‚ : Set Î©} (hAâ‚ : MeasurableSet Aâ‚) (hAâ‚‚ : MeasurableSet Aâ‚‚) (s : Tâ‚) (t : Tâ‚‚) :
    Measurable[_, borel (E Ã— E)]
      (fun Ï‰ â†¦ (indicatorProcess Xâ‚ Aâ‚ s Ï‰, indicatorProcess Xâ‚‚ Aâ‚‚ t Ï‰)) := by
  classical
  borelize (E Ã— E)
  have h_eq : (fun Ï‰ â†¦ (indicatorProcess Xâ‚ Aâ‚ s Ï‰, indicatorProcess Xâ‚‚ Aâ‚‚ t Ï‰)) =
      fun Ï‰ â†¦ if Ï‰ âˆˆ Aâ‚ then if Ï‰ âˆˆ Aâ‚‚ then (Xâ‚ s Ï‰, Xâ‚‚ t Ï‰) else (Xâ‚ s Ï‰, hE.some)
        else if Ï‰ âˆˆ Aâ‚‚ then (hE.some, Xâ‚‚ t Ï‰) else (hE.some, hE.some) := by
    ext Ï‰ <;> by_cases hÏ‰â‚ : Ï‰ âˆˆ Aâ‚ <;> by_cases hÏ‰â‚‚ : Ï‰ âˆˆ Aâ‚‚ <;> simp [hÏ‰â‚, hÏ‰â‚‚]
  rw [h_eq]
  refine Measurable.ite hAâ‚ ?_ ?_
  Â· refine Measurable.ite hAâ‚‚ (hXâ‚â‚‚ s t) ?_
    change Measurable ((fun x : E â†¦ (x, hE.some)) âˆ˜ Xâ‚ s)
    refine Measurable.comp ?_ (hXâ‚ _)
    clear hXâ‚â‚‚ -- Lean crashes without this
    fun_prop
  Â· refine Measurable.ite hAâ‚‚ ?_ measurable_const
    change Measurable ((fun x : E â†¦ (hE.some, x)) âˆ˜ Xâ‚‚ t)
    refine Measurable.comp ?_ (hXâ‚‚ _)
    clear hXâ‚â‚‚
    fun_prop

lemma measurable_edist_indicatorProcess {Tâ‚ Tâ‚‚ : Type*} [MeasurableSpace E] [BorelSpace E]
    {Xâ‚ : Tâ‚ â†’ Î© â†’ E} {Xâ‚‚ : Tâ‚‚ â†’ Î© â†’ E}
    (hXâ‚ : âˆ€ t, Measurable (Xâ‚ t)) (hXâ‚‚ : âˆ€ t, Measurable (Xâ‚‚ t))
    (hXâ‚â‚‚ : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (Xâ‚ i Ï‰, Xâ‚‚ j Ï‰))
    {Aâ‚ Aâ‚‚ : Set Î©} (hAâ‚ : MeasurableSet Aâ‚) (hAâ‚‚ : MeasurableSet Aâ‚‚) (s : Tâ‚) (t : Tâ‚‚) :
    Measurable (fun Ï‰ â†¦ edist (indicatorProcess Xâ‚ Aâ‚ s Ï‰) (indicatorProcess Xâ‚‚ Aâ‚‚ t Ï‰)) := by
  borelize (E Ã— E)
  refine StronglyMeasurable.measurable ?_
  exact continuous_edist.stronglyMeasurable.comp_measurable
    (measurable_pair_indicatorProcess hXâ‚ hXâ‚‚ hXâ‚â‚‚ hAâ‚ hAâ‚‚ s t)

end IndicatorProcess

/-- A countable dense subset of a second-countable topological space. -/
def denseCountable (T : Type*) [TopologicalSpace T] [SecondCountableTopology T] : Set T :=
  (TopologicalSpace.exists_countable_dense T).choose

lemma dense_denseCountable {T : Type*} [TopologicalSpace T] [SecondCountableTopology T] :
    Dense (denseCountable T) :=
  (TopologicalSpace.exists_countable_dense T).choose_spec.2

lemma countable_denseCountable {T : Type*} [TopologicalSpace T] [SecondCountableTopology T] :
    (denseCountable T).Countable :=
  (TopologicalSpace.exists_countable_dense T).choose_spec.1

variable [MeasurableSpace E] [BorelSpace E] [CompleteSpace E] [hE : Nonempty E]
  [SecondCountableTopology T]

open Classical in
/-- A class of processes built from an other. Used to ensure measurability. -/
def IsLimitOfIndicator (Y X : T â†’ Î© â†’ E) (P : Measure Î©) (U : Set T) : Prop :=
  âˆƒ (A : Set Î©), MeasurableSet A âˆ§
    (âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A) âˆ§
    (âˆ€ t âˆˆ U, âˆ€ Ï‰ âˆˆ A, âˆƒ c, Tendsto (fun t' : denseCountable T â†¦ X t' Ï‰)
      (comap Subtype.val (ğ“ t)) (ğ“ c)) âˆ§
    âˆ€ t Ï‰, Y t Ï‰ = if t âˆˆ U then dense_denseCountable.extend
      (fun t' : denseCountable T â†¦ indicatorProcess X A t' Ï‰) t else hE.some

omit [CompleteSpace E] in
lemma IsLimitOfIndicator.measurable {Y X : T â†’ Î© â†’ E}
    (hX : âˆ€ t, Measurable (X t)) (hY : IsLimitOfIndicator Y X P U) (t : T) :
    Measurable (Y t) := by
  obtain âŸ¨A, hA, hA_ae, hY_tendsto, hY_eqâŸ© := hY
  change Measurable (fun Ï‰ â†¦ Y t Ï‰)
  simp_rw [hY_eq]
  by_cases htU : t âˆˆ U
  Â· simp only [htU, â†“reduceIte]
    refine measurable_limUnder_of_exists_tendsto
      (f := fun (t' : denseCountable T) Ï‰ â†¦ indicatorProcess X A t' Ï‰) (fun Ï‰ â†¦ ?_) (fun t' â†¦ ?_)
    Â· by_cases hÏ‰ : Ï‰ âˆˆ A
      Â· simpa [hÏ‰, indicatorProcess] using hY_tendsto t htU Ï‰ hÏ‰
      Â· simp only [indicatorProcess, hÏ‰, â†“reduceIte]
        exact âŸ¨hE.some, tendsto_const_nhdsâŸ©
    Â· simp only
      exact Measurable.ite hA (hX t') measurable_const
  Â· simp only [htU, â†“reduceIte]
    exact measurable_const

/-- A HÃ¶lder continuous modification of a process `X`. -/
noncomputable
def holderModification (X : T â†’ Î© â†’ E) (Î² : â„â‰¥0) (p : â„) (U : Set T) [DecidablePred (Â· âˆˆ U)] :
    T â†’ Î© â†’ E :=
  fun t Ï‰ â†¦ if t âˆˆ U then limUnder (comap Subtype.val (ğ“ t))
    (fun t' : denseCountable T â†¦ indicatorProcess X (holderSet X (denseCountable T) p Î² U) t' Ï‰)
    else hE.some

lemma isLimitOfIndicator_holderModification
    (hT : HasBoundedInternalCoveringNumber U c d) [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q) (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < (q - d) / p) :
    IsLimitOfIndicator (holderModification X Î² p U) X P U := by
  let A := holderSet X (denseCountable T) p Î² U
  have hA_meas : MeasurableSet A := hX.measurableSet_holderSet countable_denseCountable
  have hA_ae : âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A := ae_mem_holderSet hT hX hc hd_pos hdq_lt hÎ²_pos hÎ²_lt
    countable_denseCountable
  refine âŸ¨A, hA_meas, hA_ae, fun t htU Ï‰ hÏ‰ â†¦ ?_, fun t Ï‰ â†¦ ?_âŸ©
  Â· exact exists_tendsto_of_mem_holderSet hT hU hd_pos hX.p_pos hÎ²_pos dense_denseCountable
      hÏ‰ t htU
  Â· simp only [holderModification, indicatorProcess, A, Dense.extend, IsDenseInducing.extend]
    rfl

omit [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology T] in
lemma exists_tendsto_indicatorProcess_holderSet
    (hT : HasBoundedInternalCoveringNumber U c d) (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M) (hd_pos : 0 < d) (hÎ²_pos : 0 < Î²)
    {T' : Set T} (hT'_dense : Dense T') (t : T) (htU : t âˆˆ U) (Ï‰ : Î©) :
    âˆƒ c, Tendsto (fun x : T' â†¦ indicatorProcess X (holderSet X T' p Î² U) x Ï‰)
      (comap Subtype.val (ğ“ t)) (ğ“ c) := by
  by_cases hÏ‰ : Ï‰ âˆˆ holderSet X T' p Î² U
  Â· have : (fun x : T' â†¦ indicatorProcess X (holderSet X T' p Î² U) x Ï‰)
        =á¶ [comap Subtype.val (ğ“ t)] (fun x : T' â†¦ X x Ï‰) := by
      suffices âˆ€á¶  (x : T') in (comap Subtype.val (ğ“ t)), â†‘x âˆˆ U by
        filter_upwards [this] with x hx
        simp [hÏ‰, indicatorProcess]
      simp only [eventually_comap, Subtype.forall]
      suffices âˆ€á¶  b in ğ“ t, b âˆˆ U by
        filter_upwards [this] with x hx
        rintro x' hx' rfl
        exact hx
      rw [eventually_mem_set]
      exact hU.mem_nhds htU
    simp_rw [tendsto_congr' this]
    exact exists_tendsto_of_mem_holderSet hT hU hd_pos hX.p_pos hÎ²_pos
      hT'_dense hÏ‰ t htU
  Â· simp only [indicatorProcess, hÏ‰, â†“reduceIte]
    exact âŸ¨hE.some, tendsto_const_nhdsâŸ©

lemma measurable_holderModification (hT : HasBoundedInternalCoveringNumber U c d)
    [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q) (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < (q - d) / p)
    (t : T) :
    Measurable (holderModification X Î² p U t) :=
  IsLimitOfIndicator.measurable hX.measurable
    (isLimitOfIndicator_holderModification hT hU hX hc hd_pos hdq_lt hÎ²_pos hÎ²_lt) t

omit [MeasurableSpace E] [BorelSpace E] in
lemma holderOnWith_holderModification
    (hT : HasBoundedInternalCoveringNumber U c d) [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M) (hd_pos : 0 < d) (hÎ²_pos : 0 < Î²) (Ï‰ : Î©) :
    âˆƒ C : â„â‰¥0, HolderOnWith C Î² (holderModification X Î² p U Â· Ï‰) U := by
  let C Ï‰ := â¨† (s : (denseCountable T âˆ© U : Set T)) (t : (denseCountable T âˆ© U : Set T)),
    edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p)
  simp only [holderModification]
  suffices âˆƒ C, HolderOnWith C Î²
      (fun x â†¦ limUnder (comap Subtype.val (ğ“ x))
        fun t' : denseCountable T â†¦ indicatorProcess X (holderSet X (denseCountable T) p Î² U) t' Ï‰)
      U by
    obtain âŸ¨C, hHolderâŸ© := this
    refine âŸ¨C, ?_âŸ©
    -- missing congr lemma?
    intro s hs t ht
    specialize hHolder s hs t ht
    simpa only [hs, â†“reduceIte, ht]
  by_cases hÏ‰ : Ï‰ âˆˆ holderSet X (denseCountable T) p Î² U
  Â· simp only [hÏ‰, â†“reduceIte,indicatorProcess]
    refine âŸ¨(C Ï‰ ^ pâ»Â¹).toNNReal, ?_âŸ©
    refine dense_denseCountable.holderOnWith_extend hU ?_ hÎ²_pos
    exact holderOnWith_of_mem_holderSet hT hd_pos hX.p_pos hÎ²_pos hÏ‰
  Â· simp only [hÏ‰, â†“reduceIte, indicatorProcess]
    refine âŸ¨0, HolderWith.holderOnWith ?_ _âŸ©
    exact dense_denseCountable.holderWith_extend (by simp [HolderWith]) hÎ²_pos

omit [MeasurableSpace E] [BorelSpace E] in
lemma uniformContinuousOn_holderModification
    (hT : HasBoundedInternalCoveringNumber U c d) [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M) (hd_pos : 0 < d) (hÎ²_pos : 0 < Î²)
    (Ï‰ : Î©) :
    UniformContinuousOn (fun t : T â†¦ holderModification X Î² p U t Ï‰) U := by
  obtain âŸ¨C, hHolderâŸ© := holderOnWith_holderModification hT hU hX hd_pos hÎ²_pos Ï‰
  exact hHolder.uniformContinuousOn hÎ²_pos

omit [MeasurableSpace E] [BorelSpace E] in
lemma continuousOn_holderModification
    (hT : HasBoundedInternalCoveringNumber U c d) [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M) (hd_pos : 0 < d) (hÎ²_pos : 0 < Î²) (Ï‰ : Î©) :
    ContinuousOn (fun t : T â†¦ holderModification X Î² p U t Ï‰) U :=
  (uniformContinuousOn_holderModification hT hU hX hd_pos hÎ²_pos Ï‰).continuousOn

omit [CompleteSpace E] hE in
lemma _root_.Measurable.of_edist_eq_zero {X Y : Î© â†’ E} (hX : Measurable X)
    (h_eq_zero : âˆ€ Ï‰, edist (Y Ï‰) (X Ï‰) = 0) :
    Measurable Y := by
  refine measurable_of_isOpen fun U hU â†¦ ?_
  suffices Y â»Â¹' U = X â»Â¹' U by rw [this]; exact hX hU.measurableSet
  ext Ï‰
  simp only [Set.mem_preimage, â† hU.mem_nhds_iff]
  suffices ğ“ (X Ï‰) = ğ“ (Y Ï‰) by rw [this]
  symm
  refine Inseparable.nhds_eq ?_
  rw [EMetric.inseparable_iff]
  exact h_eq_zero Ï‰

omit [MeasurableSpace E] [BorelSpace E] [CompleteSpace E] in
lemma edist_limUnder_prod_eq_zero {Î± Î² : Type*} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²}
    [lâ‚.NeBot] [lâ‚‚.NeBot]
    {f : Î± â†’ E} {g : Î² â†’ E} (hf : âˆƒ c, Tendsto f lâ‚ (ğ“ c)) (hg : âˆƒ c, Tendsto g lâ‚‚ (ğ“ c)) :
    edist (limUnder (lâ‚ Ã—Ë¢ lâ‚‚) fun p : Î± Ã— Î² â†¦ (f p.1, g p.2))
      (limUnder lâ‚ f, limUnder lâ‚‚ g) = 0 := by
  have h_prod : âˆƒ c, Tendsto (fun p : Î± Ã— Î² â†¦ (f p.1, g p.2)) (lâ‚ Ã—Ë¢ lâ‚‚) (ğ“ c) := by
    obtain âŸ¨câ‚€, hâ‚€âŸ© := hf
    obtain âŸ¨câ‚, hâ‚âŸ© := hg
    use (câ‚€, câ‚)
    rw [nhds_prod_eq]
    apply Filter.Tendsto.prodMk
    Â· exact hâ‚€.comp tendsto_fst
    Â· exact hâ‚.comp tendsto_snd
  rw [â† EMetric.inseparable_iff]
  refine tendsto_nhds_unique_inseparable (f := fun p : Î± Ã— Î² â†¦ (f p.1, g p.2)) (l := lâ‚ Ã—Ë¢ lâ‚‚) ?_ ?_
  Â· exact tendsto_nhds_limUnder h_prod
  Â· refine Tendsto.prodMk_nhds ?_ ?_
    Â· exact (tendsto_nhds_limUnder hf).comp tendsto_fst
    Â· exact (tendsto_nhds_limUnder hg).comp tendsto_snd

omit [MeasurableSpace E] [BorelSpace E] [CompleteSpace E] [SecondCountableTopology T] in
lemma measurable_pair_limUnder_comap {Xâ‚ Xâ‚‚ : T â†’ Î© â†’ E} {T' : Set T} (hT'_dense : Dense T')
    (hXâ‚â‚‚ : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (Xâ‚ i Ï‰, Xâ‚‚ j Ï‰))
    (s t : T)
    (hXâ‚_tendsto : âˆ€ Ï‰, âˆƒ x, Tendsto (fun t' : T' â†¦ Xâ‚ t' Ï‰) (comap Subtype.val (ğ“ s)) (ğ“ x))
    (hXâ‚‚_tendsto : âˆ€ Ï‰, âˆƒ x, Tendsto (fun t' : T' â†¦ Xâ‚‚ t' Ï‰) (comap Subtype.val (ğ“ t)) (ğ“ x)) :
    Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦
      (limUnder (comap Subtype.val (ğ“ s)) fun t' : T' â†¦ Xâ‚ t' Ï‰,
        limUnder (comap Subtype.val (ğ“ t)) fun t' : T' â†¦ Xâ‚‚ t' Ï‰) := by
  have : @NeBot { x // x âˆˆ T' } (comap Subtype.val (ğ“ s)) := by
    apply IsDenseInducing.comap_nhds_neBot (Dense.isDenseInducing_val hT'_dense)
  have : @NeBot { x // x âˆˆ T' } (comap Subtype.val (ğ“ t)) := by
    apply IsDenseInducing.comap_nhds_neBot (Dense.isDenseInducing_val hT'_dense)
  let f (x : T' Ã— T') (Ï‰ : Î©) := (Xâ‚ x.1 Ï‰, Xâ‚‚ x.2 Ï‰)
  have hf_tendsto Ï‰ : âˆƒ c,
      Tendsto (f Â· Ï‰) (comap Subtype.val (ğ“ s) Ã—Ë¢ comap Subtype.val (ğ“ t)) (ğ“ c) := by
    obtain âŸ¨câ‚€, hâ‚€âŸ© := hXâ‚_tendsto Ï‰
    obtain âŸ¨câ‚, hâ‚âŸ© := hXâ‚‚_tendsto Ï‰
    use (câ‚€, câ‚)
    rw [nhds_prod_eq]
    apply Filter.Tendsto.prodMk
    Â· exact hâ‚€.comp tendsto_fst
    Â· exact hâ‚.comp tendsto_snd
  have h_edist_zero Ï‰ : edist ((limUnder (comap Subtype.val (ğ“ s)) fun (t' : T') â†¦ Xâ‚ t' Ï‰,
        limUnder (comap Subtype.val (ğ“ t)) fun (t' : T') â†¦ Xâ‚‚ t' Ï‰))
      (limUnder ((comap Subtype.val (ğ“ s)) Ã—Ë¢ (comap Subtype.val (ğ“ t)))
        fun (p : T' Ã— T') â†¦ (Xâ‚ p.1 Ï‰, Xâ‚‚ p.2 Ï‰)) = 0 := by
    have h := edist_limUnder_prod_eq_zero (hXâ‚_tendsto Ï‰) (hXâ‚‚_tendsto Ï‰)
    rwa [edist_comm] at h
  borelize (E Ã— E)
  refine Measurable.of_edist_eq_zero ?_ h_edist_zero
  exact measurable_limUnder_of_exists_tendsto hf_tendsto (fun i â†¦ hXâ‚â‚‚ i.1 i.2)

omit [CompleteSpace E] in
lemma measurable_pair_limUnder_indicatorProcess {X X' : T â†’ Î© â†’ E}
    (hX : âˆ€ t, Measurable (X t)) (hX' : âˆ€ t, Measurable (X' t))
    (hX_pair : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (X i Ï‰, X' j Ï‰))
    {Aâ‚ Aâ‚‚ : Set Î©} (hAâ‚ : MeasurableSet Aâ‚) (hAâ‚‚ : MeasurableSet Aâ‚‚)
    (s t : T)
    (h_tendstoâ‚ : âˆ€ Ï‰ âˆˆ Aâ‚, âˆƒ c, Tendsto (fun t' : denseCountable T â†¦ X t' Ï‰)
      (comap Subtype.val (ğ“ s)) (ğ“ c))
    (h_tendstoâ‚‚ : âˆ€ Ï‰ âˆˆ Aâ‚‚, âˆƒ c, Tendsto (fun t' : denseCountable T â†¦ X' t' Ï‰)
      (comap Subtype.val (ğ“ t)) (ğ“ c)) :
    Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦
      (limUnder (comap Subtype.val (ğ“ s)) fun t' : denseCountable T â†¦ indicatorProcess X Aâ‚ t' Ï‰,
        limUnder (comap Subtype.val (ğ“ t))
          fun t' : denseCountable T â†¦ indicatorProcess X' Aâ‚‚ t' Ï‰) := by
  refine measurable_pair_limUnder_comap dense_denseCountable ?_ _ _ ?_ ?_
    (Xâ‚ := indicatorProcess X Aâ‚)
  Â· exact fun i j â†¦ measurable_pair_indicatorProcess hX hX' hX_pair hAâ‚ hAâ‚‚ i j
  Â· intro Ï‰
    by_cases hÏ‰ : Ï‰ âˆˆ Aâ‚
    Â· simpa [hÏ‰, indicatorProcess] using h_tendstoâ‚ Ï‰ hÏ‰
    Â· simp only [indicatorProcess, hÏ‰, â†“reduceIte]
      exact âŸ¨hE.some, tendsto_const_nhdsâŸ©
  Â· intro Ï‰
    by_cases hÏ‰ : Ï‰ âˆˆ Aâ‚‚
    Â· simpa [hÏ‰, indicatorProcess] using h_tendstoâ‚‚ Ï‰ hÏ‰
    Â· simp only [indicatorProcess, hÏ‰, â†“reduceIte]
      exact âŸ¨hE.some, tendsto_const_nhdsâŸ©

omit [CompleteSpace E] in
lemma IsLimitOfIndicator.measurable_pair {Y X Z X' : T â†’ Î© â†’ E} {Uâ‚ Uâ‚‚ : Set T}
    (hX : âˆ€ t, Measurable (X t)) (hX' : âˆ€ t, Measurable (X' t))
    (hX_pair : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (X i Ï‰, X' j Ï‰))
    (hY : IsLimitOfIndicator Y X P Uâ‚) (hZ : IsLimitOfIndicator Z X' P Uâ‚‚)
    (s t : T) :
    Measurable[_, borel (E Ã— E)] (fun Ï‰ â†¦ (Y s Ï‰, Z t Ï‰)) := by
  have : @NeBot { x // x âˆˆ denseCountable T } (comap Subtype.val (ğ“ s)) := by
    apply IsDenseInducing.comap_nhds_neBot (Dense.isDenseInducing_val dense_denseCountable)
  have : @NeBot { x // x âˆˆ denseCountable T } (comap Subtype.val (ğ“ t)) := by
    apply IsDenseInducing.comap_nhds_neBot (Dense.isDenseInducing_val dense_denseCountable)
  obtain âŸ¨Aâ‚, hAâ‚, hAâ‚_ae, hY_tendsto, hY_eqâŸ© := hY
  obtain âŸ¨Aâ‚‚, hAâ‚‚, hAâ‚‚_ae, hZ_tendsto, hZ_eqâŸ© := hZ
  simp_rw [hY_eq, hZ_eq]
  simp only [Dense.extend, IsDenseInducing.extend]
  by_cases hsUâ‚ : s âˆˆ Uâ‚ <;> by_cases htUâ‚‚ : t âˆˆ Uâ‚‚
  Â· simp only [hsUâ‚, â†“reduceIte, htUâ‚‚]
    exact measurable_pair_limUnder_indicatorProcess hX hX' hX_pair hAâ‚ hAâ‚‚ s t (hY_tendsto s hsUâ‚)
      (hZ_tendsto t htUâ‚‚)
  Â· simp only [hsUâ‚, â†“reduceIte, htUâ‚‚]
    suffices Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦
        (limUnder (comap Subtype.val (ğ“ s)) fun t' : denseCountable T â†¦ indicatorProcess X Aâ‚ t' Ï‰,
        limUnder (comap Subtype.val (ğ“ t))
          fun t' : denseCountable T â†¦ indicatorProcess X' âˆ… t' Ï‰) by
      simp only [indicatorProcess_apply, Set.mem_empty_iff_false, â†“reduceIte] at this
      borelize (E Ã— E)
      refine this.of_edist_eq_zero fun Ï‰ â†¦ ?_
      suffices edist (limUnder (comap Subtype.val (ğ“ t)) fun (t' : denseCountable T) â†¦ hE.some)
          hE.some = 0 by
        rw [Prod.edist_eq]
        simp only [ENNReal.max_eq_zero_iff]
        rw [edist_comm hE.some, this]
        simp only [and_true]
        exact edist_self _
      rw [â† EMetric.inseparable_iff]
      refine tendsto_nhds_unique_inseparable (f := fun t' : denseCountable T â†¦ hE.some)
        (l := comap Subtype.val (ğ“ t)) ?_ ?_
      Â· exact tendsto_nhds_limUnder (âŸ¨hE.some, tendsto_const_nhdsâŸ© : âˆƒ c, Tendsto _ _ _)
      Â· exact tendsto_const_nhds
    exact measurable_pair_limUnder_indicatorProcess hX hX' hX_pair hAâ‚ MeasurableSet.empty s t
      (hY_tendsto s hsUâ‚) (by simp)
  Â· simp only [hsUâ‚, â†“reduceIte, htUâ‚‚]
    suffices Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦
        (limUnder (comap Subtype.val (ğ“ s)) fun t' : denseCountable T â†¦ indicatorProcess X âˆ… t' Ï‰,
        limUnder (comap Subtype.val (ğ“ t))
          fun t' : denseCountable T â†¦ indicatorProcess X' Aâ‚‚ t' Ï‰) by
      simp only [indicatorProcess_apply, Set.mem_empty_iff_false, â†“reduceIte] at this
      borelize (E Ã— E)
      refine this.of_edist_eq_zero fun Ï‰ â†¦ ?_
      suffices edist (limUnder (comap Subtype.val (ğ“ s)) fun (t' : denseCountable T) â†¦ hE.some)
          hE.some = 0 by
        rw [Prod.edist_eq]
        simp only [ENNReal.max_eq_zero_iff]
        rw [edist_comm hE.some, this]
        simp only [true_and]
        exact edist_self _
      rw [â† EMetric.inseparable_iff]
      refine tendsto_nhds_unique_inseparable (f := fun t' : denseCountable T â†¦ hE.some)
        (l := comap Subtype.val (ğ“ s)) ?_ ?_
      Â· exact tendsto_nhds_limUnder (âŸ¨hE.some, tendsto_const_nhdsâŸ© : âˆƒ c, Tendsto _ _ _)
      Â· exact tendsto_const_nhds
    exact measurable_pair_limUnder_indicatorProcess hX hX' hX_pair MeasurableSet.empty hAâ‚‚ s t
      (by simp) (hZ_tendsto t htUâ‚‚)
  Â· simp [hsUâ‚, htUâ‚‚]

omit [CompleteSpace E] in
lemma IsLimitOfIndicator.measurable_edist {Y X Z X' : T â†’ Î© â†’ E} {Uâ‚ Uâ‚‚ : Set T}
    (hX : âˆ€ t, Measurable (X t)) (hX' : âˆ€ t, Measurable (X' t))
    (hX_pair : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (X i Ï‰, X' j Ï‰))
    (hY : IsLimitOfIndicator Y X P Uâ‚) (hZ : IsLimitOfIndicator Z X' P Uâ‚‚)
    (s t : T) :
    Measurable (fun Ï‰ â†¦ edist (Y s Ï‰) (Z t Ï‰)) := by
  borelize (E Ã— E)
  refine StronglyMeasurable.measurable ?_
  exact continuous_edist.stronglyMeasurable.comp_measurable
    (hY.measurable_pair hX hX' hX_pair hZ s t)

end PseudoEMetricSpace

section EMetricSpace

variable [PseudoEMetricSpace T] [EMetricSpace E] [hE : Nonempty E]

variable [MeasurableSpace E] [BorelSpace E] [CompleteSpace E]
  [SecondCountableTopology T]

omit [MeasurableSpace E] [BorelSpace E] [CompleteSpace E] in
lemma IsLimitOfIndicator.indicatorProcess {Y X : T â†’ Î© â†’ E}
    (hY : IsLimitOfIndicator Y X P U) (A : Set Î©) (hA_meas : MeasurableSet A)
    (hA_ae : âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A) :
    IsLimitOfIndicator (fun t â†¦ indicatorProcess Y A t) X P U := by
  obtain âŸ¨A', hA', hA'_ae, hY_tendsto, hY_eqâŸ© := hY
  refine âŸ¨A âˆ© A', MeasurableSet.inter hA_meas hA', ?_, fun t htU Ï‰ hÏ‰ â†¦ ?_, fun t Ï‰ â†¦ ?_âŸ©
  Â· filter_upwards [hA_ae, hA'_ae] with Ï‰ hÏ‰â‚ hÏ‰â‚‚ using âŸ¨hÏ‰â‚, hÏ‰â‚‚âŸ©
  Â· exact hY_tendsto t htU Ï‰ hÏ‰.2
  Â· classical
    by_cases hÏ‰ : Ï‰ âˆˆ A
    Â· simp [hÏ‰, hY_eq]
    Â· simp only [indicatorProcess_apply, hÏ‰, â†“reduceIte, Set.mem_inter_iff, false_and]
      by_cases htU : t âˆˆ U
      Â· simp only [htU, â†“reduceIte]
        rw [Dense.extend_eq_of_tendsto]
        exact tendsto_const_nhds
      Â· simp only [htU, â†“reduceIte]

omit [MeasurableSpace E] [BorelSpace E] [CompleteSpace E] in
lemma holderModification_eq (hT : HasBoundedInternalCoveringNumber U c d) (hU : IsOpen U)
    [DecidablePred (Â· âˆˆ U)]
    (hX : IsKolmogorovProcess X P p q M) (hd_pos : 0 < d) (hÎ²_pos : 0 < Î²)
    (t' : denseCountable T) (ht'U : â†‘t' âˆˆ U)
    {Ï‰ : Î©} (hÏ‰ : Ï‰ âˆˆ holderSet X (denseCountable T) p Î² U) :
    holderModification X Î² p U t' Ï‰ = X t' Ï‰ := by
  simp only [holderModification, ht'U, â†“reduceIte, indicatorProcess, hÏ‰]
  have : @NeBot { x // x âˆˆ denseCountable T } (comap Subtype.val (ğ“ t')) := by
    rw [â† nhds_subtype]
    infer_instance
  rw [Tendsto.limUnder_eq]
  rw [â† nhds_subtype]
  refine ContinuousOn.continuousAt ?_ ?_ (s := {x : denseCountable T | (x : T) âˆˆ U})
  Â· exact continuousOn_of_mem_holderSet hT hd_pos hX.p_pos hÎ²_pos hÏ‰
  Â· refine IsOpen.mem_nhds ?_ ?_
    Â· exact Continuous.isOpen_preimage (by fun_prop) _ hU
    Â· simpa

lemma measurable_pair_holderModification
    {Uâ‚ Uâ‚‚ : Set T} [DecidablePred (Â· âˆˆ Uâ‚)] [DecidablePred (Â· âˆˆ Uâ‚‚)]
    (hUâ‚ : IsOpen Uâ‚) (hUâ‚‚ : IsOpen Uâ‚‚)
    {Xâ‚ Xâ‚‚ : T â†’ Î© â†’ E} {câ‚ câ‚‚ : â„â‰¥0âˆ} {pâ‚ pâ‚‚ qâ‚ qâ‚‚ dâ‚ dâ‚‚ : â„} {Mâ‚ Mâ‚‚ Î²â‚ Î²â‚‚ : â„â‰¥0}
    (hTâ‚ : HasBoundedInternalCoveringNumber Uâ‚ câ‚ dâ‚)
    (hTâ‚‚ : HasBoundedInternalCoveringNumber Uâ‚‚ câ‚‚ dâ‚‚)
    (hXâ‚ : IsKolmogorovProcess Xâ‚ P pâ‚ qâ‚ Mâ‚)
    (hXâ‚‚ : IsKolmogorovProcess Xâ‚‚ P pâ‚‚ qâ‚‚ Mâ‚‚)
    (hcâ‚ : câ‚ â‰  âˆ) (hcâ‚‚ : câ‚‚ â‰  âˆ)
    (hdâ‚_pos : 0 < dâ‚) (hdâ‚‚_pos : 0 < dâ‚‚)
    (hdqâ‚_lt : dâ‚ < qâ‚) (hdqâ‚‚_lt : dâ‚‚ < qâ‚‚)
    (hÎ²â‚_pos : 0 < Î²â‚) (hÎ²â‚‚_pos : 0 < Î²â‚‚)
    (hÎ²â‚_lt : Î²â‚ < (qâ‚ - dâ‚) / pâ‚) (hÎ²â‚‚_lt : Î²â‚‚ < (qâ‚‚ - dâ‚‚) / pâ‚‚)
    (hXâ‚â‚‚ : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (Xâ‚ i Ï‰, Xâ‚‚ j Ï‰))
    (s t : T) :
    Measurable[_, borel (E Ã— E)]
      (fun Ï‰ â†¦ (holderModification Xâ‚ Î²â‚ pâ‚ Uâ‚ s Ï‰, holderModification Xâ‚‚ Î²â‚‚ pâ‚‚ Uâ‚‚ t Ï‰)) :=
  IsLimitOfIndicator.measurable_pair
    hXâ‚.measurable hXâ‚‚.measurable hXâ‚â‚‚
    (isLimitOfIndicator_holderModification hTâ‚ hUâ‚ hXâ‚ hcâ‚ hdâ‚_pos hdqâ‚_lt hÎ²â‚_pos hÎ²â‚_lt)
    (isLimitOfIndicator_holderModification hTâ‚‚ hUâ‚‚ hXâ‚‚ hcâ‚‚ hdâ‚‚_pos hdqâ‚‚_lt hÎ²â‚‚_pos hÎ²â‚‚_lt) s t

lemma measurable_pair_holderModification_self
    (hT : HasBoundedInternalCoveringNumber U c d) [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q) (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < (q - d) / p)
    (s t : T) :
    Measurable[_, borel (E Ã— E)]
      (fun Ï‰ â†¦ (holderModification X Î² p U s Ï‰, holderModification X Î² p U t Ï‰)) :=
  measurable_pair_holderModification hU hU hT hT hX hX hc hc hd_pos hd_pos hdq_lt hdq_lt
    hÎ²_pos hÎ²_pos hÎ²_lt hÎ²_lt hX.measurablePair s t

lemma measurable_edist_holderModification
    {Uâ‚ Uâ‚‚ : Set T} [DecidablePred (Â· âˆˆ Uâ‚)] [DecidablePred (Â· âˆˆ Uâ‚‚)]
    (hUâ‚ : IsOpen Uâ‚) (hUâ‚‚ : IsOpen Uâ‚‚)
    {Xâ‚ Xâ‚‚ : T â†’ Î© â†’ E} {câ‚ câ‚‚ : â„â‰¥0âˆ} {pâ‚ pâ‚‚ qâ‚ qâ‚‚ dâ‚ dâ‚‚ : â„} {Mâ‚ Mâ‚‚ Î²â‚ Î²â‚‚ : â„â‰¥0}
    (hTâ‚ : HasBoundedInternalCoveringNumber Uâ‚ câ‚ dâ‚)
    (hTâ‚‚ : HasBoundedInternalCoveringNumber Uâ‚‚ câ‚‚ dâ‚‚)
    (hXâ‚ : IsKolmogorovProcess Xâ‚ P pâ‚ qâ‚ Mâ‚)
    (hXâ‚‚ : IsKolmogorovProcess Xâ‚‚ P pâ‚‚ qâ‚‚ Mâ‚‚)
    (hcâ‚ : câ‚ â‰  âˆ) (hcâ‚‚ : câ‚‚ â‰  âˆ)
    (hdâ‚_pos : 0 < dâ‚) (hdâ‚‚_pos : 0 < dâ‚‚)
    (hdqâ‚_lt : dâ‚ < qâ‚) (hdqâ‚‚_lt : dâ‚‚ < qâ‚‚)
    (hÎ²â‚_pos : 0 < Î²â‚) (hÎ²â‚‚_pos : 0 < Î²â‚‚)
    (hÎ²â‚_lt : Î²â‚ < (qâ‚ - dâ‚) / pâ‚) (hÎ²â‚‚_lt : Î²â‚‚ < (qâ‚‚ - dâ‚‚) / pâ‚‚)
    (hXâ‚â‚‚ : âˆ€ i j, Measurable[_, borel (E Ã— E)] fun Ï‰ â†¦ (Xâ‚ i Ï‰, Xâ‚‚ j Ï‰))
    (s t : T) :
    Measurable (fun Ï‰ â†¦ edist (holderModification Xâ‚ Î²â‚ pâ‚ Uâ‚ s Ï‰)
        (holderModification Xâ‚‚ Î²â‚‚ pâ‚‚ Uâ‚‚ t Ï‰)) :=
  IsLimitOfIndicator.measurable_edist
    hXâ‚.measurable hXâ‚‚.measurable hXâ‚â‚‚
    (isLimitOfIndicator_holderModification hTâ‚ hUâ‚ hXâ‚ hcâ‚ hdâ‚_pos hdqâ‚_lt hÎ²â‚_pos hÎ²â‚_lt)
    (isLimitOfIndicator_holderModification hTâ‚‚ hUâ‚‚ hXâ‚‚ hcâ‚‚ hdâ‚‚_pos hdqâ‚‚_lt hÎ²â‚‚_pos hÎ²â‚‚_lt) s t

lemma measurable_edist_holderModification' {Î²â‚ Î²â‚‚ : â„â‰¥0}
    (hT : HasBoundedInternalCoveringNumber U c d) [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q)
    (hÎ²â‚_pos : 0 < Î²â‚) (hÎ²â‚_lt : Î²â‚ < (q - d) / p)
    (hÎ²â‚‚_pos : 0 < Î²â‚‚) (hÎ²â‚‚_lt : Î²â‚‚ < (q - d) / p)
    (s t : T) :
    Measurable (fun Ï‰ â†¦ edist (holderModification X Î²â‚ p U s Ï‰)
      (holderModification X Î²â‚‚ p U t Ï‰)) :=
  measurable_edist_holderModification hU hU hT hT hX hX hc hc hd_pos hd_pos hdq_lt hdq_lt
    hÎ²â‚_pos hÎ²â‚‚_pos hÎ²â‚_lt hÎ²â‚‚_lt hX.measurablePair s t

variable [IsFiniteMeasure P]

lemma modification_holderModification (hT : HasBoundedInternalCoveringNumber U c d)
    [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q) (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < (q - d) / p)
    (t : T) (htU : t âˆˆ U) :
    holderModification X Î² p U t =áµ[P] X t := by
  -- For each `Ï‰`, `C Ï‰ < âˆ` is a bound on `edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p)`
  let C Ï‰ := â¨† (s : (denseCountable T âˆ© U : Set T)) (t : (denseCountable T âˆ© U : Set T)),
    edist (X s Ï‰) (X t Ï‰) ^ p / edist s t ^ (Î² * p)
  -- Let `A` be the event that `C Ï‰ < âˆ` and `X s Ï‰ = X t Ï‰` for `edist s t = 0`.
  -- This is an event of probability 1.
  let A := holderSet X (denseCountable T) p Î² U
  have hA_ae : âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A := ae_mem_holderSet hT hX hc hd_pos hdq_lt hÎ²_pos hÎ²_lt
    countable_denseCountable
  have hPA {s : Set Î©} : P (s âˆ© A) = P s := by
    rw [Set.inter_comm, Measure.measure_inter_eq_of_ae hA_ae]
  -- Properties of the modification
  let Y := holderModification X Î² p U
  have hY_eq {Ï‰ : Î©} (hÏ‰ : Ï‰ âˆˆ A) (t : denseCountable T) (htU : â†‘t âˆˆ U) : Y t Ï‰ = X t Ï‰ := by
    exact holderModification_eq hT hU hX hd_pos hÎ²_pos t htU hÏ‰
  classical
  have hY_unif Ï‰ : UniformContinuousOn (fun t â†¦ Y t Ï‰) U :=
    uniformContinuousOn_holderModification hT hU hX hd_pos hÎ²_pos Ï‰
  have hY_cont Ï‰ : ContinuousOn (fun t â†¦ Y t Ï‰) U :=
    continuousOn_holderModification hT hU hX hd_pos hÎ²_pos Ï‰
  -- Main proof
  suffices âˆ€áµ Ï‰ âˆ‚P, edist (Y t Ï‰) (X t Ï‰) â‰¤ 0 by
    filter_upwards [this] with Ï‰ h using by simpa using h
  obtain âŸ¨u, huU, huâŸ© : âˆƒ u : â„• â†’ denseCountable T, (âˆ€ n, â†‘(u n) âˆˆ U) âˆ§
      Tendsto (fun n â†¦ (u n : T)) atTop (ğ“ t) := by
    have ht_mem_closure : t âˆˆ closure (denseCountable T) := by
      simp [dense_denseCountable.closure_eq]
    rw [mem_closure_iff_seq_limit] at ht_mem_closure
    obtain âŸ¨u', hu'_mem, hu'âŸ© := ht_mem_closure
    obtain âŸ¨tâ‚€, htâ‚€âŸ© : âˆƒ t' : denseCountable T, â†‘t' âˆˆ U := by
      obtain âŸ¨t', ht'âŸ© := dense_denseCountable.exists_mem_open hU âŸ¨t, htUâŸ©
      exact âŸ¨âŸ¨t', ht'.1âŸ©, ht'.2âŸ©
    refine âŸ¨fun n â†¦ if u' n âˆˆ U then âŸ¨u' n, hu'_mem nâŸ© else tâ‚€, fun n â†¦ ?_, ?_âŸ©
    Â· simp only
      split_ifs with hmem
      Â· simp [hmem]
      Â· simp [htâ‚€]
    Â· have h_eventually_mem := Tendsto.eventually_mem hu' (hU.mem_nhds htU)
      refine hu'.congr' ?_
      filter_upwards [h_eventually_mem] with n hmem
      simp [hmem]
  have h_le n {Ï‰} (hÏ‰ : Ï‰ âˆˆ A) : edist (Y t Ï‰) (X t Ï‰)
      â‰¤ edist (Y t Ï‰) (Y (u n) Ï‰) + edist (X (u n) Ï‰) (X t Ï‰) := by
    refine (edist_triangle4 (Y t Ï‰) (Y (u n) Ï‰) (X (u n) Ï‰) (X t Ï‰)).trans_eq ?_
    simp [hY_eq hÏ‰ (u n) (huU n)]
  -- `X (u n)` converges in measure to `X t`
  have h_tendsto_X : TendstoInMeasure P (fun n â†¦ X (u n)) atTop (X t) :=
    hX.tendstoInMeasure (hd_pos.trans hdq_lt) hu
  -- `Y (u n)` converges in measure to `Y t`
  have h_tendsto_Y : TendstoInMeasure P (fun n â†¦ Y (u n)) atTop (Y t) := by
    have h_ae Ï‰ : Tendsto (fun n â†¦ Y (u n) Ï‰) atTop (ğ“ (Y t Ï‰)) := by
      refine (hY_cont Ï‰ t htU).tendsto.comp ?_
      exact tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ hu (.of_forall huU)
    refine tendstoInMeasure_of_tendsto_ae_of_measurable_edist (fun n â†¦ ?_) (ae_of_all _ h_ae)
    exact measurable_edist_holderModification' hT hU hX hc hd_pos hdq_lt hÎ²_pos hÎ²_lt hÎ²_pos hÎ²_lt
      (u n) t
  refine (ae_le_const_iff_forall_gt_measure_zero _ _).mpr fun Îµ hÎµ â†¦ ?_
  suffices Tendsto (fun n : â„• â†¦ P {Ï‰ | Îµ â‰¤ edist (Y t Ï‰) (X t Ï‰)}) atTop (ğ“ 0) by
    simpa using this
  have hP_le n : P {Ï‰ | Îµ â‰¤ edist (Y t Ï‰) (X t Ï‰)}
      â‰¤ P {Ï‰ | Îµ/2 â‰¤ edist (Y (u n) Ï‰) (Y t Ï‰)} + P {Ï‰ | Îµ/2 â‰¤ edist (X (u n) Ï‰) (X t Ï‰)} := by
    calc P {Ï‰ | Îµ â‰¤ edist (Y t Ï‰) (X t Ï‰)}
    _ = P ({Ï‰ | Îµ â‰¤ edist (Y t Ï‰) (X t Ï‰)} âˆ© A) := by rw [hPA]
    _ â‰¤ P ({Ï‰ | Îµ â‰¤ edist (Y (u n) Ï‰) (Y t Ï‰) + edist (X (u n) Ï‰) (X t Ï‰)} âˆ© A) := by
      refine measure_mono fun Ï‰ â†¦ ?_
      simp only [Set.mem_inter_iff, Set.mem_setOf_eq, and_imp]
      refine fun hÎµ_le hÏ‰ â†¦ âŸ¨(hÎµ_le.trans (h_le n hÏ‰)).trans_eq ?_, hÏ‰âŸ©
      rw [edist_comm]
    _ = P {Ï‰ | Îµ â‰¤ edist (Y (u n) Ï‰) (Y t Ï‰) + edist (X (u n) Ï‰) (X t Ï‰)} := by rw [hPA]
    _ â‰¤ P {Ï‰ | Îµ / 2 â‰¤ edist (Y (u n) Ï‰) (Y t Ï‰)}
        + P {Ï‰ | Îµ / 2 â‰¤ edist (X (u n) Ï‰) (X t Ï‰)} := measure_add_ge_le_add_measure_ge_half
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds ?_ (fun _ â†¦ zero_le') hP_le
  rw [â† add_zero (0 : â„â‰¥0âˆ)]
  exact Tendsto.add (h_tendsto_Y (Îµ / 2) (ENNReal.half_pos hÎµ.ne'))
    (h_tendsto_X (Îµ / 2) (ENNReal.half_pos hÎµ.ne'))

lemma exists_modification_holder_aux' (hT : HasBoundedInternalCoveringNumber U c d)
    [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q)
    (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < (q - d) / p) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t âˆˆ U, Y t =áµ[P] X t) âˆ§
      (âˆ€ Ï‰, âˆƒ C : â„â‰¥0, HolderOnWith C Î² (Y Â· Ï‰) U) âˆ§ IsLimitOfIndicator Y X P U := by
  refine âŸ¨holderModification X Î² p U, ?_, ?_, ?_, ?_âŸ©
  Â· exact measurable_holderModification hT hU hX hc hd_pos hdq_lt hÎ²_pos hÎ²_lt
  Â· exact modification_holderModification hT hU hX hc hd_pos hdq_lt hÎ²_pos hÎ²_lt
  Â· exact holderOnWith_holderModification hT hU hX hd_pos hÎ²_pos
  Â· exact isLimitOfIndicator_holderModification hT hU hX hc hd_pos hdq_lt hÎ²_pos hÎ²_lt

lemma exists_modification_holder_aux (hT : HasBoundedInternalCoveringNumber U c d)
    [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsAEKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q)
    (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < (q - d) / p) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t âˆˆ U, Y t =áµ[P] X t)âˆ§
      âˆ€ Ï‰, âˆƒ C : â„â‰¥0, HolderOnWith C Î² (Y Â· Ï‰) U := by
  obtain âŸ¨Y, hY_meas, hY_eq, hY_holder, _âŸ© :=
    exists_modification_holder_aux' hT hU hX.IsKolmogorovProcess_mk hc hd_pos hdq_lt
      hÎ²_pos hÎ²_lt
  refine âŸ¨Y, hY_meas, fun t htU â†¦ ?_, hY_holderâŸ©
  filter_upwards [hX.ae_eq_mk t, hY_eq t htU] with Ï‰ hÏ‰1 hÏ‰2 using hÏ‰2.trans hÏ‰1.symm

omit [MeasurableSpace E] [BorelSpace E] [Nonempty E] [CompleteSpace E] in
lemma StronglyMeasurable.measurableSet_eq_of_continuous {f g : T â†’ Î© â†’ E}
    (hf : âˆ€ Ï‰, Continuous (f Â· Ï‰)) (hg : âˆ€ Ï‰, Continuous (g Â· Ï‰))
    (hf_meas : âˆ€ t, StronglyMeasurable (f t)) (hg_meas : âˆ€ t, StronglyMeasurable (g t)) :
    MeasurableSet {Ï‰ | âˆ€ t, f t Ï‰ = g t Ï‰} := by
  obtain âŸ¨T', hT'_countable, hT'_denseâŸ© := TopologicalSpace.exists_countable_dense T
  have : {Ï‰ | âˆ€ (t : T), f t Ï‰ = g t Ï‰} = {Ï‰ | âˆ€ (t : T'), f t Ï‰ = g t Ï‰} := by
    ext Ï‰
    simp only [Set.mem_setOf_eq, Subtype.forall]
    refine âŸ¨fun h t _ â†¦ h t, fun h â†¦ ?_âŸ©
    rw [â† funext_iff]
    exact Continuous.ext_on hT'_dense (hf Ï‰) (hg Ï‰) h
  rw [this]
  have : {Ï‰ | âˆ€ (t : T'), f t Ï‰ = g t Ï‰} = â‹‚ (t : T'), {Ï‰ | f t Ï‰ = g t Ï‰} := by
    ext; simp
  rw [this]
  have : Countable T' := hT'_countable
  refine MeasurableSet.iInter (fun t â†¦ ?_)
  exact StronglyMeasurable.measurableSet_eq_fun (hf_meas t) (hg_meas t)

omit [MeasurableSpace E] [BorelSpace E] [Nonempty E] [CompleteSpace E] in
lemma Measurable.measurableSet_eq_of_continuous {f g : T â†’ Î© â†’ E}
    (hf : âˆ€ Ï‰, Continuous (f Â· Ï‰)) (hg : âˆ€ Ï‰, Continuous (g Â· Ï‰))
    (h_meas : âˆ€ t, Measurable (fun Ï‰ â†¦ edist (f t Ï‰) (g t Ï‰))) :
    MeasurableSet {Ï‰ | âˆ€ t, f t Ï‰ = g t Ï‰} := by
  obtain âŸ¨T', hT'_countable, hT'_denseâŸ© := TopologicalSpace.exists_countable_dense T
  have : {Ï‰ | âˆ€ (t : T), f t Ï‰ = g t Ï‰} = {Ï‰ | âˆ€ (t : T'), f t Ï‰ = g t Ï‰} := by
    ext Ï‰
    simp only [Set.mem_setOf_eq, Subtype.forall]
    refine âŸ¨fun h t _ â†¦ h t, fun h â†¦ ?_âŸ©
    rw [â† funext_iff]
    exact Continuous.ext_on hT'_dense (hf Ï‰) (hg Ï‰) h
  rw [this]
  have : {Ï‰ | âˆ€ (t : T'), f t Ï‰ = g t Ï‰} = â‹‚ (t : T'), {Ï‰ | f t Ï‰ = g t Ï‰} := by
    ext; simp
  rw [this]
  have : Countable T' := hT'_countable
  refine MeasurableSet.iInter (fun t â†¦ ?_)
  suffices MeasurableSet {Ï‰ | edist (f t Ï‰) (g t Ï‰) = 0} by
    convert this with Ï‰
    simp
  exact StronglyMeasurable.measurableSet_eq_fun (h_meas t).stronglyMeasurable (by fun_prop)

omit hE [MeasurableSpace E] [BorelSpace E] [CompleteSpace E] in
lemma Measurable.measurableSet_eqOn_of_continuous {f g : T â†’ Î© â†’ E} (hU : IsOpen U)
    (hf : âˆ€ Ï‰, ContinuousOn (f Â· Ï‰) U) (hg : âˆ€ Ï‰, ContinuousOn (g Â· Ï‰) U)
    (h_meas : âˆ€ t, Measurable (fun Ï‰ â†¦ edist (f t Ï‰) (g t Ï‰))) :
    MeasurableSet {Ï‰ | âˆ€ t âˆˆ U, f t Ï‰ = g t Ï‰} := by
  obtain âŸ¨T', hT'_countable, hT'_denseâŸ© := TopologicalSpace.exists_countable_dense T
  have : {Ï‰ | âˆ€ t âˆˆ U, f t Ï‰ = g t Ï‰} = {Ï‰ | âˆ€ (t : T'), â†‘t âˆˆ U â†’ f t Ï‰ = g t Ï‰} := by
    ext Ï‰
    simp only [Set.mem_setOf_eq, Subtype.forall]
    refine âŸ¨fun h t _ â†¦ h t, fun h â†¦ ?_âŸ©
    have h_eqOn : Set.EqOn (f Â· Ï‰) (g Â· Ï‰) (T' âˆ© U) := by
      intro t htU
      exact h t htU.1 htU.2
    refine Set.EqOn.of_subset_closure h_eqOn (hf Ï‰) (hg Ï‰) Set.inter_subset_right ?_
    exact subset_closure_dense_inter hT'_dense hU
  rw [this]
  have : {Ï‰ | âˆ€ (t : T'), â†‘t âˆˆ U â†’ f t Ï‰ = g t Ï‰}
      = â‹‚ (t : T') (ht : â†‘t âˆˆ U), {Ï‰ | f t Ï‰ = g t Ï‰} := by
    ext; simp
  rw [this]
  have : Countable T' := hT'_countable
  refine MeasurableSet.iInter (fun t â†¦ ?_)
  by_cases htU : â†‘t âˆˆ U
  Â· simp only [htU, Set.iInter_true]
    suffices MeasurableSet {Ï‰ | edist (f t Ï‰) (g t Ï‰) = 0} by
      convert this with Ï‰
      simp
    exact StronglyMeasurable.measurableSet_eq_fun (h_meas t).stronglyMeasurable (by fun_prop)
  Â· simp [htU]

lemma exists_modification_holder'' (hT : HasBoundedInternalCoveringNumber U c d)
    [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t âˆˆ U, Y t =áµ[P] X t) âˆ§
      (âˆ€ (Î² : â„â‰¥0), 0 < Î² â†’ Î² < (q - d) / p â†’ âˆ€ Ï‰, âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U) âˆ§
      IsLimitOfIndicator Y X P U := by
  have h_ratio_pos : 0 < (q - d) / p := by
    have : 0 < p := hX.p_pos
    bound
  obtain âŸ¨Î²', hÎ²'_mono, hÎ²'_mem, hÎ²'_tendstoâŸ© := exists_seq_strictMono_tendsto' h_ratio_pos
  let Î² : â„• â†’ â„â‰¥0 := fun n â†¦ âŸ¨Î²' n, (hÎ²'_mem n).1.leâŸ©
  have hÎ²_pos : âˆ€ n, 0 < Î² n := fun n â†¦ mod_cast (hÎ²'_mem n).1
  obtain âŸ¨T', hT'_countable, hT'_denseâŸ© := TopologicalSpace.exists_countable_dense T
  classical
  have := fun n â†¦ exists_modification_holder_aux' hT hU hX hc hd_pos hdq_lt
    (hÎ²_pos n) (mod_cast (hÎ²'_mem n).2)
  choose Z hZ_meas hZ_ae_eq hZ_holder hZ_isLimit using this
  have hZ_ae_eq' n : âˆ€áµ Ï‰ âˆ‚P, âˆ€ t âˆˆ U, Z n t Ï‰ = Z 0 t Ï‰ := by
    refine indistinguishable_of_modification_on hU
      (ae_of_all _ fun Ï‰ â†¦ ?_) (ae_of_all _ fun Ï‰ â†¦ ?_) ?_
    Â· obtain âŸ¨_, hâŸ© := hZ_holder n Ï‰
      exact h.continuousOn (hÎ²_pos n)
    Â· obtain âŸ¨_, hâŸ© := hZ_holder 0 Ï‰
      exact h.continuousOn (hÎ²_pos 0)
    Â· intro t htU
      filter_upwards [hZ_ae_eq n t htU, hZ_ae_eq 0 t htU] with Ï‰ hÏ‰â‚ hÏ‰â‚‚ using hÏ‰â‚.trans hÏ‰â‚‚.symm
  rw [â† ae_all_iff] at hZ_ae_eq'
  let A := {Ï‰ | âˆ€ n t, t âˆˆ U â†’ Z n t Ï‰ = Z 0 t Ï‰}
  have hA : MeasurableSet A := by
    have : A = â‹‚ n, {Ï‰ | âˆ€ t, t âˆˆ U â†’ Z n t Ï‰ = Z 0 t Ï‰} := by ext; simp [A]
    rw [this]
    refine MeasurableSet.iInter (fun n â†¦ ?_)
    refine Measurable.measurableSet_eqOn_of_continuous hU (fun Ï‰ â†¦ ?_) (fun Ï‰ â†¦ ?_) fun t â†¦ ?_
    Â· obtain âŸ¨_, hâŸ© := hZ_holder n Ï‰
      exact h.continuousOn (hÎ²_pos n)
    Â· obtain âŸ¨_, hâŸ© := hZ_holder 0 Ï‰
      exact h.continuousOn (hÎ²_pos 0)
    Â· refine IsLimitOfIndicator.measurable_edist hX.measurable hX.measurable
        hX.measurablePair (hZ_isLimit n) (hZ_isLimit 0) t t
  have hA_ae : âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A := hZ_ae_eq'
  classical
  let Y (t : T) (Ï‰ : Î©) : E := indicatorProcess (Z 0) A t Ï‰
  refine âŸ¨Y, fun t â†¦ Measurable.ite hA (hZ_meas 0 t) (by fun_prop), fun t htU â†¦ ?_, ?_, ?_âŸ©
  Â· filter_upwards [hA_ae, hZ_ae_eq 0 t htU] with Ï‰ hÏ‰ hÏ‰â‚‚
    simpa only [hÏ‰, â†“reduceIte, Y, indicatorProcess] using hÏ‰â‚‚
  Â· intro Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt Ï‰
    by_cases hÏ‰ : Ï‰ âˆˆ A
    swap; Â· simp [hÏ‰, Y, HolderOnWith]
    simp only [hÏ‰, â†“reduceIte, Y, indicatorProcess]
    obtain âŸ¨n, hnâŸ© : âˆƒ n, Î²â‚€ < Î² n := by
      obtain âŸ¨n, hnâŸ© : âˆƒ n, Î²â‚€ < Î²' n := (Tendsto.eventually_const_lt hÎ²â‚€_lt hÎ²'_tendsto).exists
      exact âŸ¨n, mod_cast hnâŸ©
    suffices âˆƒ C, HolderOnWith C (Î² n) (fun x â†¦ Z 0 x Ï‰) U by
      obtain âŸ¨C, hCâŸ© := this
      refine HolderOnWith.mono_right' hC hn.le (C' := (EMetric.diam U).toNNReal) ?_
      rw [ENNReal.coe_toNNReal (hT.diam_lt_top hd_pos).ne]
      exact fun x hx y hy â†¦ EMetric.edist_le_diam_of_mem hx hy
    simp only [Set.mem_setOf_eq, A] at hÏ‰
    obtain âŸ¨C, hCâŸ© := hZ_holder n Ï‰
    refine âŸ¨C, fun s hs t ht â†¦ ?_âŸ©
    specialize hC s hs t ht
    simpa [â† hÏ‰ n s hs, â† hÏ‰ n t ht]
  Â· exact IsLimitOfIndicator.indicatorProcess (hZ_isLimit 0) A hA hA_ae

lemma exists_modification_holder (hT : HasBoundedInternalCoveringNumber U c d)
    [DecidablePred (Â· âˆˆ U)] (hU : IsOpen U)
    (hX : IsAEKolmogorovProcess X P p q M)
    (hc : c â‰  âˆ) (hd_pos : 0 < d) (hdq_lt : d < q) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t âˆˆ U, Y t =áµ[P] X t)
      âˆ§ âˆ€ (Î² : â„â‰¥0) (_ : 0 < Î²) (_ : Î² < (q - d) / p) Ï‰, âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U := by
  obtain âŸ¨Y, hY_meas, hY_eq, hY_holder, _âŸ© :=
    exists_modification_holder'' hT hU hX.IsKolmogorovProcess_mk hc hd_pos hdq_lt
  refine âŸ¨Y, hY_meas, fun t htU â†¦ ?_, hY_holderâŸ©
  filter_upwards [hX.ae_eq_mk t, hY_eq t htU] with Ï‰ hÏ‰1 hÏ‰2 using hÏ‰2.trans hÏ‰1.symm

omit [SecondCountableTopology T] in
lemma _root_.IsCoverWithBoundedCoveringNumber.hasBoundedInternalCoveringNumber_univ
    {C : â„• â†’ Set T} {c : â„• â†’ â„â‰¥0âˆ}
    (hC : IsCoverWithBoundedCoveringNumber C (Set.univ : Set T) c (fun _ â†¦ d)) (n : â„•) :
    HasBoundedInternalCoveringNumber (Set.univ : Set (C n)) (c n) d := by
  have h := hC.hasBoundedCoveringNumber n
  refine fun Îµ hÎµ â†¦ ?_
  specialize h Îµ (hÎµ.trans_eq ?_)
  Â· unfold EMetric.diam
    simp [iSup_subtype]
  refine le_of_eq_of_le ?_ h
  simp only [ENat.toENNReal_inj]
  unfold internalCoveringNumber
  simp only [Set.subset_univ, iInf_pos]
  classical
  refine le_antisymm ?_ ?_
  Â· simp only [le_iInf_iff]
    intro A hA hA_cover
    refine (iInfâ‚‚_le (A.subtype (C n) : Finset (C n)) (fun x _ â†¦ ?_)).trans ?_
    Â· have âŸ¨c, hc_mem, hc_edistâŸ© := hA_cover x x.2
      exact âŸ¨âŸ¨c, hA hc_memâŸ©, by simpa using hc_mem, hc_edistâŸ©
    Â· simp only [Finset.card_subtype, Nat.cast_le]
      exact Finset.card_filter_le _ _
  Â· simp only [le_iInf_iff]
    intro A hA_cover
    refine (iInfâ‚‚_le (A.image (fun x : C n â†¦ (x : T))) (by simp)).trans ?_
    refine (iInf_le _ ?_).trans ?_
    Â· intro x hx_mem
      obtain âŸ¨c, hc_mem, hcâŸ© := hA_cover âŸ¨x, hx_memâŸ© (Set.mem_univ _)
      exact âŸ¨c, by simpa using hc_mem, hcâŸ©
    Â· exact mod_cast Finset.card_image_le

lemma exists_modification_holder''' {C : â„• â†’ Set T} {c : â„• â†’ â„â‰¥0âˆ}
    (hC : IsCoverWithBoundedCoveringNumber C (Set.univ : Set T) c (fun _ â†¦ d))
    (hX : IsKolmogorovProcess X P p q M) (hc : âˆ€ n, c n â‰  âˆ)
    (hd_pos : 0 < d) (hdq_lt : d < q) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t, Y t =áµ[P] X t) âˆ§
      (âˆ€ Ï‰ t, âˆƒ U âˆˆ ğ“ t, âˆ€ (Î² : â„â‰¥0), 0 < Î² â†’ Î² < (q - d) / p â†’ âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U) âˆ§
      IsLimitOfIndicator Y X P Set.univ := by
  have hp_pos : 0 < p := hX.p_pos
  have h_div_pos : 0 < (q - d) / p := by bound
  let âŸ¨Î²â‚€', hÎ²â‚€_pos', hÎ²â‚€_lt'âŸ© := exists_between h_div_pos
  let Î²â‚€ : â„â‰¥0 := âŸ¨Î²â‚€', hÎ²â‚€_pos'.leâŸ©
  have hÎ²â‚€_pos : 0 < Î²â‚€ := mod_cast hÎ²â‚€_pos'
  have hÎ²â‚€_lt : Î²â‚€ < (q - d) / p := mod_cast hÎ²â‚€_lt'
  classical
  choose Z hZ hZ_eq hZ_holder hZ_extend
    using fun n â†¦ exists_modification_holder'' (hC.hasBoundedCoveringNumber n) (hC.isOpen n) hX
      (hc n) hd_pos hdq_lt
  have hZ_ae_eq : âˆ€áµ Ï‰ âˆ‚P, âˆ€ n t (ht : t âˆˆ C n), Z n t Ï‰ = Z (n + 1) t Ï‰ := by
    rw [ae_all_iff]
    intro n
    refine indistinguishable_of_modification_on (hC.isOpen n)
      (ae_of_all _ fun Ï‰ â†¦ ?_) (ae_of_all _ fun Ï‰ â†¦ ?_) ?_
    Â· obtain âŸ¨_, hâŸ© := hZ_holder n Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt Ï‰
      exact h.continuousOn hÎ²â‚€_pos
    Â· obtain âŸ¨_, hâŸ© := hZ_holder (n + 1) Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt Ï‰
      have h_n_add_one := h.continuousOn hÎ²â‚€_pos
      refine h_n_add_one.mono ?_
      exact hC.mono _ _ (Nat.le_succ _)
    Â· intro t htCn
      filter_upwards [hZ_eq n t htCn, hZ_eq (n + 1) t (hC.mono _ _ (Nat.le_succ _) htCn)]
        with Ï‰ hÏ‰â‚ hÏ‰â‚‚
      exact hÏ‰â‚.trans hÏ‰â‚‚.symm
  let A := {Ï‰ | âˆ€ n t (ht : t âˆˆ C n), Z n t Ï‰ = Z (n + 1) t Ï‰}
  have hA_eq_le {Ï‰} (hÏ‰ : Ï‰ âˆˆ A) {n m} (hnm : n â‰¤ m) (t : C n) : Z n t Ï‰ = Z m t Ï‰ := by
    induction m with
    | zero => simp only [nonpos_iff_eq_zero] at hnm; subst hnm; simp
    | succ m hm =>
      by_cases hnm' : n â‰¤ m
      Â· exact (hm hnm').trans (hÏ‰ m t (hC.mono _ _ hnm' t.2))
      Â· have : n = m + 1 := by omega
        subst this
        rfl
  have hA : MeasurableSet A := by
    have : A = â‹‚ n, {Ï‰ | âˆ€ t âˆˆ C n, Z n t Ï‰ = Z (n + 1) t Ï‰} := by ext; simp [A]
    rw [this]
    refine MeasurableSet.iInter (fun n â†¦ ?_)
    refine Measurable.measurableSet_eqOn_of_continuous (hC.isOpen n)
      (fun Ï‰ â†¦ ?_) (fun Ï‰ â†¦ ?_) fun t â†¦ ?_
    Â· obtain âŸ¨_, hâŸ© :=  hZ_holder n Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt Ï‰
      exact h.continuousOn hÎ²â‚€_pos
    Â· obtain âŸ¨_, hâŸ© :=  hZ_holder (n + 1) Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt Ï‰
      have h_cont := h.continuousOn hÎ²â‚€_pos
      refine h_cont.mono ?_
      exact hC.mono _ _ (Nat.le_succ _)
    Â· refine IsLimitOfIndicator.measurable_edist ?_ ?_ ?_ (hZ_extend n) (hZ_extend (n + 1)) _ _
      Â· exact hX.measurable
      Â· exact hX.measurable
      Â· intro i j
        exact hX.measurablePair i j
  have hA_ae : âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A := hZ_ae_eq
  classical
  -- for each `t`, find `n` such that `t âˆˆ C n` and call it `nt t`
  have h_mem t : âˆƒ n, t âˆˆ C n := by
    have ht : t âˆˆ â‹ƒ n, C n := hC.subset_iUnion (by simp : t âˆˆ Set.univ)
    simpa using ht
  let nt t := Nat.find (h_mem t)
  have hnt t : t âˆˆ C (nt t) := Nat.find_spec (h_mem t)
  choose A' hA'_meas hA'_ae hZ_tendsto hZ_eq' using hZ_extend
  let Y (t : T) (Ï‰ : Î©) : E := if Ï‰ âˆˆ (A âˆ© â‹‚ n, A' n) then Z (nt t) t Ï‰ else hE.some
  have hY_eq {Ï‰} (hÏ‰ : Ï‰ âˆˆ A âˆ© â‹‚ n, A' n) n (t : T) (ht : t âˆˆ C n) : Y t Ï‰ = Z n t Ï‰ := by
    simp only [hÏ‰, â†“reduceIte, Y]
    exact hA_eq_le hÏ‰.1 (Nat.find_le ht) âŸ¨t, hnt tâŸ©
  have hA_inter_meas : MeasurableSet (A âˆ© â‹‚ n, A' n) :=
    MeasurableSet.inter hA (MeasurableSet.iInter hA'_meas)
  have hA_inter_ae : âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A âˆ© â‹‚ n, A' n := by
    simp only [Set.mem_inter_iff, Set.mem_iInter, eventually_and, ae_all_iff]
    exact âŸ¨hA_ae, hA'_aeâŸ©
  refine âŸ¨Y, fun t â†¦ Measurable.ite hA_inter_meas (hZ _ _) (by fun_prop), fun t â†¦ ?_, ?_, ?_âŸ©
  Â· specialize hZ (nt t) t
    filter_upwards [hA_inter_ae, hZ_eq (nt t) t (hnt t)] with Ï‰ hÏ‰ hÏ‰â‚‚
    simp only [hÏ‰, â†“reduceIte, hÏ‰â‚‚, Y, A]
  Â· intro Ï‰ t
    refine âŸ¨C (nt t), (hC.isOpen (nt t)).mem_nhds (hnt t), ?_âŸ©
    intro Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt
    by_cases hÏ‰ : Ï‰ âˆˆ A âˆ© â‹‚ n, A' n
    swap
    Â· unfold Y
      simp_rw [if_neg hÏ‰]
      simp [HolderOnWith]
    obtain âŸ¨C', hC'âŸ© := hZ_holder (nt t) Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt Ï‰
    refine âŸ¨C', ?_âŸ©
    intro s hs s' hs'
    simp only
    rw [hY_eq hÏ‰ (nt t) s hs, hY_eq hÏ‰ (nt t) s' hs']
    exact hC' s hs s' hs'
  Â· refine âŸ¨A âˆ© â‹‚ n, A' n, ?_, ?_, ?_, ?_âŸ©
    Â· exact hA.inter (MeasurableSet.iInter hA'_meas)
    Â· simp only [Set.mem_inter_iff, Set.mem_iInter, eventually_and, ae_all_iff]
      exact âŸ¨hA_ae, hA'_aeâŸ©
    Â· rintro t - Ï‰ hÏ‰
      simp only [Set.mem_inter_iff, Set.mem_iInter] at hÏ‰
      exact hZ_tendsto (nt t) t (hnt t) Ï‰ (hÏ‰.2 (nt t))
    Â· intro t Ï‰
      classical
      simp only [Set.mem_univ, â†“reduceIte, indicatorProcess_apply]
      by_cases hÏ‰ : Ï‰ âˆˆ A âˆ© â‹‚ n, A' n
      swap
      Â· simp only [hÏ‰, â†“reduceIte, Y]
        rw [Dense.extend_eq_of_tendsto]
        exact tendsto_const_nhds
      simp only [hZ_eq', indicatorProcess_apply, hÏ‰, â†“reduceIte, Y]
      simp only [Set.mem_inter_iff, Set.mem_iInter] at hÏ‰
      simp [hÏ‰, â†“reduceIte, hnt]

lemma exists_modification_holder' {C : â„• â†’ Set T} {c : â„• â†’ â„â‰¥0âˆ}
    (hC : IsCoverWithBoundedCoveringNumber C (Set.univ : Set T) c (fun _ â†¦ d))
    (hX : IsAEKolmogorovProcess X P p q M) (hc : âˆ€ n, c n â‰  âˆ)
    (hd_pos : 0 < d) (hdq_lt : d < q) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t, Y t =áµ[P] X t)
      âˆ§ âˆ€ Ï‰ t, âˆƒ U âˆˆ ğ“ t, âˆ€ (Î² : â„â‰¥0) (_ : 0 < Î²) (_ : Î² < (q - d) / p),
        âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U := by
  obtain âŸ¨Y, hY_meas, hY_eq, hY_holder, _âŸ© :=
    exists_modification_holder''' hC hX.IsKolmogorovProcess_mk hc hd_pos hdq_lt
  refine âŸ¨Y, hY_meas, fun t â†¦ ?_, hY_holderâŸ©
  filter_upwards [hX.ae_eq_mk t, hY_eq t] with Ï‰ hÏ‰1 hÏ‰2 using hÏ‰2.trans hÏ‰1.symm

lemma exists_modification_holder_iSup' {C : â„• â†’ Set T} {c : â„• â†’ â„â‰¥0âˆ} {p q : â„• â†’ â„} {M : â„• â†’ â„â‰¥0}
    (hC : IsCoverWithBoundedCoveringNumber C (Set.univ : Set T) c (fun _ â†¦ d))
    (hX : âˆ€ n, IsKolmogorovProcess X P (p n) (q n) (M n)) (hc : âˆ€ n, c n â‰  âˆ)
    (hd_pos : 0 < d) (hdq_lt : âˆ€ n, d < q n) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t, Y t =áµ[P] X t)
      âˆ§ âˆ€ Ï‰ t (Î² : â„â‰¥0) (_ : 0 < Î²) (_ : Î² < â¨† n, (q n - d) / (p n)),
        âˆƒ U âˆˆ ğ“ t, âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U := by
  have hp_pos : âˆ€ n, 0 < p n := fun n â†¦ (hX n).p_pos
  by_cases h_bdd : BddAbove (Set.range fun n â†¦ (q n - d) / p n)
  swap
  Â· refine âŸ¨X, (hX 0).measurable, fun _ â†¦ EventuallyEq.rfl, fun Ï‰ t Î² hÎ²_pos hÎ²_lt â†¦ ?_âŸ©
    simp only [ciSup_of_not_bddAbove h_bdd, Real.sSup_empty] at hÎ²_lt
    norm_cast at hÎ²_lt
    exact absurd hÎ²_pos hÎ²_lt.not_gt
  have h_ratio_pos n : 0 < (q n - d) / p n := by
    have : 0 < q n - d := by bound
    specialize hp_pos n
    positivity
  let Î² : â„• â†’ â„â‰¥0 := fun n â†¦ âŸ¨(q n - d) / p n, (h_ratio_pos n).leâŸ©
  have hÎ²_pos : âˆ€ n, 0 < Î² n := fun n â†¦ mod_cast h_ratio_pos n
  have h_exists := fun n â†¦ exists_modification_holder''' hC (hX n) hc hd_pos (hdq_lt n)
  choose Z hZ_meas hZ_ae_eq hZ_holder hZ_limit using h_exists
  have hZ_cont n Ï‰ : Continuous fun t â†¦ Z n t Ï‰ := by
    refine continuous_iff_continuousAt.mpr fun t â†¦ ?_
    obtain âŸ¨U, hU_mem, hUâŸ© := hZ_holder n Ï‰ t
    have hÎ²_pos_half : 0 < Î² n / 2 := by specialize hÎ²_pos n; positivity
    specialize hU (Î² n / 2) hÎ²_pos_half ?_
    Â· simp [Î², h_ratio_pos]
    Â· obtain âŸ¨_, hâŸ© := hU
      exact (h.continuousOn hÎ²_pos_half).continuousAt hU_mem
  have hZ_ae_eq' n : âˆ€áµ Ï‰ âˆ‚P, âˆ€ t, Z n t Ï‰ = Z 0 t Ï‰ := by
    refine indistinguishable_of_modification (ae_of_all _ (hZ_cont n)) (ae_of_all _ (hZ_cont 0)) ?_
    intro t
    filter_upwards [hZ_ae_eq n t, hZ_ae_eq 0 t] with Ï‰ hÏ‰â‚ hÏ‰â‚‚ using hÏ‰â‚.trans hÏ‰â‚‚.symm
  rw [â† ae_all_iff] at hZ_ae_eq'
  let A := {Ï‰ | âˆ€ n t, Z n t Ï‰ = Z 0 t Ï‰}
  have hA : MeasurableSet A := by
    have : A = â‹‚ n, {Ï‰ | âˆ€ t, Z n t Ï‰ = Z 0 t Ï‰} := by ext; simp [A]
    rw [this]
    refine MeasurableSet.iInter (fun n â†¦ ?_)
    refine Measurable.measurableSet_eq_of_continuous (hZ_cont n) (hZ_cont 0) fun t â†¦ ?_
    refine IsLimitOfIndicator.measurable_edist (hX n).measurable (hX 0).measurable
      (hX 0).measurablePair (hZ_limit n) (hZ_limit 0) t t
  have hA_ae : âˆ€áµ Ï‰ âˆ‚P, Ï‰ âˆˆ A := hZ_ae_eq'
  classical
  let Y (t : T) (Ï‰ : Î©) : E := if Ï‰ âˆˆ A then Z 0 t Ï‰ else Nonempty.some inferInstance
  refine âŸ¨Y, fun t â†¦ Measurable.ite hA (hZ_meas 0 t) (by fun_prop), fun t â†¦ ?_, ?_âŸ©
  Â· filter_upwards [hA_ae, hZ_ae_eq 0 t] with Ï‰ hÏ‰ hÏ‰â‚‚
    simpa only [hÏ‰, â†“reduceIte, Y] using hÏ‰â‚‚
  Â· intro Ï‰ t Î²â‚€ hÎ²â‚€_pos hÎ²â‚€_lt
    by_cases hÏ‰ : Ï‰ âˆˆ A
    swap; Â· exact âŸ¨.univ, by simp [hÏ‰, Y, HolderOnWith]âŸ©
    simp only [hÏ‰, â†“reduceIte, Y]
    obtain âŸ¨n, hnâŸ© : âˆƒ n, Î²â‚€ < Î² n := by
      rwa [lt_ciSup_iff h_bdd] at hÎ²â‚€_lt
    refine âŸ¨(hZ_holder n Ï‰ t).choose, (hZ_holder n Ï‰ t).choose_spec.1, ?_âŸ©
    simp_rw [â† hÏ‰ n]
    exact (hZ_holder n Ï‰ t).choose_spec.2 Î²â‚€ hÎ²â‚€_pos hn

lemma exists_modification_holder_iSup {C : â„• â†’ Set T} {c : â„• â†’ â„â‰¥0âˆ} {p q : â„• â†’ â„} {M : â„• â†’ â„â‰¥0}
    (hC : IsCoverWithBoundedCoveringNumber C (Set.univ : Set T) c (fun _ â†¦ d))
    (hX : âˆ€ n, IsAEKolmogorovProcess X P (p n) (q n) (M n)) (hc : âˆ€ n, c n â‰  âˆ)
    (hd_pos : 0 < d) (hdq_lt : âˆ€ n, d < q n) :
    âˆƒ Y : T â†’ Î© â†’ E, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t, Y t =áµ[P] X t)
      âˆ§ âˆ€ Ï‰ t (Î² : â„â‰¥0) (_ : 0 < Î²) (_ : Î² < â¨† n, (q n - d) / (p n)),
        âˆƒ U âˆˆ ğ“ t, âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U := by
  let X' := (hX 0).mk X
  have hX' : âˆ€ n, IsKolmogorovProcess X' P (p n) (q n) (M n) := fun n â†¦ by
    constructor
    Â· exact fun s t â†¦ (hX 0).IsKolmogorovProcess_mk.measurablePair s t
    Â· intro s t
      have h_le := (hX n).kolmogorovCondition s t
      refine le_trans (le_of_eq ?_) h_le
      refine lintegral_congr_ae ?_
      filter_upwards [(hX 0).ae_eq_mk s, (hX 0).ae_eq_mk t] with Ï‰ hÏ‰1 hÏ‰2 using by rw [hÏ‰1, hÏ‰2]
    Â· exact (hX n).p_pos
    Â· exact (hX n).q_pos
  obtain âŸ¨Y, hY_meas, hY_eq, hY_holderâŸ© :=
    exists_modification_holder_iSup' hC hX' hc hd_pos hdq_lt
  refine âŸ¨Y, hY_meas, fun t â†¦ ?_, hY_holderâŸ©
  filter_upwards [ (hX 0).ae_eq_mk t, hY_eq t] with Ï‰ hÏ‰1 hÏ‰2 using hÏ‰2.trans hÏ‰1.symm

end EMetricSpace

end ProbabilityTheory
