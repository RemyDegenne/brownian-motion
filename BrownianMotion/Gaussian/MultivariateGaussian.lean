/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import BrownianMotion.Gaussian.Gaussian


/-!
# Multivariate Gaussian distributions
-/

open MeasureTheory ProbabilityTheory Filter Matrix NormedSpace
open scoped ENNReal NNReal Topology RealInnerProductSpace

namespace ProbabilityTheory

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E] [FiniteDimensional â„ E]
  [MeasurableSpace E]
  {d : â„•}

variable (E) in
/-- Standard Gaussian distribution on `E`. -/
noncomputable
def stdGaussian : Measure E :=
  (Measure.pi (fun _ : Fin (Module.finrank â„ E) â†¦ gaussianReal 0 1)).map
    (fun x â†¦ âˆ‘ i, x i â€¢ stdOrthonormalBasis â„ E i)

variable [BorelSpace E]

instance isProbabilityMeasure_stdGaussian : IsProbabilityMeasure (stdGaussian E) :=
    isProbabilityMeasure_map (Measurable.aemeasurable (by fun_prop))

-- TODO: generalize to `f` taking values in a Banach space
lemma integrable_eval_pi {Î¹ ğ•œ : Type*} [Fintype Î¹] [NormedCommRing ğ•œ] {X : Î¹ â†’ Type*} {i : Î¹}
    {mX : âˆ€ i, MeasurableSpace (X i)} {Î¼ : (i : Î¹) â†’ Measure (X i)}
    [âˆ€ i, IsFiniteMeasure (Î¼ i)] {f : X i â†’ ğ•œ} (hf : Integrable f (Î¼ i)) :
    Integrable (fun x â†¦ f (x i)) (Measure.pi Î¼) := by
  classical
  let g : Î  i, X i â†’ ğ•œ := fun j â†¦ if h : j = i then h â–¸ f else 1
  have : (fun x â†¦ âˆ j, g j (x j)) = fun (x : Î  i, X i) â†¦ f (x i) := by
    ext x
    rw [show f (x i) = g i (x i) by simp [g]]
    exact Finset.prod_eq_single_of_mem i (by simp) (fun j _ hj â†¦ by simp [g, hj])
  rw [â† this]
  refine Integrable.fintype_prod_dep fun j â†¦ ?_
  by_cases h : j = i
  Â· cases h; simpa [g]
  Â· simpa [g, h] using integrable_const 1

-- TODO: generalize to `f` taking values in a Banach space
lemma integral_eval_pi {Î¹ ğ•œ : Type*} [Fintype Î¹] [RCLike ğ•œ] {X : Î¹ â†’ Type*} {i : Î¹}
    {mX : âˆ€ i, MeasurableSpace (X i)} {Î¼ : (i : Î¹) â†’ Measure (X i)}
    [âˆ€ i, IsProbabilityMeasure (Î¼ i)] {f : X i â†’ ğ•œ} :
    âˆ« (x : Î  i, X i), f (x i) âˆ‚Measure.pi Î¼ = âˆ« x, f x âˆ‚Î¼ i := by
  classical
  let g : Î  i, X i â†’ ğ•œ := fun j â†¦ if h : j = i then h â–¸ f else 1
  have : (fun x â†¦ âˆ j, g j (x j)) = fun (x : Î  i, X i) â†¦ f (x i) := by
    ext x
    rw [show f (x i) = g i (x i) by simp [g]]
    exact Finset.prod_eq_single_of_mem i (by simp) (fun j _ hj â†¦ by simp [g, hj])
  rw [â† this, integral_fintype_prod_eq_prod, show âˆ« x, f x âˆ‚Î¼ i = âˆ« x, g i x âˆ‚Î¼ i by simp [g]]
  exact Finset.prod_eq_single_of_mem i (by simp) (fun j _ hj â†¦ by simp [g, hj])

@[simp]
lemma integral_id_stdGaussian : âˆ« x, x âˆ‚(stdGaussian E) = 0 := by
  rw [stdGaussian, integral_map _ (by fun_prop)]
  swap; Â· exact (Finset.measurable_sum _ (by fun_prop)).aemeasurable -- todo: add fun_prop tag
  rw [integral_finset_sum]
  swap
  Â· refine fun i _ â†¦ Integrable.smul_const ?_ _
    convert integrable_eval_pi (i := i) (f := id) ?_
    Â· infer_instance
    Â· rw [â† memLp_one_iff_integrable]
      exact memLp_id_gaussianReal 1
  refine Finset.sum_eq_zero fun i _ â†¦ ?_
  have : (âˆ« (a : Fin (Module.finrank â„ E) â†’ â„), a i âˆ‚Measure.pi fun x â†¦ gaussianReal 0 1)
      = âˆ« x, x âˆ‚gaussianReal 0 1 := by
    convert integral_eval_pi (i := i)
    Â· rfl
    Â· infer_instance
  simp [integral_smul_const, this]

lemma isCentered_stdGaussian : âˆ€ L : Dual â„ E, (stdGaussian E)[L] = 0 := by
  intro L
  rw [L.integral_comp_id_comm, integral_id_stdGaussian, map_zero]
  rw [stdGaussian, integrable_map_measure]
  Â· rw [Function.id_comp]
    exact integrable_finset_sum _ fun i _ â†¦ Integrable.smul_const
      (integrable_eval_pi (f := id) (IsGaussian.integrable_id _)) _
  Â· exact aestronglyMeasurable_id
  Â· exact Measurable.aemeasurable (by fun_prop)

lemma variance_dual_stdGaussian (L : Dual â„ E) : Var[L; stdGaussian E] = â€–Lâ€– ^ 2 := by
  rw [stdGaussian, variance_map]
  Â· have : L âˆ˜ (fun x : Fin (Module.finrank â„ E) â†’ â„ â†¦ âˆ‘ i, x i â€¢ stdOrthonormalBasis â„ E i) =
        âˆ‘ i, (fun x : Fin (Module.finrank â„ E) â†’ â„ â†¦ L (stdOrthonormalBasis â„ E i) * x i) := by
      ext x; simp [mul_comm]
    rw [this, variance_pi]
    Â· change âˆ‘ i, Var[fun x â†¦ _ * (id x); gaussianReal 0 1] = _
      simp_rw [variance_mul, variance_id_gaussianReal, (stdOrthonormalBasis â„ E).norm_dual]
      simp
    Â· exact fun i â†¦ ((isGaussian_gaussianReal 0 1).memLp_two_id _).const_mul _
  Â· exact L.continuous.aemeasurable
  Â· exact Measurable.aemeasurable (by fun_prop)

lemma charFun_stdGaussian (t : E) : charFun (stdGaussian E) t = Complex.exp (- â€–tâ€– ^ 2 / 2) := by
  rw [charFun_apply, stdGaussian, integral_map]
  Â· simp_rw [sum_inner, Complex.ofReal_sum, Finset.sum_mul, Complex.exp_sum,
      integral_fintype_prod_eq_prod
        (f := fun i x â†¦ Complex.exp (âŸªx â€¢ stdOrthonormalBasis â„ E i, tâŸ« * Complex.I)),
      real_inner_smul_left, mul_comm _ (âŸª_, _âŸ«), Complex.ofReal_mul, â† charFun_apply_real,
      charFun_gaussianReal]
    simp only [Complex.ofReal_zero, mul_zero, zero_mul, NNReal.coe_one, Complex.ofReal_one, one_mul,
      zero_sub]
    simp_rw [â† Complex.exp_sum, Finset.sum_neg_distrib, â† Finset.sum_div, â† Complex.ofReal_pow,
      â† Complex.ofReal_sum, â† (stdOrthonormalBasis â„ E).norm_sq_eq_sum_sq_inner_right, neg_div]
  Â· exact Measurable.aemeasurable (by fun_prop)
  Â· exact Measurable.aestronglyMeasurable (by fun_prop)

instance isGaussian_stdGaussian : IsGaussian (stdGaussian E) := by
  refine isGaussian_iff_gaussian_charFun.2 ?_
  use 0, ContinuousBilinForm.inner E, ContinuousBilinForm.isPosSemidef_inner
  simp [charFun_stdGaussian, real_inner_self_eq_norm_sq, neg_div]

lemma charFunDual_stdGaussian (L : Dual â„ E) :
    charFunDual (stdGaussian E) L = Complex.exp (- â€–Lâ€– ^ 2 / 2) := by
  rw [IsGaussian.charFunDual_eq, integral_complex_ofReal, isCentered_stdGaussian,
    variance_dual_stdGaussian]
  simp [neg_div]

lemma covInnerBilin_stdGaussian :
    covInnerBilin (stdGaussian E) = ContinuousBilinForm.inner E := by
  refine gaussian_charFun_congr 0 _ ContinuousBilinForm.isPosSemidef_inner (fun t â†¦ ?_) |>.2.symm
  simp [charFun_stdGaussian, real_inner_self_eq_norm_sq, neg_div]

lemma covMatrix_stdGaussian : covMatrix (stdGaussian E) = 1 := by
  rw [covMatrix, covInnerBilin_stdGaussian, ContinuousBilinForm.inner_toMatrix_eq_one]

lemma stdGaussian_map {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F] [MeasurableSpace F]
    [BorelSpace F] (f : E â‰ƒâ‚—áµ¢[â„] F) :
    haveI := f.finiteDimensional; (stdGaussian E).map f = stdGaussian F := by
  have := f.finiteDimensional
  apply Measure.ext_of_charFunDual
  ext L
  simp_rw [â† f.coe_coe_eq_coe, charFunDual_map, charFunDual_stdGaussian,
    L.opNorm_comp_linearIsometryEquiv]

lemma pi_eq_stdGaussian {n : Type*} [Fintype n] :
    Measure.pi (fun _ â†¦ gaussianReal 0 1) = stdGaussian (EuclideanSpace â„ n) := by
  -- This instance is not found automatically, probably a defeq issue between
  -- `n â†’ â„` and `EuclideanSpace â„ n`.
  have : IsFiniteMeasure (Measure.pi fun _ : n â†¦ gaussianReal 0 1) := inferInstance
  apply Measure.ext_of_charFun (E := EuclideanSpace â„ n)
  ext t
  simp_rw [charFun_stdGaussian, charFun_pi, charFun_gaussianReal, â† Complex.exp_sum,
    â† Complex.ofReal_pow, EuclideanSpace.real_norm_sq_eq]
  simp [Finset.sum_div, neg_div]

lemma stdGaussian_eq_pi_map_orthonormalBasis {Î¹ : Type*} [Fintype Î¹] (b : OrthonormalBasis Î¹ â„ E) :
    stdGaussian E = (Measure.pi fun _ : Î¹ â†¦ gaussianReal 0 1).map
      (fun x â†¦ âˆ‘ i, x i â€¢ b i) := by
  have : (fun (x : Î¹ â†’ â„) â†¦ âˆ‘ i, x i â€¢ b i) =
      â‡‘((EuclideanSpace.basisFun Î¹ â„).equiv b (Equiv.refl Î¹)) := by
    simp_rw [â† b.equiv_apply_euclideanSpace]
  rw [this, pi_eq_stdGaussian, stdGaussian_map (f := (EuclideanSpace.basisFun Î¹ â„).equiv _ _)]

variable {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹]

noncomputable
def multivariateGaussian (Î¼ : EuclideanSpace â„ Î¹) (S : Matrix Î¹ Î¹ â„)
    (hS : S.PosSemidef) :
    Measure (EuclideanSpace â„ Î¹) :=
  (stdGaussian (EuclideanSpace â„ Î¹)).map (fun x â†¦ Î¼ + toEuclideanCLM (ğ•œ := â„) hS.sqrt x)

variable {Î¼ : EuclideanSpace â„ Î¹} {S : Matrix Î¹ Î¹ â„} {hS : S.PosSemidef}

instance isGaussian_multivariateGaussian : IsGaussian (multivariateGaussian Î¼ S hS) := by
  have h : (fun x â†¦ Î¼ + x) âˆ˜ ((toEuclideanCLM (ğ•œ := â„) hS.sqrt)) =
    (fun x â†¦ Î¼ + (toEuclideanCLM (ğ•œ := â„) hS.sqrt) x) := rfl
  simp only [multivariateGaussian]
  rw [â† h, â† Measure.map_map (measurable_const_add Î¼) (by measurability)]
  infer_instance

@[simp]
lemma integral_id_multivariateGaussian : âˆ« x, x âˆ‚(multivariateGaussian Î¼ S hS) = Î¼ := by
  rw [multivariateGaussian, integral_map (by fun_prop) (by fun_prop),
    integral_add (integrable_const _), integral_const]
  Â· simp [ContinuousLinearMap.integral_comp_comm _ (IsGaussian.integrable_fun_id _)]
  Â· have h_id : Integrable id ((stdGaussian (EuclideanSpace â„ Î¹)).map
      (toEuclideanCLM (ğ•œ := â„) hS.sqrt)) := IsGaussian.integrable_id _
    exact h_id.comp_measurable (by fun_prop)

lemma inner_toEuclideanCLM (x y : EuclideanSpace â„ Î¹) :
    âŸªx, toEuclideanCLM (ğ•œ := â„) S yâŸ«
      = (EuclideanSpace.basisFun Î¹ â„).toBasis.repr x â¬áµ¥ S
        *áµ¥ (EuclideanSpace.basisFun Î¹ â„).toBasis.repr y := by
  simp only [toEuclideanCLM, AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, LinearEquiv.coe_coe,
    LinearEquiv.invFun_eq_symm, LinearMap.coe_toContinuousLinearMap_symm, StarAlgEquiv.trans_apply,
    LinearMap.toMatrixOrthonormal_symm_apply, LinearMap.toMatrix_symm, StarAlgEquiv.coe_mk,
    RingEquiv.coe_mk, Equiv.coe_fn_mk, LinearMap.coe_toContinuousLinearMap', toLin_apply,
    mulVec_eq_sum, OrthonormalBasis.coe_toBasis_repr_apply, EuclideanSpace.basisFun_repr,
    op_smul_eq_smul, Finset.sum_apply, Pi.smul_apply, transpose_apply, smul_eq_mul,
    OrthonormalBasis.coe_toBasis, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    RCLike.inner_apply, conj_trivial, dotProduct]
  congr with i
  rw [mul_comm]
  congr
  rw [Finset.sum_apply]
  simp

lemma covInnerBilin_multivariateGaussian :
    covInnerBilin (multivariateGaussian Î¼ S hS)
      = ContinuousBilinForm.ofMatrix S (EuclideanSpace.basisFun Î¹ â„).toBasis := by
  have h : (fun x â†¦ Î¼ + x) âˆ˜ ((toEuclideanCLM (ğ•œ := â„) hS.sqrt)) =
    (fun x â†¦ Î¼ + (toEuclideanCLM (ğ•œ := â„) hS.sqrt) x) := rfl
  simp only [multivariateGaussian]
  rw [â† h, â† Measure.map_map (measurable_const_add Î¼) (by fun_prop)]
  rw [covInnerBilin_map_const_add]
  swap; Â· exact IsGaussian.memLp_two_id _
  ext x y
  rw [covInnerBilin_map, covInnerBilin_stdGaussian]
  swap; Â· exact IsGaussian.memLp_two_id _
  rw [ContinuousBilinForm.inner_apply, ContinuousBilinForm.ofMatrix_apply,
    ContinuousLinearMap.adjoint_inner_left]
  rw [IsSelfAdjoint.adjoint_eq]
  swap
  Â· unfold _root_.IsSelfAdjoint
    rw [â† map_star, EmbeddingLike.apply_eq_iff_eq]
    exact hS.posSemidef_sqrt.isHermitian
  calc âŸªx, (toEuclideanCLM (ğ•œ := â„) hS.sqrt) (toEuclideanCLM (ğ•œ := â„) hS.sqrt y)âŸ«
  _ = âŸªx, toEuclideanCLM (ğ•œ := â„) S yâŸ« := by
    congr 1
    have : (toEuclideanCLM (ğ•œ := â„) hS.sqrt).comp (toEuclideanCLM (ğ•œ := â„) hS.sqrt)
        = toEuclideanCLM (ğ•œ := â„) (hS.sqrt * hS.sqrt) := by
      rw [map_mul]
      rfl
    rw [PosSemidef.sqrt_mul_self, ContinuousLinearMap.ext_iff] at this
    rw [â† this y]
    simp
  _ = ((EuclideanSpace.basisFun Î¹ â„).toBasis.repr x) â¬áµ¥
      S *áµ¥ ((EuclideanSpace.basisFun Î¹ â„).toBasis.repr y) := inner_toEuclideanCLM _ _

lemma charFun_multivariateGaussian (x : EuclideanSpace â„ Î¹) :
    charFun (multivariateGaussian Î¼ S hS) x =
      Complex.exp (âŸªx, Î¼âŸ« * Complex.I
        - ContinuousBilinForm.ofMatrix S (EuclideanSpace.basisFun Î¹ â„).toBasis x x / 2) := by
  rw [IsGaussian.charFun_eq]
  congr
  Â· exact integral_id_multivariateGaussian
  Â· exact covInnerBilin_multivariateGaussian

end ProbabilityTheory
