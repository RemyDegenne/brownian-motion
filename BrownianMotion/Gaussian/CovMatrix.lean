/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import BrownianMotion.Gaussian.CovarianceBilin

/-!
# Covariance matrix

-/

open MeasureTheory InnerProductSpace NormedSpace
open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
  [MeasurableSpace E] [BorelSpace E] {Î¼ : Measure E}

/-- Covariance of a measure on an inner product space, as a continuous bilinear form. -/
noncomputable
def covInnerBilin (Î¼ : Measure E) : E â†’L[â„] E â†’L[â„] â„ :=
  ContinuousLinearMap.bilinearComp (covarianceBilin Î¼)
    (toDualMap â„ E).toContinuousLinearMap (toDualMap â„ E).toContinuousLinearMap

lemma covInnerBilin_eq_covarianceBilin (x y : E) :
    covInnerBilin Î¼ x y = covarianceBilin Î¼ (toDualMap â„ E x) (toDualMap â„ E y) := rfl

lemma covInnerBilin_apply [CompleteSpace E] [IsFiniteMeasure Î¼] (h : MemLp id 2 Î¼) (x y : E) :
    covInnerBilin Î¼ x y = âˆ« z, âŸªx, z - Î¼[id]âŸ«_â„ * âŸªy, z - Î¼[id]âŸ«_â„ âˆ‚Î¼ := by
  simp_rw [covInnerBilin, ContinuousLinearMap.bilinearComp_apply, covarianceBilin_apply' h]
  simp only [LinearIsometry.coe_toContinuousLinearMap, id_eq, toDualMap_apply]

nonrec
lemma IsGaussian.covInnerBilin_apply [IsGaussian Î¼] [SecondCountableTopology E] [CompleteSpace E]
    (x y : E) :
    covInnerBilin Î¼ x y = âˆ« z, âŸªx, z - Î¼[id]âŸ«_â„ * âŸªy, z - Î¼[id]âŸ«_â„ âˆ‚Î¼ :=
  covInnerBilin_apply (IsGaussian.memLp_id Î¼ 2 (by simp)) x y

lemma covInnerBilin_comm [IsFiniteMeasure Î¼] (h : MemLp id 2 Î¼) (x y : E) :
    covInnerBilin Î¼ x y = covInnerBilin Î¼ y x := by
  rw [covInnerBilin_eq_covarianceBilin, covarianceBilin_comm h, covInnerBilin_eq_covarianceBilin]

lemma covInnerBilin_self [CompleteSpace E] [IsFiniteMeasure Î¼] (h : MemLp id 2 Î¼) (x : E) :
    covInnerBilin Î¼ x x = Var[fun u â†¦ âŸªx, uâŸ«_â„; Î¼] := by
  rw [covInnerBilin_eq_covarianceBilin, covarianceBilin_same_eq_variance h]
  congr

lemma covInnerBilin_self_nonneg [CompleteSpace E] [IsFiniteMeasure Î¼] (h : MemLp id 2 Î¼) (x : E) :
    0 â‰¤ covInnerBilin Î¼ x x := by
  rw [covInnerBilin_self h]
  exact variance_nonneg _ Î¼

variable [FiniteDimensional â„ E]

lemma covInnerBilin_map {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F]
    [MeasurableSpace F] [BorelSpace F] [CompleteSpace E] [CompleteSpace F] [IsFiniteMeasure Î¼]
    (h : MemLp id 2 Î¼) (L : E â†’L[â„] F) (u v : F) :
    covInnerBilin (Î¼.map L) u v = covInnerBilin Î¼ (L.adjoint u) (L.adjoint v) := by
  sorry

/-- Covariance matrix of a measure on a finite dimensional inner product space. -/
noncomputable
def covMatrix (Î¼ : Measure E) : Matrix (Fin (Module.finrank â„ E)) (Fin (Module.finrank â„ E)) â„ :=
  Matrix.of fun i j â†¦ covInnerBilin Î¼ (stdOrthonormalBasis â„ E i) (stdOrthonormalBasis â„ E j)

lemma covMatrix_mulVec (x : Fin (Module.finrank â„ E) â†’ â„) :
    (covMatrix Î¼).mulVec x = fun i â†¦
      covInnerBilin Î¼ (stdOrthonormalBasis â„ E i) (âˆ‘ j, x j â€¢ stdOrthonormalBasis â„ E j) := by
  ext
  simp [covMatrix, Matrix.mulVec_eq_sum]

lemma dotProduct_covMatrix_mulVec (x y : Fin (Module.finrank â„ E) â†’ â„) :
    x â¬áµ¥ (covMatrix Î¼).mulVec y =
      covInnerBilin Î¼ (âˆ‘ j, x j â€¢ stdOrthonormalBasis â„ E j)
        (âˆ‘ j, y j â€¢ stdOrthonormalBasis â„ E j) := by
  rw [covMatrix_mulVec, dotProduct]
  simp only [map_sum, map_smul, smul_eq_mul, Finset.mul_sum, ContinuousLinearMap.coe_sum',
    ContinuousLinearMap.coe_smul', Finset.sum_apply, Pi.smul_apply]
  rw [Finset.sum_comm]
  congr with i
  congr with j
  simp_rw [â† mul_assoc]
  rw [mul_comm (x j)]

section toBilin

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  (f : E â†’L[ğ•œ] E â†’L[ğ•œ] ğ•œ)

def _root_.ContinuousLinearMap.toBilin : LinearMap.BilinForm ğ•œ E where
  toFun x := (f x).toLinearMap
  map_add' x y := by simp
  map_smul' m x := by simp

@[simp]
lemma _root_.ContinuousLinearMap.toBilin_apply (x y : E) : f.toBilin x y = f x y := rfl

lemma _root_.ContinuousLinearMap.toBilin_apply' (x : E) : f.toBilin x = (f x).toLinearMap := rfl

end toBilin

lemma covMatrix_toBilin :
    (covMatrix Î¼).toBilin (stdOrthonormalBasis â„ E).toBasis = (covInnerBilin Î¼).toBilin := by
  apply LinearMap.BilinForm.ext_basis (stdOrthonormalBasis â„ E).toBasis
  simp [covMatrix]

lemma _root_.OrthonormalBasis.inner_eq_ite {Î¹ ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] [Fintype Î¹] [DecidableEq Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) {i j : Î¹} :
    âŸªb i, b jâŸ«_ğ•œ = if i = j then 1 else 0 := by
  split_ifs with h
  Â· simp [h, inner_self_eq_norm_sq_to_K]
  Â· simp [h]

lemma _root_.OrthonormalBasis.toBilin_apply_eq_dotProduct {n E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace â„ E] [Fintype n] [DecidableEq n] (b : OrthonormalBasis n â„ E) (x y : E)
    (A : Matrix n n â„) : A.toBilin b.toBasis x y = âŸªx, A.toLin b.toBasis b.toBasis yâŸ«_â„ := by
  let f : LinearMap.BilinForm â„ E :=
    { toFun x :=
        { toFun y := âŸªx, A.toLin b.toBasis b.toBasis yâŸ«_â„
          map_add' y z := by simp [inner_add_right]
          map_smul' m y := by simp [inner_smul_right] }
      map_add' x y := by
        ext z
        simp [inner_add_left]
      map_smul' m x := by
        ext z
        simp [inner_smul_left] }
  change _ = f x y
  revert x y
  refine LinearMap.BilinForm.ext_iff.1 <| LinearMap.BilinForm.ext_basis b.toBasis ?_
  simp [f, Matrix.toLin_apply, Matrix.mulVec, dotProduct, inner_sum, inner_smul_right,
    OrthonormalBasis.inner_eq_ite]

lemma covMatrix_map {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F]
    [MeasurableSpace F] [BorelSpace F] [CompleteSpace E] [CompleteSpace F] [FiniteDimensional â„ F]
    [IsFiniteMeasure Î¼] (h : MemLp id 2 Î¼) (L : E â†’L[â„] F) (i j : Fin (Module.finrank â„ F)) :
    covMatrix (Î¼.map L) i j =
    âŸª(L.adjoint (stdOrthonormalBasis â„ F i)),
    (covMatrix Î¼).toLin (stdOrthonormalBasis â„ E).toBasis (stdOrthonormalBasis â„ E).toBasis
    (L.adjoint (stdOrthonormalBasis â„ F j))âŸ«_â„ := by
  rw [â† OrthonormalBasis.toBilin_apply_eq_dotProduct, covMatrix_toBilin,
    ContinuousLinearMap.toBilin_apply, covMatrix, Matrix.of_apply, covInnerBilin_map h]

lemma posSemidef_covMatrix [IsGaussian Î¼] : (covMatrix Î¼).PosSemidef := by
  constructor
  Â· simp only [Matrix.IsHermitian, covMatrix, Matrix.conjTranspose_eq_transpose_of_trivial]
    ext i j
    simp only [Matrix.transpose_apply, Matrix.of_apply]
    rw [covInnerBilin_comm]
    exact IsGaussian.memLp_id Î¼ 2 (by simp)
  Â· intro x
    rw [star_trivial, dotProduct_covMatrix_mulVec]
    exact covInnerBilin_self_nonneg (IsGaussian.memLp_id Î¼ 2 (by simp)) _

end ProbabilityTheory
