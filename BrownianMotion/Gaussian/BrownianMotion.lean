/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import BrownianMotion.Auxiliary.HasGaussianLaw
import BrownianMotion.Continuity.KolmogorovChentsov
import BrownianMotion.Gaussian.GaussianProcess
import BrownianMotion.Gaussian.Moment
import BrownianMotion.Gaussian.ProjectiveLimit
import Mathlib.Topology.ContinuousMap.SecondCountableSpace

/-!
# Brownian motion

-/

open MeasureTheory NNReal WithLp Finset
open scoped ENNReal NNReal Topology

variable {T Î© E : Type*} {mÎ© : MeasurableSpace Î©} {P : Measure Î©}

namespace Finset

/-! ### Indexing the elements of a finset in order -/

variable [LinearOrder T] (I : Finset T)

/-- Given a finite set `I : Finset T` of cardinality `n`, `ofFin : Fin #I â†’ T`
is the map `(tâ‚, ..., tâ‚™)`, where `tâ‚ < ... < tâ‚™` are the elements of `I`. -/
noncomputable def ofFin (i : Fin #I) : T := (I.sort (Â· â‰¤ Â·)).get
  (Fin.cast (I.length_sort (Â· â‰¤ Â·)).symm i)

lemma monotone_ofFin : Monotone I.ofFin :=
  fun i j hij â†¦ (I.sort_sorted (Â· â‰¤ Â·)).rel_get_of_le (by simpa)

lemma ofFin_mem (i : Fin #I) : I.ofFin i âˆˆ I := I.mem_sort (Â· â‰¤ Â·) |>.1 <| List.get_mem _ _

/-- Given a finite set `I : Finset T`, and `t : I`,
`I.toFin t` returns the position of `t` in `I`. -/
noncomputable def toFin (i : I) : Fin #I :=
  haveI : NeZero #I := âŸ¨Nonempty.card_ne_zero âŸ¨i.1, i.2âŸ©âŸ©
  Fin.ofNat #I ((I.sort (Â· â‰¤ Â·)).idxOf i.1)

@[simp]
lemma ofFin_toFin (i : I) : I.ofFin (I.toFin i) = i.1 := by
  rw [toFin, ofFin]
  nth_rw 2 [â† (I.sort (Â· â‰¤ Â·)).idxOf_get (a := i.1)]
  Â· congr
    ext
    simp only [Fin.ofNat_eq_cast, Fin.coe_cast, Fin.val_natCast]
    rw [Nat.mod_eq_of_lt]
    rw [â† I.length_sort (Â· â‰¤ Â·)]
    exact List.idxOf_lt_length_of_mem <| I.mem_sort (Â· â‰¤ Â·) |>.2 i.2
  exact List.idxOf_lt_length_of_mem <| I.mem_sort (Â· â‰¤ Â·) |>.2 i.2

@[simp]
lemma toFin_ofFin (i : Fin #I) : I.toFin âŸ¨I.ofFin i, ofFin_mem I iâŸ© = i := by
  simp_rw [toFin, ofFin]
  rw [List.get_idxOf (sort_nodup ..)]
  simp

section Bot

variable [Bot T]

/-- Given a finite set `I : Finset T` of cardinality `n`, `ofFin : Fin #I â†’ T`
is the map `(âŠ¥, tâ‚, ..., tâ‚™)`, where `tâ‚ < ... < tâ‚™` are the elements of `I`. -/
noncomputable def ofFin' (i : Fin (#I + 1)) : T :=
  if h : i = 0
    then âŠ¥
    else I.ofFin (i.pred h)

@[simp]
lemma ofFin'_zero : I.ofFin' 0 = âŠ¥ := rfl

lemma ofFin'_of_ne_zero (i : Fin (#I + 1)) (hi : i â‰  0) :
    I.ofFin' i = I.ofFin (i.pred hi) := by rw [ofFin', dif_neg hi]

@[simp]
lemma ofFin'_succ (i : Fin #I) :
    I.ofFin' i.succ = I.ofFin i := by
  rw [ofFin'_of_ne_zero, Fin.pred_succ]
  simp

lemma ofFin'_mem (i : Fin (#I + 1)) (hi : i â‰  0) : I.ofFin' i âˆˆ I := by
  rw [ofFin'_of_ne_zero _ _ hi]
  exact ofFin_mem I _

end Bot

lemma monotone_ofFin' [OrderBot T] : Monotone (I.ofFin') := by
  intro i j hij
  obtain rfl | hi := eq_or_ne i 0
  Â· simp
  rw [ofFin'_of_ne_zero I i hi, ofFin'_of_ne_zero I j (by aesop)]
  apply monotone_ofFin
  simpa

end Finset

namespace ProbabilityTheory

section Increments

/-! ### Independent increments -/

/-- A process `X : T â†’ Î© â†’ E` has independent increments if for any `n â‰¥ 1` and `tâ‚ â‰¤ ... â‰¤ tâ‚™`,
the random variables `X tâ‚‚ - X tâ‚, ..., X tâ‚™ - X tâ‚™â‚‹â‚` are independent. -/
def HasIndepIncrements [Preorder T] [Sub E] [MeasurableSpace E] (X : T â†’ Î© â†’ E) (P : Measure Î©) :
    Prop :=
  âˆ€ n, âˆ€ t : Fin (n + 1) â†’ T, Monotone t â†’
    iIndepFun (fun (i : Fin n) Ï‰ â†¦ X (t i.succ) Ï‰ - X (t i.castSucc) Ï‰) P

/-- `incrementsToRestrict I` is a continuous linear map `f` such that
`f (xâ‚œâ‚, xâ‚œâ‚‚ - xâ‚œâ‚, ..., xâ‚œâ‚™ - xâ‚œâ‚™â‚‹â‚) = (xâ‚œâ‚, ..., xâ‚œâ‚™)`. -/
noncomputable def incrementsToRestrict [LinearOrder T] (R : Type*) [Semiring R] [AddCommMonoid E]
    [Module R E] [TopologicalSpace E] [ContinuousAdd E] (I : Finset T) :
    (Fin #I â†’ E) â†’L[R] (I â†’ E) :=
  { toFun x i := âˆ‘ j â‰¤ I.toFin i, x j
    map_add' x y := by ext; simp [sum_add_distrib]
    map_smul' m x := by ext; simp [smul_sum]
    cont := by fun_prop }

lemma incrementsToRestrict_increments_ofFin'_aeeq_restrict [LinearOrder T] (R : Type*) [OrderBot T]
    [Semiring R] [AddCommGroup E] [Module R E] [TopologicalSpace E] [ContinuousAdd E]
    {X : T â†’ Î© â†’ E} (h : âˆ€áµ Ï‰ âˆ‚P, X âŠ¥ Ï‰ = 0) (I : Finset T) :
    (fun Ï‰ â†¦ I.restrict (X Â· Ï‰)) =áµ[P]
      (incrementsToRestrict R I) âˆ˜
        (fun Ï‰ i â†¦ X (I.ofFin' i.succ) Ï‰ - X (I.ofFin' i.castSucc) Ï‰) := by
  filter_upwards [h] with Ï‰ hÏ‰
  ext t
  simp only [restrict, incrementsToRestrict, ContinuousLinearMap.coe_mk', LinearMap.coe_mk,
    AddHom.coe_mk, Function.comp_apply]
  rw [Fin.sum_Iic_sub (I.toFin t) (fun j â†¦ X (I.ofFin' j) Ï‰)]
  simp [hÏ‰]

lemma HasIndepIncrements.indepFun_sub_sub [Preorder T] [MeasurableSpace E] [AddGroup E]
    {X : T â†’ Î© â†’ E} (h : HasIndepIncrements X P) {r s t : T} (hrs : r â‰¤ s) (hst : s â‰¤ t) :
    IndepFun (X s - X r) (X t - X s) P := by
  let Ï„ : Fin (2 + 1) â†’ T := ![r, s, t]
  have hÏ„ : Monotone Ï„ := by
    intro i j hij
    fin_cases i <;> fin_cases j
    any_goals simp only [Nat.reduceAdd, Fin.zero_eta, Fin.isValue, Matrix.cons_val_zero, le_refl, Ï„]
    any_goals assumption
    any_goals contradiction
    exact hrs.trans hst
  have h' : (0 : Fin (1 + 1)) â‰  (1 : Fin (1 + 1)) := by simp
  simpa using (h 2 Ï„ hÏ„).indepFun h'

lemma HasIndepIncrements.indepFun_eval_sub [Preorder T] [MeasurableSpace E] [AddGroup E]
    {X : T â†’ Î© â†’ E} (h : HasIndepIncrements X P) {r s t : T} (hrs : r â‰¤ s) (hst : s â‰¤ t)
    (hX : âˆ€áµ Ï‰ âˆ‚P, X r Ï‰ = 0) :
    IndepFun (X s) (X t - X s) P := by
  have := h.indepFun_sub_sub hrs hst
  refine this.congr ?_ .rfl
  filter_upwards [hX] with Ï‰ hÏ‰ using by simp [hÏ‰]

lemma HasIndepIncrements.isGaussianProcess_restrict [LinearOrder T] [OrderBot T]
    [NormedAddCommGroup E] [NormedSpace â„ E] [MeasurableSpace E] [BorelSpace E]
    [SecondCountableTopology E] [CompleteSpace E]
    {X : T â†’ Î© â†’ E} (law : âˆ€ t, HasGaussianLaw (X t) P) (h_bot : âˆ€áµ Ï‰ âˆ‚P, X âŠ¥ Ï‰ = 0)
    (incr : HasIndepIncrements X P) :
    IsGaussianProcess X P where
  hasGaussianLaw I := by
    have := (law âŠ¥).isProbabilityMeasure
    obtain rfl | hI := I.eq_empty_or_nonempty
    Â· constructor
      have : P.map (fun Ï‰ â†¦ Finset.restrict âˆ… fun x â†¦ X x Ï‰) = .dirac Classical.ofNonempty := by
        ext s -
        apply Subsingleton.set_cases (p := fun s â†¦ Measure.map _ _ s = _)
        Â· simp
        simp only [measure_univ]
        exact @measure_univ _ _ _
          (Measure.isProbabilityMeasure_map (aemeasurable_pi_lambda _ fun _ â†¦ (law _).aemeasurable))
      rw [this]
      infer_instance
    have := incrementsToRestrict_increments_ofFin'_aeeq_restrict â„ h_bot I
    refine @HasGaussianLaw.congr _ _ _ _ _ _ _ _ _ _ ?_ this.symm
    refine @HasGaussianLaw.map _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ ?_
    exact (incr _ _ (monotone_ofFin' I)).hasGaussianLaw fun i â†¦
      incr.indepFun_eval_sub bot_le
        (monotone_ofFin' I (Fin.castSucc_le_succ i)) h_bot |>.hasGaussianLaw_sub (law _) (law _)

end Increments

section IsPreBrownian

variable (X : â„â‰¥0 â†’ Î© â†’ â„)

/-- A stochastic process is called **pre-Brownian** if its finite-dimensional laws are those
of a Brownian motion, see `gaussianProjectiveFamily`. -/
class IsPreBrownian (P : Measure Î© := by volume_tac) : Prop where
  hasLaw : âˆ€ I : Finset â„â‰¥0, HasLaw (fun Ï‰ â†¦ I.restrict (X Â· Ï‰)) (gaussianProjectiveFamily I) P

variable {X} {P : Measure Î©}

lemma IsPreBrownian.congr {Y : â„â‰¥0 â†’ Î© â†’ â„} [hX : IsPreBrownian X P] (h : âˆ€ t, X t =áµ[P] Y t) :
    IsPreBrownian Y P where
  hasLaw I := by
    refine (hX.hasLaw I).congr ?_
    have : âˆ€áµ Ï‰ âˆ‚P, âˆ€ i : I, X i Ï‰ = Y i Ï‰ := ae_all_iff.2 fun _ â†¦ h _
    filter_upwards [this] with Ï‰ hÏ‰ using funext fun i â†¦ (hÏ‰ i).symm

instance IsPreBrownian.isGaussianProcess [IsPreBrownian X P] : IsGaussianProcess X P where
  hasGaussianLaw I := (IsPreBrownian.hasLaw I).hasGaussianLaw

lemma IsPreBrownian.aemeasurable [IsPreBrownian X P] (t : â„â‰¥0) : AEMeasurable (X t) P :=
  HasGaussianLaw.aemeasurable

lemma IsPreBrownian.hasLaw_gaussianLimit [IsPreBrownian X P]
    (hX : AEMeasurable (fun Ï‰ â†¦ (X Â· Ï‰)) P) :
    HasLaw (fun Ï‰ â†¦ (X Â· Ï‰)) gaussianLimit P where
  aemeasurable := hX
  map_eq := by
    refine isProjectiveLimit_gaussianLimit.unique (fun I â†¦ ?_) |>.symm
    rw [AEMeasurable.map_map_of_aemeasurable (by fun_prop) hX]
    exact (IsPreBrownian.hasLaw I).map_eq

lemma HasLaw.isPreBrownian (hX : HasLaw (fun Ï‰ â†¦ (X Â· Ï‰)) gaussianLimit P) :
    IsPreBrownian X P where
  hasLaw _ := hasLaw_restrict_gaussianLimit.comp hX

lemma IsPreBrownian.hasLaw_eval [h : IsPreBrownian X P] (t : â„â‰¥0) :
    HasLaw (X t) (gaussianReal 0 t) P :=
  (hasLaw_eval_gaussianProjectiveFamily âŸ¨t, by simpâŸ©).comp (h.hasLaw {t})

lemma IsPreBrownian.hasLaw_sub [IsPreBrownian X P] (s t : â„â‰¥0) :
    HasLaw (X s - X t) (gaussianReal 0 (max (s - t) (t - s))) P :=
  (hasLaw_eval_sub_eval_gaussianProjectiveFamily {s, t} âŸ¨s, by simpâŸ© âŸ¨t, by simpâŸ©).comp
    (IsPreBrownian.hasLaw _)

lemma IsPreBrownian.integral_eval [h : IsPreBrownian X P] (t : â„â‰¥0) :
    P[X t] = 0 := by
  rw [(h.hasLaw_eval t).integral_eq, integral_id_gaussianReal]

lemma IsPreBrownian.covariance_eval [h : IsPreBrownian X P] (s t : â„â‰¥0) :
    cov[X s, X t; P] = min s t := by
  convert (h.hasLaw {s, t}).covariance_comp
    (f := Function.eval âŸ¨s, by simpâŸ©) (g := Function.eval âŸ¨t, by simpâŸ©) ?_ ?_
  Â· rw [covariance_eval_gaussianProjectiveFamily]
  all_goals exact Measurable.aemeasurable (by fun_prop)

lemma IsPreBrownian.covariance_fun_eval [h : IsPreBrownian X P] (s t : â„â‰¥0) :
    cov[fun Ï‰ â†¦ X s Ï‰, fun Ï‰ â†¦ X t Ï‰; P] = min s t :=
  h.covariance_eval s t

section dirac

lemma HasLaw.aeeq_of_dirac' {ğ“§ : Type*} {mğ“§ : MeasurableSpace ğ“§} [MeasurableSingletonClass ğ“§]
    {x : ğ“§} {X : Î© â†’ ğ“§}
    (hX : HasLaw X (.dirac x) P) : X =áµ[P] (fun _ â†¦ x) := by
  apply ae_of_ae_map (p := fun y â†¦ y = x) hX.aemeasurable
  rw [hX.map_eq, ae_dirac_iff]
  simp

lemma HasLaw.aeeq_of_dirac {ğ“§ : Type*} {mğ“§ : MeasurableSpace ğ“§} [MeasurableSingletonClass ğ“§]
    {x : ğ“§} {X : Î© â†’ ğ“§}
    (hX : HasLaw X (.dirac x) P) : âˆ€áµ Ï‰ âˆ‚P, X Ï‰ = x := hX.aeeq_of_dirac'

lemma HasLaw.aeeq_const {X : Î© â†’ â„} {Î¼ : â„} (hX : HasLaw X (gaussianReal Î¼ 0) P) :
    âˆ€áµ Ï‰ âˆ‚P, X Ï‰ = Î¼ := by
  rw [gaussianReal_zero_var] at hX
  exact hX.aeeq_of_dirac

end dirac

lemma IsPreBrownian.hasIndepIncrements [h : IsPreBrownian X P] : HasIndepIncrements X P := by
  have : IsProbabilityMeasure P := h.isGaussianProcess.isProbabilityMeasure
  refine fun n t ht â†¦ HasGaussianLaw.iIndepFun_of_covariance_eq_zero fun i j hij â†¦ ?_
  rw [covariance_fun_sub_left, covariance_fun_sub_right, covariance_fun_sub_right]
  Â· simp_rw [IsPreBrownian.covariance_fun_eval]
    wlog h' : i < j generalizing i j
    Â· simp_rw [â† this j i hij.symm (by omega), min_comm]
      ring
    have h1 : i.succ â‰¤ j.succ := Fin.strictMono_succ h' |>.le
    have h2 : i.castSucc â‰¤ j.succ := Fin.le_of_lt h1
    have h3 : i.castSucc â‰¤ j.castSucc := Fin.le_castSucc_iff.mpr h1
    rw [min_eq_left (ht h1), min_eq_left (ht h'), min_eq_left (ht h2), min_eq_left (ht h3)]
    simp
  all_goals exact HasGaussianLaw.memLp_two

lemma isPreBrownian_of (h1 : IsGaussianProcess X P) (h2 : âˆ€ t, P[X t] = 0)
    (h3 : âˆ€ s t, cov[X s, X t; P] = min s t) :
    IsPreBrownian X P where
  hasLaw I := by
    refine âŸ¨aemeasurable_pi_lambda _ fun _ â†¦ h1.aemeasurable _, ?_âŸ©
    apply (MeasurableEquiv.toLp 2 (_ â†’ â„)).map_measurableEquiv_injective
    rw [MeasurableEquiv.coe_toLp, â† PiLp.continuousLinearEquiv_symm_apply 2 â„]
    apply IsGaussian.ext
    Â· rw [integral_map, integral_map, integral_map]
      Â· simp only [PiLp.continuousLinearEquiv_symm_apply, id_eq]
        simp_rw [â† PiLp.continuousLinearEquiv_symm_apply 2 â„, â† ContinuousLinearEquiv.coe_coe]
        rw [ContinuousLinearMap.integral_comp_id_comm, integral_id_gaussianProjectiveFamily,
          ContinuousLinearMap.integral_comp_comm]
        Â· simp only [ContinuousLinearEquiv.coe_coe, PiLp.continuousLinearEquiv_symm_apply]
          congr with i
          rw [eval_integral]
          Â· simpa using h2 _
          Â· exact fun _ â†¦ h1.hasGaussianLaw_eval.integrable
        Â· exact Integrable.of_eval fun _ â†¦ h1.hasGaussianLaw_eval.integrable
        Â· exact IsGaussian.integrable_id
      any_goals fun_prop
      exact aemeasurable_pi_lambda _ fun _ â†¦ h1.aemeasurable _
    Â· refine ContinuousBilinForm.ext_of_isSymm (isPosSemidef_covInnerBilin ?_).isSymm
        (isPosSemidef_covInnerBilin ?_).isSymm fun x â†¦ ?_
      any_goals exact IsGaussian.memLp_two_id
      rw [PiLp.continuousLinearEquiv_symm_apply, covInnerBilin_apply_pi, covInnerBilin_apply_pi]
      Â· congrm âˆ‘ i, âˆ‘ j, _ * ?_
        rw [covariance_eval_gaussianProjectiveFamily, covariance_map]
        Â· exact h3 _ _
        any_goals exact Measurable.aestronglyMeasurable (by fun_prop)
        Â· exact aemeasurable_pi_lambda _ (fun _ â†¦ h1.aemeasurable _)
      all_goals exact fun _ â†¦ HasGaussianLaw.memLp_two

lemma isPreBrownian_of_hasLaw_of_hasIndepIncrements
    (law : âˆ€ t, HasLaw (X t) (gaussianReal 0 t) P) (incr : HasIndepIncrements X P) :
    IsPreBrownian X P := by
  apply isPreBrownian_of
  Â· exact incr.isGaussianProcess_restrict (fun t â†¦ (law t).hasGaussianLaw) (law 0).aeeq_const
  Â· intro t
    rw [(law t).integral_eq, integral_id_gaussianReal]
  Â· intro s t
    wlog hst : s â‰¤ t generalizing s t
    Â· rw [min_comm, covariance_comm, this t s (by grind)]
    have aeeq : âˆ€áµ Ï‰ âˆ‚P, X 0 Ï‰ = 0 := by
      apply HasLaw.aeeq_of_dirac
      rw [â† gaussianReal_zero_var]
      exact law 0
    have h1 := incr.indepFun_eval_sub (zero_le s) hst aeeq
    have := (law 0).isProbabilityMeasure_iff.1 inferInstance
    have h2 : X t = X t - X s + X s := by simp
    rw [h2, covariance_add_right, h1.covariance_eq_zero, covariance_self, (law s).variance_eq,
      variance_id_gaussianReal]
    Â· simpa
    Â· exact (law s).aemeasurable
    Â· exact (law s).hasGaussianLaw.memLp_two
    Â· exact (law t).hasGaussianLaw.memLp_two.sub (law s).hasGaussianLaw.memLp_two
    Â· exact (law s).hasGaussianLaw.memLp_two
    Â· exact (law t).hasGaussianLaw.memLp_two.sub (law s).hasGaussianLaw.memLp_two
    Â· exact (law s).hasGaussianLaw.memLp_two

end IsPreBrownian

def preBrownian : â„â‰¥0 â†’ (â„â‰¥0 â†’ â„) â†’ â„ := fun t Ï‰ â†¦ Ï‰ t

@[fun_prop]
lemma measurable_preBrownian (t : â„â‰¥0) : Measurable (preBrownian t) := by
  unfold preBrownian
  fun_prop

lemma hasLaw_preBrownian : HasLaw (fun Ï‰ â†¦ (preBrownian Â· Ï‰)) gaussianLimit gaussianLimit where
  aemeasurable := (measurable_pi_lambda _ measurable_preBrownian).aemeasurable
  map_eq := Measure.map_id

instance isPreBrownian_preBrownian : IsPreBrownian (preBrownian) gaussianLimit :=
  hasLaw_preBrownian.isPreBrownian

lemma hasLaw_restrict_preBrownian (I : Finset â„â‰¥0) :
    HasLaw (fun Ï‰ â†¦ I.restrict (preBrownian Â· Ï‰)) (gaussianProjectiveFamily I) gaussianLimit :=
  IsPreBrownian.hasLaw I

lemma hasLaw_preBrownian_eval (t : â„â‰¥0) :
    HasLaw (preBrownian t) (gaussianReal 0 t) gaussianLimit :=
  IsPreBrownian.hasLaw_eval t

lemma hasLaw_preBrownian_sub (s t : â„â‰¥0) :
    HasLaw (preBrownian s - preBrownian t) (gaussianReal 0 (max (s - t) (t - s))) gaussianLimit :=
  IsPreBrownian.hasLaw_sub s t

lemma isKolmogorovProcess_preBrownian {n : â„•} (hn : 0 < n) :
    IsKolmogorovProcess preBrownian gaussianLimit (2 * n) n
      (Nat.doubleFactorial (2 * n - 1)) := by
  constructor
  Â· intro s t
    rw [â† BorelSpace.measurable_eq]
    fun_prop
  rotate_left
  Â· positivity
  Â· positivity
  refine fun s t â†¦ Eq.le ?_
  norm_cast
  simp_rw [edist_dist, Real.dist_eq]
  change âˆ«â» Ï‰, (fun x â†¦ (ENNReal.ofReal |x|) ^ (2 * n))
    ((preBrownian s - preBrownian t) Ï‰) âˆ‚_ = _
  rw [(hasLaw_preBrownian_sub s t).lintegral_comp (f := fun x â†¦ (ENNReal.ofReal |x|) ^ (2 * n))
    (by fun_prop)]
  simp_rw [â† fun x â†¦ ENNReal.ofReal_pow (abs_nonneg x)]
  rw [â† ofReal_integral_eq_lintegral_ofReal]
  Â· simp_rw [pow_two_mul_abs]
    rw [â† centralMoment_of_integral_id_eq_zero _ (by simp), â† NNReal.sq_sqrt (max _ _),
    centralMoment_fun_two_mul_gaussianReal, ENNReal.ofReal_mul (by positivity), mul_comm]
    norm_cast
    rw [pow_mul, NNReal.sq_sqrt, â† ENNReal.ofReal_pow dist_nonneg, â† NNReal.nndist_eq,
      NNReal.coe_pow, coe_nndist]
  Â· simp_rw [â† Real.norm_eq_abs]
    apply MemLp.integrable_norm_pow'
    exact IsGaussian.memLp_id _ _ (ENNReal.natCast_ne_top (2 * n))
  Â· exact ae_of_all _ fun _ â†¦ by positivity

lemma exists_brownian :
    âˆƒ Y : â„â‰¥0 â†’ (â„â‰¥0 â†’ â„) â†’ â„, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t, Y t =áµ[gaussianLimit] preBrownian t)
      âˆ§ âˆ€ Ï‰ t (Î² : â„â‰¥0) (_ : 0 < Î²) (_ : Î² < â¨† n, (((n + 2 : â„•) : â„) - 1) / (2 * (n + 2 : â„•))),
          âˆƒ U âˆˆ ğ“ t, âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U :=
  exists_modification_holder_iSup isCoverWithBoundedCoveringNumber_Ico_nnreal
    (fun n â†¦ (isKolmogorovProcess_preBrownian (by positivity : 0 < n + 2)).IsAEKolmogorovProcess)
    (fun n â†¦ by finiteness) zero_lt_one (fun n â†¦ by simp; norm_cast; omega)

noncomputable
def brownian : â„â‰¥0 â†’ (â„â‰¥0 â†’ â„) â†’ â„ :=
  exists_brownian.choose

@[fun_prop]
lemma measurable_brownian (t : â„â‰¥0) : Measurable (brownian t) :=
  exists_brownian.choose_spec.1 t

lemma brownian_ae_eq_preBrownian (t : â„â‰¥0) :
    brownian t =áµ[gaussianLimit] preBrownian t :=
  exists_brownian.choose_spec.2.1 t

lemma memHolder_brownian (Ï‰ : â„â‰¥0 â†’ â„) (t : â„â‰¥0) (Î² : â„â‰¥0) (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < 2â»Â¹) :
    âˆƒ U âˆˆ ğ“ t, âˆƒ C, HolderOnWith C Î² (brownian Â· Ï‰) U := by
  convert exists_brownian.choose_spec.2.2 Ï‰ t Î² hÎ²_pos ?_
  suffices â¨† n, (((n + 2 : â„•) : â„) - 1) / (2 * (n + 2 : â„•)) = 2â»Â¹ by rw [this]; norm_cast
  refine iSup_eq_of_forall_le_of_tendsto (F := Filter.atTop) (fun n â†¦ ?_) ?_
  Â· calc
    ((â†‘(n + 2) : â„) - 1) / (2 * â†‘(n + 2)) = 2â»Â¹ * (n + 1) / (n + 2) := by
      simp only [Nat.cast_add, Nat.cast_ofNat]; field_simp; ring
    _ â‰¤ 2â»Â¹ * 1 := by grw [mul_div_assoc, (div_le_oneâ‚€ (by positivity)).2]; linarith
    _ = 2â»Â¹ := mul_one _
  Â· have : (fun n : â„• â†¦ ((â†‘(n + 2) : â„) - 1) / (2 * â†‘(n + 2))) =
        (fun n : â„• â†¦ 2â»Â¹ * ((n : â„) / (n + 1))) âˆ˜ (fun n â†¦ n + 1) := by
      ext n
      simp only [Nat.cast_add, Nat.cast_ofNat, Function.comp_apply, Nat.cast_one]
      field_simp
      ring
    rw [this]
    refine Filter.Tendsto.comp ?_ (Filter.tendsto_add_atTop_nat 1)
    nth_rw 2 [â† mul_one 2â»Â¹]
    exact (tendsto_natCast_div_add_atTop (1 : â„)).const_mul _

@[fun_prop]
lemma continuous_brownian (Ï‰ : â„â‰¥0 â†’ â„) : Continuous (brownian Â· Ï‰) := by
  refine continuous_iff_continuousAt.mpr fun t â†¦ ?_
  obtain âŸ¨U, hu_mem, âŸ¨C, hâŸ©âŸ© := memHolder_brownian Ï‰ t 4â»Â¹ (by norm_num)
    (NNReal.inv_lt_inv (by norm_num) (by norm_num))
  exact (h.continuousOn (by norm_num)).continuousAt hu_mem

instance IsPreBrownian_brownian : IsPreBrownian brownian gaussianLimit :=
  IsPreBrownian.congr fun t â†¦ (brownian_ae_eq_preBrownian t).symm

lemma hasLaw_restrict_brownian {I : Finset â„â‰¥0} :
    HasLaw (fun Ï‰ â†¦ I.restrict (brownian Â· Ï‰)) (gaussianProjectiveFamily I) gaussianLimit :=
  IsPreBrownian.hasLaw I

lemma hasLaw_brownian : HasLaw (fun Ï‰ â†¦ (brownian Â· Ï‰)) gaussianLimit gaussianLimit :=
  IsPreBrownian.hasLaw_gaussianLimit
    (measurable_pi_lambda _ fun t â†¦ measurable_brownian t).aemeasurable

lemma hasLaw_brownian_eval {t : â„â‰¥0} :
    HasLaw (brownian t) (gaussianReal 0 t) gaussianLimit :=
  IsPreBrownian.hasLaw_eval t

lemma hasLaw_brownian_sub {s t : â„â‰¥0} :
    HasLaw (brownian s - brownian t) (gaussianReal 0 (max (s - t) (t - s))) gaussianLimit :=
  IsPreBrownian.hasLaw_sub s t

lemma measurable_brownian_uncurry : Measurable brownian.uncurry :=
  measurable_uncurry_of_continuous_of_measurable continuous_brownian measurable_brownian

lemma isKolmogorovProcess_brownian {n : â„•} (hn : 0 < n) :
    IsKolmogorovProcess brownian gaussianLimit (2 * n) n
      (Nat.doubleFactorial (2 * n - 1)) where
  measurablePair := measurable_pair_of_measurable measurable_brownian
  kolmogorovCondition := (isKolmogorovProcess_preBrownian hn).IsAEKolmogorovProcess.congr
    (fun t â†¦ (brownian_ae_eq_preBrownian t).symm) |>.kolmogorovCondition
  p_pos := by positivity
  q_pos := by positivity

lemma covariance_brownian (s t : â„â‰¥0) : cov[brownian s, brownian t; gaussianLimit] = min s t :=
    IsPreBrownian.covariance_eval s t

lemma hasIndepIncrements_brownian : HasIndepIncrements brownian gaussianLimit :=
  IsPreBrownian.hasIndepIncrements

section Measure

noncomputable
def wienerMeasureAux : Measure {f : â„â‰¥0 â†’ â„ // Continuous f} :=
  gaussianLimit.map (fun Ï‰ â†¦ (âŸ¨fun t â†¦ brownian t Ï‰, continuous_brownian Ï‰âŸ©))

section ContinuousMap.MeasurableSpace

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

instance : MeasurableSpace C(X, Y) := borel _
instance : BorelSpace C(X, Y) where
  measurable_eq := rfl

lemma isClosed_sUnion_of_finite {X : Type*} [TopologicalSpace X] {s : Set (Set X)}
    (h1 : s.Finite) (h2 : âˆ€ t âˆˆ s, IsClosed t) : IsClosed (â‹ƒâ‚€ s) := by
  rw [Set.sUnion_eq_biUnion]
  exact h1.isClosed_biUnion h2

open TopologicalSpace in
lemma ContinuousMap.borel_eq_iSup_comap_eval [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y] :
    borel C(X, Y) = â¨† a : X, (borel Y).comap fun b â†¦ b a := by
  -- https://math.stackexchange.com/questions/4789531/when-does-the-borel-sigma-algebra-of-compact-convergence-coincide-with-the-pr
  apply le_antisymm
  swap
  Â· refine iSup_le fun x â†¦ ?_
    simp_rw [â† measurable_iff_comap_le, â† BorelSpace.measurable_eq]
    exact Continuous.measurable (by fun_prop)
  -- Denote by `M(K, U)` the set of functions `f` such that `Set.MapsTo f K U`. These form a
  -- basis for the compact-open topology when `K` is compact and `U` is open.
  -- Because `C(X, Y)` is second-countable, it suffices to prove that those sets are measurable.
  -- Let therefore `K` be a compact set of `X` and `U` an open set of `Y`.
  rw [borel_eq_generateFrom_of_subbasis ContinuousMap.compactOpen_eq]
  refine MeasurableSpace.generateFrom_le fun s hs â†¦ ?_
  obtain âŸ¨K, hK, U, hU, hsâŸ© := hs
  rw [â† hs]
  -- Consider `V` a countable basis of the topology on Y.
  let V := countableBasis Y
  have hV : IsTopologicalBasis V := isBasis_countableBasis Y
  have cV : V.Countable := countable_countableBasis Y
  let Wâ‚ := {v | v âˆˆ V âˆ§ closure v âŠ† U}
  -- Consider `W` the set of `closure v`, where `v âˆˆ V` and `closure v âŠ† U`.
  let W := {v | âˆƒ u âˆˆ V, v âŠ† U âˆ§ v = closure u}
  -- Because `V` is countable, so is `W`.
  have cW : W.Countable := by
    apply (cV.image closure ).mono
    rintro - âŸ¨u, hu, -, rflâŸ©
    exact âŸ¨u, hu, rflâŸ©
  -- Because `Y` is regular, we can write that `U = â‹ƒ_{v âˆˆ W} v`.
  have U_eq_sUnion_W : U = â‹ƒâ‚€ W := by
    ext x
    rw [Set.mem_sUnion]
    constructor
    Â· intro hx
      obtain âŸ¨v, âŸ¨hv1, hv2âŸ©, hv3âŸ© := hV.nhds_basis_closure x |>.mem_iff.1 <| hU.mem_nhds hx
      exact âŸ¨closure v, âŸ¨v, hv2, hv3, rflâŸ©, subset_closure hv1âŸ©
    Â· rintro âŸ¨-, âŸ¨t, ht1, ht2, rflâŸ©, hxâŸ©
      exact ht2 hx
  -- Similarly, we can write that `U = â‹ƒ_{v âˆˆ V, closure v âŠ† U} v`.
  have U_eq_sUnion_Wâ‚ : U = â‹ƒâ‚€ Wâ‚ := by
    ext x
    rw [Set.mem_sUnion]
    refine âŸ¨fun hx â†¦ ?_, fun âŸ¨t, âŸ¨ht1, ht2âŸ©, hxâŸ© â†¦ ht2 <| subset_closure hxâŸ©
    obtain âŸ¨v, âŸ¨hv1, hv2âŸ©, hv3âŸ© := hV.nhds_basis_closure x |>.mem_iff.1 <| hU.mem_nhds hx
    exact âŸ¨v, âŸ¨hv2, hv3âŸ©, hv1âŸ©
  -- For any continuous `f` such that `f '' K âŠ† U`, because `K` is compact, `f '' K` is compact.
  -- But we just proved that `U = â‹ƒ_{v âˆˆ V, closure v âŠ† U} v`, and each `v âˆˆ V` is open,
  -- so there exists `J` a finite set of `v âˆˆ V` such that `closure v âŠ† U` and
  -- `f '' K âŠ† â‹ƒ v âˆˆ J, v`. We thus have `f '' K âŠ† â‹ƒ v âˆˆ J, closure v`. This is equivalent to
  -- having `I` a finite subset of `W` such that `f '' K âŠ† â‹ƒ v âˆˆ I, v`.
  have (f : C(X, Y)) (hf : K.MapsTo f U) : âˆƒ I, I.Finite âˆ§ I âŠ† W âˆ§ K.MapsTo f (â‹ƒâ‚€ I) := by
    simp_rw [Set.mapsTo_iff_image_subset] at hf âŠ¢
    rw [U_eq_sUnion_Wâ‚, Set.sUnion_eq_biUnion] at hf
    have : âˆ€ i âˆˆ {v | v âˆˆ V âˆ§ closure v âŠ† U}, IsOpen i :=
      fun x âŸ¨hx, _âŸ© â†¦ hV.isOpen hx
    obtain âŸ¨b, hb1, hb2, hb3âŸ© := (hK.image f.continuous).elim_finite_subcover_image this hf
    refine âŸ¨closure '' b, hb2.image _, ?_, ?_âŸ©
    Â· rintro - âŸ¨v, hv, rflâŸ©
      exact âŸ¨v, (hb1 hv).1, (hb1 hv).2, rflâŸ©
    rw [â† Set.sUnion_eq_biUnion] at hb3
    exact hb3.trans <| Set.sUnion_mono_subsets fun _ â†¦ subset_closure
  -- Therefore, we obtain that
  -- `M(K, U) = â‹ƒ_{I âŠ† W, I finite}, M(K, â‹ƒ v âˆˆ I, v)`.
  have : {f : C(X, Y) | K.MapsTo f U} =
      â‹ƒâ‚€ {v | âˆƒ I, I.Finite âˆ§ I âŠ† W âˆ§ v = {f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)}} := by
    ext f
    rw [Set.mem_sUnion]
    refine âŸ¨fun h â†¦ ?_, ?_âŸ©
    Â· obtain âŸ¨I, hI1, hI2, hI3âŸ© := this f h
      exact âŸ¨{f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)}, âŸ¨I, hI1, hI2, rflâŸ©, hI3âŸ©
    Â· rintro âŸ¨-, âŸ¨I, hI1, hI2, rflâŸ©, hâŸ©
      simp only [Set.mapsTo_iff_image_subset] at h âŠ¢
      rw [U_eq_sUnion_W]
      exact h.trans <| Set.sUnion_subset_sUnion hI2
  simp only
  rw [this]
  -- In particular, because `W` is countable, si this is a countable union.
  -- To show measurability it is therefore enough to show the measurability of each term.
  apply MeasurableSet.sUnion
  Â· let f : Set (Set Y) â†’ Set C(X, Y) := fun I â†¦ {f : C(X, Y) | Set.MapsTo (â‡‘f) K (â‹ƒâ‚€ I)}
    refine ((Set.countable_setOf_finite_subset cW).image f).mono ?_
    rintro - âŸ¨I, hI1, hI2, rflâŸ©
    exact âŸ¨I, âŸ¨hI1, hI2âŸ©, rflâŸ©
  -- Consider now `I` a finite subset of `W`.
  rintro - âŸ¨I, hI1, hI2, rflâŸ©
  -- First, `â‹ƒ v âˆˆ I, v` is closed as a finite union of closed sets.
  have hI : IsClosed (â‹ƒâ‚€ I) := by
    refine isClosed_sUnion_of_finite hI1 fun x hx â†¦ ?_
    obtain âŸ¨u, -, -, rflâŸ© := hI2 hx
    exact isClosed_closure
  -- Consider `Q` a countable dense subset of `K`, which exists by second-countability assumption.
  obtain âŸ¨Q, cQ, dQâŸ© := TopologicalSpace.exists_countable_dense K
  have Q_sub_K : Subtype.val '' Q âŠ† K := Subtype.coe_image_subset K Q
  -- Because `f` is continuous and `â‹ƒ v âˆˆ I, v` is closed and `Q` is dense in `K`, having
  -- `f '' K âŠ† â‹ƒ v âˆˆ I, v` is the same as `f '' Q âŠ† â‹ƒ v âˆˆ I, v`.
  have : {f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)} =
      {f : C(X, Y) | (Subtype.val '' Q).MapsTo f (â‹ƒâ‚€ I)} := by
    ext f
    refine âŸ¨fun h x hx â†¦ h (Q_sub_K hx), fun h x hx â†¦ ?_âŸ©
    obtain âŸ¨u, hu1, hu2âŸ© := mem_closure_iff_seq_limit.1 <| Subtype.dense_iff.1 dQ hx
    exact hI.mem_of_tendsto ((f.continuous.tendsto x).comp hu2)
      (Filter.Eventually.of_forall fun n â†¦ h (hu1 n))
  -- We can write `M(Q, â‹ƒ v âˆˆ I, v) = â‹‚ q âˆˆ Q, (fun f â†¦ f q) â»Â¹' (â‹ƒ v âˆˆ I, v)`.
  have : {f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)} =
      â‹‚ q âˆˆ Subtype.val '' Q, (fun f â†¦ f q) â»Â¹' (â‹ƒâ‚€ I) := by
    rw [this]
    ext f
    rw [Set.mem_iInterâ‚‚]
    exact âŸ¨fun h x hx â†¦ h hx, fun h x hx â†¦ h x hxâŸ©
  rw [this]
  -- This is a countable intersection, so it suffices to prove that each term is measurable.
  -- Because `â‹ƒ v âˆˆ I, v` is closed, it is measurable, so it suffices to prove that
  -- for any `q âˆˆ Q`, `fun f â†¦ f q` is measurable for the product Ïƒ-algebra.
  -- The latter is the coarsest Ïƒ-algebra which makes the maps `fun f â†¦ f x` measurable,
  -- so we are done.
  refine MeasurableSet.biInter (cQ.image _)
    fun q hq â†¦ MeasurableSet.preimage hI.measurableSet (Measurable.le (le_iSup _ q) ?_)
  rw [BorelSpace.measurable_eq (Î± := Y)]
  exact comap_measurable _

lemma ContinuousMap.measurableSpace_eq_iSup_comap_eval
    [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y] :
    (inferInstance : MeasurableSpace C(X, Y))
      = â¨† a : X, (inferInstance : MeasurableSpace Y).comap fun b â†¦ b a := by
  simp_rw [BorelSpace.measurable_eq, borel_eq_iSup_comap_eval]

lemma ContinuousMap.measurable_iff_eval {Î± : Type*} [MeasurableSpace Î±]
    [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y]
    (g : Î± â†’ C(X, Y)) :
    Measurable g â†” âˆ€ (x : X), Measurable fun (a : Î±) â†¦ g a x := by
  simp_rw [ContinuousMap.measurableSpace_eq_iSup_comap_eval, measurable_iff_comap_le,
    MeasurableSpace.comap_iSup, iSup_le_iff, MeasurableSpace.comap_comp, Function.comp_def]

end ContinuousMap.MeasurableSpace

def MeasurableEquiv.continuousMap : {f : â„â‰¥0 â†’ â„ // Continuous f} â‰ƒáµ C(â„â‰¥0, â„) where
  toFun := fun f â†¦ ContinuousMap.mk f.1 f.2
  invFun := fun f â†¦ âŸ¨f, f.continuousâŸ©
  left_inv f := rfl
  right_inv f := rfl
  measurable_toFun := by
    simp only [Equiv.coe_fn_mk]
    rw [ContinuousMap.measurable_iff_eval]
    intro x
    simp only [ContinuousMap.coe_mk]
    revert x
    rw [â† measurable_pi_iff]
    exact measurable_subtype_coe
  measurable_invFun := by
    simp only [Equiv.coe_fn_symm_mk]
    refine Measurable.subtype_mk ?_
    rw [measurable_pi_iff]
    exact fun _ â†¦ Continuous.measurable (by fun_prop)


noncomputable
def wienerMeasure : Measure C(â„â‰¥0, â„) := wienerMeasureAux.map MeasurableEquiv.continuousMap

end Measure

end ProbabilityTheory
