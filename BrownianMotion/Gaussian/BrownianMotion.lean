/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import BrownianMotion.Continuity.KolmogorovChentsov
import BrownianMotion.Gaussian.GaussianProcess
import BrownianMotion.Gaussian.Moment
import BrownianMotion.Gaussian.ProjectiveLimit
import Mathlib.Analysis.Normed.Lp.MeasurableSpace
import Mathlib.Topology.ContinuousMap.SecondCountableSpace

/-!
# Brownian motion

-/

open MeasureTheory NNReal WithLp Finset
open scoped ENNReal NNReal Topology

namespace ProbabilityTheory

def preBrownian : â„â‰¥0 â†’ (â„â‰¥0 â†’ â„) â†’ â„ := fun t Ï‰ â†¦ Ï‰ t

@[fun_prop]
lemma measurable_preBrownian (t : â„â‰¥0) : Measurable (preBrownian t) := by
  unfold preBrownian
  fun_prop

lemma hasLaw_preBrownian : HasLaw (fun Ï‰ â†¦ (preBrownian Â· Ï‰)) gaussianLimit gaussianLimit where
  aemeasurable := (measurable_pi_lambda _ measurable_preBrownian).aemeasurable
  map_eq := Measure.map_id

lemma hasLaw_restrict_preBrownian (I : Finset â„â‰¥0) :
    HasLaw (fun Ï‰ â†¦ I.restrict (preBrownian Â· Ï‰)) gaussianLimit (gaussianProjectiveFamily I) :=
  hasLaw_restrict_gaussianLimit.comp hasLaw_preBrownian

lemma hasLaw_preBrownian_eval (t : â„â‰¥0) :
    HasLaw (preBrownian t) gaussianLimit (gaussianReal 0 t) :=
  (hasLaw_eval_gaussianProjectiveFamily âŸ¨t, by simpâŸ©).comp
    (hasLaw_restrict_preBrownian ({t} : Finset â„â‰¥0))

lemma isGaussianProcess_preBrownian : IsGaussianProcess preBrownian gaussianLimit := by
  intro I
  rw [(hasLaw_restrict_preBrownian I).map_eq]
  exact isGaussian_gaussianProjectiveFamily I

lemma hasLaw_preBrownian_sub (s t : â„â‰¥0) :
    HasLaw (preBrownian s - preBrownian t) gaussianLimit
      (gaussianReal 0 (max (s - t) (t - s))) := by
  have : preBrownian s - preBrownian t =
      ((fun x â†¦ x âŸ¨s, by simpâŸ©) - (fun x â†¦ x âŸ¨t, by simpâŸ©)) âˆ˜ ({s, t} : Finset â„â‰¥0).restrict := by
    ext; simp [preBrownian]
  rw [this]
  exact hasLaw_eval_sub_eval_gaussianProjectiveFamily.comp
    hasLaw_restrict_gaussianLimit

lemma isMeasurableKolmogorovProcess_preBrownian (n : â„•) :
    IsMeasurableKolmogorovProcess preBrownian gaussianLimit (2 * n) n
      (Nat.doubleFactorial (2 * n - 1)) := by
  constructor
  Â· intro s t
    rw [â† BorelSpace.measurable_eq]
    fun_prop
  refine fun s t â†¦ Eq.le ?_
  norm_cast
  simp_rw [edist_dist, Real.dist_eq]
  change âˆ«â» Ï‰, (fun x â†¦ (ENNReal.ofReal |x|) ^ (2 * n))
    ((preBrownian s - preBrownian t) Ï‰) âˆ‚_ = _
  rw [(hasLaw_preBrownian_sub s t).lintegral_comp (f := fun x â†¦ (ENNReal.ofReal |x|) ^ (2 * n))
    (by fun_prop)]
  simp_rw [â† fun x â†¦ ENNReal.ofReal_pow (abs_nonneg x)]
  rw [â† ofReal_integral_eq_lintegral_ofReal]
  Â· simp_rw [pow_two_mul_abs]
    rw [â† centralMoment_of_integral_id_eq_zero _ (by simp), â† NNReal.sq_sqrt (max _ _),
    centralMoment_fun_two_mul_gaussianReal, ENNReal.ofReal_mul (by positivity), mul_comm]
    norm_cast
    rw [pow_mul, NNReal.sq_sqrt, â† ENNReal.ofReal_pow dist_nonneg, â† NNReal.nndist_eq,
      NNReal.coe_pow, coe_nndist]
  Â· simp_rw [â† Real.norm_eq_abs]
    apply MemLp.integrable_norm_pow'
    exact IsGaussian.memLp_id _ _ (ENNReal.natCast_ne_top (2 * n))
  Â· exact ae_of_all _ fun _ â†¦ by positivity

lemma exists_brownian :
    âˆƒ Y : â„â‰¥0 â†’ (â„â‰¥0 â†’ â„) â†’ â„, (âˆ€ t, Measurable (Y t)) âˆ§ (âˆ€ t, Y t =áµ[gaussianLimit] preBrownian t)
      âˆ§ âˆ€ Ï‰ t (Î² : â„â‰¥0) (_ : 0 < Î²) (_ : Î² < â¨† n, (((n + 2 : â„•) : â„) - 1) / (2 * (n + 2 : â„•))),
          âˆƒ U âˆˆ ğ“ t, âˆƒ C, HolderOnWith C Î² (Y Â· Ï‰) U :=
  exists_modification_holder_iSup isCoverWithBoundedCoveringNumber_Ico_nnreal
    (fun n â†¦ (isMeasurableKolmogorovProcess_preBrownian (n + 2)).isKolmogorovProcess)
    (fun n â†¦ by finiteness) zero_lt_one (fun n â†¦ by positivity) (fun n â†¦ by simp; norm_cast; omega)

noncomputable
def brownian : â„â‰¥0 â†’ (â„â‰¥0 â†’ â„) â†’ â„ :=
  exists_brownian.choose

@[fun_prop]
lemma measurable_brownian (t : â„â‰¥0) : Measurable (brownian t) :=
  exists_brownian.choose_spec.1 t

lemma brownian_ae_eq_preBrownian (t : â„â‰¥0) :
    brownian t =áµ[gaussianLimit] preBrownian t :=
  exists_brownian.choose_spec.2.1 t

lemma memHolder_brownian (Ï‰ : â„â‰¥0 â†’ â„) (t : â„â‰¥0) (Î² : â„â‰¥0) (hÎ²_pos : 0 < Î²) (hÎ²_lt : Î² < 2â»Â¹) :
    âˆƒ U âˆˆ ğ“ t, âˆƒ C, HolderOnWith C Î² (brownian Â· Ï‰) U := by
  convert exists_brownian.choose_spec.2.2 Ï‰ t Î² hÎ²_pos ?_
  suffices â¨† n, (((n + 2 : â„•) : â„) - 1) / (2 * (n + 2 : â„•)) = 2â»Â¹ by rw [this]; norm_cast
  refine iSup_eq_of_forall_le_of_tendsto (F := Filter.atTop) (fun n â†¦ ?_) ?_
  Â· calc
    ((â†‘(n + 2) : â„) - 1) / (2 * â†‘(n + 2)) = 2â»Â¹ * (n + 1) / (n + 2) := by field_simp; ring
    _ â‰¤ 2â»Â¹ * 1 := by grw [mul_div_assoc, (div_le_oneâ‚€ (by positivity)).2]; linarith
    _ = 2â»Â¹ := mul_one _
  Â· have : (fun n : â„• â†¦ ((â†‘(n + 2) : â„) - 1) / (2 * â†‘(n + 2))) =
        (fun n : â„• â†¦ 2â»Â¹ * ((n : â„) / (n + 1))) âˆ˜ (fun n â†¦ n + 1) := by ext n; field_simp; ring
    rw [this]
    refine Filter.Tendsto.comp ?_ (Filter.tendsto_add_atTop_nat 1)
    nth_rw 2 [â† mul_one 2â»Â¹]
    exact (tendsto_natCast_div_add_atTop (1 : â„)).const_mul _

@[fun_prop]
lemma continuous_brownian (Ï‰ : â„â‰¥0 â†’ â„) : Continuous (brownian Â· Ï‰) := by
  refine continuous_iff_continuousAt.mpr fun t â†¦ ?_
  obtain âŸ¨U, hu_mem, âŸ¨C, hâŸ©âŸ© := memHolder_brownian Ï‰ t 4â»Â¹ (by norm_num)
    (NNReal.inv_lt_inv (by norm_num) (by norm_num))
  exact (h.continuousOn (by norm_num)).continuousAt hu_mem

lemma hasLaw_restrict_brownian {I : Finset â„â‰¥0} :
    HasLaw (fun Ï‰ â†¦ I.restrict (brownian Â· Ï‰)) gaussianLimit (gaussianProjectiveFamily I) := by
  refine (hasLaw_restrict_preBrownian I).congr ?_
  filter_upwards [ae_all_iff.2 fun i : I â†¦ brownian_ae_eq_preBrownian i.1] with Ï‰ hÏ‰
  ext; simp [hÏ‰]

lemma hasLaw_brownian : HasLaw (fun Ï‰ â†¦ (brownian Â· Ï‰)) gaussianLimit gaussianLimit where
  aemeasurable := (measurable_pi_lambda _ measurable_brownian).aemeasurable
  map_eq := by
    symm
    refine isProjectiveLimit_gaussianLimit.unique fun I â†¦ ?_
    rw [Measure.map_map (by fun_prop) (measurable_pi_lambda _ measurable_brownian)]
    exact hasLaw_restrict_brownian.map_eq

lemma hasLaw_brownian_eval {t : â„â‰¥0} :
    HasLaw (brownian t) gaussianLimit (gaussianReal 0 t) :=
  (hasLaw_preBrownian_eval t).congr (brownian_ae_eq_preBrownian t)

lemma hasLaw_brownian_sub {s t : â„â‰¥0} :
    HasLaw (brownian s - brownian t) gaussianLimit (gaussianReal 0 (max (s - t) (t - s))) :=
    (hasLaw_preBrownian_sub s t).congr
      ((brownian_ae_eq_preBrownian s).sub (brownian_ae_eq_preBrownian t))

open NNReal Filter Topology in
lemma measurable_brownian_uncurry : Measurable brownian.uncurry :=
  measurable_uncurry_of_continuous_of_measurable continuous_brownian measurable_brownian

lemma isGaussianProcess_brownian : IsGaussianProcess brownian gaussianLimit :=
  isGaussianProcess_preBrownian.modification fun t â†¦ (brownian_ae_eq_preBrownian t).symm

lemma isMeasurableKolmogorovProcess_brownian (n : â„•) :
    IsMeasurableKolmogorovProcess brownian gaussianLimit (2 * n) n
      (Nat.doubleFactorial (2 * n - 1)) where
  measurablePair := measurable_pair_of_measurable measurable_brownian
  kolmogorovCondition := (isMeasurableKolmogorovProcess_preBrownian n).isKolmogorovProcess.congr
    (fun t â†¦ (brownian_ae_eq_preBrownian t).symm) |>.kolmogorovCondition

lemma iIndepFun_iff_charFun_eq_pi {Î¹ Î© : Type*} [Fintype Î¹] {E : Î¹ â†’ Type*}
    [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace â„ (E i)] [âˆ€ i, MeasurableSpace (E i)]
    {mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} [IsProbabilityMeasure Î¼] {X : Î  i, Î© â†’ (E i)}
    [âˆ€ i, CompleteSpace (E i)] [âˆ€ i, BorelSpace (E i)]
    [âˆ€ i, SecondCountableTopology (E i)] (mX : âˆ€ i, AEMeasurable (X i) Î¼) :
    iIndepFun X Î¼ â†” âˆ€ t,
      charFun (Î¼.map fun Ï‰ â†¦ toLp 2 (X Â· Ï‰)) t = âˆ i, charFun (Î¼.map (X i)) (t i) := sorry
-- PR #26269 in Mathlib

instance IsGaussian.eval {Î¹ Î© : Type*} {E : Î¹ â†’ Type*} [Fintype Î¹] {mÎ© : MeasurableSpace Î©}
    {P : Measure Î©} [IsProbabilityMeasure P] [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, NormedSpace â„ (E i)] [âˆ€ i, MeasurableSpace (E i)] [âˆ€ i, BorelSpace (E i)]
    [âˆ€ i, SecondCountableTopology (E i)] {X : (i : Î¹) â†’ Î© â†’ E i}
    [h : IsGaussian (P.map (fun Ï‰ â†¦ (X Â· Ï‰)))] (i : Î¹) :
    IsGaussian (P.map (X i)) := by
  have : X i = (ContinuousLinearMap.proj (R := â„) (Ï† := E) i) âˆ˜ (fun Ï‰ â†¦ (X Â· Ï‰)) := by ext; simp
  rw [this, â† AEMeasurable.map_map_of_aemeasurable]
  Â· infer_instance
  Â· fun_prop
  by_contra!
  rw [Measure.map_of_not_aemeasurable] at h
  Â· exact h.toIsProbabilityMeasure.ne_zero _ rfl
  Â· exact this

instance HasGaussianLaw.eval {Î¹ Î© : Type*} {E : Î¹ â†’ Type*} [Fintype Î¹] {mÎ© : MeasurableSpace Î©}
    {P : Measure Î©} [IsProbabilityMeasure P] [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, NormedSpace â„ (E i)] [âˆ€ i, MeasurableSpace (E i)] [âˆ€ i, BorelSpace (E i)]
    [âˆ€ i, SecondCountableTopology (E i)] {X : (i : Î¹) â†’ Î© â†’ E i}
    [h : HasGaussianLaw (fun Ï‰ â†¦ (X Â· Ï‰)) P] (i : Î¹) :
    HasGaussianLaw (X i) P where
  isGaussian_map' := IsGaussian.eval i

lemma test {Î¹ Î© : Type*} [Fintype Î¹] {mÎ© : MeasurableSpace Î©} {P : Measure Î©}
    [IsProbabilityMeasure P] {X : Î¹ â†’ Î© â†’ â„}
    [h1 : HasGaussianLaw (fun Ï‰ â†¦ (X Â· Ï‰)) P]
    (h2 : âˆ€ i j : Î¹, i â‰  j â†’ cov[X i, X j; P] = 0) :
    iIndepFun X P := by
  have mX : âˆ€ i, AEMeasurable (X i) P := aemeasurable_pi_iff.1 h1.aemeasurable
  have (i : Î¹) : (inner â„ ((EuclideanSpace.basisFun Î¹ â„) i) âˆ˜ â‡‘(EuclideanSpace.equiv Î¹ â„).symm âˆ˜
      fun Ï‰ x â†¦ X x Ï‰) = X i := by ext; simp [-PiLp.inner_apply]
  refine iIndepFun_iff_charFun_eq_pi mX |>.2 fun Î¾ â†¦ ?_
  change charFun (P.map ((EuclideanSpace.equiv Î¹ â„).symm âˆ˜ _)) _ = _
  rw [IsGaussian.charFun_eq, covInnerBilin_self IsGaussian.memLp_two_id]
  nth_rw 1 2 [â† (EuclideanSpace.basisFun Î¹ â„).sum_repr' Î¾]
  simp_rw [sum_inner, basisFun_inner]
  rw [variance_fun_sum]
  Â· simp_rw [Complex.ofReal_sum, sum_mul, sum_div, â† sum_sub_distrib, Complex.exp_sum]
    congr with i
    rw [real_inner_smul_left, â† integral_inner, Finset.sum_eq_single_of_mem i, covariance_self]
    Â· simp_rw [real_inner_smul_left, variance_mul]
      conv =>
        enter [1, 1, 1, 1, 1, 2, 2, x]
        change id (inner â„ (EuclideanSpace.basisFun Î¹ â„ i) x)
      conv =>
        enter [1, 1, 2, 1, 1, 2, 1]
        change id âˆ˜ (inner â„ (EuclideanSpace.basisFun Î¹ â„ i))
      rw [â† integral_map, â† variance_map, AEMeasurable.map_map_of_aemeasurable]
      Â· rw [this, IsGaussian.charFun_eq, covInnerBilin_real_self]
        Â· simp [mul_comm]
        exact IsGaussian.memLp_two_id
      any_goals fun_prop
    Â· fun_prop
    Â· simp
    Â· rintro j - hj
      simp_rw [real_inner_smul_left, covariance_mul_left, covariance_mul_right]
      rw [covariance_map, this, this, h2 i j hj.symm]
      Â· simp
      any_goals exact Measurable.aestronglyMeasurable (by fun_prop)
      fun_prop
    Â· exact IsGaussian.integrable_id
  intro i
  rw [â† ContinuousBilinForm.inner_apply'', â† Function.id_comp â‡‘(ContinuousBilinForm.inner _ _),
    â† memLp_map_measure_iff]
  Â· exact IsGaussian.memLp_two_id
  Â· exact Measurable.aestronglyMeasurable (by fun_prop)
  Â· fun_prop

def indep_incr {Î© T E : Type*} {mÎ© : MeasurableSpace Î©} (P : Measure Î©) [Sub E] [Preorder T]
    [MeasurableSpace E] (X : T â†’ Î© â†’ E) : Prop :=
  âˆ€ n, âˆ€ t : Fin (n + 2) â†’ T, Monotone t â†’
    iIndepFun (fun i : Fin (n + 1) â†¦ X (t i.succ) - X (t i.castSucc)) P

lemma mem_pair_iff {Î± : Type*} [DecidableEq Î±] {x y z : Î±} :
    x âˆˆ ({y, z} : Finset Î±) â†” x = y âˆ¨ x = z := by
  simp

lemma cov_brownian (s t : â„â‰¥0) : cov[brownian s, brownian t; gaussianLimit] = min s t := by
  have h1 : brownian s = (fun x : ({s, t} : Finset â„â‰¥0) â†’ â„ â†¦ x âŸ¨s, by simpâŸ©) âˆ˜
      (fun Ï‰ â†¦ ({s, t} : Finset â„â‰¥0).restrict (brownian Â· Ï‰)) := by ext; simp
  have h2 : brownian t = (fun x : ({s, t} : Finset â„â‰¥0) â†’ â„ â†¦ x âŸ¨t, by simpâŸ©) âˆ˜
      (fun Ï‰ â†¦ ({s, t} : Finset â„â‰¥0).restrict (brownian Â· Ï‰)) := by ext; simp
  have : (fun Ï‰ â†¦ ({s, t} : Finset â„â‰¥0).restrict (brownian Â· Ï‰)) =áµ[gaussianLimit]
      (fun Ï‰ â†¦ ({s, t} : Finset â„â‰¥0).restrict (preBrownian Â· Ï‰)) := by
    filter_upwards [brownian_ae_eq_preBrownian s, brownian_ae_eq_preBrownian t] with Ï‰ hÏ‰â‚ hÏ‰â‚‚
    ext r
    obtain hr | hr := mem_pair_iff.1 r.2
    all_goals simp [hr, hÏ‰â‚, hÏ‰â‚‚]
  rw [h1, h2, â† covariance_map, Measure.map_congr this]
  Â· simp_rw [preBrownian, hasLaw_restrict_gaussianLimit.map_eq]
    rw [covariance_eval_gaussianProjectiveFamily]
  Â· exact Measurable.aestronglyMeasurable (by fun_prop)
  Â· exact Measurable.aestronglyMeasurable (by fun_prop)
  exact Measurable.aemeasurable <| by fun_prop

lemma indep_incr_bm : indep_incr gaussianLimit brownian := by
  intro n t ht
  apply test
  Â· exact fun i â†¦ by fun_prop
  Â· have : (fun Ï‰ â†¦
        (EuclideanSpace.measurableEquiv (Fin (n + 1))).symm
        fun i â†¦ (brownian (t i.succ) - brownian (t i.castSucc)) Ï‰) =
        (fun x â†¦ toLp 2 fun (i : Fin (n + 1)) â†¦ x i.succ - x i.castSucc) âˆ˜
        (fun x (i : Fin (n + 2)) â†¦ x âŸ¨t i, by simpâŸ©) âˆ˜ ((Finset.univ.image t).restrict) âˆ˜
        (fun Ï‰ t â†¦ brownian t Ï‰) := by ext; simp [EuclideanSpace.measurableEquiv]
    rw [this, â† Function.comp_assoc, â† Measure.map_map]
    Â· let L : ((Finset.univ.image t) â†’ â„) â†’L[â„] EuclideanSpace â„ (Fin (n + 1)) :=
        { toFun := (fun x â†¦ toLp 2 fun (i : Fin (n + 1)) â†¦ x i.succ - x i.castSucc) âˆ˜
            (fun x (i : Fin (n + 2)) â†¦ x âŸ¨t i, by simpâŸ©)
          map_add' x y := by ext; simp; ring
          map_smul' m x := by ext; simp; ring
          cont := by fun_prop }
      have : (fun x â†¦ toLp 2 fun (i : Fin (n + 1)) â†¦ x i.succ - x i.castSucc) âˆ˜
          (fun x (i : Fin (n + 2)) â†¦ x âŸ¨t i, by simpâŸ©) = â‡‘L := rfl
      rw [this]
      refine @isGaussian_map _ _ _ _ _ _ _ _ _ _ _ ?_ L
      exact isGaussianProcess_brownian.restrict (Finset.univ.image t)
    Â· fun_prop
    Â· fun_prop
  intro i j hij
  rw [covariance_sub_left, covariance_sub_right, covariance_sub_right]
  Â· simp_rw [cov_brownian]
    obtain h | h := Fin.lt_or_lt_of_ne hij
    Â· have h1 : i.succ â‰¤ j.succ := Fin.succ_le_succ_iff.mpr h.le
      have h2 : i.succ â‰¤ j.castSucc := h
      have h3 : i.castSucc â‰¤ j.succ := Fin.le_of_lt h1
      have h4 : i.castSucc â‰¤ j.castSucc := Fin.le_castSucc_iff.mpr h1
      rw [min_eq_left (ht h1), min_eq_left (ht h2), min_eq_left (ht h3), min_eq_left (ht h4)]
      simp
    Â· have h1 : j.succ â‰¤ i.succ := Fin.succ_le_succ_iff.mpr h.le
      have h2 : j.succ â‰¤ i.castSucc := h
      have h3 : j.castSucc â‰¤ i.succ := Fin.le_of_lt h1
      have h4 : j.castSucc â‰¤ i.castSucc := Fin.le_castSucc_iff.mpr h1
      rw [min_eq_right (ht h1), min_eq_right (ht h2), min_eq_right (ht h3), min_eq_right (ht h4)]
      simp
  rotate_right
  Â· exact
      (isGaussianProcess_brownian.memLp_eval (by norm_num)).sub
      (isGaussianProcess_brownian.memLp_eval (by norm_num))
  all_goals
    exact isGaussianProcess_brownian.memLp_eval (by norm_num)

section Measure

noncomputable
def wienerMeasureAux : Measure {f : â„â‰¥0 â†’ â„ // Continuous f} :=
  gaussianLimit.map (fun Ï‰ â†¦ (âŸ¨fun t â†¦ brownian t Ï‰, continuous_brownian Ï‰âŸ©))

section ContinuousMap.MeasurableSpace

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

instance : MeasurableSpace C(X, Y) := borel _
instance : BorelSpace C(X, Y) where
  measurable_eq := rfl

lemma isClosed_sUnion_of_finite {X : Type*} [TopologicalSpace X] {s : Set (Set X)}
    (h1 : s.Finite) (h2 : âˆ€ t âˆˆ s, IsClosed t) : IsClosed (â‹ƒâ‚€ s) := by
  rw [Set.sUnion_eq_biUnion]
  exact h1.isClosed_biUnion h2

open TopologicalSpace in
lemma ContinuousMap.borel_eq_iSup_comap_eval [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y] :
    borel C(X, Y) = â¨† a : X, (borel Y).comap fun b â†¦ b a := by
  -- https://math.stackexchange.com/questions/4789531/when-does-the-borel-sigma-algebra-of-compact-convergence-coincide-with-the-pr
  apply le_antisymm
  swap
  Â· refine iSup_le fun x â†¦ ?_
    simp_rw [â† measurable_iff_comap_le, â† BorelSpace.measurable_eq]
    exact Continuous.measurable (by fun_prop)
  -- Denote by `M(K, U)` the set of functions `f` such that `Set.MapsTo f K U`. These form a
  -- basis for the compact-open topology when `K` is compact and `U` is open.
  -- Because `C(X, Y)` is second-countable, it suffices to prove that those sets are measurable.
  -- Let therefore `K` be a compact set of `X` and `U` an open set of `Y`.
  rw [borel_eq_generateFrom_of_subbasis ContinuousMap.compactOpen_eq]
  refine MeasurableSpace.generateFrom_le fun s hs â†¦ ?_
  obtain âŸ¨K, hK, U, hU, hsâŸ© := hs
  rw [â† hs]
  -- Consider `V` a countable basis of the topology on Y.
  let V := countableBasis Y
  have hV : IsTopologicalBasis V := isBasis_countableBasis Y
  have cV : V.Countable := countable_countableBasis Y
  let Wâ‚ := {v | v âˆˆ V âˆ§ closure v âŠ† U}
  -- Consider `W` the set of `closure v`, where `v âˆˆ V` and `closure v âŠ† U`.
  let W := {v | âˆƒ u âˆˆ V, v âŠ† U âˆ§ v = closure u}
  -- Because `V` is countable, so is `W`.
  have cW : W.Countable := by
    apply (cV.image closure ).mono
    rintro - âŸ¨u, hu, -, rflâŸ©
    exact âŸ¨u, hu, rflâŸ©
  -- Because `Y` is regular, we can write that `U = â‹ƒ_{v âˆˆ W} v`.
  have U_eq_sUnion_W : U = â‹ƒâ‚€ W := by
    ext x
    rw [Set.mem_sUnion]
    constructor
    Â· intro hx
      obtain âŸ¨v, âŸ¨hv1, hv2âŸ©, hv3âŸ© := hV.nhds_basis_closure x |>.mem_iff.1 <| hU.mem_nhds hx
      exact âŸ¨closure v, âŸ¨v, hv2, hv3, rflâŸ©, subset_closure hv1âŸ©
    Â· rintro âŸ¨-, âŸ¨t, ht1, ht2, rflâŸ©, hxâŸ©
      exact ht2 hx
  -- Similarly, we can write that `U = â‹ƒ_{v âˆˆ V, closure v âŠ† U} v`.
  have U_eq_sUnion_Wâ‚ : U = â‹ƒâ‚€ Wâ‚ := by
    ext x
    rw [Set.mem_sUnion]
    refine âŸ¨fun hx â†¦ ?_, fun âŸ¨t, âŸ¨ht1, ht2âŸ©, hxâŸ© â†¦ ht2 <| subset_closure hxâŸ©
    obtain âŸ¨v, âŸ¨hv1, hv2âŸ©, hv3âŸ© := hV.nhds_basis_closure x |>.mem_iff.1 <| hU.mem_nhds hx
    exact âŸ¨v, âŸ¨hv2, hv3âŸ©, hv1âŸ©
  -- For any continuous `f` such that `f '' K âŠ† U`, because `K` is compact, `f '' K` is compact.
  -- But we just proved that `U = â‹ƒ_{v âˆˆ V, closure v âŠ† U} v`, and each `v âˆˆ V` is open,
  -- so there exists `J` a finite set of `v âˆˆ V` such that `closure v âŠ† U` and
  -- `f '' K âŠ† â‹ƒ v âˆˆ J, v`. We thus have `f '' K âŠ† â‹ƒ v âˆˆ J, closure v`. This is equivalent to
  -- having `I` a finite subset of `W` such that `f '' K âŠ† â‹ƒ v âˆˆ I, v`.
  have (f : C(X, Y)) (hf : K.MapsTo f U) : âˆƒ I, I.Finite âˆ§ I âŠ† W âˆ§ K.MapsTo f (â‹ƒâ‚€ I) := by
    simp_rw [Set.mapsTo'] at hf âŠ¢
    rw [U_eq_sUnion_Wâ‚, Set.sUnion_eq_biUnion] at hf
    have : âˆ€ i âˆˆ {v | v âˆˆ V âˆ§ closure v âŠ† U}, IsOpen i :=
      fun x âŸ¨hx, _âŸ© â†¦ hV.isOpen hx
    obtain âŸ¨b, hb1, hb2, hb3âŸ© := (hK.image f.continuous).elim_finite_subcover_image this hf
    refine âŸ¨closure '' b, hb2.image _, ?_, ?_âŸ©
    Â· rintro - âŸ¨v, hv, rflâŸ©
      exact âŸ¨v, (hb1 hv).1, (hb1 hv).2, rflâŸ©
    rw [â† Set.sUnion_eq_biUnion] at hb3
    exact hb3.trans <| Set.sUnion_mono_subsets fun _ â†¦ subset_closure
  -- Therefore, we obtain that
  -- `M(K, U) = â‹ƒ_{I âŠ† W, I finite}, M(K, â‹ƒ v âˆˆ I, v)`.
  have : {f : C(X, Y) | K.MapsTo f U} =
      â‹ƒâ‚€ {v | âˆƒ I, I.Finite âˆ§ I âŠ† W âˆ§ v = {f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)}} := by
    ext f
    rw [Set.mem_sUnion]
    refine âŸ¨fun h â†¦ ?_, ?_âŸ©
    Â· obtain âŸ¨I, hI1, hI2, hI3âŸ© := this f h
      exact âŸ¨{f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)}, âŸ¨I, hI1, hI2, rflâŸ©, hI3âŸ©
    Â· rintro âŸ¨-, âŸ¨I, hI1, hI2, rflâŸ©, hâŸ©
      simp only [Set.mapsTo'] at h âŠ¢
      rw [U_eq_sUnion_W]
      exact h.trans <| Set.sUnion_subset_sUnion hI2
  simp only
  rw [this]
  -- In particular, because `W` is countable, si this is a countable union.
  -- To show measurability it is therefore enough to show the measurability of each term.
  apply MeasurableSet.sUnion
  Â· let f : Set (Set Y) â†’ Set C(X, Y) := fun I â†¦ {f : C(X, Y) | Set.MapsTo (â‡‘f) K (â‹ƒâ‚€ I)}
    refine ((Set.countable_setOf_finite_subset cW).image f).mono ?_
    rintro - âŸ¨I, hI1, hI2, rflâŸ©
    exact âŸ¨I, âŸ¨hI1, hI2âŸ©, rflâŸ©
  -- Consider now `I` a finite subset of `W`.
  rintro - âŸ¨I, hI1, hI2, rflâŸ©
  -- First, `â‹ƒ v âˆˆ I, v` is closed as a finite union of closed sets.
  have hI : IsClosed (â‹ƒâ‚€ I) := by
    refine isClosed_sUnion_of_finite hI1 fun x hx â†¦ ?_
    obtain âŸ¨u, -, -, rflâŸ© := hI2 hx
    exact isClosed_closure
  -- Consider `Q` a countable dense subset of `K`, which exists by second-countability assumption.
  obtain âŸ¨Q, cQ, dQâŸ© := TopologicalSpace.exists_countable_dense K
  have Q_sub_K : Subtype.val '' Q âŠ† K := Subtype.coe_image_subset K Q
  -- Because `f` is continuous and `â‹ƒ v âˆˆ I, v` is closed and `Q` is dense in `K`, having
  -- `f '' K âŠ† â‹ƒ v âˆˆ I, v` is the same as `f '' Q âŠ† â‹ƒ v âˆˆ I, v`.
  have : {f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)} =
      {f : C(X, Y) | (Subtype.val '' Q).MapsTo f (â‹ƒâ‚€ I)} := by
    ext f
    refine âŸ¨fun h x hx â†¦ h (Q_sub_K hx), fun h x hx â†¦ ?_âŸ©
    obtain âŸ¨u, hu1, hu2âŸ© := mem_closure_iff_seq_limit.1 <| Subtype.dense_iff.1 dQ hx
    exact hI.mem_of_tendsto ((f.continuous.tendsto x).comp hu2)
      (Filter.Eventually.of_forall fun n â†¦ h (hu1 n))
  -- We can write `M(Q, â‹ƒ v âˆˆ I, v) = â‹‚ q âˆˆ Q, (fun f â†¦ f q) â»Â¹' (â‹ƒ v âˆˆ I, v)`.
  have : {f : C(X, Y) | K.MapsTo f (â‹ƒâ‚€ I)} =
      â‹‚ q âˆˆ Subtype.val '' Q, (fun f â†¦ f q) â»Â¹' (â‹ƒâ‚€ I) := by
    rw [this]
    ext f
    rw [Set.mem_iInterâ‚‚]
    exact âŸ¨fun h x hx â†¦ h hx, fun h x hx â†¦ h x hxâŸ©
  rw [this]
  -- This is a countable intersection, so it suffices to prove that each term is measurable.
  -- Because `â‹ƒ v âˆˆ I, v` is closed, it is measurable, so it suffices to prove that
  -- for any `q âˆˆ Q`, `fun f â†¦ f q` is measurable for the product Ïƒ-algebra.
  -- The latter is the coarsest Ïƒ-algebra which makes the maps `fun f â†¦ f x` measurable,
  -- so we are done.
  refine MeasurableSet.biInter (cQ.image _)
    fun q hq â†¦ MeasurableSet.preimage hI.measurableSet (Measurable.le (le_iSup _ q) ?_)
  rw [BorelSpace.measurable_eq (Î± := Y)]
  exact comap_measurable _

lemma ContinuousMap.measurableSpace_eq_iSup_comap_eval
    [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y] :
    (inferInstance : MeasurableSpace C(X, Y))
      = â¨† a : X, (inferInstance : MeasurableSpace Y).comap fun b â†¦ b a := by
  simp_rw [BorelSpace.measurable_eq, borel_eq_iSup_comap_eval]

lemma ContinuousMap.measurable_iff_eval {Î± : Type*} [MeasurableSpace Î±]
    [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y]
    (g : Î± â†’ C(X, Y)) :
    Measurable g â†” âˆ€ (x : X), Measurable fun (a : Î±) â†¦ g a x := by
  simp_rw [ContinuousMap.measurableSpace_eq_iSup_comap_eval, measurable_iff_comap_le,
    MeasurableSpace.comap_iSup, iSup_le_iff, MeasurableSpace.comap_comp, Function.comp_def]

end ContinuousMap.MeasurableSpace

def MeasurableEquiv.continuousMap : {f : â„â‰¥0 â†’ â„ // Continuous f} â‰ƒáµ C(â„â‰¥0, â„) where
  toFun := fun f â†¦ ContinuousMap.mk f.1 f.2
  invFun := fun f â†¦ âŸ¨f, f.continuousâŸ©
  left_inv f := rfl
  right_inv f := rfl
  measurable_toFun := by
    simp only [Equiv.coe_fn_mk]
    rw [ContinuousMap.measurable_iff_eval]
    intro x
    simp only [ContinuousMap.coe_mk]
    revert x
    rw [â† measurable_pi_iff]
    exact measurable_subtype_coe
  measurable_invFun := by
    simp only [Equiv.coe_fn_symm_mk]
    refine Measurable.subtype_mk ?_
    rw [measurable_pi_iff]
    exact fun _ â†¦ Continuous.measurable (by fun_prop)


noncomputable
def wienerMeasure : Measure C(â„â‰¥0, â„) := wienerMeasureAux.map MeasurableEquiv.continuousMap

end Measure

end ProbabilityTheory
