/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import BrownianMotion.Continuity.KolmogorovChentsov
import BrownianMotion.Gaussian.GaussianProcess
import BrownianMotion.Gaussian.Moment
import BrownianMotion.Gaussian.ProjectiveLimit
import Mathlib.Probability.Independence.BoundedContinuousFunction
import Mathlib.Topology.ContinuousMap.SecondCountableSpace

/-!
# Brownian motion

-/

open MeasureTheory NNReal WithLp Finset MeasurableSpace Filtration Filter
open scoped ENNReal NNReal Topology BoundedContinuousFunction

variable {T Œ© E : Type*} {mŒ© : MeasurableSpace Œ©} {P : Measure Œ©}

namespace Finset

/-! ### Indexing the elements of a finset in order -/

variable [LinearOrder T] (I : Finset T)

/-- Given a finite set `I : Finset T` of cardinality `n`, `ofFin : Fin #I ‚Üí T`
is the map `(t‚ÇÅ, ..., t‚Çô)`, where `t‚ÇÅ < ... < t‚Çô` are the elements of `I`. -/
noncomputable def ofFin (i : Fin #I) : T := (I.sort (¬∑ ‚â§ ¬∑)).get
  (Fin.cast (I.length_sort (¬∑ ‚â§ ¬∑)).symm i)

lemma monotone_ofFin : Monotone I.ofFin :=
  fun i j hij ‚Ü¶ (I.pairwise_sort (¬∑ ‚â§ ¬∑)).rel_get_of_le (by simpa)

lemma ofFin_mem (i : Fin #I) : I.ofFin i ‚àà I := I.mem_sort (¬∑ ‚â§ ¬∑) |>.1 <| List.get_mem _ _

/-- Given a finite set `I : Finset T`, and `t : I`,
`I.toFin t` returns the position of `t` in `I`. -/
noncomputable def toFin (i : I) : Fin #I :=
  haveI : NeZero #I := ‚ü®Nonempty.card_ne_zero ‚ü®i.1, i.2‚ü©‚ü©
  Fin.ofNat #I ((I.sort (¬∑ ‚â§ ¬∑)).idxOf i.1)

@[simp]
lemma ofFin_toFin (i : I) : I.ofFin (I.toFin i) = i.1 := by
  rw [toFin, ofFin]
  nth_rw 2 [‚Üê (I.sort (¬∑ ‚â§ ¬∑)).idxOf_get (a := i.1)]
  ¬∑ congr
    ext
    simp only [Fin.ofNat_eq_cast, Fin.val_cast, Fin.val_natCast]
    rw [Nat.mod_eq_of_lt]
    rw [‚Üê I.length_sort (¬∑ ‚â§ ¬∑)]
    exact List.idxOf_lt_length_of_mem <| I.mem_sort (¬∑ ‚â§ ¬∑) |>.2 i.2
  exact List.idxOf_lt_length_of_mem <| I.mem_sort (¬∑ ‚â§ ¬∑) |>.2 i.2

@[simp]
lemma toFin_ofFin (i : Fin #I) : I.toFin ‚ü®I.ofFin i, ofFin_mem I i‚ü© = i := by
  simp_rw [toFin, ofFin]
  rw [List.get_idxOf (sort_nodup ..)]
  simp

section Bot

variable [Bot T]

/-- Given a finite set `I : Finset T` of cardinality `n`, `ofFin' : Fin (#I + 1) ‚Üí T`
is the map `(‚ä•, t‚ÇÅ, ..., t‚Çô)`, where `t‚ÇÅ < ... < t‚Çô` are the elements of `I`. -/
noncomputable def ofFin' (i : Fin (#I + 1)) : T :=
  if h : i = 0
    then ‚ä•
    else I.ofFin (i.pred h)

@[simp]
lemma ofFin'_zero : I.ofFin' 0 = ‚ä• := rfl

lemma ofFin'_of_ne_zero (i : Fin (#I + 1)) (hi : i ‚â† 0) :
    I.ofFin' i = I.ofFin (i.pred hi) := by rw [ofFin', dif_neg hi]

@[simp]
lemma ofFin'_succ (i : Fin #I) :
    I.ofFin' i.succ = I.ofFin i := by
  rw [ofFin'_of_ne_zero, Fin.pred_succ]
  simp

lemma ofFin'_mem (i : Fin (#I + 1)) (hi : i ‚â† 0) : I.ofFin' i ‚àà I := by
  rw [ofFin'_of_ne_zero _ _ hi]
  exact ofFin_mem I _

end Bot

lemma monotone_ofFin' [OrderBot T] : Monotone (I.ofFin') := by
  intro i j hij
  obtain rfl | hi := eq_or_ne i 0
  ¬∑ simp
  rw [ofFin'_of_ne_zero I i hi, ofFin'_of_ne_zero I j (by aesop)]
  apply monotone_ofFin
  simpa

end Finset

namespace ProbabilityTheory

section Increments

/-! ### Independent increments -/

/-- A process `X : T ‚Üí Œ© ‚Üí E` has independent increments if for any `n ‚â• 1` and `t‚ÇÅ ‚â§ ... ‚â§ t‚Çô`,
the random variables `X t‚ÇÇ - X t‚ÇÅ, ..., X t‚Çô - X t‚Çô‚Çã‚ÇÅ` are independent. -/
def HasIndepIncrements [Preorder T] [Sub E] [MeasurableSpace E] (X : T ‚Üí Œ© ‚Üí E)
    (P : Measure Œ© := by volume_tac) :
    Prop :=
  ‚àÄ n, ‚àÄ t : Fin (n + 1) ‚Üí T, Monotone t ‚Üí
    iIndepFun (fun (i : Fin n) œâ ‚Ü¶ X (t i.succ) œâ - X (t i.castSucc) œâ) P

/-- `incrementsToRestrict I` is a continuous linear map `f` such that
`f (x‚Çú‚ÇÅ, x‚Çú‚ÇÇ - x‚Çú‚ÇÅ, ..., x‚Çú‚Çô - x‚Çú‚Çô‚Çã‚ÇÅ) = (x‚Çú‚ÇÅ, ..., x‚Çú‚Çô)`. -/
noncomputable def incrementsToRestrict [LinearOrder T] (R : Type*) [Semiring R] [AddCommMonoid E]
    [Module R E] [TopologicalSpace E] [ContinuousAdd E] (I : Finset T) :
    (Fin #I ‚Üí E) ‚ÜíL[R] (I ‚Üí E) :=
  { toFun x i := ‚àë j ‚â§ I.toFin i, x j
    map_add' x y := by ext; simp [sum_add_distrib]
    map_smul' m x := by ext; simp [smul_sum]
    cont := by fun_prop }

lemma incrementsToRestrict_increments_ofFin'_ae_eq_restrict [LinearOrder T] (R : Type*) [OrderBot T]
    [Semiring R] [AddCommGroup E] [Module R E] [TopologicalSpace E] [ContinuousAdd E]
    {X : T ‚Üí Œ© ‚Üí E} (h : ‚àÄ·µê œâ ‚àÇP, X ‚ä• œâ = 0) (I : Finset T) :
    (fun œâ ‚Ü¶ I.restrict (X ¬∑ œâ)) =·µê[P]
      (incrementsToRestrict R I) ‚àò
        (fun œâ i ‚Ü¶ X (I.ofFin' i.succ) œâ - X (I.ofFin' i.castSucc) œâ) := by
  filter_upwards [h] with œâ hœâ
  ext t
  simp only [restrict, incrementsToRestrict, ContinuousLinearMap.coe_mk', LinearMap.coe_mk,
    AddHom.coe_mk, Function.comp_apply]
  rw [Fin.sum_Iic_sub (I.toFin t) (fun j ‚Ü¶ X (I.ofFin' j) œâ)]
  simp [hœâ]

lemma HasIndepIncrements.indepFun_sub_sub [Preorder T] [MeasurableSpace E] [AddGroup E]
    {X : T ‚Üí Œ© ‚Üí E} (h : HasIndepIncrements X P) {r s t : T} (hrs : r ‚â§ s) (hst : s ‚â§ t) :
    IndepFun (X s - X r) (X t - X s) P := by
  let œÑ : Fin (2 + 1) ‚Üí T := ![r, s, t]
  have hœÑ : Monotone œÑ := by
    intro i j hij
    fin_cases i <;> fin_cases j
    any_goals simp only [Nat.reduceAdd, Fin.zero_eta, Fin.isValue, Matrix.cons_val_zero, le_refl, œÑ]
    any_goals assumption
    any_goals contradiction
    exact hrs.trans hst
  have h' : (0 : Fin (1 + 1)) ‚â† (1 : Fin (1 + 1)) := by simp
  simpa using (h 2 œÑ hœÑ).indepFun h'

lemma HasIndepIncrements.indepFun_eval_sub [Preorder T] [MeasurableSpace E] [AddGroup E]
    {X : T ‚Üí Œ© ‚Üí E} (h : HasIndepIncrements X P) {r s t : T} (hrs : r ‚â§ s) (hst : s ‚â§ t)
    (hX : ‚àÄ·µê œâ ‚àÇP, X r œâ = 0) :
    IndepFun (X s) (X t - X s) P := by
  have := h.indepFun_sub_sub hrs hst
  refine this.congr ?_ .rfl
  filter_upwards [hX] with œâ hœâ using by simp [hœâ]

/-- A stochastic process `X` with independent increments and such that `X t` is gaussian for
all `t` is a Gaussian process. -/
lemma HasIndepIncrements.isGaussianProcess [LinearOrder T] [OrderBot T]
    [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
    [SecondCountableTopology E] [CompleteSpace E]
    {X : T ‚Üí Œ© ‚Üí E} (law : ‚àÄ t, HasGaussianLaw (X t) P) (h_bot : ‚àÄ·µê œâ ‚àÇP, X ‚ä• œâ = 0)
    (incr : HasIndepIncrements X P) :
    IsGaussianProcess X P where
  hasGaussianLaw I := by
    have := (law ‚ä•).isProbabilityMeasure
    obtain rfl | hI := I.eq_empty_or_nonempty
    ¬∑ constructor
      have : P.map (fun œâ ‚Ü¶ Finset.restrict ‚àÖ fun x ‚Ü¶ X x œâ) = .dirac Classical.ofNonempty := by
        ext s -
        apply Subsingleton.set_cases (p := fun s ‚Ü¶ Measure.map _ _ s = _)
        ¬∑ simp
        simp only [measure_univ]
        exact @measure_univ _ _ _
          (Measure.isProbabilityMeasure_map (aemeasurable_pi_lambda _ fun _ ‚Ü¶ (law _).aemeasurable))
      rw [this]
      infer_instance
    have := incrementsToRestrict_increments_ofFin'_ae_eq_restrict ‚Ñù h_bot I
    refine @HasGaussianLaw.congr _ _ _ _ _ _ _ _ _ _ ?_ this.symm
    refine HasGaussianLaw.map ?_ _
    exact (incr _ _ (monotone_ofFin' I)).hasGaussianLaw fun i ‚Ü¶
      incr.indepFun_eval_sub bot_le
        (monotone_ofFin' I (Fin.castSucc_le_succ i)) h_bot
          |>.hasGaussianLaw_sub_of_sub (law _) (law _)

end Increments

section IsPreBrownian

variable (X : ‚Ñù‚â•0 ‚Üí Œ© ‚Üí ‚Ñù)

/-- A stochastic process is called **pre-Brownian** if its finite-dimensional laws are those
of a Brownian motion, see `gaussianProjectiveFamily`. -/
class IsPreBrownian (P : Measure Œ© := by volume_tac) : Prop where
  mk' ::
  hasLaw : ‚àÄ I : Finset ‚Ñù‚â•0, HasLaw (fun œâ ‚Ü¶ I.restrict (X ¬∑ œâ)) (gaussianProjectiveFamily I) P

variable {X} {P : Measure Œ©}

lemma IsPreBrownian.congr {Y : ‚Ñù‚â•0 ‚Üí Œ© ‚Üí ‚Ñù} [hX : IsPreBrownian X P] (h : ‚àÄ t, X t =·µê[P] Y t) :
    IsPreBrownian Y P where
  hasLaw I := by
    refine (hX.hasLaw I).congr ?_
    have : ‚àÄ·µê œâ ‚àÇP, ‚àÄ i : I, X i œâ = Y i œâ := ae_all_iff.2 fun _ ‚Ü¶ h _
    filter_upwards [this] with œâ hœâ using funext fun i ‚Ü¶ (hœâ i).symm

lemma IsPreBrownian.isGaussianProcess [IsPreBrownian X P] : IsGaussianProcess X P where
  hasGaussianLaw I := (IsPreBrownian.hasLaw I).hasGaussianLaw

lemma IsPreBrownian.aemeasurable [hX : IsPreBrownian X P] (t : ‚Ñù‚â•0) : AEMeasurable (X t) P :=
  HasGaussianLaw.aemeasurable (hX.isGaussianProcess.hasGaussianLaw_eval t)

lemma IsPreBrownian.hasLaw_gaussianLimit [IsPreBrownian X P]
    (hX : AEMeasurable (fun œâ ‚Ü¶ (X ¬∑ œâ)) P) :
    HasLaw (fun œâ ‚Ü¶ (X ¬∑ œâ)) gaussianLimit P where
  aemeasurable := hX
  map_eq := by
    refine isProjectiveLimit_gaussianLimit.unique (fun I ‚Ü¶ ?_) |>.symm
    rw [AEMeasurable.map_map_of_aemeasurable (by fun_prop) hX]
    exact (IsPreBrownian.hasLaw I).map_eq

lemma HasLaw.isPreBrownian (hX : HasLaw (fun œâ ‚Ü¶ (X ¬∑ œâ)) gaussianLimit P) :
    IsPreBrownian X P where
  hasLaw _ := hasLaw_restrict_gaussianLimit.comp hX

lemma IsPreBrownian.hasLaw_eval [h : IsPreBrownian X P] (t : ‚Ñù‚â•0) :
    HasLaw (X t) (gaussianReal 0 t) P :=
  (hasLaw_eval_gaussianProjectiveFamily ‚ü®t, by simp‚ü©).comp (h.hasLaw {t})

lemma IsPreBrownian.hasLaw_sub [IsPreBrownian X P] (s t : ‚Ñù‚â•0) :
    HasLaw (X s - X t) (gaussianReal 0 (max (s - t) (t - s))) P :=
  (hasLaw_eval_sub_eval_gaussianProjectiveFamily {s, t} ‚ü®s, by simp‚ü© ‚ü®t, by simp‚ü©).comp
    (IsPreBrownian.hasLaw _)

lemma IsPreBrownian.integral_eval [h : IsPreBrownian X P] (t : ‚Ñù‚â•0) :
    P[X t] = 0 := by
  rw [(h.hasLaw_eval t).integral_eq, integral_id_gaussianReal]

lemma IsPreBrownian.covariance_eval [h : IsPreBrownian X P] (s t : ‚Ñù‚â•0) :
    cov[X s, X t; P] = min s t := by
  convert (h.hasLaw {s, t}).covariance_comp
    (f := Function.eval ‚ü®s, by simp‚ü©) (g := Function.eval ‚ü®t, by simp‚ü©) ?_ ?_
  ¬∑ rw [covariance_eval_gaussianProjectiveFamily]
  all_goals exact Measurable.aemeasurable (by fun_prop)

lemma IsPreBrownian.covariance_fun_eval [h : IsPreBrownian X P] (s t : ‚Ñù‚â•0) :
    cov[fun œâ ‚Ü¶ X s œâ, fun œâ ‚Ü¶ X t œâ; P] = min s t :=
  h.covariance_eval s t

lemma IsPreBrownian.isAEKolmogorovProcess {n : ‚Ñï} (hn : 0 < n) [h : IsPreBrownian X P] :
    IsAEKolmogorovProcess X P (2 * n) n (Nat.doubleFactorial (2 * n - 1)) := by
  let Y t œâ := (h.aemeasurable t).mk (X t) œâ
  have hXY t := (h.aemeasurable t).ae_eq_mk
  have hY := h.congr hXY
  refine ‚ü®Y, ?_, ?_‚ü©
  constructor
  ¬∑ intro s t
    rw [‚Üê BorelSpace.measurable_eq]
    refine Measurable.prodMk (h.aemeasurable s).measurable_mk (h.aemeasurable t).measurable_mk
  rotate_left
  ¬∑ positivity
  ¬∑ positivity
  ¬∑ exact fun t ‚Ü¶ (h.aemeasurable t).ae_eq_mk
  refine fun s t ‚Ü¶ Eq.le ?_
  norm_cast
  simp_rw [edist_dist, Real.dist_eq]
  change ‚à´‚Åª œâ, (fun x ‚Ü¶ (ENNReal.ofReal |x|) ^ (2 * n))
    ((Y s - Y t) œâ) ‚àÇ_ = _
  rw [(hY.hasLaw_sub s t).lintegral_comp (f := fun x ‚Ü¶ (ENNReal.ofReal |x|) ^ (2 * n))
    (by fun_prop)]
  simp_rw [‚Üê fun x ‚Ü¶ ENNReal.ofReal_pow (abs_nonneg x)]
  rw [‚Üê ofReal_integral_eq_lintegral_ofReal]
  ¬∑ simp_rw [pow_two_mul_abs]
    rw [‚Üê centralMoment_of_integral_id_eq_zero _ (by simp), ‚Üê NNReal.sq_sqrt (max _ _),
    centralMoment_fun_two_mul_gaussianReal, ENNReal.ofReal_mul (by positivity), mul_comm]
    norm_cast
    rw [pow_mul, NNReal.sq_sqrt, ‚Üê ENNReal.ofReal_pow dist_nonneg, ‚Üê NNReal.nndist_eq,
      NNReal.coe_pow, coe_nndist]
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    apply MemLp.integrable_norm_pow'
    exact IsGaussian.memLp_id _ _ (ENNReal.natCast_ne_top (2 * n))
  ¬∑ exact ae_of_all _ fun _ ‚Ü¶ by positivity

/-- If `X` is a pre-Brownian process then there exists a modification of `X` which is measurable
and locally Œ≤-H√∂lder for `0 < Œ≤ < 1/2` (and thus continuous). See `IsPreBrownian.mk`. -/
lemma IsPreBrownian.exists_continuous_modification [h : IsPreBrownian X P] :
    ‚àÉ Y : ‚Ñù‚â•0 ‚Üí Œ© ‚Üí ‚Ñù, (‚àÄ t, Measurable (Y t)) ‚àß (‚àÄ t, Y t =·µê[P] X t)
      ‚àß ‚àÄ œâ t (Œ≤ : ‚Ñù‚â•0) (_ : 0 < Œ≤) (_ : Œ≤ < ‚®Ü n, (((n + 2 : ‚Ñï) : ‚Ñù) - 1) / (2 * (n + 2 : ‚Ñï))),
        ‚àÉ U ‚àà ùìù t, ‚àÉ C, HolderOnWith C Œ≤ (Y ¬∑ œâ) U :=
  haveI := h.isGaussianProcess.isProbabilityMeasure
  exists_modification_holder_iSup isCoverWithBoundedCoveringNumber_Ico_nnreal
    (fun n ‚Ü¶ h.isAEKolmogorovProcess (by positivity : 0 < n + 2))
    (fun n ‚Ü¶ by finiteness) zero_lt_one (fun n ‚Ü¶ by simp; norm_cast; omega)

/-- If `h : IsPreBrownian X P`, then `h.mk X` is a continuous modification of `X`. -/
protected noncomputable def IsPreBrownian.mk (X) [h : IsPreBrownian X P] : ‚Ñù‚â•0 ‚Üí Œ© ‚Üí ‚Ñù :=
  h.exists_continuous_modification.choose

lemma IsPreBrownian.memHolder_mk [h : IsPreBrownian X P] (œâ : Œ©) (t : ‚Ñù‚â•0) (Œ≤ : ‚Ñù‚â•0)
    (hŒ≤_pos : 0 < Œ≤) (hŒ≤_lt : Œ≤ < 2‚Åª¬π) :
    ‚àÉ U ‚àà ùìù t, ‚àÉ C, HolderOnWith C Œ≤ (h.mk X ¬∑ œâ) U := by
  convert h.exists_continuous_modification.choose_spec.2.2 œâ t Œ≤ hŒ≤_pos ?_
  suffices ‚®Ü n, (((n + 2 : ‚Ñï) : ‚Ñù) - 1) / (2 * (n + 2 : ‚Ñï)) = 2‚Åª¬π by rw [this]; norm_cast
  refine iSup_eq_of_forall_le_of_tendsto (F := Filter.atTop) (fun n ‚Ü¶ ?_) ?_
  ¬∑ calc
    ((‚Üë(n + 2) : ‚Ñù) - 1) / (2 * ‚Üë(n + 2)) = 2‚Åª¬π * (n + 1) / (n + 2) := by
      simp only [Nat.cast_add, Nat.cast_ofNat]; field_simp; ring
    _ ‚â§ 2‚Åª¬π * 1 := by grw [mul_div_assoc, (div_le_one‚ÇÄ (by positivity)).2]; linarith
    _ = 2‚Åª¬π := mul_one _
  ¬∑ have : (fun n : ‚Ñï ‚Ü¶ ((‚Üë(n + 2) : ‚Ñù) - 1) / (2 * ‚Üë(n + 2))) =
        (fun n : ‚Ñï ‚Ü¶ 2‚Åª¬π * ((n : ‚Ñù) / (n + 1))) ‚àò (fun n ‚Ü¶ n + 1) := by
      ext n
      simp only [Nat.cast_add, Nat.cast_ofNat, Function.comp_apply, Nat.cast_one]
      field_simp
      ring
    rw [this]
    refine Filter.Tendsto.comp ?_ (Filter.tendsto_add_atTop_nat 1)
    nth_rw 2 [‚Üê mul_one 2‚Åª¬π]
    exact (tendsto_natCast_div_add_atTop (1 : ‚Ñù)).const_mul _

@[fun_prop]
lemma IsPreBrownian.measurable_mk [h : IsPreBrownian X P] (t : ‚Ñù‚â•0) :
    Measurable (h.mk X t) :=
  h.exists_continuous_modification.choose_spec.1 t

lemma IsPreBrownian.mk_ae_eq [h : IsPreBrownian X P] (t : ‚Ñù‚â•0) :
    h.mk X t =·µê[P] X t :=
  h.exists_continuous_modification.choose_spec.2.1 t

lemma IsPreBrownian.continuous_mk [h : IsPreBrownian X P] (œâ : Œ©) :
    Continuous (h.mk X ¬∑ œâ) := by
  refine continuous_iff_continuousAt.mpr fun t ‚Ü¶ ?_
  obtain ‚ü®U, hu_mem, ‚ü®C, h‚ü©‚ü© := h.memHolder_mk œâ t 4‚Åª¬π (by norm_num)
    (NNReal.inv_lt_inv (by norm_num) (by norm_num))
  exact (h.continuousOn (by norm_num)).continuousAt hu_mem

lemma IsPreBrownian.hasIndepIncrements [h : IsPreBrownian X P] : HasIndepIncrements X P := by
  have : IsProbabilityMeasure P := h.isGaussianProcess.isProbabilityMeasure
  refine fun n t ht ‚Ü¶ h.isGaussianProcess.hasGaussianLaw_increments.iIndepFun_of_covariance_eq_zero
    fun i j hij ‚Ü¶ ?_
  rw [covariance_fun_sub_left, covariance_fun_sub_right, covariance_fun_sub_right]
  ¬∑ simp_rw [IsPreBrownian.covariance_fun_eval]
    wlog h' : i < j generalizing i j
    ¬∑ simp_rw [‚Üê this j i hij.symm (by omega), min_comm]
      ring
    have h1 : i.succ ‚â§ j.succ := Fin.strictMono_succ h' |>.le
    have h2 : i.castSucc ‚â§ j.succ := Fin.le_of_lt h1
    have h3 : i.castSucc ‚â§ j.castSucc := Fin.le_castSucc_iff.mpr h1
    rw [min_eq_left (ht h1), min_eq_left (ht h'), min_eq_left (ht h2), min_eq_left (ht h3)]
    simp
  any_goals exact (h.isGaussianProcess.hasGaussianLaw_eval _).memLp_two
  exact h.isGaussianProcess.hasGaussianLaw_sub.memLp_two

lemma IsGaussianProcess.isPreBrownian_of_covariance (h1 : IsGaussianProcess X P)
    (h2 : ‚àÄ t, P[X t] = 0) (h3 : ‚àÄ s t, s ‚â§ t ‚Üí cov[X s, X t; P] = s) :
    IsPreBrownian X P where
  hasLaw I := by
    refine ‚ü®aemeasurable_pi_lambda _ fun _ ‚Ü¶ h1.aemeasurable _, ?_‚ü©
    apply (MeasurableEquiv.toLp 2 (_ ‚Üí ‚Ñù)).map_measurableEquiv_injective
    rw [MeasurableEquiv.coe_toLp, ‚Üê PiLp.coe_symm_continuousLinearEquiv 2 ‚Ñù]
    have : IsGaussian
        (Measure.map (‚áë(PiLp.continuousLinearEquiv 2 ‚Ñù fun a ‚Ü¶ ‚Ñù).symm)
        (Measure.map (fun œâ ‚Ü¶ I.restrict fun x ‚Ü¶ X x œâ) P)) := by
      have := (h1.hasGaussianLaw I).isGaussian_map
      infer_instance
    apply IsGaussian.ext
    ¬∑ rw [integral_map, integral_map, integral_map]
      ¬∑ simp only [PiLp.continuousLinearEquiv_symm_apply, id_eq]
        simp_rw [‚Üê PiLp.continuousLinearEquiv_symm_apply 2 ‚Ñù, ‚Üê ContinuousLinearEquiv.coe_coe]
        rw [ContinuousLinearMap.integral_comp_id_comm, integral_id_gaussianProjectiveFamily,
          ContinuousLinearMap.integral_comp_comm]
        ¬∑ simp only [ContinuousLinearEquiv.coe_coe, PiLp.continuousLinearEquiv_symm_apply]
          congr with i
          rw [eval_integral]
          ¬∑ simpa using h2 _
          ¬∑ exact fun _ ‚Ü¶ (h1.hasGaussianLaw_eval _).integrable
        ¬∑ exact Integrable.of_eval fun _ ‚Ü¶ (h1.hasGaussianLaw_eval _).integrable
        ¬∑ exact IsGaussian.integrable_id
      any_goals fun_prop
      exact aemeasurable_pi_lambda _ fun _ ‚Ü¶ h1.aemeasurable _
    ¬∑ rw [‚Üê ContinuousLinearMap.toBilinForm_inj]
      refine LinearMap.BilinForm.ext_of_isSymm isSymm_covarianceBilin isSymm_covarianceBilin
        fun x ‚Ü¶ ?_
      simp only [ContinuousLinearMap.toBilinForm_apply]
      have : IsFiniteMeasure (Measure.map (fun œâ ‚Ü¶ I.restrict fun x ‚Ü¶ X x œâ) P) := by
        have := (h1.hasGaussianLaw I).isGaussian_map
        infer_instance
      rw [PiLp.coe_symm_continuousLinearEquiv, covarianceBilin_apply_pi, covarianceBilin_apply_pi]
      ¬∑ congrm ‚àë i, ‚àë j, _ * ?_
        rw [covariance_eval_gaussianProjectiveFamily, covariance_map]
        ¬∑ wlog hij : i.1 ‚â§ j.1 generalizing i j
          ¬∑ rw [covariance_comm, this j i (by grind), min_comm]
          rw [min_eq_left hij]
          exact h3 i j hij
        any_goals exact Measurable.aestronglyMeasurable (by fun_prop)
        exact aemeasurable_pi_lambda _ (fun _ ‚Ü¶ h1.aemeasurable _)
      ¬∑ exact fun i ‚Ü¶ (IsGaussian.hasGaussianLaw_id.eval i).memLp_two
      ¬∑ exact fun i ‚Ü¶ ((h1.hasGaussianLaw I).isGaussian_map.hasGaussianLaw_id.eval i).memLp_two

lemma HasIndepIncrements.isPreBrownian_of_hasLaw
    (law : ‚àÄ t, HasLaw (X t) (gaussianReal 0 t) P) (incr : HasIndepIncrements X P) :
    IsPreBrownian X P := by
  apply IsGaussianProcess.isPreBrownian_of_covariance
  ¬∑ exact incr.isGaussianProcess (fun t ‚Ü¶ (law t).hasGaussianLaw)
      (law 0).ae_eq_const_of_gaussianReal
  ¬∑ intro t
    rw [(law t).integral_eq, integral_id_gaussianReal]
  ¬∑ intro s t hst
    have h1 := incr.indepFun_eval_sub (zero_le s) hst (law 0).ae_eq_const_of_gaussianReal
    have := (law 0).isProbabilityMeasure_iff.2 inferInstance
    have h2 : X t = X t - X s + X s := by simp
    rw [h2, covariance_add_right, h1.covariance_eq_zero, covariance_self, (law s).variance_eq,
      variance_id_gaussianReal]
    ¬∑ simp
    ¬∑ exact (law s).aemeasurable
    ¬∑ exact (law s).hasGaussianLaw.memLp_two
    ¬∑ exact (law t).hasGaussianLaw.memLp_two.sub (law s).hasGaussianLaw.memLp_two
    ¬∑ exact (law s).hasGaussianLaw.memLp_two
    ¬∑ exact (law t).hasGaussianLaw.memLp_two.sub (law s).hasGaussianLaw.memLp_two
    ¬∑ exact (law s).hasGaussianLaw.memLp_two

lemma IsPreBrownian.smul [hX : IsPreBrownian X P] {c : ‚Ñù‚â•0} (hc : c ‚â† 0) :
    IsPreBrownian (fun t œâ ‚Ü¶ (X (c * t) œâ) / ‚àöc) P := by
  refine IsGaussianProcess.isPreBrownian_of_covariance ?_ (fun t ‚Ü¶ ?_) (fun s t hst ‚Ü¶ ?_)
  ¬∑ have this t œâ : X (c * t) œâ / ‚àöc = (1 / ‚àöc) ‚Ä¢ ((X ‚àò (c * ¬∑)) t œâ) := by
      simp [inv_mul_eq_div]
    simp_rw [this]
    exact (IsGaussianProcess.comp_right hX.isGaussianProcess _).smul _
  ¬∑ rw [integral_div, IsPreBrownian.integral_eval, zero_div]
  ¬∑ rw [covariance_fun_div_left, covariance_fun_div_right, IsPreBrownian.covariance_eval,
      min_eq_left]
    ¬∑ simp [field]
    ¬∑ exact mul_le_mul_right hst c

/-- **Weak Markov property**: If `X` is a pre-Brownian motion, then
`X (t‚ÇÄ + t) - X t‚ÇÄ` is a pre-Brownian motion which is independent from `(B t, t ‚â§ t‚ÇÄ)`.
This is the proof that it is pre-Brownian, see `IsPreBrownian.indepFun_shift` for independence. -/
lemma IsPreBrownian.shift [h : IsPreBrownian X P] (t‚ÇÄ : ‚Ñù‚â•0) :
    IsPreBrownian (fun t œâ ‚Ü¶ X (t‚ÇÄ + t) œâ - X t‚ÇÄ œâ) P := by
  refine (h.isGaussianProcess.shift t‚ÇÄ).isPreBrownian_of_covariance (fun t ‚Ü¶ ?_) (fun s t hst ‚Ü¶ ?_)
  ¬∑ rw [integral_sub, IsPreBrownian.integral_eval, IsPreBrownian.integral_eval, sub_zero]
    all_goals exact (h.isGaussianProcess.hasGaussianLaw_eval _).integrable
  ¬∑ have := h.isGaussianProcess.isProbabilityMeasure
    rw [covariance_fun_sub_left, covariance_fun_sub_right, covariance_fun_sub_right,
      h.covariance_eval, h.covariance_eval, h.covariance_eval, h.covariance_eval, ‚Üê add_min,
      min_eq_left hst, min_eq_right, min_eq_left, min_self]
    any_goals simp
    any_goals exact (h.isGaussianProcess.hasGaussianLaw_eval _).memLp_two
    exact h.isGaussianProcess.hasGaussianLaw_sub.memLp_two

/-- **Weak Markov property**: If `X` is a pre-Brownian motion, then
`X (t‚ÇÄ + t) - X t‚ÇÄ` is a pre-Brownian motion which is independent from `(B t, t ‚â§ t‚ÇÄ)`.
This is the proof that of independence, see `IsPreBrownian.shift` for the proof
that it is pre-Brownian. -/
lemma IsPreBrownian.indepFun_shift [h : IsPreBrownian X P] (hX : ‚àÄ t, Measurable (X t)) (t‚ÇÄ : ‚Ñù‚â•0) :
    IndepFun (fun œâ t ‚Ü¶ X (t‚ÇÄ + t) œâ - X t‚ÇÄ œâ) (fun œâ (t : Set.Iic t‚ÇÄ) ‚Ü¶ X t œâ) P := by
  apply IsGaussianProcess.indepFun''
  ¬∑ apply h.isGaussianProcess.of_isGaussianProcess
    rintro (t | ‚ü®t, ht‚ü©)
    ¬∑ let L : (({t‚ÇÄ, t‚ÇÄ + t} : Finset ‚Ñù‚â•0) ‚Üí ‚Ñù) ‚ÜíL[‚Ñù] ‚Ñù :=
        { toFun x := x ‚ü®t‚ÇÄ + t, by simp‚ü© - x ‚ü®t‚ÇÄ, by simp‚ü©
          map_add' x y := by simp; abel
          map_smul' c x := by simp; ring }
      exact ‚ü®_, L, fun œâ ‚Ü¶ by simp [L]‚ü©
    ¬∑ let L : (({t} : Finset ‚Ñù‚â•0) ‚Üí ‚Ñù) ‚ÜíL[‚Ñù] ‚Ñù :=
        { toFun x := x ‚ü®t, by simp‚ü©
          map_add' x y := by simp
          map_smul' c x := by simp }
      exact ‚ü®_, L, fun œâ ‚Ü¶ by simp [L]‚ü©
  any_goals fun_prop
  ¬∑ rintro s ‚ü®t, ht : t ‚â§ t‚ÇÄ‚ü©
    have := h.isGaussianProcess.isProbabilityMeasure
    rw [covariance_fun_sub_left, h.covariance_eval, h.covariance_eval, min_eq_right, min_eq_right,
      sub_self]
    ¬∑ grind
    ¬∑ simp [ht, le_add_right]
    all_goals exact (h.isGaussianProcess.hasGaussianLaw_eval _).memLp_two

lemma IsPreBrownian.inv [h : IsPreBrownian X P] :
    IsPreBrownian (fun t œâ ‚Ü¶ t * (X (1 / t) œâ)) P := by
  refine IsGaussianProcess.isPreBrownian_of_covariance ?_ (fun t ‚Ü¶ ?_) (fun s t hst ‚Ü¶ ?_)
  ¬∑ exact (IsGaussianProcess.comp_right h.isGaussianProcess _).smul _
  ¬∑ rw [integral_const_mul, IsPreBrownian.integral_eval, mul_zero]
  ¬∑ have := h.isGaussianProcess.isProbabilityMeasure
    rw [covariance_const_mul_left, covariance_const_mul_right, h.covariance_eval]
    obtain rfl | hs := eq_or_ne s 0
    ¬∑ simp
    have : 0 < t := (pos_of_ne_zero hs).trans_le hst
    rw [min_eq_right]
    ¬∑ norm_cast
      field_simp
    exact one_div_le_one_div_of_le (pos_of_ne_zero hs) hst

end IsPreBrownian

section IsBrownian

variable (X : ‚Ñù‚â•0 ‚Üí Œ© ‚Üí ‚Ñù)

/-- A stochastic process is called **Brownian** if its finite-dimensional laws are those
of a Brownian motion, see `IsPreBrownian`, and if it has almost-sure continuous paths. -/
class IsBrownian (X) (P : Measure Œ© := by volume_tac) : Prop extends IsPreBrownian X P where
  cont : ‚àÄ·µê œâ ‚àÇP, Continuous (X ¬∑ œâ)

variable {X}

instance IsPreBrownian.isBrownian_mk [h : IsPreBrownian X P] :
    IsBrownian (h.mk X) P where
  toIsPreBrownian := h.congr fun _ ‚Ü¶ (h.mk_ae_eq _).symm
  cont := ae_of_all _ h.continuous_mk

lemma IsBrownian.smul [h : IsBrownian X P] {c : ‚Ñù‚â•0} (hc : c ‚â† 0) :
    IsBrownian (fun t œâ ‚Ü¶ (X (c * t) œâ) / ‚àöc) P where
  toIsPreBrownian := h.toIsPreBrownian.smul hc
  cont := by
    filter_upwards [h.cont] with œâ h
    fun_prop

lemma IsBrownian.shift [h : IsBrownian X P] (t‚ÇÄ : ‚Ñù‚â•0) :
    IsBrownian (fun t œâ ‚Ü¶ X (t‚ÇÄ + t) œâ - X t‚ÇÄ œâ) P where
  toIsPreBrownian := h.toIsPreBrownian.shift t‚ÇÄ
  cont := by
    filter_upwards [h.cont] with œâ h
    fun_prop

/-- If `X` is a Brownian motion then so is `fun t œâ ‚Ü¶ t * (B (1 / t) œâ)`. -/
lemma IsBrownian.inv [h : IsBrownian X P] :
    IsBrownian (fun t œâ ‚Ü¶ t * (X (1 / t) œâ)) P where
  toIsPreBrownian := h.toIsPreBrownian.inv
  cont := by
    obtain ‚ü®s, cs, ds‚ü© := TopologicalSpace.exists_countable_dense ‚Ñù‚â•0
    let Y := fun t œâ ‚Ü¶ t * X (1 / t) œâ
    have hY : IsPreBrownian Y P := h.toIsPreBrownian.inv
    have h1 : ‚àÄ·µê œâ ‚àÇP, ‚àÄ q : s, Y q œâ = hY.mk Y q œâ :=
      haveI : Countable s := cs
      ae_all_iff.2 fun q ‚Ü¶ (hY.mk_ae_eq q).symm
    have h2 : ‚àÄ·µê œâ ‚àÇP, Set.EqOn (Y ¬∑ œâ) (hY.mk Y ¬∑ œâ) (s \ {0}) := by
      filter_upwards [h1] with œâ hœâ
      rintro t ‚ü®ht, -‚ü©
      exact hœâ ‚ü®t, ht‚ü©
    have h3 : ‚àÄ·µê œâ ‚àÇP, ContinuousOn (Y ¬∑ œâ) {t | t ‚â† 0} := by
      filter_upwards [h.cont] with œâ hœâ
      intro t (ht : t ‚â† 0)
      simp_rw [Y]
      apply ContinuousAt.continuousWithinAt
      fun_prop (disch := positivity)
    have : ‚àÄ·µê œâ ‚àÇP, ‚àÄ t ‚â† 0, Y t œâ = hY.mk Y t œâ := by
      filter_upwards [h2, h3] with œâ h1 h2
      convert h1.of_subset_closure h2 (hY.continuous_mk œâ |>.continuousOn) (by grind) _
      convert Set.subset_univ _
      exact (ds.diff_singleton 0).closure_eq
    have h4 : ‚àÄ·µê œâ ‚àÇP, ‚àÄ t, Y t œâ = hY.mk Y t œâ := by
      filter_upwards [this, (hY.isBrownian_mk.hasLaw_eval 0).ae_eq_const_of_gaussianReal]
        with œâ h1 h2 t
      obtain rfl | ht := eq_or_ne t 0
      ¬∑ simp_all [Y]
      exact h1 t ht
    filter_upwards [h4] with œâ h
    simp_rw [Y] at h
    simp_rw [h]
    exact hY.continuous_mk œâ

lemma IsBrownian.tendsto_nhds_zero [h : IsBrownian X P] :
    ‚àÄ·µê œâ ‚àÇP, Filter.Tendsto (X ¬∑ œâ) (ùìù 0) (ùìù 0) := by
  filter_upwards [h.cont, (h.hasLaw_eval 0).ae_eq_const_of_gaussianReal] with œâ h1 h2
  convert h1.tendsto 0
  exact h2.symm

lemma IsBrownian.tendsto_div_id_atTop [h : IsBrownian X P] :
    ‚àÄ·µê œâ ‚àÇP, Filter.Tendsto (fun t ‚Ü¶ (X t œâ) / t) .atTop (ùìù 0) := by
  filter_upwards [h.inv.tendsto_nhds_zero] with œâ hœâ
  have : (fun t ‚Ü¶ (X t œâ) / t) = (fun t ‚Ü¶ t * (X (1 / t) œâ)) ‚àò (fun t ‚Ü¶ t‚Åª¬π) := by ext; simp [field]
  rw [this]
  exact hœâ.comp tendsto_inv_atTop_zero

/-- **Blumenthal's zero-one law**: Let `ùìï` be the canonical filtration associated to a Brownian
motion. Then the `œÉ`-algebra `‚®Ö s > 0, ùìï s` is trivial. -/
lemma IsBrownian.indep_zero [h : IsBrownian X P] (hX : ‚àÄ t, Measurable (X t))
    (hX' : ‚àÄ œâ, Continuous (X ¬∑ œâ)) {A : Set Œ©}
    (hA : MeasurableSet[‚®Ö s > 0, natural X (fun t ‚Ü¶ (hX t).stronglyMeasurable) s] A) :
    P A = 0 ‚à® P A = 1 := by
  have := h.isGaussianProcess.isProbabilityMeasure
  -- We consider three different `œÉ`-algebras. `m1` is the one generated by the process `X`.
  let m1 : MeasurableSpace Œ© := .comap (fun œâ t ‚Ü¶ X t œâ) inferInstance
  -- `m2` is the one generated by the restriction of `X` to positive real numbers.
  let m2 : MeasurableSpace Œ© := .comap (fun œâ (t : Set.Ioi (0 : ‚Ñù‚â•0)) ‚Ü¶ X t œâ) inferInstance
  -- `m3` is `‚®Ö s > 0, ùìï s`, which we want to show to be trivial.
  set m3 : MeasurableSpace Œ© := ‚®Ö s > 0, natural X (fun t ‚Ü¶ (hX t).stronglyMeasurable) s
-- We easily have that `m3 ‚â§ m1 ‚â§ mŒ©`.
  have hm1 : m1 ‚â§ mŒ© := by
    apply Measurable.comap_le
    apply @measurable_pi_lambda _ _ _ mŒ©
    exact hX
  have hm3 : m3 ‚â§ m1 := by
    apply iInf‚ÇÇ_le_of_le 1 (by simp)
    rw [natural_eq_comap]
    exact comap_le_comap (fun x t ‚Ü¶ x t.1) (by fun_prop) (by ext; simp)
  have hm3' := hm3.trans hm1
  -- Because `X` is continuous, `X t ‚ü∂ X 0` as `t ‚Üí 0‚Å∫`, thus
  -- the random variable `X 0` is actually measurable with respect to `m2`, so `m1 ‚â§ m2`.
  have : m1 ‚â§ m2 := by
    simp_rw [m1, m2, comap_process]
    rw [iSup_split_single _ 0, sup_le_iff]
    constructor; swap
    ¬∑ simp_rw [‚Üê pos_iff_ne_zero, iSup_subtype, Set.mem_Ioi]
      rfl
    rw [‚Üê measurable_iff_comap_le]
    have this : Filter.Tendsto (X ‚àò ((‚Üë) : Set.Ioi (0 : ‚Ñù‚â•0) ‚Üí ‚Ñù‚â•0))
        ((ùìù[‚â†] 0).comap ((‚Üë) : _ ‚Üí ‚Ñù‚â•0)) (ùìù (X 0)) := by
      refine Filter.tendsto_comap'_iff ?_ |>.2
        (tendsto_pi_nhds.2 fun œâ ‚Ü¶ continuousAt_iff_punctured_nhds.1 (hX' œâ).continuousAt)
      convert self_mem_nhdsWithin
      ext; simp [pos_iff_ne_zero]
    have l : NeBot ((ùìù[‚â†] (0 : ‚Ñù‚â•0)).comap ((‚Üë) : Set.Ioi (0 : ‚Ñù‚â•0) ‚Üí ‚Ñù‚â•0)) := by
      refine comap_coe_neBot_of_le_principal <| le_principal_iff.2 ?_
      convert self_mem_nhdsWithin
      ext; simp [pos_iff_ne_zero]
    exact @measurable_of_tendsto_metrizable' _ _ (iSup _) _ _ _ _ _ _ _ _ l _
      (fun t ‚Ü¶ (comap_measurable _).iSup' t) this
  -- We prove the result by showing that `m3` is independent of itself.
  refine measure_eq_zero_or_one_of_indep_self hm3' ?_ hA
  -- To do so, we show that for all `A ‚àà m3`, all finite set `I ‚äÜ (0, +‚àû)` and all
  -- bounded continuous function `f : (I ‚Üí ‚Ñù) ‚Üí ‚Ñù`,
  -- `‚à´ œâ in A, f (fun t ‚Ü¶ X t) ‚àÇP = P.real A * ‚à´ œâ, f (fun t ‚Ü¶ X t) ‚àÇP`.
  refine indep_of_indep_of_le_right ?_ (hm3.trans this)
  refine indep_comap_process_of_bcf hm3' (fun _ ‚Ü¶ hX _) fun A hA I f ‚Ü¶ ?_
  -- If `I` is empty, there is nothing to do.
  obtain rfl | hI := I.eq_empty_or_nonempty
  ¬∑ have : Subsingleton ((‚àÖ : Finset (Set.Ioi (0 : ‚Ñù‚â•0))) ‚Üí ‚Ñù) := inferInstance
    simp [this.eq_zero]
  -- We now assume `I` is not empty. We then prove that for all `Œµ > 0` such that `Œµ ‚â§ min I`,
  -- `‚à´ œâ in A, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP = P.real A * ‚à´ œâ, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP`.
  -- This follows from the fact that, because `A ‚àà m3` in particular `A` is measurable
  -- with respect to `œÉ(X t | t ‚â§ Œµ)`. This `œÉ`-algebra is independent from
  -- `œÉ(X (Œµ + t) - X Œµ | t ‚â• 0)` by the weak Markov property.
  have key (Œµ : ‚Ñù‚â•0) (hŒµ1 : 0 < Œµ) (hŒµ2 : Œµ ‚â§ I.min' hI) :
      ‚à´ œâ in A, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP = P.real A * ‚à´ œâ, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP := by
    rw [IndepSets.setIntegral_eq_mul _ (by fun_prop) (hm3' A hA) (by fun_prop)]
    have := (IndepFun_iff_Indep _ _ _).1 <| h.indepFun_shift hX Œµ |>.symm
    refine indepSets_of_indepSets_of_le_right (Indep.singleton_indepSets this ?_) ?_
    ¬∑ suffices m3 ‚â§ (.comap (fun œâ (t : Set.Iic Œµ) ‚Ü¶ X t œâ) MeasurableSpace.pi) from this A hA
      apply iInf‚ÇÇ_le_of_le Œµ hŒµ1
      rw [natural_eq_comap]
    simp only [Set.setOf_subset_setOf, ‚Üê measurableSpace_le_iff]
    apply comap_le_comap (fun x t ‚Ü¶ x (t.1 - Œµ)) (by fun_prop)
    ext œâ t
    simp only [Function.comp_apply, sub_left_inj]
    rw [add_tsub_cancel_of_le]
    exact hŒµ2.trans (I.min'_le t.1 t.2)
  -- Because `f` is continuous and `X t ‚ü∂ 0` almost surely as `t ‚Üí 0`,
  -- we deduce that almost surely `f (fun t ‚Ü¶ X t - X Œµ) ‚ü∂ f (fun t ‚Ü¶ X t)` as `t ‚Üí 0`.
  have lol : ‚àÄ·µê œâ ‚àÇP, Tendsto (fun Œµ ‚Ü¶ f (fun t ‚Ü¶ X t œâ - X Œµ œâ)) (ùìù[>] 0)
      (ùìù (f (fun t ‚Ü¶ X t œâ))) := by
    filter_upwards [h.tendsto_nhds_zero] with œâ hœâ
    refine f.continuous.tendsto _ |>.comp (tendsto_pi_nhds.2 fun t ‚Ü¶ ?_)
    convert (tendsto_nhdsWithin_of_tendsto_nhds hœâ).const_sub (X t œâ)
    simp
  -- Because `f` is also bounded, we can apply the dominated convergence theorem to show that
  -- `‚à´ œâ in A, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP ‚ü∂ ‚à´ œâ in A, f (fun t ‚Ü¶ X t œâ) ‚àÇP`
  -- as `Œµ ‚Üí 0‚Å∫`.
  have h1 : Tendsto (fun Œµ ‚Ü¶ ‚à´ œâ in A, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP) (ùìù[>] 0)
      (ùìù (‚à´ œâ in A, f (fun t ‚Ü¶ X t œâ) ‚àÇP)) := by
    refine tendsto_integral_filter_of_dominated_convergence (fun _ ‚Ü¶ ‚Äñf‚Äñ) ?_ ?_
      (integrable_const _) (ae_restrict_of_ae lol)
    ¬∑ exact Eventually.of_forall fun _ ‚Ü¶ Measurable.aestronglyMeasurable (by fun_prop)
    ¬∑ exact Eventually.of_forall fun _ ‚Ü¶ ae_of_all _ fun _ ‚Ü¶ f.norm_coe_le_norm _
  -- But similarly we have that
  -- `P.real A * ‚à´ œâ, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP ‚ü∂ P.real A * ‚à´ œâ in A, f (fun t ‚Ü¶ X t œâ) ‚àÇP`
  -- as `Œµ ‚Üí 0‚Å∫`, and we can conclude by uniqueness of the limit.
  refine tendsto_nhds_unique h1 ?_
  refine Tendsto.congr' (f‚ÇÅ := fun Œµ ‚Ü¶ P.real A * ‚à´ œâ, f (fun t ‚Ü¶ X t œâ - X Œµ œâ) ‚àÇP) ?_ ?_
  ¬∑ apply eventually_nhdsGT (I.min' hI |>.2)
    rintro Œµ ‚ü®h1, h2‚ü©
    exact (key Œµ h1 h2).symm
  refine Filter.Tendsto.const_mul (b := P.real A) ?_
  refine tendsto_integral_filter_of_dominated_convergence (fun _ ‚Ü¶ ‚Äñf‚Äñ) ?_ ?_
    (integrable_const _) lol
  ¬∑ exact Eventually.of_forall fun _ ‚Ü¶ Measurable.aestronglyMeasurable (by fun_prop)
  ¬∑ exact Eventually.of_forall fun _ ‚Ü¶ ae_of_all _ fun _ ‚Ü¶ f.norm_coe_le_norm _

end IsBrownian

def preBrownian : ‚Ñù‚â•0 ‚Üí (‚Ñù‚â•0 ‚Üí ‚Ñù) ‚Üí ‚Ñù := fun t œâ ‚Ü¶ œâ t

@[fun_prop]
lemma measurable_preBrownian (t : ‚Ñù‚â•0) : Measurable (preBrownian t) := by
  unfold preBrownian
  fun_prop

lemma hasLaw_preBrownian : HasLaw (fun œâ ‚Ü¶ (preBrownian ¬∑ œâ)) gaussianLimit gaussianLimit where
  aemeasurable := (measurable_pi_lambda _ measurable_preBrownian).aemeasurable
  map_eq := Measure.map_id

instance isPreBrownian_preBrownian : IsPreBrownian preBrownian gaussianLimit :=
  hasLaw_preBrownian.isPreBrownian

-- for blueprint
lemma isGaussianProcess_preBrownian : IsGaussianProcess preBrownian gaussianLimit :=
  isPreBrownian_preBrownian.isGaussianProcess

lemma hasLaw_restrict_preBrownian (I : Finset ‚Ñù‚â•0) :
    HasLaw (fun œâ ‚Ü¶ I.restrict (preBrownian ¬∑ œâ)) (gaussianProjectiveFamily I) gaussianLimit :=
  IsPreBrownian.hasLaw I

lemma hasLaw_preBrownian_eval (t : ‚Ñù‚â•0) :
    HasLaw (preBrownian t) (gaussianReal 0 t) gaussianLimit :=
  IsPreBrownian.hasLaw_eval t

lemma hasLaw_preBrownian_sub (s t : ‚Ñù‚â•0) :
    HasLaw (preBrownian s - preBrownian t) (gaussianReal 0 (max (s - t) (t - s))) gaussianLimit :=
  IsPreBrownian.hasLaw_sub s t

lemma isKolmogorovProcess_preBrownian {n : ‚Ñï} (hn : 0 < n) :
    IsKolmogorovProcess preBrownian gaussianLimit (2 * n) n
      (Nat.doubleFactorial (2 * n - 1)) := by
  constructor
  ¬∑ intro s t
    rw [‚Üê BorelSpace.measurable_eq]
    fun_prop
  rotate_left
  ¬∑ positivity
  ¬∑ positivity
  refine fun s t ‚Ü¶ Eq.le ?_
  norm_cast
  simp_rw [edist_dist, Real.dist_eq]
  change ‚à´‚Åª œâ, (fun x ‚Ü¶ (ENNReal.ofReal |x|) ^ (2 * n))
    ((preBrownian s - preBrownian t) œâ) ‚àÇ_ = _
  rw [(hasLaw_preBrownian_sub s t).lintegral_comp (f := fun x ‚Ü¶ (ENNReal.ofReal |x|) ^ (2 * n))
    (by fun_prop)]
  simp_rw [‚Üê fun x ‚Ü¶ ENNReal.ofReal_pow (abs_nonneg x)]
  rw [‚Üê ofReal_integral_eq_lintegral_ofReal]
  ¬∑ simp_rw [pow_two_mul_abs]
    rw [‚Üê centralMoment_of_integral_id_eq_zero _ (by simp), ‚Üê NNReal.sq_sqrt (max _ _),
    centralMoment_fun_two_mul_gaussianReal, ENNReal.ofReal_mul (by positivity), mul_comm]
    norm_cast
    rw [pow_mul, NNReal.sq_sqrt, ‚Üê ENNReal.ofReal_pow dist_nonneg, ‚Üê NNReal.nndist_eq,
      NNReal.coe_pow, coe_nndist]
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    apply MemLp.integrable_norm_pow'
    exact IsGaussian.memLp_id _ _ (ENNReal.natCast_ne_top (2 * n))
  ¬∑ exact ae_of_all _ fun _ ‚Ü¶ by positivity

noncomputable
def brownian : ‚Ñù‚â•0 ‚Üí (‚Ñù‚â•0 ‚Üí ‚Ñù) ‚Üí ‚Ñù := isPreBrownian_preBrownian.mk

@[fun_prop]
lemma measurable_brownian (t : ‚Ñù‚â•0) : Measurable (brownian t) :=
  IsPreBrownian.measurable_mk t

lemma brownian_ae_eq_preBrownian (t : ‚Ñù‚â•0) :
    brownian t =·µê[gaussianLimit] preBrownian t :=
  IsPreBrownian.mk_ae_eq t

lemma memHolder_brownian (œâ : ‚Ñù‚â•0 ‚Üí ‚Ñù) (t : ‚Ñù‚â•0) (Œ≤ : ‚Ñù‚â•0) (hŒ≤_pos : 0 < Œ≤) (hŒ≤_lt : Œ≤ < 2‚Åª¬π) :
    ‚àÉ U ‚àà ùìù t, ‚àÉ C, HolderOnWith C Œ≤ (brownian ¬∑ œâ) U :=
  IsPreBrownian.memHolder_mk œâ t Œ≤ hŒ≤_pos hŒ≤_lt

@[fun_prop]
lemma continuous_brownian (œâ : ‚Ñù‚â•0 ‚Üí ‚Ñù) : Continuous (brownian ¬∑ œâ) :=
  IsPreBrownian.continuous_mk œâ

instance IsBrownian_brownian : IsBrownian brownian gaussianLimit :=
  IsPreBrownian.isBrownian_mk

-- for blueprint
lemma isGaussianProcess_brownian : IsGaussianProcess brownian gaussianLimit :=
  IsBrownian_brownian.toIsPreBrownian.isGaussianProcess

lemma hasLaw_restrict_brownian {I : Finset ‚Ñù‚â•0} :
    HasLaw (fun œâ ‚Ü¶ I.restrict (brownian ¬∑ œâ)) (gaussianProjectiveFamily I) gaussianLimit :=
  IsPreBrownian.hasLaw I

lemma hasLaw_brownian : HasLaw (fun œâ ‚Ü¶ (brownian ¬∑ œâ)) gaussianLimit gaussianLimit :=
  IsPreBrownian.hasLaw_gaussianLimit
    (measurable_pi_lambda _ fun t ‚Ü¶ measurable_brownian t).aemeasurable

lemma hasLaw_brownian_eval {t : ‚Ñù‚â•0} :
    HasLaw (brownian t) (gaussianReal 0 t) gaussianLimit :=
  IsPreBrownian.hasLaw_eval t

lemma hasLaw_brownian_sub {s t : ‚Ñù‚â•0} :
    HasLaw (brownian s - brownian t) (gaussianReal 0 (max (s - t) (t - s))) gaussianLimit :=
  IsPreBrownian.hasLaw_sub s t

lemma measurable_brownian_uncurry : Measurable brownian.uncurry :=
  measurable_uncurry_of_continuous_of_measurable continuous_brownian measurable_brownian

lemma isKolmogorovProcess_brownian {n : ‚Ñï} (hn : 0 < n) :
    IsKolmogorovProcess brownian gaussianLimit (2 * n) n
      (Nat.doubleFactorial (2 * n - 1)) where
  measurablePair := measurable_pair_of_measurable measurable_brownian
  kolmogorovCondition := (isKolmogorovProcess_preBrownian hn).IsAEKolmogorovProcess.congr
    (fun t ‚Ü¶ (brownian_ae_eq_preBrownian t).symm) |>.kolmogorovCondition
  p_pos := by positivity
  q_pos := by positivity

lemma covariance_brownian (s t : ‚Ñù‚â•0) : cov[brownian s, brownian t; gaussianLimit] = min s t :=
    IsPreBrownian.covariance_eval s t

lemma hasIndepIncrements_brownian : HasIndepIncrements brownian gaussianLimit :=
  IsPreBrownian.hasIndepIncrements

section Measure

noncomputable
def wienerMeasureAux : Measure {f : ‚Ñù‚â•0 ‚Üí ‚Ñù // Continuous f} :=
  gaussianLimit.map (fun œâ ‚Ü¶ (‚ü®fun t ‚Ü¶ brownian t œâ, continuous_brownian œâ‚ü©))

section ContinuousMap.MeasurableSpace

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

instance : MeasurableSpace C(X, Y) := borel _
instance : BorelSpace C(X, Y) where
  measurable_eq := rfl

lemma isClosed_sUnion_of_finite {X : Type*} [TopologicalSpace X] {s : Set (Set X)}
    (h1 : s.Finite) (h2 : ‚àÄ t ‚àà s, IsClosed t) : IsClosed (‚ãÉ‚ÇÄ s) := by
  rw [Set.sUnion_eq_biUnion]
  exact h1.isClosed_biUnion h2

open TopologicalSpace in
lemma ContinuousMap.borel_eq_iSup_comap_eval [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y] :
    borel C(X, Y) = ‚®Ü a : X, (borel Y).comap fun b ‚Ü¶ b a := by
  -- https://math.stackexchange.com/questions/4789531/when-does-the-borel-sigma-algebra-of-compact-convergence-coincide-with-the-pr
  apply le_antisymm
  swap
  ¬∑ refine iSup_le fun x ‚Ü¶ ?_
    simp_rw [‚Üê measurable_iff_comap_le, ‚Üê BorelSpace.measurable_eq]
    exact Continuous.measurable (by fun_prop)
  -- Denote by `M(K, U)` the set of functions `f` such that `Set.MapsTo f K U`. These form a
  -- basis for the compact-open topology when `K` is compact and `U` is open.
  -- Because `C(X, Y)` is second-countable, it suffices to prove that those sets are measurable.
  -- Let therefore `K` be a compact set of `X` and `U` an open set of `Y`.
  rw [borel_eq_generateFrom_of_subbasis ContinuousMap.compactOpen_eq]
  refine generateFrom_le fun s hs ‚Ü¶ ?_
  obtain ‚ü®K, hK, U, hU, hs‚ü© := hs
  rw [‚Üê hs]
  -- Consider `V` a countable basis of the topology on Y.
  let V := countableBasis Y
  have hV : IsTopologicalBasis V := isBasis_countableBasis Y
  have cV : V.Countable := countable_countableBasis Y
  let W‚ÇÅ := {v | v ‚àà V ‚àß closure v ‚äÜ U}
  -- Consider `W` the set of `closure v`, where `v ‚àà V` and `closure v ‚äÜ U`.
  let W := {v | ‚àÉ u ‚àà V, v ‚äÜ U ‚àß v = closure u}
  -- Because `V` is countable, so is `W`.
  have cW : W.Countable := by
    apply (cV.image closure ).mono
    rintro - ‚ü®u, hu, -, rfl‚ü©
    exact ‚ü®u, hu, rfl‚ü©
  -- Because `Y` is regular, we can write that `U = ‚ãÉ_{v ‚àà W} v`.
  have U_eq_sUnion_W : U = ‚ãÉ‚ÇÄ W := by
    ext x
    rw [Set.mem_sUnion]
    constructor
    ¬∑ intro hx
      obtain ‚ü®v, ‚ü®hv1, hv2‚ü©, hv3‚ü© := hV.nhds_basis_closure x |>.mem_iff.1 <| hU.mem_nhds hx
      exact ‚ü®closure v, ‚ü®v, hv2, hv3, rfl‚ü©, subset_closure hv1‚ü©
    ¬∑ rintro ‚ü®-, ‚ü®t, ht1, ht2, rfl‚ü©, hx‚ü©
      exact ht2 hx
  -- Similarly, we can write that `U = ‚ãÉ_{v ‚àà V, closure v ‚äÜ U} v`.
  have U_eq_sUnion_W‚ÇÅ : U = ‚ãÉ‚ÇÄ W‚ÇÅ := by
    ext x
    rw [Set.mem_sUnion]
    refine ‚ü®fun hx ‚Ü¶ ?_, fun ‚ü®t, ‚ü®ht1, ht2‚ü©, hx‚ü© ‚Ü¶ ht2 <| subset_closure hx‚ü©
    obtain ‚ü®v, ‚ü®hv1, hv2‚ü©, hv3‚ü© := hV.nhds_basis_closure x |>.mem_iff.1 <| hU.mem_nhds hx
    exact ‚ü®v, ‚ü®hv2, hv3‚ü©, hv1‚ü©
  -- For any continuous `f` such that `f '' K ‚äÜ U`, because `K` is compact, `f '' K` is compact.
  -- But we just proved that `U = ‚ãÉ_{v ‚àà V, closure v ‚äÜ U} v`, and each `v ‚àà V` is open,
  -- so there exists `J` a finite set of `v ‚àà V` such that `closure v ‚äÜ U` and
  -- `f '' K ‚äÜ ‚ãÉ v ‚àà J, v`. We thus have `f '' K ‚äÜ ‚ãÉ v ‚àà J, closure v`. This is equivalent to
  -- having `I` a finite subset of `W` such that `f '' K ‚äÜ ‚ãÉ v ‚àà I, v`.
  have (f : C(X, Y)) (hf : K.MapsTo f U) : ‚àÉ I, I.Finite ‚àß I ‚äÜ W ‚àß K.MapsTo f (‚ãÉ‚ÇÄ I) := by
    simp_rw [Set.mapsTo_iff_image_subset] at hf ‚ä¢
    rw [U_eq_sUnion_W‚ÇÅ, Set.sUnion_eq_biUnion] at hf
    have : ‚àÄ i ‚àà {v | v ‚àà V ‚àß closure v ‚äÜ U}, IsOpen i :=
      fun x ‚ü®hx, _‚ü© ‚Ü¶ hV.isOpen hx
    obtain ‚ü®b, hb1, hb2, hb3‚ü© := (hK.image f.continuous).elim_finite_subcover_image this hf
    refine ‚ü®closure '' b, hb2.image _, ?_, ?_‚ü©
    ¬∑ rintro - ‚ü®v, hv, rfl‚ü©
      exact ‚ü®v, (hb1 hv).1, (hb1 hv).2, rfl‚ü©
    rw [‚Üê Set.sUnion_eq_biUnion] at hb3
    exact hb3.trans <| Set.sUnion_mono_subsets fun _ ‚Ü¶ subset_closure
  -- Therefore, we obtain that
  -- `M(K, U) = ‚ãÉ_{I ‚äÜ W, I finite}, M(K, ‚ãÉ v ‚àà I, v)`.
  have : {f : C(X, Y) | K.MapsTo f U} =
      ‚ãÉ‚ÇÄ {v | ‚àÉ I, I.Finite ‚àß I ‚äÜ W ‚àß v = {f : C(X, Y) | K.MapsTo f (‚ãÉ‚ÇÄ I)}} := by
    ext f
    rw [Set.mem_sUnion]
    refine ‚ü®fun h ‚Ü¶ ?_, ?_‚ü©
    ¬∑ obtain ‚ü®I, hI1, hI2, hI3‚ü© := this f h
      exact ‚ü®{f : C(X, Y) | K.MapsTo f (‚ãÉ‚ÇÄ I)}, ‚ü®I, hI1, hI2, rfl‚ü©, hI3‚ü©
    ¬∑ rintro ‚ü®-, ‚ü®I, hI1, hI2, rfl‚ü©, h‚ü©
      simp only [Set.mapsTo_iff_image_subset] at h ‚ä¢
      rw [U_eq_sUnion_W]
      exact h.trans <| Set.sUnion_subset_sUnion hI2
  simp only
  rw [this]
  -- In particular, because `W` is countable, si this is a countable union.
  -- To show measurability it is therefore enough to show the measurability of each term.
  apply MeasurableSet.sUnion
  ¬∑ let f : Set (Set Y) ‚Üí Set C(X, Y) := fun I ‚Ü¶ {f : C(X, Y) | Set.MapsTo (‚áëf) K (‚ãÉ‚ÇÄ I)}
    refine ((Set.countable_setOf_finite_subset cW).image f).mono ?_
    rintro - ‚ü®I, hI1, hI2, rfl‚ü©
    exact ‚ü®I, ‚ü®hI1, hI2‚ü©, rfl‚ü©
  -- Consider now `I` a finite subset of `W`.
  rintro - ‚ü®I, hI1, hI2, rfl‚ü©
  -- First, `‚ãÉ v ‚àà I, v` is closed as a finite union of closed sets.
  have hI : IsClosed (‚ãÉ‚ÇÄ I) := by
    refine isClosed_sUnion_of_finite hI1 fun x hx ‚Ü¶ ?_
    obtain ‚ü®u, -, -, rfl‚ü© := hI2 hx
    exact isClosed_closure
  -- Consider `Q` a countable dense subset of `K`, which exists by second-countability assumption.
  obtain ‚ü®Q, cQ, dQ‚ü© := TopologicalSpace.exists_countable_dense K
  have Q_sub_K : Subtype.val '' Q ‚äÜ K := Subtype.coe_image_subset K Q
  -- Because `f` is continuous and `‚ãÉ v ‚àà I, v` is closed and `Q` is dense in `K`, having
  -- `f '' K ‚äÜ ‚ãÉ v ‚àà I, v` is the same as `f '' Q ‚äÜ ‚ãÉ v ‚àà I, v`.
  have : {f : C(X, Y) | K.MapsTo f (‚ãÉ‚ÇÄ I)} =
      {f : C(X, Y) | (Subtype.val '' Q).MapsTo f (‚ãÉ‚ÇÄ I)} := by
    ext f
    refine ‚ü®fun h x hx ‚Ü¶ h (Q_sub_K hx), fun h x hx ‚Ü¶ ?_‚ü©
    obtain ‚ü®u, hu1, hu2‚ü© := mem_closure_iff_seq_limit.1 <| Subtype.dense_iff.1 dQ hx
    exact hI.mem_of_tendsto ((f.continuous.tendsto x).comp hu2)
      (Filter.Eventually.of_forall fun n ‚Ü¶ h (hu1 n))
  -- We can write `M(Q, ‚ãÉ v ‚àà I, v) = ‚ãÇ q ‚àà Q, (fun f ‚Ü¶ f q) ‚Åª¬π' (‚ãÉ v ‚àà I, v)`.
  have : {f : C(X, Y) | K.MapsTo f (‚ãÉ‚ÇÄ I)} =
      ‚ãÇ q ‚àà Subtype.val '' Q, (fun f ‚Ü¶ f q) ‚Åª¬π' (‚ãÉ‚ÇÄ I) := by
    rw [this]
    ext f
    rw [Set.mem_iInter‚ÇÇ]
    exact ‚ü®fun h x hx ‚Ü¶ h hx, fun h x hx ‚Ü¶ h x hx‚ü©
  rw [this]
  -- This is a countable intersection, so it suffices to prove that each term is measurable.
  -- Because `‚ãÉ v ‚àà I, v` is closed, it is measurable, so it suffices to prove that
  -- for any `q ‚àà Q`, `fun f ‚Ü¶ f q` is measurable for the product œÉ-algebra.
  -- The latter is the coarsest œÉ-algebra which makes the maps `fun f ‚Ü¶ f x` measurable,
  -- so we are done.
  refine MeasurableSet.biInter (cQ.image _)
    fun q hq ‚Ü¶ MeasurableSet.preimage hI.measurableSet (Measurable.le (le_iSup _ q) ?_)
  rw [BorelSpace.measurable_eq (Œ± := Y)]
  exact comap_measurable _

lemma ContinuousMap.measurableSpace_eq_iSup_comap_eval
    [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y] :
    (inferInstance : MeasurableSpace C(X, Y))
      = ‚®Ü a : X, (inferInstance : MeasurableSpace Y).comap fun b ‚Ü¶ b a := by
  simp_rw [BorelSpace.measurable_eq, borel_eq_iSup_comap_eval]

lemma ContinuousMap.measurable_iff_eval {Œ± : Type*} [MeasurableSpace Œ±]
    [SecondCountableTopology X] [SecondCountableTopology Y]
    [LocallyCompactSpace X] [RegularSpace Y] [MeasurableSpace Y] [BorelSpace Y]
    (g : Œ± ‚Üí C(X, Y)) :
    Measurable g ‚Üî ‚àÄ (x : X), Measurable fun (a : Œ±) ‚Ü¶ g a x := by
  simp_rw [ContinuousMap.measurableSpace_eq_iSup_comap_eval, measurable_iff_comap_le,
    MeasurableSpace.comap_iSup, iSup_le_iff, MeasurableSpace.comap_comp, Function.comp_def]

end ContinuousMap.MeasurableSpace

def MeasurableEquiv.continuousMap : {f : ‚Ñù‚â•0 ‚Üí ‚Ñù // Continuous f} ‚âÉ·µê C(‚Ñù‚â•0, ‚Ñù) where
  toFun := fun f ‚Ü¶ ContinuousMap.mk f.1 f.2
  invFun := fun f ‚Ü¶ ‚ü®f, f.continuous‚ü©
  left_inv f := rfl
  right_inv f := rfl
  measurable_toFun := by
    simp only [Equiv.coe_fn_mk]
    rw [ContinuousMap.measurable_iff_eval]
    intro x
    simp only [ContinuousMap.coe_mk]
    revert x
    rw [‚Üê measurable_pi_iff]
    exact measurable_subtype_coe
  measurable_invFun := by
    simp only [Equiv.coe_fn_symm_mk]
    refine Measurable.subtype_mk ?_
    rw [measurable_pi_iff]
    exact fun _ ‚Ü¶ Continuous.measurable (by fun_prop)

noncomputable
def wienerMeasure : Measure C(‚Ñù‚â•0, ‚Ñù) := wienerMeasureAux.map MeasurableEquiv.continuousMap

end Measure

end ProbabilityTheory
