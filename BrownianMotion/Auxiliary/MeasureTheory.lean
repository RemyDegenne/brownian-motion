import BrownianMotion.Auxiliary.Algebra
import BrownianMotion.Auxiliary.Metric
import BrownianMotion.Auxiliary.WithLp
import Mathlib.MeasureTheory.Measure.Lebesgue.VolumeOfBalls
import Mathlib.Probability.Distributions.Gaussian.Real
import Mathlib.Probability.Independence.ZeroOne
import Mathlib.Probability.Moments.Covariance
import Mathlib.Probability.Process.Filtration
/-!
# Measure theory lemmas to be upstreamed to Mathlib
-/

open MeasureTheory WithLp

open scoped ENNReal NNReal ProbabilityTheory

attribute [fun_prop] aemeasurable_id'

@[to_additive]
theorem Filter.EventuallyEq.div' {Î± Î² : Type*} [Div Î²] {f f' g g' : Î± â†’ Î²} {l : Filter Î±}
    (h : f =á¶ [l] g) (h' : f' =á¶ [l] g') : f / f' =á¶ [l] g / g' :=
  h.compâ‚‚ (Â· / Â·) h'

namespace ProbabilityTheory

@[simp]
lemma charFun_toDual_symm_eq_charFunDual {E : Type*} [NormedAddCommGroup E] [CompleteSpace E]
    [InnerProductSpace â„ E] {mE : MeasurableSpace E} {Î¼ : Measure E} (L : StrongDual â„ E) :
    charFun Î¼ ((InnerProductSpace.toDual â„ E).symm L) = charFunDual Î¼ L := by
  rw [charFun_eq_charFunDual_toDualMap]
  congr with x
  simp

lemma eq_gaussianReal_integral_variance {Î¼ : Measure â„} {m : â„} {v : â„â‰¥0}
    (h : Î¼ = gaussianReal m v) : Î¼ = gaussianReal Î¼[id] Var[id; Î¼].toNNReal := by
  simp [h]

section iIndepFun

variable {Î¹ : Type*} [Fintype Î¹] {Î© : Î¹ â†’ Type*} {mÎ© : âˆ€ i, MeasurableSpace (Î© i)}
  {Î¼ : (i : Î¹) â†’ Measure (Î© i)}

variable [âˆ€ i, IsProbabilityMeasure (Î¼ i)]

lemma variance_pi {X : Î  i, Î© i â†’ â„} (h : âˆ€ i, MemLp (X i) 2 (Î¼ i)) :
    Var[âˆ‘ i, fun Ï‰ â†¦ X i (Ï‰ i); Measure.pi Î¼] = âˆ‘ i, Var[X i; Î¼ i] := by
  rw [IndepFun.variance_sum]
  Â· congr with i
    change Var[(X i) âˆ˜ (fun Ï‰ â†¦ Ï‰ i); Measure.pi Î¼] = _
    rw [â† variance_map, (measurePreserving_eval _ i).map_eq]
    Â· rw [(measurePreserving_eval _ i).map_eq]
      exact (h i).aestronglyMeasurable.aemeasurable
    Â· exact Measurable.aemeasurable (by fun_prop)
  Â· exact fun i _ â†¦ (h i).comp_measurePreserving (measurePreserving_eval _ i)
  Â· exact fun i _ j _ hij â†¦
      (iIndepFun_pi fun i â†¦ (h i).aestronglyMeasurable.aemeasurable).indepFun hij

end iIndepFun

section covariance

lemma centralMoment_of_integral_id_eq_zero {Î© : Type*} {mÎ© : MeasurableSpace Î©}
    {Î¼ : Measure Î©} {X : Î© â†’ â„} (p : â„•) (hX : Î¼[X] = 0) :
    centralMoment X p Î¼ = âˆ« Ï‰, X Ï‰ ^ p âˆ‚Î¼ := by
  rw [centralMoment]
  simp [hX]

end covariance

end ProbabilityTheory

namespace ContinuousLinearMap

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
    [NormedSpace ğ•œ E] [NormedSpace â„ E] [NormedSpace ğ•œ F] [NormedSpace â„ F] [CompleteSpace E]
    [CompleteSpace F] [MeasurableSpace E] {Î¼ : Measure E}

lemma integral_comp_id_comm' (h : Integrable _root_.id Î¼) (L : E â†’L[ğ•œ] F) :
    Î¼[L] = L Î¼[_root_.id] := by
  change âˆ« x, L (_root_.id x) âˆ‚Î¼ = _
  rw [L.integral_comp_comm h]

lemma integral_comp_id_comm (h : Integrable _root_.id Î¼) (L : E â†’L[ğ•œ] F) :
    Î¼[L] = L (âˆ« x, x âˆ‚Î¼) :=
  L.integral_comp_id_comm' h

variable [OpensMeasurableSpace E] [MeasurableSpace F] [BorelSpace F] [SecondCountableTopology F]

lemma integral_id_map (h : Integrable _root_.id Î¼) (L : E â†’L[ğ•œ] F) :
    âˆ« x, x âˆ‚(Î¼.map L) = L (âˆ« x, x âˆ‚Î¼) := by
  rw [integral_map (by fun_prop) (by fun_prop)]
  simp [L.integral_comp_id_comm h]

end ContinuousLinearMap

lemma MeasurableEquiv.coe_toLp_symm_eq {Î¹ : Type*} :
    â‡‘(MeasurableEquiv.toLp 2 (Î¹ â†’ â„)).symm = â‡‘(EuclideanSpace.equiv Î¹ â„) := rfl

@[simp]
lemma zero_mem_parallelepiped {Î¹ E : Type*} [Fintype Î¹] [AddCommGroup E] [Module â„ E] {v : Î¹ â†’ E} :
    0 âˆˆ parallelepiped v := âŸ¨0, by simp, by simpâŸ©

@[simp]
lemma nonempty_parallelepiped {Î¹ E : Type*} [Fintype Î¹] [AddCommGroup E] [Module â„ E] {v : Î¹ â†’ E} :
    (parallelepiped v).Nonempty := âŸ¨0, zero_mem_parallelepipedâŸ©

@[simp, nontriviality]
lemma volume_of_nonempty_of_subsingleton {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
    [FiniteDimensional â„ E] [MeasurableSpace E] [Subsingleton E] {s : Set E} (hs : s.Nonempty) :
    volume s = 1 := by
  rw [Subsingleton.eq_univ_of_nonempty hs,
    â† Subsingleton.eq_univ_of_nonempty (nonempty_parallelepiped (v := stdOrthonormalBasis â„ E))]
  exact (stdOrthonormalBasis â„ E).toBasis.addHaar_self

--generalizing `MeasureTheory.Measure.addHaar_ball_center`

@[to_additive]
lemma MeasureTheory.Measure.IsMulLeftInvariant.measure_ball_const
    {G : Type*} [Group G] [PseudoMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulLeftInvariant] [IsIsometricSMul G G]
    [MeasurableMul G] (a b : G) (r : â„) :
    Î¼ (Metric.ball a r) = Î¼ (Metric.ball b r) := by
  rw [show a = (b / a)â»Â¹ * b by simp, â† Metric.preimage_mul_left_ball, â† Measure.map_apply,
    map_mul_left_eq_self]
  Â· fun_prop
  Â· exact Metric.isOpen_ball.measurableSet

@[to_additive]
lemma MeasureTheory.Measure.IsMulRightInvariant.measure_ball_const
    {G : Type*} [CommGroup G] [PseudoMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulRightInvariant] [IsIsometricSMul Gáµáµ’áµ– G]
    [MeasurableMul G] (a b : G) (r : â„) :
    Î¼ (Metric.ball a r) = Î¼ (Metric.ball b r) := by
  rw [show a = b / (b / a) by simp, â† Metric.preimage_mul_right_ball, â† Measure.map_apply,
    map_mul_right_eq_self]
  Â· fun_prop
  Â· exact Metric.isOpen_ball.measurableSet

@[to_additive]
lemma MeasureTheory.Measure.IsMulLeftInvariant.measure_closedBall_const
    {G : Type*} [Group G] [PseudoMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulLeftInvariant] [IsIsometricSMul G G]
    [MeasurableMul G] (a b : G) (r : â„) :
    Î¼ (Metric.closedBall a r) = Î¼ (Metric.closedBall b r) := by
  rw [show a = (b / a)â»Â¹ * b by simp, â† Metric.preimage_mul_left_closedBall, â† Measure.map_apply,
    map_mul_left_eq_self]
  Â· fun_prop
  Â· exact Metric.isClosed_closedBall.measurableSet

@[to_additive]
lemma MeasureTheory.Measure.IsMulRightInvariant.measure_closeBall_const
    {G : Type*} [CommGroup G] [PseudoMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulRightInvariant] [IsIsometricSMul Gáµáµ’áµ– G]
    [MeasurableMul G] (a b : G) (r : â„) :
    Î¼ (Metric.closedBall a r) = Î¼ (Metric.closedBall b r) := by
  rw [show a = b / (b / a) by simp, â† Metric.preimage_mul_right_closedBall, â† Measure.map_apply,
    map_mul_right_eq_self]
  Â· fun_prop
  Â· exact Metric.isClosed_closedBall.measurableSet

@[to_additive]
lemma MeasureTheory.Measure.IsMulLeftInvariant.measure_ball_const'
    {G : Type*} [Group G] [PseudoEMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulLeftInvariant] [IsIsometricSMul G G]
    [MeasurableMul G] (a b : G) (r : â„â‰¥0âˆ) :
    Î¼ (EMetric.ball a r) = Î¼ (EMetric.ball b r) := by
  rw [show a = (b / a)â»Â¹ * b by simp, â† EMetric.preimage_mul_left_ball, â† Measure.map_apply,
    map_mul_left_eq_self]
  Â· fun_prop
  Â· exact EMetric.isOpen_ball.measurableSet

@[to_additive]
lemma MeasureTheory.Measure.IsMulRightInvariant.measure_ball_const'
    {G : Type*} [CommGroup G] [PseudoEMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulRightInvariant] [IsIsometricSMul Gáµáµ’áµ– G]
    [MeasurableMul G] (a b : G) (r : â„â‰¥0âˆ) :
    Î¼ (EMetric.ball a r) = Î¼ (EMetric.ball b r) := by
  rw [show a = b / (b / a) by simp, â† EMetric.preimage_mul_right_ball, â† Measure.map_apply,
    map_mul_right_eq_self]
  Â· fun_prop
  Â· exact EMetric.isOpen_ball.measurableSet

@[to_additive]
lemma MeasureTheory.Measure.IsMulLeftInvariant.measure_closedBall_const'
    {G : Type*} [Group G] [PseudoEMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulLeftInvariant] [IsIsometricSMul G G]
    [MeasurableMul G] (a b : G) (r : â„â‰¥0âˆ) :
    Î¼ (EMetric.closedBall a r) = Î¼ (EMetric.closedBall b r) := by
  rw [show a = (b / a)â»Â¹ * b by simp, â† EMetric.preimage_mul_left_closedBall, â† Measure.map_apply,
    map_mul_left_eq_self]
  Â· fun_prop
  Â· exact EMetric.isClosed_closedBall.measurableSet

@[to_additive]
lemma MeasureTheory.Measure.IsMulRightInvariant.measure_closeBall_const'
    {G : Type*} [CommGroup G] [PseudoEMetricSpace G] [MeasurableSpace G]
    [OpensMeasurableSpace G] (Î¼ : Measure G) [Î¼.IsMulRightInvariant] [IsIsometricSMul Gáµáµ’áµ– G]
    [MeasurableMul G] (a b : G) (r : â„â‰¥0âˆ) :
    Î¼ (EMetric.closedBall a r) = Î¼ (EMetric.closedBall b r) := by
  rw [show a = b / (b / a) by simp, â† EMetric.preimage_mul_right_closedBall, â† Measure.map_apply,
    map_mul_right_eq_self]
  Â· fun_prop
  Â· exact EMetric.isClosed_closedBall.measurableSet

open Metric

lemma InnerProductSpace.volume_closedBall_div {E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace â„ E] [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E]
    (x y : E) {r s : â„} (hr : 0 < r) (hs : 0 < s) :
    volume (closedBall x r) / volume (closedBall y s) =
      ENNReal.ofReal (r / s) ^ (Module.finrank â„ E) := by
  obtain _ | _ := subsingleton_or_nontrivial E
  Â· simp [hr.le, hs.le]
  rw [InnerProductSpace.volume_closedBall, InnerProductSpace.volume_closedBall,
    ENNReal.mul_div_mul_right _ _ (by positivity) (by simp)]
  simp_rw [â† ENNReal.rpow_natCast]
  rw [â† ENNReal.div_rpow_of_nonneg _ _ (by simp), ENNReal.ofReal_div_of_pos hs]

lemma InnerProductSpace.volume_closedBall_div' {E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace â„ E] [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E]
    (x y : E) (r s : â„â‰¥0âˆ) :
    volume (EMetric.closedBall x r) / volume (EMetric.closedBall y s) =
      (r / s) ^ (Module.finrank â„ E) := by
  nontriviality E
  obtain rfl | hr := eq_top_or_lt_top r <;> obtain rfl | hs := eq_top_or_lt_top s
  Â· simp
  Â· lift s to â„â‰¥0 using hs.ne
    simp [ENNReal.top_div, emetric_closedBall_nnreal, (isCompact_closedBall _ _).measure_ne_top]
  Â· simp
  Â· obtain rfl | hr' := eq_zero_or_pos r <;> obtain rfl | hs' := eq_zero_or_pos s
    Â· simp
    Â· simp
    Â· simp [ENNReal.div_zero, hr'.ne', EMetric.measure_closedBall_pos volume x hr'.ne' |>.ne']
    lift r to â„â‰¥0 using hr.ne
    lift s to â„â‰¥0 using hs.ne
    simp_rw [emetric_closedBall_nnreal]
    rw [volume_closedBall_div, ENNReal.ofReal_div_of_pos]
    Â· simp
    all_goals simp_all

section covariance

namespace ProbabilityTheory

variable {Î© : Type*} {mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} {X Y Z : Î© â†’ â„} (c : â„)

lemma covariance_fun_add_left [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[fun Ï‰ â†¦ X Ï‰ + Y Ï‰, Z; Î¼] = cov[fun Ï‰ â†¦ X Ï‰, Z; Î¼] + cov[fun Ï‰ â†¦ Y Ï‰, Z; Î¼] :=
  covariance_add_left hX hY hZ

lemma covariance_fun_add_right [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[X, fun Ï‰ â†¦ Y Ï‰ + Z Ï‰; Î¼] = cov[X, fun Ï‰ â†¦ Y Ï‰; Î¼] + cov[X, fun Ï‰ â†¦ Z Ï‰; Î¼] :=
  covariance_add_right hX hY hZ

lemma covariance_fun_sub_left [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[fun Ï‰ â†¦ X Ï‰ - Y Ï‰, Z; Î¼] = cov[X, Z; Î¼] - cov[Y, Z; Î¼] :=
  covariance_sub_left hX hY hZ

lemma covariance_fun_sub_right [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[X, fun Ï‰ â†¦ Y Ï‰ - Z Ï‰; Î¼] = cov[X, fun Ï‰ â†¦ Y Ï‰; Î¼] - cov[X, fun Ï‰ â†¦ Z Ï‰; Î¼] :=
  covariance_sub_right hX hY hZ

lemma covariance_fun_div_left :
    cov[fun Ï‰ â†¦ X Ï‰ / c, Y; Î¼] = cov[X, Y; Î¼] / c := by
  simp_rw [â† inv_mul_eq_div, covariance_mul_left]

lemma covariance_fun_div_right :
    cov[X, fun Ï‰ â†¦ Y Ï‰ / c; Î¼] = cov[X, Y; Î¼] / c := by
  simp_rw [â† inv_mul_eq_div, covariance_mul_right]

lemma variance_fun_div (hX : AEMeasurable X Î¼) :
    Var[fun Ï‰ â†¦ X Ï‰ / c; Î¼] = Var[X; Î¼] / c ^ 2 := by
  rw [â† covariance_self (by fun_prop), covariance_fun_div_left, covariance_fun_div_right,
    covariance_self hX]
  field_simp

end ProbabilityTheory

open ProbabilityTheory MeasurableSpace

lemma MeasurableSpace.comap_process {Î© T : Type*} {ğ“§ : T â†’ Type*} [âˆ€ t, MeasurableSpace (ğ“§ t)]
    (X : (t : T) â†’ Î© â†’ ğ“§ t) :
    MeasurableSpace.comap (fun Ï‰ t â†¦ X t Ï‰) MeasurableSpace.pi =
      â¨† t, MeasurableSpace.comap (X t) inferInstance := by
  simp_rw [MeasurableSpace.pi, MeasurableSpace.comap_iSup, MeasurableSpace.comap_comp]
  rfl

lemma MeasurableSpace.comap_le_comap
    {Î© ğ“§ ğ“¨ : Type*} [mğ“§ : MeasurableSpace ğ“§] [mğ“¨ : MeasurableSpace ğ“¨]
    {X : Î© â†’ ğ“§} {Y : Î© â†’ ğ“¨} (f : ğ“§ â†’ ğ“¨) (hf : Measurable f) (h : Y = f âˆ˜ X) :
    mğ“¨.comap Y â‰¤ mğ“§.comap X := by
  rw [h, â† MeasurableSpace.comap_comp]
  exact MeasurableSpace.comap_mono hf.comap_le

lemma MeasureTheory.Filtration.natural_eq_comap {Î© Î¹ : Type*} {m : MeasurableSpace Î©}
    {Î² : Î¹ â†’ Type*} [(i : Î¹) â†’ TopologicalSpace (Î² i)]
    [âˆ€ (i : Î¹), TopologicalSpace.MetrizableSpace (Î² i)]
    [mÎ² : (i : Î¹) â†’ MeasurableSpace (Î² i)] [âˆ€ (i : Î¹), BorelSpace (Î² i)] [Preorder Î¹]
    (u : (i : Î¹) â†’ Î© â†’ Î² i)
    (hum : âˆ€ (i : Î¹), StronglyMeasurable (u i)) (i : Î¹) :
    Filtration.natural u hum i = .comap (fun Ï‰ (j : Set.Iic i) â†¦ u j Ï‰) inferInstance := by
  simp_rw [Filtration.natural, comap_process, iSup_subtype']
  rfl

lemma ProbabilityTheory.measure_eq_zero_or_one_of_indep_self {Î© : Type*} {m mÎ© : MeasurableSpace Î©}
    {P : Measure Î©} [IsZeroOrProbabilityMeasure P]
    (hm1 : m â‰¤ mÎ©) (hm2 : Indep m m P) {A : Set Î©} (hA : MeasurableSet[m] A) :
    P A = 0 âˆ¨ P A = 1 := by
  rw [Indep_iff_IndepSets, indepSets_iff_singleton_indepSets] at hm2
  replace hm2 := indepSets_iff_singleton_indepSets.1 (hm2 A hA).symm A hA
  exact measure_eq_zero_or_one_of_indepSet_self <|
    (indepSet_iff_indepSets_singleton (hm1 A hA) (hm1 A hA) P).2 hm2

lemma MeasurableSpace.generateFrom_singleton_eq_comap_indicator_one {Î© : Type*} {A : Set Î©} :
    MeasurableSpace.generateFrom {A} =
      MeasurableSpace.comap (A.indicator (1 : Î© â†’ â„)) inferInstance := by
  refine le_antisymm (MeasurableSpace.generateFrom_le fun s hs â†¦ ?_)
    (Measurable.comap_le ?_)
  Â· simp only [Set.mem_singleton_iff] at hs
    rw [hs, â† measurable_indicator_const_iff (1 : â„)]
    exact comap_measurable _
  Â· apply (measurable_indicator_const_iff 1).2 ?_
    exact MeasurableSpace.measurableSet_generateFrom (by simp)

lemma ProbabilityTheory.singleton_indepSets_comap_iff {Î© : Type*} {mÎ© : MeasurableSpace Î©}
    {P : Measure Î©} [IsZeroOrProbabilityMeasure P] {ğ“§ : Type*}
    {mğ“§ : MeasurableSpace ğ“§} {A : Set Î©} {X : Î© â†’ ğ“§} (hX : Measurable X) (hA : MeasurableSet A) :
    IndepSets {A} {s | MeasurableSet[mğ“§.comap X] s} P â†”
      (A.indicator (1 : Î© â†’ â„)) âŸ‚áµ¢[P] X := by
  rw [IndepFun_iff_Indep, â† generateFrom_singleton_eq_comap_indicator_one]
  constructor
  Â· exact IndepSets.indep (generateFrom_le (by simpa)) hX.comap_le (by simp [IsPiSystem])
      (@MeasurableSpace.isPiSystem_measurableSet _ (mğ“§.comap X)) rfl (by simp)
  Â· refine fun h â†¦ Indep.indepSets ?_
    convert h
    simp

lemma IndepSets.setIntegral_eq_mul {Î© ğ“§ : Type*} {mÎ© : MeasurableSpace Î©}
    {Î¼ : Measure Î©} {mğ“§ : MeasurableSpace ğ“§} {X : Î© â†’ ğ“§} [IsZeroOrProbabilityMeasure Î¼]
    {f : ğ“§ â†’ â„} {A : Set Î©} (hA1 : IndepSets {A} {s | MeasurableSet[mğ“§.comap X] s} Î¼)
    (hX : Measurable X) (hA2 : MeasurableSet A)
    (hf : AEStronglyMeasurable f (Î¼.map X)) :
    âˆ« Ï‰ in A, f (X Ï‰) âˆ‚Î¼ = Î¼.real A * âˆ« Ï‰, f (X Ï‰) âˆ‚Î¼ :=
  calc âˆ« Ï‰ in A, f (X Ï‰) âˆ‚Î¼
    = âˆ« Ï‰, (A.indicator 1 Ï‰) * f (X Ï‰) âˆ‚Î¼ := by
        rw [â† integral_indicator hA2]
        congr with Ï‰
        by_cases hÏ‰ : Ï‰ âˆˆ A <;> simp [hÏ‰]
  _ = âˆ« Ï‰, id (A.indicator 1 Ï‰) * f (X Ï‰) âˆ‚Î¼ := by simp
  _ = Î¼.real A * âˆ« Ï‰, f (X Ï‰) âˆ‚Î¼ := by
    rw [IndepFun.integral_fun_comp_mul_comp]
    Â· simp [integral_indicator_one hA2]
    Â· exact (singleton_indepSets_comap_iff hX hA2).1 hA1
    Â· exact (aemeasurable_indicator_const_iff 1).2 hA2.nullMeasurableSet
    Â· exact hX.aemeasurable
    Â· fun_prop
    Â· exact hf

lemma Indep.singleton_indepSets {Î© : Type*} {m1 m2 mÎ© : MeasurableSpace Î©}
    {P : Measure Î©} (h : Indep m1 m2 P) {A : Set Î©}
    (hA : MeasurableSet[m1] A) : IndepSets {A} {s | MeasurableSet[m2] s} P := by
  have := (Indep_iff_IndepSets m1 m2 P).1 h
  apply indepSets_of_indepSets_of_le_left this
  simpa

lemma measurableSpace_le_iff {Î© : Type*} {m1 m2 : MeasurableSpace Î©} :
    m1 â‰¤ m2 â†” âˆ€ s, MeasurableSet[m1] s â†’ MeasurableSet[m2] s := by aesop

end covariance
