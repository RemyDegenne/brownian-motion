import Mathlib.LinearAlgebra.Matrix.BilinearForm
import Mathlib.LinearAlgebra.Matrix.SchurComplement

/-!
# Continuous bilinear forms
-/

open scoped Matrix

variable (ğ•œ E : Type*) [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/- The type of continuous bilinear forms. -/
abbrev ContinuousBilinForm := E â†’L[ğ•œ] E â†’L[ğ•œ] ğ•œ

variable {ğ•œ E} (f : ContinuousBilinForm ğ•œ E)

namespace ContinuousBilinForm

/-- The underlying bilinear form of a continuous bilinear form -/
def toBilinForm : LinearMap.BilinForm ğ•œ E where
  toFun x := f x
  map_add' x y := by simp
  map_smul' m x := by simp

@[simp]
lemma toBilinForm_apply (x y : E) : f.toBilinForm x y = f x y := rfl

variable {n : Type*} (b : Basis n ğ•œ E)

/-- A continuous bilinear form `f` is symmetric if for any `x, y` we have `f x y = f y x`. -/
structure IsSymm : Prop where
  map_symm : âˆ€ x y, f x y = f y x

variable {f}

/-- Polarization identity: a symmetric continuous bilinear form can be expressed through values
it takes on the diagonal. -/
lemma IsSymm.polarization (x y : E) (hf : f.IsSymm) :
    f x y = (f (x + y) (x + y) - f x x - f y y) / 2 := by
  simp only [map_add, ContinuousLinearMap.add_apply]
  rw [hf.map_symm y x]
  ring

/-- A symmetric continuous bilinear form is characterized by the values it takes on the diagonal. -/
lemma ext_of_isSymm {g : ContinuousBilinForm ğ•œ E} (hf : IsSymm f) (hg : IsSymm g)
    (h : âˆ€ x, f x x = g x x) : f = g := by
  ext x y
  rw [hf.polarization, hg.polarization]
  simp_rw [h]

/-- A symmetric continuous bilinear form is characterized by the values it takes on the diagonal. -/
lemma ext_iff_of_isSymm {g : ContinuousBilinForm ğ•œ E} (hf : IsSymm f) (hg : IsSymm g) :
    f = g â†” âˆ€ x, f x x = g x x where
  mp h := by simp [h]
  mpr := ext_of_isSymm hf hg

variable (f)

lemma isSymm_def : f.IsSymm â†” âˆ€ x y, f x y = f y x where
  mp := fun âŸ¨hâŸ© â†¦ h
  mpr h := âŸ¨hâŸ©

lemma isSymm_iff_basis : f.IsSymm â†” âˆ€ i j, f (b i) (b j) = f (b j) (b i) where
  mp := fun âŸ¨hâŸ© i j â†¦ h _ _
  mpr := by
    refine fun h â†¦ âŸ¨fun x y â†¦ ?_âŸ©
    obtain âŸ¨fx, tx, ix, -, hxâŸ© := Submodule.mem_span_iff_exists_finset_subset.1
      (by simp : x âˆˆ Submodule.span ğ•œ (Set.range b))
    obtain âŸ¨fy, ty, iy, -, hyâŸ© := Submodule.mem_span_iff_exists_finset_subset.1
      (by simp : y âˆˆ Submodule.span ğ•œ (Set.range b))
    rw [â† hx, â† hy]
    simp [Finset.mul_sum]
    rw [Finset.sum_comm]
    refine Finset.sum_congr rfl (fun bâ‚ hâ‚ â†¦ Finset.sum_congr rfl fun bâ‚‚ hâ‚‚ â†¦ ?_)
    rw [mul_left_comm]
    obtain âŸ¨i, rflâŸ© := ix hâ‚
    obtain âŸ¨j, rflâŸ© := iy hâ‚‚
    rw [h]

variable [Fintype n] [DecidableEq n]

/-- A continuous bilinear map on a finite dimensional space can be represented by a matrix. -/
noncomputable def toMatrix : Matrix n n ğ•œ :=
  BilinForm.toMatrix b f.toBilinForm

@[simp]
lemma toMatrix_apply (i j : n) : f.toMatrix b i j = f (b i) (b j) := by
  simp [toMatrix]

lemma dotProduct_toMatrix_mulVec (x y : n â†’ ğ•œ) :
    x â¬áµ¥ (f.toMatrix b) *áµ¥ y = f (b.equivFun.symm x) (b.equivFun.symm y) := by
  simp only [dotProduct, Matrix.mulVec_eq_sum, op_smul_eq_smul, Finset.sum_apply, Pi.smul_apply,
    Matrix.transpose_apply, toMatrix_apply, smul_eq_mul, Finset.mul_sum, Basis.equivFun_symm_apply,
    map_sum, map_smul, ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_smul']
  rw [Finset.sum_comm]
  refine Finset.sum_congr rfl (fun i _ â†¦ Finset.sum_congr rfl fun j _ â†¦ ?_)
  ring

lemma apply_eq_dotProduct_toMatrix_mulVec (x y : E) :
    f x y = (b.repr x) â¬áµ¥ (f.toMatrix b) *áµ¥ (b.repr y) := by
  nth_rw 1 [â† b.sum_repr x, â† b.sum_repr y]
  simp [dotProduct, Matrix.mulVec_eq_sum, Finset.mul_sum]
  rw [Finset.sum_comm]
  refine Finset.sum_congr rfl (fun i _ â†¦ Finset.sum_congr rfl fun j _ â†¦ ?_)
  ring

/-- A continuous bilinear map `f` is positive if for any `0 â‰¤ x`, `0 â‰¤ re (f x x)` -/
structure IsPos : Prop where
  nonneg_re_apply_self : âˆ€ x, 0 â‰¤ RCLike.re (f x x)

lemma isPos_def : f.IsPos â†” âˆ€ x, 0 â‰¤ RCLike.re (f x x) where
  mp := fun âŸ¨hâŸ© â†¦ h
  mpr h := âŸ¨hâŸ©

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (f : ContinuousBilinForm â„ E)
    (b : Basis n â„ E)

lemma isPos_def_real : f.IsPos â†” âˆ€ x, 0 â‰¤ f x x := by simp [isPos_def]

lemma isSymm_iff_isHermitian_toMatrix : f.IsSymm â†” (f.toMatrix b).IsHermitian := by
  rw [isSymm_iff_basis f b, Matrix.IsHermitian.ext_iff]
  simp [Eq.comm]

/-- A continuous bilinear map is positive semidefinite if it is symmetric and positive. We only
define it for the real field, because for the complex case we may want to consider sesquilinear
forms instead. -/
structure IsPosSemidef : Prop extends f.IsSymm, f.IsPos

variable {f}

lemma IsPosSemidef.isSymm (hf : IsPosSemidef f) : IsSymm f := hf.toIsSymm

lemma IsPosSemidef.isPos (hf : IsPosSemidef f) : IsPos f := hf.toIsPos

variable (f)

lemma isPosSemidef_iff : f.IsPosSemidef â†” f.IsSymm âˆ§ f.IsPos where
  mp h := âŸ¨h.isSymm, h.isPosâŸ©
  mpr := fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ âŸ¨hâ‚, hâ‚‚âŸ©

lemma isPosSemidef_iff_posSemidef_toMatrix {f : ContinuousBilinForm â„ E} (b : Basis n â„ E) :
    f.IsPosSemidef â†” (f.toMatrix b).PosSemidef := by
  rw [isPosSemidef_iff, Matrix.PosSemidef]
  apply and_congr (f.isSymm_iff_isHermitian_toMatrix b)
  rw [isPos_def]
  refine âŸ¨fun h x â†¦ ?_, fun h x â†¦ ?_âŸ©
  Â· rw [dotProduct_toMatrix_mulVec]
    exact h _
  Â· rw [apply_eq_dotProduct_toMatrix_mulVec f b]
    exact h _

end ContinuousBilinForm
