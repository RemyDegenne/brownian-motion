import BrownianMotion.Auxiliary.MeasureTheory
import Mathlib.Probability.Distributions.Gaussian.Basic
import Mathlib.Probability.HasLaw

open MeasureTheory ENNReal WithLp

namespace ProbabilityTheory

variable {Œ© : Type*} {mŒ© : MeasurableSpace Œ©}

section HasLaw

variable {ùìß} {mùìß : MeasurableSpace ùìß} (X : Œ© ‚Üí ùìß) (Œº : Measure ùìß)
  (P : Measure Œ© := by volume_tac)

variable {X Œº} {P : Measure Œ©}

lemma hasLaw_map [IsProbabilityMeasure P] (hX : AEMeasurable X P) : HasLaw X (P.map X) P where
  aemeasurable := hX
  map_eq := rfl

end HasLaw

section HasGaussianLaw

variable {E : Type*} (X : Œ© ‚Üí E) (P : Measure Œ©)

section Basic

variable [TopologicalSpace E] [AddCommMonoid E] [Module ‚Ñù E] [mE : MeasurableSpace E]

/-- The predicate `HasGaussianLaw X P` means that under the measure `P`,
`X` has a Gaussian distribution -/
class HasGaussianLaw :
    Prop where
  protected isGaussian_map : IsGaussian (P.map X)

attribute [instance] HasGaussianLaw.isGaussian_map

variable {X P}

lemma HasGaussianLaw.congr {Y : Œ© ‚Üí E} [HasGaussianLaw X P] (h : ‚àÄ·µê œâ ‚àÇP, X œâ = Y œâ) :
    HasGaussianLaw Y P where
  isGaussian_map := by
    rw [‚Üê Measure.map_congr h]
    infer_instance

instance IsGaussian.hasGaussianLaw [IsGaussian (P.map X)] :
    HasGaussianLaw X P where
  isGaussian_map := inferInstance

instance IsGaussian.hasGaussianLaw_fun [IsGaussian (P.map X)] :
    HasGaussianLaw (fun œâ ‚Ü¶ X œâ) P where
  isGaussian_map := inferInstance

variable {mE} in
instance IsGaussian.hasGaussianLaw_id {Œº : Measure E} [IsGaussian Œº] :
    HasGaussianLaw id Œº where
  isGaussian_map := by rwa [Measure.map_id]

@[fun_prop, measurability]
lemma HasGaussianLaw.aemeasurable [hX : HasGaussianLaw X P] : AEMeasurable X P := by
  by_contra! h
  have := hX.isGaussian_map
  rw [Measure.map_of_not_aemeasurable h] at this
  exact this.toIsProbabilityMeasure.ne_zero _ rfl

lemma HasGaussianLaw.isProbabilityMeasure [HasGaussianLaw X P] : IsProbabilityMeasure P where
  measure_univ := by
    rw [‚Üê Set.preimage_univ (f := X), ‚Üê Measure.map_apply_of_aemeasurable (by fun_prop) .univ,
      measure_univ]

variable {mE} in
lemma HasLaw.hasGaussianLaw {Œº : Measure E} (hX : HasLaw X Œº P) [IsGaussian Œº] :
    HasGaussianLaw X P where
  isGaussian_map := by rwa [hX.map_eq]

end Basic

section NormedSpace

variable [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
    {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [MeasurableSpace F] [BorelSpace F]
    (L : E ‚ÜíL[‚Ñù] F) {X P}

instance HasGaussianLaw.map [HasGaussianLaw X P] : HasGaussianLaw (L ‚àò X) P where
  isGaussian_map := by
    rw [‚Üê AEMeasurable.map_map_of_aemeasurable]
    ¬∑ infer_instance
    all_goals fun_prop

instance HasGaussianLaw.map_fun [hX : HasGaussianLaw X P] : HasGaussianLaw (fun œâ ‚Ü¶ L (X œâ)) P :=
  hX.map L

variable (L : E ‚âÉL[‚Ñù] F)

instance HasGaussianLaw.map_equiv [HasGaussianLaw X P] : HasGaussianLaw (L ‚àò X) P where
  isGaussian_map := by
    rw [‚Üê AEMeasurable.map_map_of_aemeasurable]
    ¬∑ infer_instance
    all_goals fun_prop

instance HasGaussianLaw.map_equiv_fun [hX : HasGaussianLaw X P] :
    HasGaussianLaw (fun œâ ‚Ü¶ L (X œâ)) P := hX.map_equiv L

instance HasGaussianLaw.eval {Œπ Œ© : Type*} {E : Œπ ‚Üí Type*} [Fintype Œπ] {mŒ© : MeasurableSpace Œ©}
    {P : Measure Œ©} [‚àÄ i, NormedAddCommGroup (E i)]
    [‚àÄ i, NormedSpace ‚Ñù (E i)] [‚àÄ i, MeasurableSpace (E i)] [‚àÄ i, BorelSpace (E i)]
    [‚àÄ i, SecondCountableTopology (E i)] {X : (i : Œπ) ‚Üí Œ© ‚Üí E i}
    [h : HasGaussianLaw (fun œâ ‚Ü¶ (X ¬∑ œâ)) P] (i : Œπ) :
    HasGaussianLaw (X i) P := by
  have : X i = (ContinuousLinearMap.proj (R := ‚Ñù) (œÜ := E) i) ‚àò (fun œâ ‚Ü¶ (X ¬∑ œâ)) := by ext; simp
  rw [this]
  infer_instance

instance HasGaussianLaw.toLp_comp (p : ‚Ñù‚â•0‚àû) [Fact (1 ‚â§ p)] {Œπ : Type*} [Fintype Œπ] {E : Œπ ‚Üí Type*}
    [‚àÄ i, NormedAddCommGroup (E i)] [‚àÄ i, NormedSpace ‚Ñù (E i)] [‚àÄ i, MeasurableSpace (E i)]
    [‚àÄ i, BorelSpace (E i)] [‚àÄ i, SecondCountableTopology (E i)]
    {X : (i : Œπ) ‚Üí Œ© ‚Üí E i} [HasGaussianLaw (fun œâ ‚Ü¶ (X ¬∑ œâ)) P] :
    HasGaussianLaw (fun œâ ‚Ü¶ toLp p (X ¬∑ œâ)) P := by
  rw [‚Üê PiLp.continuousLinearEquiv_symm_apply p ‚Ñù]
  infer_instance

instance HasGaussianLaw.toLp_comp' (p : ‚Ñù‚â•0‚àû) [Fact (1 ‚â§ p)] {E F : Type*}
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù F]
    [MeasurableSpace E] [MeasurableSpace F] [BorelSpace E] [BorelSpace F]
    [SecondCountableTopology E] [SecondCountableTopology F]
    {X : Œ© ‚Üí E} {Y : Œ© ‚Üí F} [HasGaussianLaw (fun œâ ‚Ü¶ (X œâ, Y œâ)) P] :
    HasGaussianLaw (fun œâ ‚Ü¶ toLp p (X œâ, Y œâ)) P := by
  conv => enter [1, œâ]; change (WithLp.prodContinuousLinearEquiv p ‚Ñù E F).symm _
  infer_instance

lemma HasGaussianLaw.fst {E F : Type*}
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù F]
    [MeasurableSpace E] [MeasurableSpace F] [BorelSpace E] [BorelSpace F]
    [SecondCountableTopology E] [SecondCountableTopology F]
    {X : Œ© ‚Üí E} {Y : Œ© ‚Üí F} [HasGaussianLaw (fun œâ ‚Ü¶ (X œâ, Y œâ)) P] :
    HasGaussianLaw X P := by
  have : X = (ContinuousLinearMap.fst ‚Ñù E F) ‚àò (fun œâ ‚Ü¶ (X œâ, Y œâ)) := by ext; simp
  rw [this]
  infer_instance

lemma HasGaussianLaw.snd {E F : Type*}
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù F]
    [MeasurableSpace E] [MeasurableSpace F] [BorelSpace E] [BorelSpace F]
    [SecondCountableTopology E] [SecondCountableTopology F]
    {X : Œ© ‚Üí E} {Y : Œ© ‚Üí F} [HasGaussianLaw (fun œâ ‚Ü¶ (X œâ, Y œâ)) P] :
    HasGaussianLaw Y P := by
  have : Y = (ContinuousLinearMap.snd ‚Ñù E F) ‚àò (fun œâ ‚Ü¶ (X œâ, Y œâ)) := by ext; simp
  rw [this]
  infer_instance

instance IsGaussian.map_eval {Œπ : Type*} [Fintype Œπ] {E : Œπ ‚Üí Type*}
    [‚àÄ i, NormedAddCommGroup (E i)] [‚àÄ i, NormedSpace ‚Ñù (E i)] {mE : ‚àÄ i, MeasurableSpace (E i)}
    [‚àÄ i, BorelSpace (E i)] [‚àÄ i, SecondCountableTopology (E i)]
    {Œº : Measure (Œ† i, E i)} [IsGaussian Œº] (i : Œπ) : HasGaussianLaw (fun x ‚Ü¶ x i) Œº := by
  refine HasGaussianLaw.eval (Œ© := Œ† j, E j) (X := fun i x ‚Ü¶ x i) (h := ?_) i
  exact IsGaussian.hasGaussianLaw_id

instance IsGaussian.map_eval_piLp (p : ‚Ñù‚â•0‚àû) [Fact (1 ‚â§ p)] {Œπ : Type*} [Fintype Œπ] {E : Œπ ‚Üí Type*}
    [‚àÄ i, NormedAddCommGroup (E i)] [‚àÄ i, NormedSpace ‚Ñù (E i)] {mE : ‚àÄ i, MeasurableSpace (E i)}
    [‚àÄ i, BorelSpace (E i)] [‚àÄ i, SecondCountableTopology (E i)]
    {Œº : Measure (PiLp p E)} [IsGaussian Œº] (i : Œπ) : HasGaussianLaw (fun x ‚Ü¶ x i) Œº :=
  IsGaussian.map_eval i

instance HasGaussianLaw.sub {Œπ Œ© E : Type*} [Fintype Œπ] {mŒ© : MeasurableSpace Œ©}
    {P : Measure Œ©} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
    [SecondCountableTopology E] {X : Œπ ‚Üí Œ© ‚Üí E}
    [h : HasGaussianLaw (fun œâ ‚Ü¶ (X ¬∑ œâ)) P] (i j : Œπ) :
    HasGaussianLaw (X i - X j) P := by
  have : X i - X j = (ContinuousLinearMap.proj (R := ‚Ñù) (œÜ := fun _ ‚Ü¶ E) i -
      ContinuousLinearMap.proj (R := ‚Ñù) (œÜ := fun _ ‚Ü¶ E) j) ‚àò (fun œâ ‚Ü¶ (X ¬∑ œâ)) := by ext; simp
  rw [this]
  infer_instance

instance IsGaussian.map_eval_sub_eval {Œπ E : Type*} [Fintype Œπ]
    [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
    [SecondCountableTopology E] {Œº : Measure (Œπ ‚Üí E)} [IsGaussian Œº] (i j : Œπ) :
    HasGaussianLaw (fun x ‚Ü¶ x i - x j) Œº := by
  refine HasGaussianLaw.sub (Œ© := Œπ ‚Üí E) (X := fun i x ‚Ü¶ x i) (h := ?_) i j
  exact IsGaussian.hasGaussianLaw_id

instance IsGaussian.map_eval_sub_eval_piLp (p : ‚Ñù‚â•0‚àû) [Fact (1 ‚â§ p)] {Œπ E : Type*} [Fintype Œπ]
    [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
    [SecondCountableTopology E]
    {Œº : Measure (PiLp p (fun _ ‚Ü¶ E))} [IsGaussian Œº] (i j : Œπ) :
    HasGaussianLaw (fun x ‚Ü¶ x i - x j) Œº :=
  IsGaussian.map_eval_sub_eval i j

end NormedSpace

end HasGaussianLaw

end ProbabilityTheory
