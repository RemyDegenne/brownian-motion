import Mathlib.Analysis.Normed.Lp.MeasurableSpace
import Mathlib.Probability.Distributions.Gaussian.Basic
import Mathlib.Probability.HasLaw

open MeasureTheory ENNReal WithLp

namespace ProbabilityTheory

variable {Î© : Type*} {mÎ© : MeasurableSpace Î©}

section HasLaw

variable {ğ“§} {mğ“§ : MeasurableSpace ğ“§} {X : Î© â†’ ğ“§} {Î¼ : Measure ğ“§} {P : Measure Î©}

lemma hasLaw_map (hX : AEMeasurable X P) : HasLaw X (P.map X) P where
  aemeasurable := hX
  map_eq := rfl

section dirac

lemma HasLaw.aeeq_of_dirac' {ğ“§ : Type*} {mğ“§ : MeasurableSpace ğ“§} [MeasurableSingletonClass ğ“§]
    {x : ğ“§} {X : Î© â†’ ğ“§}
    (hX : HasLaw X (.dirac x) P) : X =áµ[P] (fun _ â†¦ x) := by
  apply ae_of_ae_map (p := fun y â†¦ y = x) hX.aemeasurable
  rw [hX.map_eq, ae_dirac_iff]
  simp

lemma HasLaw.aeeq_of_dirac {ğ“§ : Type*} {mğ“§ : MeasurableSpace ğ“§} [MeasurableSingletonClass ğ“§]
    {x : ğ“§} {X : Î© â†’ ğ“§}
    (hX : HasLaw X (.dirac x) P) : âˆ€áµ Ï‰ âˆ‚P, X Ï‰ = x := hX.aeeq_of_dirac'

lemma HasLaw.aeeq_const_of_gaussianReal {X : Î© â†’ â„} {Î¼ : â„} (hX : HasLaw X (gaussianReal Î¼ 0) P) :
    âˆ€áµ Ï‰ âˆ‚P, X Ï‰ = Î¼ := by
  rw [gaussianReal_zero_var] at hX
  exact hX.aeeq_of_dirac

end dirac

variable {E : Type*} [NormedAddCommGroup E] [MeasurableSpace E] [BorelSpace E]
    [SecondCountableTopology E] [IsProbabilityMeasure P] {X Y : Î© â†’ E}

lemma IndepFun.charFunDual_map_add_eq_mul [NormedSpace â„ E]
    (mX : AEMeasurable X P) (mY : AEMeasurable Y P) (hXY : IndepFun X Y P) :
    charFunDual (P.map (X + Y)) = charFunDual (P.map X) * charFunDual (P.map Y) := by
  ext L
  rw [(hXY.hasLaw_add (hasLaw_map mX) (hasLaw_map mY)).map_eq, charFunDual_conv, Pi.mul_apply]

lemma IndepFun.charFun_map_add_eq_mul [InnerProductSpace â„ E]
    (mX : AEMeasurable X P) (mY : AEMeasurable Y P) (hXY : IndepFun X Y P) :
    charFun (P.map (X + Y)) = charFun (P.map X) * charFun (P.map Y) := by
  ext t
  rw [(hXY.hasLaw_add (hasLaw_map mX) (hasLaw_map mY)).map_eq, charFun_conv, Pi.mul_apply]

end HasLaw

section HasGaussianLaw

variable {E : Type*} (X : Î© â†’ E) (P : Measure Î©)

section Basic

variable [TopologicalSpace E] [AddCommMonoid E] [Module â„ E] [mE : MeasurableSpace E]

/-- The predicate `HasGaussianLaw X P` means that under the measure `P`,
`X` has a Gaussian distribution -/
class HasGaussianLaw :
    Prop where
  protected isGaussian_map : IsGaussian (P.map X)

attribute [instance] HasGaussianLaw.isGaussian_map

variable {X P}

lemma HasGaussianLaw.congr {Y : Î© â†’ E} [HasGaussianLaw X P] (h : âˆ€áµ Ï‰ âˆ‚P, X Ï‰ = Y Ï‰) :
    HasGaussianLaw Y P where
  isGaussian_map := by
    rw [â† Measure.map_congr h]
    infer_instance

instance IsGaussian.hasGaussianLaw [IsGaussian (P.map X)] :
    HasGaussianLaw X P where
  isGaussian_map := inferInstance

instance IsGaussian.hasGaussianLaw_fun [IsGaussian (P.map X)] :
    HasGaussianLaw (fun Ï‰ â†¦ X Ï‰) P where
  isGaussian_map := inferInstance

variable {mE} in
instance IsGaussian.hasGaussianLaw_id {Î¼ : Measure E} [IsGaussian Î¼] :
    HasGaussianLaw id Î¼ where
  isGaussian_map := by rwa [Measure.map_id]

@[fun_prop, measurability]
lemma HasGaussianLaw.aemeasurable [hX : HasGaussianLaw X P] : AEMeasurable X P := by
  by_contra! h
  have := hX.isGaussian_map
  rw [Measure.map_of_not_aemeasurable h] at this
  exact this.toIsProbabilityMeasure.ne_zero _ rfl

lemma HasGaussianLaw.isProbabilityMeasure [HasGaussianLaw X P] : IsProbabilityMeasure P where
  measure_univ := by
    rw [â† Set.preimage_univ (f := X), â† Measure.map_apply_of_aemeasurable (by fun_prop) .univ,
      measure_univ]

variable {mE} in
lemma HasLaw.hasGaussianLaw {Î¼ : Measure E} (hX : HasLaw X Î¼ P) [IsGaussian Î¼] :
    HasGaussianLaw X P where
  isGaussian_map := by rwa [hX.map_eq]

end Basic

section NormedSpace

variable [NormedAddCommGroup E] [NormedSpace â„ E] [MeasurableSpace E] [BorelSpace E]
    {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [MeasurableSpace F] [BorelSpace F]
    (L : E â†’L[â„] F) {X P}

instance HasGaussianLaw.map [HasGaussianLaw X P] : HasGaussianLaw (L âˆ˜ X) P where
  isGaussian_map := by
    rw [â† AEMeasurable.map_map_of_aemeasurable]
    Â· infer_instance
    all_goals fun_prop

instance HasGaussianLaw.map_fun [hX : HasGaussianLaw X P] : HasGaussianLaw (fun Ï‰ â†¦ L (X Ï‰)) P :=
  hX.map L

variable (L : E â‰ƒL[â„] F)

instance HasGaussianLaw.map_equiv [HasGaussianLaw X P] : HasGaussianLaw (L âˆ˜ X) P where
  isGaussian_map := by
    rw [â† AEMeasurable.map_map_of_aemeasurable]
    Â· infer_instance
    all_goals fun_prop

instance HasGaussianLaw.map_equiv_fun [hX : HasGaussianLaw X P] :
    HasGaussianLaw (fun Ï‰ â†¦ L (X Ï‰)) P := hX.map_equiv L

section SpecificMaps

section Prod

variable [SecondCountableTopologyEither E F] {Y : Î© â†’ F}

instance HasGaussianLaw.toLp_comp_prodMk (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)]
    [HasGaussianLaw (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) P] :
    HasGaussianLaw (fun Ï‰ â†¦ toLp p (X Ï‰, Y Ï‰)) P := by
  conv => enter [1, Ï‰]; change (WithLp.prodContinuousLinearEquiv p â„ E F).symm _
  infer_instance

lemma HasGaussianLaw.fst [HasGaussianLaw (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) P] :
    HasGaussianLaw X P := by
  have : X = (ContinuousLinearMap.fst â„ E F) âˆ˜ (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) := by ext; simp
  rw [this]
  infer_instance

lemma HasGaussianLaw.snd [HasGaussianLaw (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) P] :
    HasGaussianLaw Y P := by
  have : Y = (ContinuousLinearMap.snd â„ E F) âˆ˜ (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) := by ext; simp
  rw [this]
  infer_instance

end Prod

section Pi

variable [SecondCountableTopology E] {Î¹ : Type*} [Fintype Î¹] {X : Î¹ â†’ Î© â†’ E}

section Nondependent

instance HasGaussianLaw.sub [h : HasGaussianLaw (fun Ï‰ â†¦ (X Â· Ï‰)) P] (i j : Î¹) :
    HasGaussianLaw (X i - X j) P := by
  have : X i - X j = (ContinuousLinearMap.proj (R := â„) (Ï† := fun _ â†¦ E) i -
      ContinuousLinearMap.proj (R := â„) (Ï† := fun _ â†¦ E) j) âˆ˜ (fun Ï‰ â†¦ (X Â· Ï‰)) := by ext; simp
  rw [this]
  infer_instance

instance IsGaussian.hasGaussianLaw_sub_eval {Î¼ : Measure (Î¹ â†’ E)} [IsGaussian Î¼] (i j : Î¹) :
    HasGaussianLaw (fun x â†¦ x i - x j) Î¼ :=
  HasGaussianLaw.sub (h := IsGaussian.hasGaussianLaw_id) i j

instance IsGaussian.hasGaussianLaw_sub_eval_piLp (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)]
    {Î¼ : Measure (PiLp p (fun _ â†¦ E))} [IsGaussian Î¼] (i j : Î¹) :
    HasGaussianLaw (fun x â†¦ x i - x j) Î¼ :=
  IsGaussian.hasGaussianLaw_sub_eval i j

end Nondependent

variable {E : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
  [âˆ€ i, NormedSpace â„ (E i)] [âˆ€ i, MeasurableSpace (E i)] [âˆ€ i, BorelSpace (E i)]
  [âˆ€ i, SecondCountableTopology (E i)] {X : (i : Î¹) â†’ Î© â†’ E i}

instance HasGaussianLaw.eval [âˆ€ i, SecondCountableTopology (E i)] {X : (i : Î¹) â†’ Î© â†’ E i}
    [h : HasGaussianLaw (fun Ï‰ â†¦ (X Â· Ï‰)) P] (i : Î¹) :
    HasGaussianLaw (X i) P := by
  have : X i = (ContinuousLinearMap.proj (R := â„) (Ï† := E) i) âˆ˜ (fun Ï‰ â†¦ (X Â· Ï‰)) := by ext; simp
  rw [this]
  infer_instance

instance HasGaussianLaw.toLp_comp_pi (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)]
    [HasGaussianLaw (fun Ï‰ â†¦ (X Â· Ï‰)) P] :
    HasGaussianLaw (fun Ï‰ â†¦ toLp p (X Â· Ï‰)) P := by
  rw [â† PiLp.continuousLinearEquiv_symm_apply p â„]
  infer_instance

instance IsGaussian.hasGaussianLaw_eval {Î¼ : Measure (Î  i, E i)} [IsGaussian Î¼] (i : Î¹) :
    HasGaussianLaw (fun x â†¦ x i) Î¼ :=
  HasGaussianLaw.eval (h := IsGaussian.hasGaussianLaw_id) i

instance IsGaussian.hasGaussianLaw_eval_piLp (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)]
    {Î¼ : Measure (PiLp p E)} [IsGaussian Î¼] (i : Î¹) : HasGaussianLaw (fun x â†¦ x i) Î¼ :=
  IsGaussian.hasGaussianLaw_eval i

end Pi

end SpecificMaps

end NormedSpace

end HasGaussianLaw

end ProbabilityTheory
