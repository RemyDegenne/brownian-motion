/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Thomas Zhu
-/
import BrownianMotion.Auxiliary.Martingale
import Mathlib.Probability.Martingale.OptionalStopping

/-! # Doob's Láµ– inequality

-/

open MeasureTheory Filter Finset
open scoped ENNReal NNReal Topology

namespace ProbabilityTheory

variable {Î¹ Î© E : Type*} [LinearOrder Î¹]
  [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  {mÎ© : MeasurableSpace Î©} {P : Measure Î©} {X : Î¹ â†’ Î© â†’ E} {ğ“• : Filtration Î¹ mÎ©}
  {Y : Î¹ â†’ Î© â†’ â„} [IsFiniteMeasure P]

section Countable

-- TODO: Mathlib version uses `Îµ â€¢ (P ...) â‰¤ ENNReal.ofReal (âˆ« Ï‰ in ..., ... âˆ‚P)`
-- which should be changed to `Îµ â€¢ (P.real ...) â‰¤ âˆ« Ï‰ in ..., ... âˆ‚P`,
-- using the more recent `Measure.real` API as follows:
lemma maximal_ineq' {ğ“• : Filtration â„• mÎ©} {f : â„• â†’ Î© â†’ â„} (hsub : Submartingale f ğ“• P)
    (hnonneg : 0 â‰¤ f) (Îµ : â„â‰¥0) (n : â„•) :
    Îµ â€¢ P.real {Ï‰ | (Îµ : â„) â‰¤ (range (n + 1)).sup' nonempty_range_add_one fun k => f k Ï‰} â‰¤
    âˆ« Ï‰ in {Ï‰ | (Îµ : â„) â‰¤ (range (n + 1)).sup' nonempty_range_add_one fun k => f k Ï‰},
      f n Ï‰ âˆ‚P := by
  calc
    _ = (Îµ â€¢ P {Ï‰ | (Îµ : â„) â‰¤ (range (n + 1)).sup' nonempty_range_add_one fun k => f k Ï‰}).toReal :=
      (ENNReal.toReal_smul ..).symm
    _ â‰¤ _ := by
      apply ENNReal.toReal_le_of_le_ofReal (integral_nonneg (hnonneg n))
      exact maximal_ineq hsub hnonneg n

-- NB: This might be shortended, if the mathlib result is generalized to
-- a more general discrete index space.
/-- Auxiliary lemma for `maximal_ineq_countable` where the index set is a Finset. -/
lemma maximal_ineq_finset (hsub : Submartingale Y ğ“• P) (hnonneg : 0 â‰¤ Y) (Îµ : â„â‰¥0) {n : Î¹}
    {J : Finset Î¹} (hJn : âˆ€ i âˆˆ J, i â‰¤ n) (hnJ : n âˆˆ J) :
    Îµ â€¢ P.real {Ï‰ | (Îµ : â„) â‰¤ J.sup' âŸ¨n, hnJâŸ© fun i â†¦ Y i Ï‰} â‰¤
     âˆ« Ï‰ in {Ï‰ | (Îµ : â„) â‰¤ J.sup' âŸ¨n, hnJâŸ© fun i â†¦ Y i Ï‰}, Y n Ï‰ âˆ‚P := by
  -- Convert to â„•-indexed submartingale defined by (Y(jâ‚), â‹¯, Y(jâ‚˜), Y(n), Y(n), â‹¯)
  -- where J = {jâ‚, â‹¯, jâ‚˜, n}, and jâ‚ < â‹¯ < jâ‚˜ = n
  classical
  let toÎ¹ (k : â„•) : Î¹ := if hn : k < #J then J.orderEmbOfFin rfl âŸ¨k, hnâŸ© else n
  have toÎ¹_mono : Monotone toÎ¹ := fun k l hkl â†¦ by
    unfold toÎ¹
    split_ifs with hk hl hl
    exacts [(J.orderEmbOfFin rfl).monotone hkl, hJn _ (orderEmbOfFin_mem ..), by omega, le_refl _]
  have hcongr (Ï‰ : Î©) : J.sup' âŸ¨n, hnJâŸ© (fun i â†¦ Y i Ï‰) =
      (range (#J + 1)).sup' nonempty_range_add_one fun k â†¦ Y (toÎ¹ k) Ï‰ := by
    unfold toÎ¹
    apply le_antisymm
    Â· refine sup'_le _ _ fun i hi â†¦ ?_
      refine le_sup'_of_le _ (b := ((J.orderIsoOfFin rfl).symm âŸ¨i, hiâŸ© : â„•)) ?_ ?_
      Â· simp
      Â· simp [orderEmbOfFin]
    Â· refine sup'_le _ _ fun k hk â†¦ ?_
      apply le_sup' fun i â†¦ Y i Ï‰
      split_ifs
      exacts [orderEmbOfFin_mem .., hnJ]
  calc
    _ = Îµ â€¢ P.real
        {Ï‰ | (Îµ : â„) â‰¤ (range (#J + 1)).sup' nonempty_range_add_one fun k â†¦ Y (toÎ¹ k) Ï‰} := by
      simp_rw [hcongr]
    _ â‰¤ âˆ« Ï‰ in {Ï‰ | (Îµ : â„) â‰¤ (range (#J + 1)).sup' nonempty_range_add_one fun k â†¦ Y (toÎ¹ k) Ï‰},
        Y n Ï‰ âˆ‚P := by
      convert maximal_ineq' (hsub.indexComap toÎ¹_mono) (fun _ â†¦ hnonneg _) Îµ #J
      simp [toÎ¹]
    _ = _ := by
      congr! with Ï‰
      simp_rw [hcongr]

variable [Countable Î¹]

lemma _root_.tendsto_inv_add_atTop_nhds_zero_nat {ğ•œ : Type*} [DivisionSemiring ğ•œ] [CharZero ğ•œ]
    [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] :
    Tendsto (fun n : â„• â†¦ ((n : ğ•œ) + 1)â»Â¹) atTop (ğ“ 0) :=
  by simpa using tendsto_one_div_add_atTop_nhds_zero_nat (ğ•œ := ğ•œ)

lemma maximal_ineq_countable_ennReal (hsub : Submartingale Y ğ“• P) (hnonneg : 0 â‰¤ Y) (Îµ : â„â‰¥0)
    (n : Î¹) :
    Îµ â€¢ P.real {Ï‰ | (Îµ : â„â‰¥0âˆ) â‰¤ â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰)} â‰¤
      âˆ« Ï‰ in {Ï‰ | (Îµ : â„â‰¥0âˆ) â‰¤ â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰)}, Y n Ï‰ âˆ‚P := by
  let supY (Ï‰ : Î©) := â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰)
  -- WLOG `Îµ > 0`
  rcases eq_or_ne Îµ 0 with rfl | hÎµ0
  Â· simpa using integral_nonneg (hnonneg n)
  -- Construct an increasing sequence `J k` of finite sets with union `(-âˆ, n]`
  have : Nonempty Î¹ := âŸ¨nâŸ©
  obtain âŸ¨f : â„• â†’ Î¹, hfâŸ© := exists_surjective_nat Î¹
  let J (k : â„•) : Finset Î¹ := insert n ((range k).image f |>.filter (Â· â‰¤ n))
  have hJn (k) : âˆ€ i âˆˆ J k, i â‰¤ n := by simp [J]
  have hnJ (k) : n âˆˆ J k := by simp [J]
  have hJmono {k l : â„•} (hkl : k â‰¤ l) : J k âŠ† J l := by
    unfold J
    gcongr
    exact image_mono _ (range_mono hkl)
  have hmemJ (k) (h : f k â‰¤ n) : f k âˆˆ J (k + 1) := by
    simpa [J, h] using .inr âŸ¨k, by omega, rflâŸ©
  -- The long inequality (see blueprint)
  have hlt (Îµ' : â„â‰¥0) (hÎµ' : Îµ' < Îµ) :
    Îµ' â€¢ P.real {Ï‰ | (Îµ' : â„â‰¥0âˆ) < supY Ï‰} â‰¤ âˆ« Ï‰ in {Ï‰ | (Îµ' : â„â‰¥0âˆ) â‰¤ supY Ï‰}, Y n Ï‰ âˆ‚P := by
    have hbdd : BddAbove <| Set.range fun k â†¦
        âˆ« Ï‰ in {Ï‰ | (Îµ' : â„) â‰¤ (J k).sup' âŸ¨n, hnJ kâŸ© fun i â†¦ Y i Ï‰}, Y n Ï‰ âˆ‚P := by
      use âˆ« Ï‰, Y n Ï‰ âˆ‚P
      simpa [upperBounds] using fun k â†¦
        setIntegral_le_integral (hsub.integrable n) (.of_forall (hnonneg n))
    calc
      _ = Îµ' â€¢ P.real (â‹ƒ i â‰¤ n, {Ï‰ | (Îµ' : â„) < Y i Ï‰}) := by
        congr!; ext Ï‰
        simp_rw [supY, lt_iSup_iff]
        lift Y to Î¹ â†’ Î© â†’ â„â‰¥0 using hnonneg
        simp
      _ = Îµ' â€¢ P.real (â‹ƒ k, {Ï‰ | (Îµ' : â„) < (J k).sup' âŸ¨n, hnJ kâŸ© fun i â†¦ Y i Ï‰}) := by
        congr!; ext Ï‰
        simp only [Set.mem_iUnion, Set.mem_setOf_eq, exists_prop, lt_sup'_iff]
        constructor
        Â· rintro âŸ¨i, hi, hâŸ©
          obtain âŸ¨k, rflâŸ© := hf i
          use k + 1, f k, hmemJ k hi
        Â· rintro âŸ¨k, i, hi, hâŸ©
          use i, by simp [hJn k i hi]
      _ = â¨† k, Îµ' â€¢ P.real {Ï‰ | (Îµ' : â„) < (J k).sup' âŸ¨n, hnJ kâŸ© fun i â†¦ Y i Ï‰} := by
        rw [Measure.real, Monotone.measure_iUnion, ENNReal.toReal_iSup]
        Â· apply Real.mul_iSup_of_nonneg
          simp
        Â· finiteness
        intro k l hkl
        simpa using fun Ï‰ i hi h â†¦ âŸ¨i, hJmono hkl hi, hâŸ©
      _ â‰¤ â¨† k, Îµ' â€¢ P.real {Ï‰ | (Îµ' : â„) â‰¤ (J k).sup' âŸ¨n, hnJ kâŸ© fun i â†¦ Y i Ï‰} := by
        gcongr
        Â· use Îµ' â€¢ P.real Set.univ
          simp only [upperBounds, le_sup'_iff, Set.mem_range, forall_exists_index,
            forall_apply_eq_imp_iff, Set.mem_setOf_eq]
          intro k
          gcongr
          Â· finiteness -- gcongr bug?
          simp
        Â· finiteness -- gcongr bug?
        Â· exact fun h â†¦ h.le
      _ â‰¤ â¨† k, âˆ« Ï‰ in {Ï‰ | (Îµ' : â„) â‰¤ (J k).sup' âŸ¨n, hnJ kâŸ© fun i â†¦ Y i Ï‰}, Y n Ï‰ âˆ‚P := by
        gcongr with k
        Â· exact hbdd
        Â· exact maximal_ineq_finset hsub hnonneg Îµ' (hJn k) (hnJ k)
      _ â‰¤ âˆ« Ï‰ in {Ï‰ | (Îµ' : â„â‰¥0âˆ) â‰¤ supY Ï‰}, Y n Ï‰ âˆ‚P := by
        refine (ciSup_le_iff hbdd).mpr fun k â†¦ ?_
        gcongr with Ï‰
        Â· filter_upwards; exact fun Ï‰ â†¦ hnonneg _ _
        Â· exact (hsub.integrable n).restrict
        Â· simp only [le_sup'_iff, forall_exists_index, and_imp]
          intro i hi h
          apply le_iSupâ‚‚_of_le i (hJn k i hi)
          lift Y to Î¹ â†’ Î© â†’ â„â‰¥0 using hnonneg
          simpa using h
  -- Take `Îµ' := Îµ - 1 / (r + 1) â†‘ Îµ` where `r â†’ âˆ`
  -- (This is needed instead of `Îµ' â†‘ Îµ` directly, because `tendsto_measure_iInter_atTop` and
  -- `tendsto_setIntegral_of_antitone` lemmas require `atTop` instead of `ğ“[<] Îµ`)
  clear * - hÎµ0 hsub hlt
  let Îµ' (r : â„•) : â„â‰¥0 := Îµ - (r + 1 : â„â‰¥0)â»Â¹
  -- TODO: is there a way to avoid duplicaiton below
  have hinter_lt (c : Î© â†’ â„â‰¥0âˆ) : {Ï‰ | Îµ â‰¤ c Ï‰} = â‹‚ r : â„•, {Ï‰ | Îµ' r < c Ï‰} := by
    ext Ï‰
    simp only [Set.mem_setOf_eq, Set.mem_iInter]
    constructor
    Â· intro h r
      push_cast [Îµ']
      exact h.trans_lt' (ENNReal.sub_lt_self (by simp) (by simpa) (by simp))
    Â· refine fun h â†¦ le_of_forall_lt fun Îµ' hÎµ' â†¦ ?_
      have : Tendsto (fun r : â„• â†¦ (((r + 1)â»Â¹ : â„â‰¥0) : â„â‰¥0âˆ)) atTop (ğ“ 0) := by
        rw [â† ENNReal.tendsto_toNNReal_iff (by finiteness) (by finiteness)]
        change Tendsto (fun r : â„• â†¦ _) _ _
        simpa using tendsto_inv_add_atTop_nhds_zero_nat (ğ•œ := â„â‰¥0)
      obtain âŸ¨r, hrâŸ© := this.eventually_lt_const (tsub_pos_of_lt hÎµ') |>.exists
      exact (lt_tsub_comm.mp hr).trans (h r)
  have hinter_le (c : Î© â†’ â„â‰¥0âˆ) : {Ï‰ | Îµ â‰¤ c Ï‰} = â‹‚ r : â„•, {Ï‰ | Îµ' r â‰¤ c Ï‰} := by
    -- same as hinter, but with â‰¤ instead of <
    ext Ï‰
    simp only [Set.mem_setOf_eq, Set.mem_iInter]
    constructor
    Â· intro h r
      push_cast [Îµ']
      exact h.trans' tsub_le_self
    Â· refine fun h â†¦ le_of_forall_lt fun Îµ' hÎµ' â†¦ ?_
      have : Tendsto (fun r : â„• â†¦ (((r + 1)â»Â¹ : â„â‰¥0) : â„â‰¥0âˆ)) atTop (ğ“ 0) := by
        rw [â† ENNReal.tendsto_toNNReal_iff (by finiteness) (by finiteness)]
        change Tendsto (fun r : â„• â†¦ _) _ _
        simpa using tendsto_inv_add_atTop_nhds_zero_nat (ğ•œ := â„â‰¥0)
      obtain âŸ¨r, hrâŸ© := this.eventually_lt_const (tsub_pos_of_lt hÎµ') |>.exists
      exact (lt_tsub_comm.mp hr).trans_le (h r)
  have hmeasY (i : Î¹) : Measurable (Y i) :=
    (hsub.stronglyMeasurable i).measurable.mono (ğ“•.le _) (le_refl _)
  have hÎµ'mono : Monotone fun r â†¦ (Îµ' r : â„â‰¥0âˆ) := by intro _ _ _; dsimp [Îµ']; gcongr
  -- LHS of `hlt` tends to LHS of `âŠ¢`
  have hl : Tendsto
      (fun r : â„• â†¦ Îµ' r â€¢ P.real {Ï‰ | Îµ' r < supY Ï‰})
      atTop (ğ“ <| Îµ â€¢ P.real {Ï‰ | Îµ â‰¤ supY Ï‰}) := by
    apply (show Tendsto .. by simpa using tendsto_inv_add_atTop_nhds_zero_nat.const_sub Îµ).smul
    erw [ENNReal.tendsto_toReal_iff (by finiteness) (by finiteness)]
    convert tendsto_measure_iInter_atTop ?_ ?_ ?_
    Â· exact hinter_lt _
    Â· infer_instance
    Â· exact fun r â†¦ (measurableSet_lt measurable_const (by fun_prop)).nullMeasurableSet
    Â· exact Set.monotone_preimage.comp_antitone hÎµ'mono.Ioi
    Â· use 0; finiteness
  -- RHS of `hlt` tends to RHS of `âŠ¢`
  have hr : Tendsto
      (fun r : â„• â†¦ âˆ« Ï‰ in {Ï‰ | Îµ' r â‰¤ supY Ï‰}, Y n Ï‰ âˆ‚P)
      atTop (ğ“ <| âˆ« Ï‰ in {Ï‰ | Îµ â‰¤ supY Ï‰}, Y n Ï‰ âˆ‚P) := by
    convert tendsto_setIntegral_of_antitone ?_ ?_ ?_
    Â· exact hinter_le _
    Â· infer_instance
    Â· exact fun r â†¦ measurableSet_le measurable_const (by fun_prop)
    Â· exact Set.monotone_preimage.comp_antitone hÎµ'mono.Ici
    Â· use 0; exact (hsub.integrable n).integrableOn
  -- Conclude
  exact le_of_tendsto_of_tendsto hl hr (.of_forall fun r â†¦ hlt _ (by simpa [Îµ'] using hÎµ0.bot_lt))

-- TODO: add this to Mathlib
attribute [aesop (rule_sets := [finiteness]) safe apply] ENNReal.nnreal_smul_ne_top

theorem _root_.ENNReal.ofReal_smul {a : â„â‰¥0} {b : â„} :
    ENNReal.ofReal (a â€¢ b) = a â€¢ ENNReal.ofReal b := by
  erw [ENNReal.ofReal_mul (by simp)]
  simp
  rfl

/-- Alternative form of `Submartingale.ae_bddAbove`. -/
lemma _root_.MeasureTheory.Submartingale.iSup_ofReal_ne_top (hsub : Submartingale Y ğ“• P)
    (hnonneg : 0 â‰¤ Y) (n : Î¹) : âˆ€áµ Ï‰ âˆ‚P, â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰) â‰  âˆ := by
  let supY (Ï‰ : Î©) := â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰)
  have hmeasY (i : Î¹) : Measurable (Y i) :=
    (hsub.stronglyMeasurable i).measurable.mono (ğ“•.le _) (le_refl _)
  change P {Ï‰ | Â¬supY Ï‰ â‰  âˆ} = 0
  push_neg
  convert Antitone.measure_iInter (s := fun Îµ : â„â‰¥0 â†¦ {Ï‰ | (Îµ : â„â‰¥0âˆ) â‰¤ supY Ï‰}) ?_ ?_ ?_
  Â· ext Ï‰
    simp only [Set.mem_setOf_eq, Set.mem_iInter]
    constructor
    Â· simp +contextual
    Â· apply ENNReal.eq_top_of_forall_nnreal_le
  Â· symm
    erw [â† le_bot_iff]
    calc
      _ â‰¤ â¨… Îµ > (0 : â„â‰¥0), ENNReal.ofReal (Îµâ»Â¹ â€¢ âˆ« Ï‰ in {Ï‰ | Îµ â‰¤ supY Ï‰}, Y n Ï‰ âˆ‚P) := by
        gcongr with Îµ
        refine le_iInf fun hÎµ0 â†¦ ?_
        rw [ENNReal.ofReal_smul, le_inv_smul_iff_of_pos hÎµ0, ENNReal.le_ofReal_iff_toReal_le]
        Â· simpa using maximal_ineq_countable_ennReal hsub hnonneg Îµ n
        Â· finiteness
        Â· exact setIntegral_nonneg (measurableSet_le measurable_const (by fun_prop))
            fun Ï‰ _ â†¦ hnonneg n Ï‰
      _ â‰¤ â¨… Îµ > (0 : â„â‰¥0), ENNReal.ofReal (Îµâ»Â¹ â€¢ âˆ« Ï‰, Y n Ï‰ âˆ‚P) := by
        gcongr with Îµ hÎµ0
        Â· exact .of_forall (hnonneg n)
        Â· exact hsub.integrable n
        Â· exact P.restrict_le_self
      _ = 0 := by
        apply iInf_eq_of_tendsto
        Â· intro Îµâ‚ Îµâ‚‚ h
          refine le_iInf fun hÎµâ‚ â†¦ ?_
          simp only [iInf_pos (hÎµâ‚.trans_le h)]
          gcongr
          exact integral_nonneg (hnonneg n)
        Â· convert (ENNReal.tendsto_ofReal ((tendsto_inv_atTop_zero (ğ•œ := â„â‰¥0)).smul_const
            (âˆ« Ï‰, Y n Ï‰ âˆ‚P))).congr' ?_
          Â· simp
          Â· filter_upwards [eventually_gt_atTop 0] with Îµ hÎµ0
            simp [hÎµ0]
  Â· exact Set.monotone_preimage.comp_antitone ENNReal.coe_mono.Ici
  Â· exact fun r â†¦ (measurableSet_le measurable_const (by fun_prop)).nullMeasurableSet
  Â· use 0; finiteness

/-- Doob's maximal inequality implies that the supremum process of a nonnegative submartingale is
a.s. bounded. -/
theorem _root_.MeasureTheory.Submartingale.ae_bddAbove_Iic (hsub : Submartingale Y ğ“• P)
    (hnonneg : 0 â‰¤ Y) (n : Î¹) :
    âˆ€áµ Ï‰ âˆ‚P, BddAbove ((fun i â†¦ Y i Ï‰) '' Set.Iic n) := by
  filter_upwards [hsub.iSup_ofReal_ne_top hnonneg n] with Ï‰ h
  use (â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰)).toReal
  rintro _ âŸ¨i, hi : i â‰¤ n, rflâŸ©
  rw [â† ENNReal.ofReal_le_iff_le_toReal h]
  exact le_iSupâ‚‚ (f := fun i _ â†¦ ENNReal.ofReal (Y i Ï‰)) i hi

/-- **Doob's maximal inequality** for a countable index set. -/
theorem maximal_ineq_countable (hsub : Submartingale Y ğ“• P) (hnonneg : 0 â‰¤ Y) (Îµ : â„â‰¥0) (n : Î¹) :
    -- We use `â¨† i : Set.Iic n` instead of `â¨† i â‰¤ n` because of incomplete API for `cbiSup`.
    Îµ â€¢ P.real {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, Y i Ï‰} â‰¤
      âˆ« Ï‰ in {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, Y i Ï‰}, Y n Ï‰ âˆ‚P := by
  have (Ï‰ : Î©) : â¨† i : Set.Iic n, Y i Ï‰ = (â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰)).toReal := by
    rw [iSup_subtype', ENNReal.toReal_iSup]
    Â· congr with i
      rw [ENNReal.toReal_ofReal (hnonneg _ _)]
    Â· finiteness
  have : {Ï‰ | Îµ â‰¤ â¨† i : Set.Iic n, Y i Ï‰} =áµ[P] {Ï‰ | Îµ â‰¤ â¨† i â‰¤ n, ENNReal.ofReal (Y i Ï‰)} := by
    filter_upwards [hsub.iSup_ofReal_ne_top hnonneg n] with Ï‰ htop
    ext
    change _ â‰¤ _ â†” _ â‰¤ _
    rw [â† ENNReal.ofReal_coe_nnreal, ENNReal.ofReal_le_iff_le_toReal htop, this]
  rw [measureReal_congr this, setIntegral_congr_set this]
  exact maximal_ineq_countable_ennReal hsub hnonneg Îµ n

theorem maximal_ineq_norm_countable (hmar : Martingale X ğ“• P) (Îµ : â„â‰¥0) (n : Î¹) :
    Îµ â€¢ P.real {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, â€–X i Ï‰â€–} â‰¤
      âˆ« Ï‰ in {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, â€–X i Ï‰â€–}, â€–X n Ï‰â€– âˆ‚P :=
  maximal_ineq_countable hmar.submartingale_norm (fun _ _ â†¦ norm_nonneg _) Îµ n

end Countable

variable [TopologicalSpace Î¹] [SecondCountableTopology Î¹]

theorem maximal_ineq (hsub : Submartingale Y ğ“• P) (hnonneg : 0 â‰¤ Y) (Îµ : â„â‰¥0) (n : Î¹) :
    Îµ â€¢ P.real {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, Y i Ï‰} â‰¤
      âˆ« Ï‰ in {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, Y i Ï‰}, Y n Ï‰ âˆ‚P := by
  obtain âŸ¨T, hT_countable, hT_denseâŸ© := TopologicalSpace.exists_countable_dense Î¹
  sorry

theorem maximal_ineq_norm (hmar : Martingale X ğ“• P) (Îµ : â„â‰¥0) (n : Î¹) :
    Îµ â€¢ P.real {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, â€–X i Ï‰â€–} â‰¤
      âˆ« Ï‰ in {Ï‰ | (Îµ : â„) â‰¤ â¨† i : Set.Iic n, â€–X i Ï‰â€–}, â€–X n Ï‰â€– âˆ‚P := by
  sorry

end ProbabilityTheory
