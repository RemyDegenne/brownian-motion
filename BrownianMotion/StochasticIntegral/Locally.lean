/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
import Mathlib.Probability.Process.Stopping
import BrownianMotion.StochasticIntegral.Predictable

/-! # Local properties of processes

-/

open MeasureTheory Filter
open scoped ENNReal Topology

namespace ProbabilityTheory

variable {Î¹ Î© E : Type*} {mÎ© : MeasurableSpace Î©} {P : Measure Î©}

/-- A localizing sequence is a sequence of stopping times that is almost surely increasing and
tends almost surely to infinity. -/
structure IsLocalizingSequence [Preorder Î¹] (ğ“• : Filtration Î¹ mÎ©) (Ï„ : â„• â†’ Î© â†’ WithTop Î¹)
    (P : Measure Î© := by volume_tac) :
    Prop where
  isStoppingTime : âˆ€ n, IsStoppingTime ğ“• (Ï„ n)
  mono : âˆ€áµ Ï‰ âˆ‚P, Monotone (Ï„ Â· Ï‰)
  tendsto_top : âˆ€áµ Ï‰ âˆ‚P, Tendsto (Ï„ Â· Ï‰) atTop atTop

lemma isLocalizingSequence_const_top [Preorder Î¹] (ğ“• : Filtration Î¹ mÎ©) (P : Measure Î©) :
    IsLocalizingSequence ğ“• (fun _ _ â†¦ âŠ¤) P where
  isStoppingTime n := by simp [IsStoppingTime]
  mono := ae_of_all _ fun _ _ _ _ â†¦ by simp
  tendsto_top := by filter_upwards [] with Ï‰ using by simp [tendsto_atTop]

section LinearOrder

variable [LinearOrder Î¹] {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E}
  {p q : (Î¹ â†’ Î© â†’ E) â†’ Prop}

-- Move. Can this be generalized?
theorem _root_.Filter.Tendsto.min_atTop_atTop {Î± Î² : Type*}
    [Nonempty Î²] [LinearOrder Î²] [LinearOrder Î±]
    {f g : Î² â†’ Î±} (hf : Tendsto f atTop atTop) (hg : Tendsto g atTop atTop) :
    Tendsto (fun x â†¦ f x âŠ“ g x) atTop atTop := by
  rw [Filter.tendsto_atTop_atTop] at *
  exact fun a â†¦ let âŸ¨bâ‚, hbâ‚âŸ© := hf a; let âŸ¨bâ‚‚, hbâ‚‚âŸ© := hg a
    âŸ¨max bâ‚ bâ‚‚, fun B hB â†¦ le_min (hbâ‚ _ (max_le_iff.1 hB).1) (hbâ‚‚ _ (max_le_iff.1 hB).2)âŸ©

lemma IsLocalizingSequence.min {Ï„ Ïƒ : â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : IsLocalizingSequence ğ“• Ïƒ P) :
    IsLocalizingSequence ğ“• (min Ï„ Ïƒ) P where
  isStoppingTime n := (hÏ„.isStoppingTime n).min (hÏƒ.isStoppingTime n)
  mono := by filter_upwards [hÏ„.mono, hÏƒ.mono] with Ï‰ hÏ„Ï‰ hÏƒÏ‰; exact hÏ„Ï‰.min hÏƒÏ‰
  tendsto_top := by
    filter_upwards [hÏ„.tendsto_top, hÏƒ.tendsto_top] with Ï‰ hÏ„Ï‰ hÏƒÏ‰ using hÏ„Ï‰.min_atTop_atTop hÏƒÏ‰

variable [OrderBot Î¹]

@[simp]
lemma stoppedProcess_const_top : stoppedProcess X (fun _ â†¦ âŠ¤) = X := by
  unfold stoppedProcess
  simp

/-- A stochastic process `X` is said to satisfy a property `p` locally with respect to a
filtration `ğ“•` if there exists a localizing sequence `(Ï„_n)` such that for all `n`, the stopped
process of `fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)` satisfies `p`. -/
def Locally [Zero E] (p : (Î¹ â†’ Î© â†’ E) â†’ Prop) (ğ“• : Filtration Î¹ mÎ©)
    (X : Î¹ â†’ Î© â†’ E) (P : Measure Î© := by volume_tac) : Prop :=
  âˆƒ Ï„ : â„• â†’ Î© â†’ WithTop Î¹, IsLocalizingSequence ğ“• Ï„ P âˆ§
    âˆ€ n, p (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)) (Ï„ n))

/-- A localizing sequence, witness of the local property of the stochastic process. -/
noncomputable
def Locally.localSeq [Zero E] (hX : Locally p ğ“• X P) :
    â„• â†’ Î© â†’ WithTop Î¹ :=
  hX.choose

lemma Locally.IsLocalizingSequence [Zero E] (hX : Locally p ğ“• X P) :
    IsLocalizingSequence ğ“• (hX.localSeq) P :=
  hX.choose_spec.1

lemma Locally.stoppedProcess [Zero E] (hX : Locally p ğ“• X P) (n : â„•) :
    p (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < hX.localSeq n Ï‰}.indicator (X i)) (hX.localSeq n)) :=
  hX.choose_spec.2 n

lemma locally_of_prop [Zero E] (hp : p X) : Locally p ğ“• X P :=
  âŸ¨fun n _ â†¦ (âŠ¤ : WithTop Î¹), isLocalizingSequence_const_top _ _, by simpaâŸ©

lemma Locally.mono [Zero E] (hpq : âˆ€ X, p X â†’ q X) (hpX : Locally p ğ“• X P) :
    Locally q ğ“• X P :=
  âŸ¨hpX.localSeq, hpX.IsLocalizingSequence, fun n â†¦ hpq _ <| hpX.stoppedProcess nâŸ©

lemma Locally.of_and [Zero E] (hX : Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P) :
    Locally p ğ“• X P âˆ§ Locally q ğ“• X P :=
  âŸ¨hX.mono <| fun _ â†¦ And.left, hX.mono <| fun _ â†¦ And.rightâŸ©

lemma Locally.of_and_left [Zero E] (hX : Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P) :
    Locally p ğ“• X P :=
  hX.of_and.left

lemma Locally.of_and_right [Zero E] (hX : Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P) :
    Locally q ğ“• X P :=
  hX.of_and.right

/-- A property of stochastic processes is said to be stable if it is preserved under taking
the stopped process by a stopping time. -/
def IsStable [Zero E] (p : (Î¹ â†’ Î© â†’ E) â†’ Prop) (ğ“• : Filtration Î¹ mÎ©) : Prop :=
  âˆ€ X : Î¹ â†’ Î© â†’ E, p X â†’ âˆ€ Ï„ : Î© â†’ WithTop Î¹, IsStoppingTime ğ“• Ï„ â†’
    p (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ Ï‰}.indicator (X i)) Ï„)

-- Move
lemma stoppedProcess_indicator_comm {Î² : Type*} [Zero Î²] {u : Î¹ â†’ Î© â†’ Î²}
    {Ï„ : Î© â†’ WithTop Î¹} {s : Set Î©} (i : Î¹) :
    stoppedProcess (fun i â†¦ s.indicator (u i)) Ï„ i
      = s.indicator (stoppedProcess u Ï„ i) := by
  ext Ï‰
  rw [Set.indicator]
  split_ifs with hÏ‰
  Â· rw [stoppedProcess, Set.indicator_of_mem hÏ‰, stoppedProcess]
  Â· rw [stoppedProcess, Set.indicator_of_notMem hÏ‰]

-- Move
lemma stoppedProcess_indicator_comm' {Î² : Type*} [Zero Î²] {u : Î¹ â†’ Î© â†’ Î²}
    {Ï„ : Î© â†’ WithTop Î¹} {s : Set Î©} :
    stoppedProcess (fun i â†¦ s.indicator (u i)) Ï„
      = fun i â†¦ s.indicator (stoppedProcess u Ï„ i) := by
  ext i Ï‰
  rw [stoppedProcess_indicator_comm]

-- Move
theorem _root_.MeasureTheory.stoppedValue_stoppedProcess_apply
    {Î² : Type*} {Ï‰ : Î©} {u : Î¹ â†’ Î© â†’ Î²} {Ï„ Ïƒ : Î© â†’ WithTop Î¹} (hÏ‰ : Ïƒ Ï‰ â‰  âŠ¤) :
    stoppedValue (stoppedProcess u Ï„) Ïƒ Ï‰ = stoppedValue u (fun Ï‰ â†¦ min (Ïƒ Ï‰) (Ï„ Ï‰)) Ï‰ := by
  simp only [stoppedValue_stoppedProcess, ne_eq, hÏ‰, not_false_eq_true, reduceIte]

-- Move
@[simp] theorem _root_.MeasureTheory.stoppedProcess_stoppedProcess
    {Î² : Type*} {u : Î¹ â†’ Î© â†’ Î²} {Ï„ Ïƒ : Î© â†’ WithTop Î¹} :
    stoppedProcess (stoppedProcess u Ï„) Ïƒ = stoppedProcess u (Ïƒ âŠ“ Ï„) := by
  ext i Ï‰
  rw [stoppedProcess, stoppedProcess, stoppedProcess]
  by_cases hÏ„ : Ï„ Ï‰ = âŠ¤
  Â· simp only [hÏ„, le_top, inf_of_le_left, WithTop.untopD_coe, Pi.inf_apply]
  by_cases hÏƒ : Ïƒ Ï‰ = âŠ¤
  Â· simp only [hÏƒ, le_top, inf_of_le_left, WithTop.untopD_coe, Pi.inf_apply, inf_of_le_right]
  by_cases hÏƒÏ„ : Ïƒ Ï‰ â‰¤ Ï„ Ï‰
  Â· rw [min_eq_left, WithTop.untopA_eq_untop (lt_of_le_of_lt ((min_le_right _ _).trans hÏƒÏ„) <|
        WithTop.lt_top_iff_ne_top.2 hÏ„).ne, WithTop.coe_untop]
    Â· simp only [Pi.inf_apply, hÏƒÏ„, inf_of_le_left]
    Â· refine le_trans ?_ hÏƒÏ„
      rw [WithTop.untopA_eq_untop (lt_of_le_of_lt ((min_le_right _ _).trans hÏƒÏ„) <|
        WithTop.lt_top_iff_ne_top.2 hÏ„).ne, WithTop.coe_untop]
      exact min_le_right _ _
  Â· nth_rewrite 2 [WithTop.untopA_eq_untop ]
    Â· rw [WithTop.coe_untop, min_assoc]
      rfl
    Â· exact (lt_of_le_of_lt (min_le_right _ _) <| WithTop.lt_top_iff_ne_top.2 hÏƒ).ne

-- Move
@[simp] theorem _root_.MeasureTheory.stoppedProcess_stoppedProcess'
    {Î² : Type*} {u : Î¹ â†’ Î© â†’ Î²} {Ï„ Ïƒ : Î© â†’ WithTop Î¹} :
    stoppedProcess (stoppedProcess u Ï„) Ïƒ = stoppedProcess u (fun Ï‰ â†¦ min (Ïƒ Ï‰) (Ï„ Ï‰)) := by
  simp; rfl

-- Move
theorem _root_.MeasureTheory.stoppedProcess_stoppedProcess_of_le_right
    {Î² : Type*} {u : Î¹ â†’ Î© â†’ Î²} {Ï„ Ïƒ : Î© â†’ WithTop Î¹} (h : Ïƒ â‰¤ Ï„) :
    stoppedProcess (stoppedProcess u Ï„) Ïƒ = stoppedProcess u Ïƒ := by
  rw [stoppedProcess_stoppedProcess, inf_of_le_left h]

-- Move
theorem _root_.MeasureTheory.stoppedProcess_stoppedProcess_of_le_left
    {Î² : Type*} {u : Î¹ â†’ Î© â†’ Î²} {Ï„ Ïƒ : Î© â†’ WithTop Î¹} (h : Ï„ â‰¤ Ïƒ) :
    stoppedProcess (stoppedProcess u Ï„) Ïƒ = stoppedProcess u Ï„ := by
  rw [stoppedProcess_stoppedProcess, inf_of_le_right h]

-- Move
theorem _root_.MeasureTheory.stoppedProcess_eq_stoppedValue_apply
    {Î² : Type*} {u : Î¹ â†’ Î© â†’ Î²} {Ï„ : Î© â†’ WithTop Î¹} (i : Î¹) (Ï‰ : Î©) :
    stoppedProcess u Ï„ i Ï‰ = stoppedValue u (fun Ï‰ â†¦ min i (Ï„ Ï‰)) Ï‰ := rfl

lemma locally_and [Zero E] (hp : IsStable p ğ“•) (hq : IsStable q ğ“•) :
    Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P â†” Locally p ğ“• X P âˆ§ Locally q ğ“• X P := by
  refine âŸ¨Locally.of_and, fun âŸ¨hpX, hqXâŸ© â†¦
    âŸ¨_, hpX.IsLocalizingSequence.min hqX.IsLocalizingSequence, fun n â†¦ âŸ¨?_, ?_âŸ©âŸ©âŸ©
  Â· convert hp _ (hpX.stoppedProcess n) _ <| hqX.IsLocalizingSequence.isStoppingTime n using 1
    ext i Ï‰
    rw [stoppedProcess_indicator_comm]
    simp_rw [Pi.inf_apply, lt_inf_iff, inf_comm (hpX.localSeq n)]
    rw [â† stoppedProcess_stoppedProcess, â† stoppedProcess_indicator_comm,
      (_ : {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰ âˆ§ âŠ¥ < hqX.localSeq n Ï‰}
        = {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰} âˆ© {Ï‰ | âŠ¥ < hqX.localSeq n Ï‰}), Set.inter_comm]
    Â· simp_rw [â† Set.indicator_indicator]
      rfl
    Â· rfl
  Â· convert hq _ (hqX.stoppedProcess n) _ <| hpX.IsLocalizingSequence.isStoppingTime n using 1
    ext i Ï‰
    rw [stoppedProcess_indicator_comm]
    simp_rw [Pi.inf_apply, lt_inf_iff]
    rw [â† stoppedProcess_stoppedProcess, â† stoppedProcess_indicator_comm,
      (_ : {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰ âˆ§ âŠ¥ < hqX.localSeq n Ï‰}
        = {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰} âˆ© {Ï‰ | âŠ¥ < hqX.localSeq n Ï‰})]
    Â· simp_rw [â† Set.indicator_indicator]
      rfl
    Â· rfl

end LinearOrder

section ConditionallyCompleteLinearOrderBot

variable [ConditionallyCompleteLinearOrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
  [DenselyOrdered Î¹] [FirstCountableTopology Î¹]
  {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} {p q : (Î¹ â†’ Î© â†’ E) â†’ Prop}

-- Move
lemma isStoppingTime_of_measurableSet_lt_of_isRightContinuous [NoMaxOrder Î¹]
    {Ï„ : Î© â†’ WithTop Î¹} (hğ“• : IsRightContinuous ğ“•) (hÏ„ : âˆ€ i, MeasurableSet[ğ“• i] {Ï‰ | Ï„ Ï‰ < i}) :
    IsStoppingTime ğ“• Ï„ := by
  intro i
  obtain âŸ¨u, huâ‚, huâ‚‚, huâ‚ƒâŸ© := exists_seq_strictAnti_tendsto i
  refine MeasurableSet.of_compl ?_
  rw [(_ : {Ï‰ | Ï„ Ï‰ â‰¤ i}á¶œ = â‹ƒ n, {Ï‰ | u n â‰¤ Ï„ Ï‰})]
  Â· refine measurableSet_of_isRightContinuous ?_
    simp_rw [MeasurableSpace.measurableSet_iInf]
    intros j hj
    obtain âŸ¨N, hNâŸ© := (huâ‚ƒ.eventually_le_const hj).exists
    rw [(_ : â‹ƒ n, {Ï‰ | u n â‰¤ Ï„ Ï‰} = â‹ƒ n â‰¥ N, {Ï‰ | u n â‰¤ Ï„ Ï‰})]
    Â· refine MeasurableSet.iUnion <| fun n â†¦ MeasurableSet.iUnion <| fun hn â†¦
        ğ“•.mono ((huâ‚.antitone hn).trans hN) _ <| MeasurableSet.of_compl ?_
      rw [(by ext; simp : {Ï‰ | u n â‰¤ Ï„ Ï‰}á¶œ = {Ï‰ | Ï„ Ï‰ < u n})]
      exact hÏ„ (u n)
    Â· ext Ï‰
      simp only [Set.mem_iUnion, Set.mem_setOf_eq, ge_iff_le, exists_prop]
      constructor
      Â· rintro âŸ¨i, hleâŸ©
        refine âŸ¨i + N, N.le_add_left i, le_trans ?_ hleâŸ©
        norm_cast
        exact huâ‚.antitone <| i.le_add_right N
      Â· rintro âŸ¨i, -, hiâŸ©
        exact âŸ¨i, hiâŸ©
  Â· ext Ï‰
    simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_le, Set.mem_iUnion]
    constructor
    Â· intro h
      by_cases hÏ„ : Ï„ Ï‰ = âŠ¤
      Â· exact âŸ¨0, hÏ„ â–¸ le_topâŸ©
      Â· have hlt : i < (Ï„ Ï‰).untop hÏ„ := by
          rwa [WithTop.lt_untop_iff]
        obtain âŸ¨N, hNâŸ© := (huâ‚ƒ.eventually_le_const hlt).exists
        refine âŸ¨N, WithTop.coe_le_iff.2 <| fun n hn â†¦ hN.trans ?_âŸ©
        simp only [hn, WithTop.untop_coe, le_refl]
    Â· rintro âŸ¨j, hjâŸ©
      refine lt_of_lt_of_le ?_ hj
      norm_cast
      exact huâ‚‚ _

-- Move
lemma isStoppingTime_of_measurableSet_lt_of_isRightContinuous' {Ï„ : Î© â†’ WithTop Î¹}
    (hğ“• : IsRightContinuous ğ“•) (hÏ„ : âˆ€ i, Â¬ IsMax i â†’ MeasurableSet[ğ“• i] {Ï‰ | Ï„ Ï‰ < i})
    (hÏ„max : âˆ€ i, IsMax i â†’ MeasurableSet[ğ“• i] {Ï‰ | Ï„ Ï‰ = âŠ¤}) :
    IsStoppingTime ğ“• Ï„ := by
  intro i
  by_cases hmax : IsMax i
  Â· rw [(_ : {Ï‰ | Ï„ Ï‰ â‰¤ i} = {Ï‰ | Ï„ Ï‰ = âŠ¤}á¶œ)]
    Â· exact (hÏ„max i hmax).compl
    Â· ext Ï‰
      simp only [Set.mem_setOf_eq, Set.mem_compl_iff]
      constructor
      Â· rintro hle htop
        rw [htop] at hle
        simp only [top_le_iff, WithTop.coe_ne_top] at hle
      Â· intro htop
        rw [â† WithTop.coe_untop _ htop]
        norm_cast
        exact not_lt.1 hmax.not_lt
  rw [not_isMax_iff] at hmax
  obtain âŸ¨j, hjâŸ© := hmax
  obtain âŸ¨u, huâ‚, huâ‚‚, huâ‚ƒâŸ© := exists_seq_strictAnti_tendsto' hj
  refine MeasurableSet.of_compl ?_
  rw [(_ : {Ï‰ | Ï„ Ï‰ â‰¤ i}á¶œ = â‹ƒ n, {Ï‰ | u n â‰¤ Ï„ Ï‰})]
  Â· refine measurableSet_of_isRightContinuous ?_
    simp_rw [MeasurableSpace.measurableSet_iInf]
    intros j hj
    obtain âŸ¨N, hNâŸ© := (huâ‚ƒ.eventually_le_const hj).exists
    rw [(_ : â‹ƒ n, {Ï‰ | u n â‰¤ Ï„ Ï‰} = â‹ƒ n > N, {Ï‰ | u n â‰¤ Ï„ Ï‰})]
    Â· refine MeasurableSet.iUnion <| fun n â†¦ MeasurableSet.iUnion <| fun hn â†¦
        ğ“•.mono ((huâ‚ hn).le.trans hN) _ <| MeasurableSet.of_compl ?_
      rw [(by ext; simp : {Ï‰ | u n â‰¤ Ï„ Ï‰}á¶œ = {Ï‰ | Ï„ Ï‰ < u n})]
      refine hÏ„ (u n) <| not_isMax_iff.2 âŸ¨u N, huâ‚ hnâŸ©
    Â· ext Ï‰
      simp only [Set.mem_iUnion, Set.mem_setOf_eq, gt_iff_lt, exists_prop]
      constructor
      Â· rintro âŸ¨i, hleâŸ©
        refine âŸ¨i + N + 1, by linarith, le_trans ?_ hleâŸ©
        norm_cast
        exact huâ‚.antitone (by linarith)
      Â· rintro âŸ¨i, -, hiâŸ©
        exact âŸ¨i, hiâŸ©
  Â· ext Ï‰
    simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_le, Set.mem_iUnion]
    constructor
    Â· intro h
      by_cases hÏ„ : Ï„ Ï‰ = âŠ¤
      Â· exact âŸ¨0, hÏ„ â–¸ le_topâŸ©
      Â· have hlt : i < (Ï„ Ï‰).untop hÏ„ := by
          rwa [WithTop.lt_untop_iff]
        obtain âŸ¨N, hNâŸ© := (huâ‚ƒ.eventually_le_const hlt).exists
        refine âŸ¨N, WithTop.coe_le_iff.2 <| fun n hn â†¦ hN.trans ?_âŸ©
        simp only [hn, WithTop.untop_coe, le_refl]
    Â· rintro âŸ¨j, hjâŸ©
      refine lt_of_lt_of_le ?_ hj
      norm_cast
      exact (huâ‚‚ j).1

variable [NoMaxOrder Î¹]

-- Move. Weaken the lattice assumption?
lemma IsStoppingTime.iInf {ğ“• : Filtration Î¹ mÎ©} {Ï„ : â„• â†’ Î© â†’ WithTop Î¹}
    (s : Set â„•) (hğ“• : IsRightContinuous ğ“•) (hÏ„ : âˆ€ n, IsStoppingTime ğ“• (Ï„ n)) :
    IsStoppingTime ğ“• (fun Ï‰ â†¦ â¨… (n) (_ : n âˆˆ s), Ï„ n Ï‰) := by
  refine isStoppingTime_of_measurableSet_lt_of_isRightContinuous hğ“• <| fun i â†¦ ?_
  refine MeasurableSet.of_compl ?_
  rw [(_ : {Ï‰ | â¨… n âˆˆ s, Ï„ n Ï‰ < i}á¶œ = â‹‚ n âˆˆ s, {Ï‰ | i â‰¤ Ï„ n Ï‰})]
  Â· exact MeasurableSet.iInter <| fun n â†¦ MeasurableSet.iInter <| fun hn â†¦ (hÏ„ n).measurableSet_ge i
  Â· ext Ï‰
    simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_lt, le_iInf_iff, Set.mem_iInter]

-- 1: IsStoppingTime.iInf
-- 2: Given a sequence of stopping times `Ï„â‚™` which converge to infinity,
--  `Ïƒâ‚™ := inf_{k â‰¤ n} Ï„â‚–` defines a localizing sequence.

lemma IsLocalizingSequence.exists_subseq_isStoppingTime_tendsto_atTop
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ nk : â„• â†’ â„•, StrictMono nk
      âˆ§ âˆ€áµ Ï‰ âˆ‚P, Tendsto (fun i â†¦ (Ï„ i Ï‰) âŠ“ (Ïƒ i (nk i) Ï‰)) atTop atTop := by
  sorry

lemma isLocalizingSequence_of_isStoppingTime_tendsto_atTop
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} (hğ“• : IsRightContinuous ğ“•)
    (hÏ„ : âˆ€ n, IsStoppingTime ğ“• (Ï„ n)) (hTends : âˆ€áµ Ï‰ âˆ‚P, Tendsto (Ï„ Â· Ï‰) atTop atTop) :
    IsLocalizingSequence ğ“• (fun i Ï‰ â†¦ â¨… j â‰¥ i, Ï„ j Ï‰) P where
  isStoppingTime (n : â„•) := IsStoppingTime.iInf {j | j â‰¥ n} hğ“• (fun j â†¦ hÏ„ j)
  mono :=  ae_of_all _ <| fun Ï‰ n m hnm â†¦ iInf_le_iInf_of_subset <| fun k hk â†¦ hnm.trans hk
  tendsto_top := by
    filter_upwards [hTends] with Ï‰ hÏ‰
    rw [tendsto_atTop_atTop] at âŠ¢ hÏ‰
    intro C
    obtain âŸ¨i, hiâŸ© := hÏ‰ C
    exact âŸ¨i, fun j hij â†¦ le_iInf <| fun k â†¦ le_iInf fun hk â†¦ hi _ <| hij.trans hkâŸ©

lemma IsLocalizingSequence.exists_subseq_isLocalizingSequence_tendsto_atTop
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹} (hğ“• : IsRightContinuous ğ“•)
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ nk : â„• â†’ â„•, IsLocalizingSequence ğ“• (fun i Ï‰ â†¦ â¨… j â‰¥ i, (Ï„ j Ï‰) âŠ“ (Ïƒ j (nk j) Ï‰)) P := by
  obtain âŸ¨nk, hnkâ‚, hnkâ‚‚âŸ© := hÏ„.exists_subseq_isStoppingTime_tendsto_atTop hÏƒ
  exact âŸ¨nk, isLocalizingSequence_of_isStoppingTime_tendsto_atTop hğ“•
    (fun j â†¦ (hÏ„.isStoppingTime j).min <| (hÏƒ j).isStoppingTime (nk j)) hnkâ‚‚âŸ©

/-- A stable property holding locally is idempotent. -/
lemma locally_locally [Zero E] (hğ“• : IsRightContinuous ğ“•) (hp : IsStable p ğ“•) :
    Locally (fun Y â†¦ Locally p ğ“• Y P) ğ“• X P â†” Locally p ğ“• X P := by
  refine âŸ¨fun hL â†¦ ?_, fun hL â†¦ ?_âŸ©
  Â· have hLL := hL.stoppedProcess
    choose Ï„ hÏ„â‚ hÏ„â‚‚ using hLL
    obtain âŸ¨nk, hnkâŸ© :=
      hL.IsLocalizingSequence.exists_subseq_isLocalizingSequence_tendsto_atTop hğ“• hÏ„â‚
    refine âŸ¨_, hnk, fun n â†¦ ?_âŸ©
    have := hp _ (hÏ„â‚‚ n (nk n)) (fun Ï‰ â†¦ â¨… j â‰¥ n, (hL.localSeq j Ï‰) âŠ“ (Ï„ j (nk j) Ï‰)) ?_
    Â· rw [stoppedProcess_indicator_comm', â† stoppedProcess_stoppedProcess_of_le_right
        (Ï„ := fun Ï‰ â†¦ (hL.localSeq n Ï‰) âŠ“ (Ï„ n (nk n) Ï‰))
        (fun _ â†¦ (iInf_le _ n).trans <| iInf_le _ le_rfl), â† stoppedProcess_indicator_comm']
      convert this using 2
      ext i Ï‰
      rw [stoppedProcess_indicator_comm', stoppedProcess_indicator_comm',
        stoppedProcess_indicator_comm', Set.indicator_indicator, Set.indicator_indicator]
      Â· congr 1
        Â· ext Ï‰'
          simp only [ge_iff_le, Set.mem_setOf_eq, Set.mem_inter_iff]
          exact âŸ¨fun h â†¦ âŸ¨âŸ¨h, lt_of_lt_of_le h <| (iInf_le _ n).trans <|
              (iInf_le _ le_rfl).trans <| min_le_right _ _âŸ©,
            lt_of_lt_of_le h <| (iInf_le _ n).trans <| (iInf_le _ le_rfl).trans <| min_le_left _ _âŸ©,
            fun h â†¦ h.1.1âŸ©
        Â· rw [stoppedProcess_stoppedProcess, inf_comm]; rfl
    Â· exact IsStoppingTime.iInf {j | j â‰¥ n} hğ“• <| fun j â†¦
        (hL.IsLocalizingSequence.isStoppingTime j).min <| (hÏ„â‚ j).isStoppingTime (nk j)
  Â· exact âŸ¨hL.localSeq, hL.IsLocalizingSequence, fun n â†¦ locally_of_prop <| hL.stoppedProcess nâŸ©

/-- If `p` implies `q` locally, then `p` locally implies `q` locally. -/
lemma locally_induction [Zero E] (hğ“• : IsRightContinuous ğ“•)
    (hpq : âˆ€ Y, p Y â†’ Locally q ğ“• Y P) (hq : IsStable q ğ“•) (hpX : Locally p ğ“• X P) :
    Locally q ğ“• X P :=
  (locally_locally hğ“• hq).1 <| hpX.mono hpq

end ConditionallyCompleteLinearOrderBot

end ProbabilityTheory
