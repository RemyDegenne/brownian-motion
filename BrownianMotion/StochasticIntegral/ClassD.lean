/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import BrownianMotion.StochasticIntegral.LocalMartingale
import Mathlib.Probability.Process.HittingTime

/-! # Locally integrable, class D, class DL

-/

open MeasureTheory Filter Function TopologicalSpace
open scoped ENNReal


/-- Helper Lemma -/
@[simp]
lemma WithTop.untopA_coe {Î± : Type*} [Nonempty Î±] {Ïƒ : Î±} :
    (Ïƒ : WithTop Î±).untopA = Ïƒ := rfl

namespace MeasureTheory

variable {Î¹ Î© E : Type*} [LinearOrder Î¹] [OrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
  [MeasurableSpace Î¹] [BorelSpace Î¹] [NormedAddCommGroup E] {mÎ© : MeasurableSpace Î©} {P : Measure Î©}
  {X : Î¹ â†’ Î© â†’ E} {Ï„ : Î© â†’ WithTop Î¹} {ğ“• : Filtration Î¹ mÎ©}

lemma ProgMeasurable.stronglyMeasurable_uncurry_stoppedProcess_const
    (hX : ProgMeasurable ğ“• X) (t : Î¹) :
    StronglyMeasurable <| uncurry (MeasureTheory.stoppedProcess X (fun _ â†¦ t)) := by
  let g : Î¹ Ã— Î© â†’ (Set.Iic t) Ã— Î© := fun p â†¦ (âŸ¨min p.1 t, min_le_right p.1 tâŸ©, p.2)
  have hg_meas : Measurable[_, MeasurableSpace.prod inferInstance (ğ“• t)] g := by
    apply Measurable.prodMk _ (measurable_snd.mono le_rfl (ğ“•.le t))
    fun_prop
  exact StronglyMeasurable.comp_measurable (hX t) hg_meas

lemma ProgMeasurable.stronglyMeasurable_uncurry_of_isCountablyGenerated_atTop
    [IsCountablyGenerated (atTop : Filter Î¹)] (hX : ProgMeasurable ğ“• X) :
    StronglyMeasurable (uncurry X) := by
  rcases exists_seq_monotone_tendsto_atTop_atTop (Î± := Î¹) with âŸ¨t, -, ht_limâŸ©
  refine stronglyMeasurable_of_tendsto atTop
    (fun n â†¦ stronglyMeasurable_uncurry_stoppedProcess_const hX (t n)) ?_
  rw [tendsto_pi_nhds]
  intro âŸ¨s, Ï‰âŸ©
  apply tendsto_const_nhds.congr'
  filter_upwards [ht_lim.eventually (Filter.eventually_ge_atTop s)] with n hn
  simp only [uncurry_apply_pair, MeasureTheory.stoppedProcess]
  rw [â† WithTop.coe_min, WithTop.untopA_coe, min_eq_left hn]

private lemma ProgMeasurable.stronglyMeasurable_stoppedValue_stoppedProcess
    [SecondCountableTopology Î¹] [PseudoMetrizableSpace Î¹]
    (hX_prog : ProgMeasurable ğ“• X) (hÏ„ : IsStoppingTime ğ“• Ï„)
    (Ïƒ : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤}) :
    StronglyMeasurable (stoppedValue
      (MeasureTheory.stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ Ï‰}.indicator (X i)) Ï„) Ïƒ.1) := by
  have hY_prog := ProbabilityTheory.isStable_progMeasurable X hX_prog Ï„ hÏ„
  have hY_sm := ProgMeasurable.stronglyMeasurable_uncurry_of_isCountablyGenerated_atTop hY_prog
  let idx_map : Î© â†’ Î¹ := fun Ï‰ â†¦ (Ïƒ.val Ï‰).untop (Ïƒ.property.2 Ï‰)
  have h_idx_meas : Measurable[mÎ©] idx_map := by
    have h_emb : MeasurableEmbedding (fun x : Î¹ â†¦ (x : WithTop Î¹)) := by
      apply Topology.IsEmbedding.measurableEmbedding WithTop.isEmbedding_coe
      rw [WithTop.range_coe]
      exact IsOpen.measurableSet isOpen_Iio
    apply (MeasurableEmbedding.measurable_comp_iff h_emb).mp
    convert IsStoppingTime.measurable' (m := mÎ©) Ïƒ.property.1
    ext
    simp [idx_map]
  convert (hY_sm.comp_measurable (h_idx_meas.prodMk measurable_id))
  ext Ï‰
  simp only [stoppedValue, uncurry, Function.comp_apply]
  congr 2
  rw [WithTop.untopA_eq_untop]

end MeasureTheory

namespace ProbabilityTheory

variable {Î¹ Î© E : Type*} [NormedAddCommGroup E] {mÎ© : MeasurableSpace Î©} {P : Measure Î©}
  {X : Î¹ â†’ Î© â†’ E}

/-- The condition that the running supremum process `(t, Ï‰) â†¦ sup_{s â‰¤ t} â€–X s Ï‰â€–` is strongly
measurable as a function on the product. -/
def HasStronglyMeasurableSupProcess [LinearOrder Î¹] [MeasurableSpace Î¹] (X : Î¹ â†’ Î© â†’ E) : Prop :=
  (StronglyMeasurable (fun (tÏ‰ : Î¹ Ã— Î©) â†¦ â¨† s â‰¤ tÏ‰.1, â€–X s tÏ‰.2â€–â‚‘))

/-- A stochastic process has integrable supremum if the function `(t, Ï‰) â†¦ sup_{s â‰¤ t} â€–X s Ï‰â€–`
is strongly measurable and if for all `t`, the random variable `Ï‰ â†¦ sup_{s â‰¤ t} â€–X s Ï‰â€–`
is integrable. -/
def HasIntegrableSup [LinearOrder Î¹] [MeasurableSpace Î¹] (X : Î¹ â†’ Î© â†’ E)
    (P : Measure Î© := by volume_tac) : Prop :=
  HasStronglyMeasurableSupProcess (mÎ©:= mÎ©) X âˆ§ âˆ€ t, Integrable (fun Ï‰ â†¦ â¨† s â‰¤ t, â€–X s Ï‰â€–â‚‘) P

/-- A stochastic process has locally integrable supremum if it satisfies locally the property that
for all `t`, the random variable `Ï‰ â†¦ sup_{s â‰¤ t} â€–X s Ï‰â€–` is integrable. -/
def HasLocallyIntegrableSup [LinearOrder Î¹] [OrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    [MeasurableSpace Î¹]
    (X : Î¹ â†’ Î© â†’ E) (ğ“• : Filtration Î¹ mÎ©) (P : Measure Î© := by volume_tac) : Prop :=
  Locally (HasIntegrableSup Â· P) ğ“• X P

section Defs

variable [Preorder Î¹] [Nonempty Î¹] [MeasurableSpace Î¹]

/-- A stochastic process $(X_t)$ is of class D (or in the Doob-Meyer class) if it is adapted
and the set $\{X_\tau \mid \tau \text{ is a finite stopping time}\}$ is uniformly integrable. -/
structure ClassD (X : Î¹ â†’ Î© â†’ E) (ğ“• : Filtration Î¹ mÎ©) (P : Measure Î©) : Prop where
  progMeasurable : ProgMeasurable ğ“• X
  uniformIntegrable : UniformIntegrable
    (fun (Ï„ : {T : Î© â†’ WithTop Î¹ | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤}) â†¦ stoppedValue X Ï„.1) 1 P

/-- A stochastic process $(X_t)$ is of class DL if it is adapted and for all $t$, the set
$\{X_\tau \mid \tau \text{ is a stopping time with } \tau \le t\}$ is uniformly integrable. -/
structure ClassDL (X : Î¹ â†’ Î© â†’ E) (ğ“• : Filtration Î¹ mÎ©) (P : Measure Î©) : Prop where
  progMeasurable : ProgMeasurable ğ“• X
  uniformIntegrable (t : Î¹) : UniformIntegrable
    (fun (Ï„ : {T : Î© â†’ WithTop Î¹ | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰¤ t}) â†¦ stoppedValue X Ï„.1) 1 P

/-- Class D implies Class DL. -/
lemma ClassD.classDL {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} (hX : ClassD X ğ“• P) :
    ClassDL X ğ“• P := by
  let f (t : Î¹) : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ (Ï‰ : Î©), T Ï‰ â‰¤ t} â†’
      {T | IsStoppingTime ğ“• T âˆ§ âˆ€ (Ï‰ : Î©), T Ï‰ â‰  âŠ¤} :=
    fun Ï„ => âŸ¨Ï„, Ï„.2.1, fun Ï‰ => ne_of_lt
      (lt_of_le_of_lt (Ï„.2.2 Ï‰) (WithTop.coe_lt_top t))âŸ©
  exact âŸ¨hX.1, fun t => hX.2.comp (f t)âŸ©

end Defs

section LinearOrder

variable [LinearOrder Î¹] {ğ“• : Filtration Î¹ mÎ©}

section RightContinuous

variable [TopologicalSpace Î¹] [SecondCountableTopology Î¹] [OrderTopology Î¹] [OrderBot Î¹]
  [MeasurableSpace Î¹]

/-- If `{stoppedValue X T : T is a bounded stopping time}` is uniformly integrable, then `X` is
of class D. -/
lemma classD_of_uniformIntegrable_bounded_stoppingTime
    (hX : UniformIntegrable (fun T : {T | IsStoppingTime ğ“• T âˆ§ âˆƒ t : Î¹, âˆ€ Ï‰, T Ï‰ â‰¤ t}
      â†¦ stoppedValue X T) 1 P) (hm : ProgMeasurable ğ“• X) :
    ClassD X ğ“• P := by
  have (T : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤}) :
      âˆƒ N : â„• â†’ {T | IsStoppingTime ğ“• T âˆ§ âˆƒ t : Î¹, âˆ€ Ï‰, T Ï‰ â‰¤ t},
      âˆ€áµ Ï‰ âˆ‚P, Tendsto (fun n â†¦ stoppedValue X (N n).1 Ï‰) atTop (nhds (stoppedValue X T.1 Ï‰)) := by
    obtain âŸ¨v, hvâŸ© := exists_seq_monotone_tendsto_atTop_atTop Î¹
    refine âŸ¨fun n => âŸ¨(fun Ï‰ => v n) âŠ“ T.1, âŸ¨IsStoppingTime.min ?_ T.2.1, ?_âŸ©âŸ©, ?_âŸ©
    Â· exact (isStoppingTime_const ğ“• (v n))
    Â· exact âŸ¨v n, fun Ï‰ => by simpâŸ©
    Â· filter_upwards with Ï‰
      simp_all only [tendsto_atTop_atTop, tendsto_atTop_nhds]
      intro U hU _
      obtain âŸ¨N, hNâŸ© := hv.2 (T.1 Ï‰).untopA
      refine âŸ¨N, fun n hn => ?_âŸ©
      have : (T.1 Ï‰).untopA = T.1 Ï‰ := by simp [WithTop.untopA_eq_untop (T.2.2 Ï‰)]
      rw [stoppedValue, Pi.inf_apply, â† this, â† WithTop.coe_min (v n) (T.1 Ï‰).untopA,
        min_eq_right (hN n hn)]
      simpa using hU
  exact âŸ¨hm, (hX.uniformIntegrable_of_ae_tendsto _).comp
    (fun T : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤} => âŸ¨stoppedValue X T.1, this TâŸ©)âŸ©

variable [NormedSpace â„ E] [CompleteSpace E]

section Order

variable [PseudoMetrizableSpace Î¹] [BorelSpace Î¹] [Lattice E]
  [HasSolidNorm E] [IsOrderedAddMonoid E] [IsOrderedModule â„ E] [IsFiniteMeasure P]

/-- A nonnegative right-continuous submartingale is of class DL. -/
lemma _root_.MeasureTheory.Submartingale.classDL (hX1 : Submartingale X ğ“• P)
    (hX2 : âˆ€ Ï‰, RightContinuous (X Â· Ï‰)) (hX3 : 0 â‰¤ X) :
    ClassDL X ğ“• P := by
  refine âŸ¨StronglyAdapted.progMeasurable_of_rightContinuous hX1.1 hX2, fun t => ?_âŸ©
  have := (hX1.2.2 t).uniformIntegrable_condExp' (fun T :
    {T | IsStoppingTime ğ“• T âˆ§ âˆ€ (Ï‰ : Î©), T Ï‰ â‰¤ t} => IsStoppingTime.measurableSpace_le T.2.1)
  refine uniformIntegrable_of_dominated this (fun T => ?_) (fun T => âŸ¨T, ?_âŸ©)
  Â· exact ((stronglyMeasurable_stoppedValue_of_le (hX1.1.progMeasurable_of_rightContinuous
      hX2) T.2.1 T.2.2).mono (ğ“•.le' t)).aestronglyMeasurable
  Â· have : stoppedValue X T.1 â‰¤áµ[P] P[stoppedValue X (fun Ï‰ => t)|T.2.1.measurableSpace] := by
      suffices lem : stoppedValue X ((fun Ï‰ => t) âŠ“ T.1) â‰¤áµ[P]
        P[stoppedValue X (fun Ï‰ => t)|T.2.1.measurableSpace] from by
        have : T.1 âŠ“ (fun Ï‰ => t) = T.1 := by simpa [inf_eq_left] using T.2.2
        simpa [inf_comm, this] using lem
      exact hX1.stoppedValue_min_ae_le_condExp ğ“• hX2
        (Eventually.of_forall (fun Ï‰ => le_rfl)) T.2.1 (isStoppingTime_const ğ“• t)
    simp only [stoppedValue_const] at this
    filter_upwards [this] with Ï‰ hÏ‰
    have p1 : 0 â‰¤ stoppedValue X T.1 Ï‰ := by simpa [stoppedValue] using (hX3 (T.1 Ï‰).untopA Ï‰)
    have p2 := abs_of_nonneg (le_trans p1 hÏ‰)
    rw [â† abs_of_nonneg p1, â† p2] at hÏ‰
    exact norm_le_norm_of_abs_le_abs hÏ‰

lemma _root_.MeasureTheory.Submartingale.uniformIntegrable_bounded_stoppingTime
    (hX1 : Submartingale X ğ“• P) (hX2 : âˆ€ Ï‰, RightContinuous (X Â· Ï‰)) (hX3 : 0 â‰¤ X)
    (hX4 : UniformIntegrable X 1 P) :
    UniformIntegrable (fun T : {T | IsStoppingTime ğ“• T âˆ§ âˆƒ t : Î¹, âˆ€ Ï‰, T Ï‰ â‰¤ t}
      â†¦ stoppedValue X T) 1 P := by
  have hcond := hX4.condExp' (fun T : {T | IsStoppingTime ğ“• T âˆ§ âˆƒ t : Î¹, âˆ€ Ï‰, T Ï‰ â‰¤ t} =>
      IsStoppingTime.measurableSpace_le T.2.1)
  refine uniformIntegrable_of_dominated hcond (fun âŸ¨T, hT, âŸ¨t, htâŸ©âŸ© => ?_)
    (fun âŸ¨T, hT, âŸ¨t, htâŸ©âŸ© => âŸ¨âŸ¨t, âŸ¨T, hT, âŸ¨t, htâŸ©âŸ©âŸ©, ?_âŸ©)
  Â· exact ((stronglyMeasurable_stoppedValue_of_le
      (hX1.1.progMeasurable_of_rightContinuous hX2) hT ht).mono (ğ“•.le' t)).aestronglyMeasurable
  Â· have : stoppedValue X T â‰¤áµ[P] P[stoppedValue X (fun Ï‰ => t)|hT.measurableSpace] := by
      suffices lem : stoppedValue X ((fun Ï‰ => t) âŠ“ T) â‰¤áµ[P]
          P[stoppedValue X (fun Ï‰ => t)|hT.measurableSpace] by
        have : T âŠ“ (fun _ â†¦ t) = T := by simpa [inf_eq_left] using ht
        simpa [inf_comm, this] using lem
      exact hX1.stoppedValue_min_ae_le_condExp ğ“• hX2
        (ae_of_all _ fun _ â†¦ le_rfl) hT (isStoppingTime_const ğ“• t)
    simp only [stoppedValue_const] at this
    filter_upwards [this] with Ï‰ hÏ‰
    apply norm_le_norm_of_abs_le_abs
    have p1 : 0 â‰¤ stoppedValue X T Ï‰ := by simpa [stoppedValue] using (hX3 (T Ï‰).untopA Ï‰)
    have p2 : |P[X t|hT.measurableSpace] Ï‰| = P[X t|hT.measurableSpace] Ï‰ :=
      abs_of_nonneg (p1.trans hÏ‰)
    rwa [â† abs_of_nonneg p1, â† p2] at hÏ‰

/-- A nonnegative right-continuous submartingale is of class D iff it is uniformly integrable. -/
lemma _root_.MeasureTheory.Submartingale.classD_iff_uniformIntegrable
    (hX1 : Submartingale X ğ“• P) (hX2 : âˆ€ Ï‰, RightContinuous (X Â· Ï‰)) (hX3 : 0 â‰¤ X) :
    ClassD X ğ“• P â†” UniformIntegrable X 1 P := by
  let S := {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤}
  let G (T : S) : Î© â†’ E := stoppedValue X T.1
  refine âŸ¨fun hp => ?_, fun hq => ?_âŸ©
  Â· let constT (t : Î¹) : S := âŸ¨fun Ï‰ : Î© => t, âŸ¨isStoppingTime_const ğ“• t, by simpâŸ©âŸ©
    have eq : X = G âˆ˜ constT := by ext; simp [constT, G, stoppedValue]
    simpa [eq] using hp.2.comp constT
  Â· refine classD_of_uniformIntegrable_bounded_stoppingTime ?_ ?_
    Â· exact hX1.uniformIntegrable_bounded_stoppingTime hX2 hX3 hq
    Â· exact hX1.1.progMeasurable_of_rightContinuous hX2

end Order

/-- A martingale with right-continuous paths is of class DL. -/
lemma _root_.MeasureTheory.Martingale.classDL [PseudoMetrizableSpace Î¹] [BorelSpace Î¹]
    [IsFiniteMeasure P]
    (hX1 : Martingale X ğ“• P) (hX2 : âˆ€ Ï‰, RightContinuous (X Â· Ï‰)) :
    ClassDL X ğ“• P := by
  let Y := fun t Ï‰ â†¦ â€–X t Ï‰â€–
  have hY_sub : Submartingale Y ğ“• P := hX1.submartingale_convex_comp
    (convexOn_norm convex_univ) continuous_norm
    (fun t â†¦ (hX1.integrable t).norm)
  have hY_cont : âˆ€ Ï‰, RightContinuous (Y Â· Ï‰) := fun Ï‰ t â†¦ (hX2 Ï‰ t).norm
  have hY_nonneg : 0 â‰¤ Y := fun t Ï‰ â†¦ norm_nonneg _
  have hY_DL : ClassDL Y ğ“• P := hY_sub.classDL hY_cont hY_nonneg
  have h_prog := hX1.stronglyAdapted.progMeasurable_of_rightContinuous hX2
  refine âŸ¨h_prog, fun t â†¦ ?_âŸ©
  rw [uniformIntegrable_iff_norm]
  Â· exact hY_DL.uniformIntegrable t
  Â· exact fun T â†¦ ((stronglyMeasurable_stoppedValue_of_le h_prog T.2.1 T.2.2).mono
      (ğ“•.le' t)).aestronglyMeasurable

lemma _root_.MeasureTheory.Martingale.classD_iff_uniformIntegrable (hX1 : Martingale X ğ“• P)
    (hX2 : âˆ€ Ï‰, RightContinuous (X Â· Ï‰)) :
    ClassD X ğ“• P â†” UniformIntegrable X 1 P := by
  sorry

end RightContinuous

section ClassDClassDL

/-- If the index type has a top element, then class DL implies class D. -/
lemma ClassDL.classD {Î¹ : Type*} [Preorder Î¹] {X : Î¹ â†’ Î© â†’ E} {ğ“• : Filtration Î¹ mÎ©}
    [OrderTop Î¹] [MeasurableSpace Î¹]
    (hX : ClassDL X ğ“• P) :
    ClassD X ğ“• P := by
  let A := {T : Î© â†’ WithTop Î¹ | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤}
  let B := {T : Î© â†’ WithTop Î¹ | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰¤ (âŠ¤ : Î¹)}
  let f : A â†’ B := fun T => âŸ¨T, âŸ¨T.2.1, fun Ï‰ => ?_âŸ©âŸ©
  Â· have : (fun T : A â†¦ stoppedValue X T.1) = (fun T â†¦ stoppedValue X T.1) âˆ˜ f := by ext; simp [f]
    refine âŸ¨hX.1, ?_âŸ©
    rw [this]
    exact UniformIntegrable.comp (hX.2 (âŠ¤ : Î¹)) f
  Â· have := T.2.2 Ï‰
    simp only [ne_eq, WithTop.ne_top_iff_exists] at this
    obtain âŸ¨a, haâŸ© := this
    exact ha â–¸ WithTop.coe_le_coe.mpr (le_top (a := a))

variable [OrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹]
  {Ï„ : Î© â†’ WithTop Î¹}

/-- The class of processes that are jointly measurable is stable. -/
lemma isStable_stronglyMeasurable_uncurry [SecondCountableTopology Î¹] :
    IsStable ğ“• (fun (X : Î¹ â†’ Î© â†’ E) â†¦ StronglyMeasurable (uncurry X)) := by
  intro X hX Ï„ hÏ„
  let M : Î¹ Ã— Î© â†’ Î¹ Ã— Î© := fun p â†¦ ((min â†‘p.1 (Ï„ p.2)).untopA, p.2)
  have hM : Measurable M := (WithTop.measurable_coe.comp measurable_fst).min
    (hÏ„.measurable'.comp measurable_snd) |>.untopA.prodMk measurable_snd
  have h_eq : uncurry (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ Ï‰}.indicator (X i)) Ï„) =
      {p | âŠ¥ < Ï„ p.2}.indicator (uncurry X âˆ˜ M) := by
    ext âŸ¨t, Ï‰âŸ©
    simp [stoppedProcess, Set.indicator_apply, M]
  rw [h_eq]
  exact StronglyMeasurable.indicator (hX.comp_measurable hM)
    (measurableSet_lt measurable_const (hÏ„.measurable'.comp measurable_snd))

omit [TopologicalSpace Î¹] [OrderTopology Î¹] [MeasurableSpace Î¹] in
private lemma stoppedValue_stoppedProcess_dominated_le (X : Î¹ â†’ Î© â†’ E) (hÏ„ : IsStoppingTime ğ“• Ï„)
    (Ïƒ : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤}) :
    âˆƒ rho : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤},
      rho.1 â‰¤ Ïƒ.1 âˆ§
      âˆ€áµ Ï‰ âˆ‚P, â€–stoppedValue (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ Ï‰}.indicator (X i)) Ï„) Ïƒ.1 Ï‰â€– â‰¤
        â€–stoppedValue X rho.1 Ï‰â€– := by
  let rho_val := Ïƒ.1 âŠ“ Ï„
  have h_rho_stop : IsStoppingTime ğ“• rho_val := IsStoppingTime.min Ïƒ.2.1 hÏ„
  have h_rho_finite : âˆ€ Ï‰, rho_val Ï‰ â‰  âŠ¤ :=
    fun Ï‰ â†¦ ne_of_lt (lt_of_le_of_lt inf_le_left (lt_top_iff_ne_top.mpr (Ïƒ.2.2 Ï‰)))
  refine âŸ¨âŸ¨rho_val, h_rho_stop, h_rho_finiteâŸ©, fun Ï‰ â†¦ inf_le_left, ?_âŸ©
  filter_upwards with Ï‰
  simp only [stoppedValue, stoppedProcess, Set.indicator, Set.mem_setOf_eq, rho_val]
  split_ifs with h_bot
  Â· apply le_of_eq
    congr
    rw [WithTop.untopA_eq_untop, WithTop.coe_untop]
    exact Ïƒ.prop.2 Ï‰
  Â· simp only [norm_zero]; exact norm_nonneg _

lemma HasStronglyMeasurableSupProcess.of_stronglyMeasurable_isCadlag
    (hX1 : StronglyMeasurable (uncurry X)) (hX2 : âˆ€ Ï‰ : Î©, IsCadlag (X Â· Ï‰)) :
    HasStronglyMeasurableSupProcess (mÎ© := mÎ©) X := by
  sorry

lemma _root_.MeasureTheory.ProgMeasurable.hasStronglyMeasurableSupProcess_of_isCadlag
    [IsCountablyGenerated (atTop : Filter Î¹)]
    (hX_prog : ProgMeasurable ğ“• X) (hX_cadlag : âˆ€ Ï‰ : Î©, IsCadlag (X Â· Ï‰)) :
    HasStronglyMeasurableSupProcess (mÎ© := mÎ©) X := by
  refine HasStronglyMeasurableSupProcess.of_stronglyMeasurable_isCadlag ?_ hX_cadlag
  exact ProgMeasurable.stronglyMeasurable_uncurry_of_isCountablyGenerated_atTop hX_prog

lemma isStable_hasStronglyMeasurableSupProcess [SecondCountableTopology Î¹] :
    IsStable ğ“• (HasStronglyMeasurableSupProcess (E := E) (mÎ© := mÎ©) Â·) := by
  intro X hX Ï„ hÏ„
  unfold HasStronglyMeasurableSupProcess at hX âŠ¢
  let M : Î¹ Ã— Î© â†’ Î¹ Ã— Î© := fun p â†¦ ((min â†‘p.1 (Ï„ p.2)).untopA, p.2)
  have hM : Measurable M := (WithTop.measurable_coe.comp measurable_fst).min
      (hÏ„.measurable'.comp measurable_snd) |>.untopA.prodMk measurable_snd
  have key_eq : (fun p : Î¹ Ã— Î© â†¦ â¨† s â‰¤ p.1, â€–stoppedProcess
          (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ Ï‰}.indicator (X i)) Ï„ s p.2â€–â‚‘) =
      {p | âŠ¥ < Ï„ p.2}.indicator (fun p â†¦ â¨† s â‰¤ (M p).1, â€–X s (M p).2â€–â‚‘) := by
    ext âŸ¨t, Ï‰âŸ©; simp only [M, stoppedProcess, Set.indicator_apply, Set.mem_setOf_eq]
    split_ifs with h
    swap; Â· simp
    apply le_antisymm
    Â· refine iSupâ‚‚_le fun s hst â†¦ ?_
      apply le_iSupâ‚‚_of_le (min â†‘s (Ï„ Ï‰)).untopA ?_
      Â· exact le_rfl
      Â· refine WithTop.untopA_mono (by simp) ?_
        gcongr
        exact mod_cast hst
    Â· refine iSupâ‚‚_le fun u hu â†¦ ?_
      rw [WithTop.le_untopA_iff (by simp)] at hu
      Â· apply le_iSupâ‚‚_of_le (Î± := â„â‰¥0âˆ) u ?_
        Â· rw [min_eq_left]
          Â· exact le_rfl
          Â· exact le_trans hu (min_le_right _ _)
        Â· exact WithTop.coe_le_coe.mp (le_trans hu (min_le_left _ _))
  rw [key_eq]
  exact StronglyMeasurable.indicator (hX.comp_measurable hM)
    (measurableSet_lt measurable_const (hÏ„.measurable'.comp measurable_snd))

/-- The class of processes with integrable supremum is stable. -/
lemma isStable_hasIntegrableSup [SecondCountableTopology Î¹] :
    IsStable ğ“• (HasIntegrableSup (E := E) Â· P) := by
  refine fun X hX Ï„ hÏ„ â†¦ âŸ¨isStable_hasStronglyMeasurableSupProcess X hX.1 Ï„ hÏ„, ?_âŸ©
  refine fun t â†¦ âŸ¨(isStable_hasStronglyMeasurableSupProcess X hX.1 Ï„ hÏ„).comp_measurable
      (measurable_const.prodMk measurable_id) |>.aestronglyMeasurable, ?_âŸ©
  have h_bound := (hX.2 t).hasFiniteIntegral
  simp_rw [hasFiniteIntegral_def, enorm_eq_self] at h_bound âŠ¢
  refine lt_of_le_of_lt (lintegral_mono fun Ï‰ â†¦ ?_) h_bound
  apply iSupâ‚‚_le
  intro s hs
  simp only [stoppedProcess, Set.indicator_apply, Set.mem_setOf_eq]
  split_ifs with h_bot
  Â· refine le_iSupâ‚‚_of_le (min â†‘s (Ï„ Ï‰)).untopA ?_ le_rfl
    Â· rw [WithTop.untopA_le_iff]
      Â· exact le_trans (min_le_left _ _) (WithTop.coe_le_coe.mpr hs)
      Â· exact ne_top_of_le_ne_top WithTop.coe_ne_top (min_le_left _ _)
  Â· simp

/-- The class of processes with locally integrable supremum is stable. -/
lemma isStable_hasLocallyIntegrableSup [SecondCountableTopology Î¹] :
    IsStable ğ“• (HasLocallyIntegrableSup (E := E) Â· ğ“• P) :=
  IsStable.isStable_locally isStable_hasIntegrableSup

/-- The Class D is stable. -/
lemma isStable_classD [PseudoMetrizableSpace Î¹] [SecondCountableTopology Î¹] :
    IsStable ğ“• (ClassD (E := E) Â· ğ“• P) := by
  refine fun X âŸ¨hX_prog, hUI_XâŸ© Ï„ hÏ„ â†¦ âŸ¨isStable_progMeasurable X hX_prog Ï„ hÏ„, ?_âŸ©
  refine uniformIntegrable_of_dominated hUI_X
    (fun _ â†¦ (ProgMeasurable.stronglyMeasurable_stoppedValue_stoppedProcess hX_prog hÏ„
      _).aestronglyMeasurable) fun Ïƒ â†¦ ?_
  rcases stoppedValue_stoppedProcess_dominated_le X hÏ„ Ïƒ with âŸ¨rho, _, h_domâŸ©
  exact âŸ¨rho, h_domâŸ©

/-- The Class DL is stable. -/
lemma isStable_classDL [SecondCountableTopology Î¹] [PseudoMetrizableSpace Î¹] :
    IsStable ğ“• (ClassDL (E := E) Â· ğ“• P) := by
  refine fun X âŸ¨hX_prog, hUI_XâŸ© Ï„ hÏ„ â†¦ âŸ¨isStable_progMeasurable X hX_prog Ï„ hÏ„, fun t â†¦ ?_âŸ©
  let embed : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰¤ t} â†’
              {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤} :=
    fun Ïƒ â†¦ âŸ¨Ïƒ.1, Ïƒ.2.1, fun Ï‰ â†¦ ne_top_of_le_ne_top WithTop.coe_ne_top (Ïƒ.2.2 Ï‰)âŸ©
  refine uniformIntegrable_of_dominated (hUI_X t) (fun Ïƒ â†¦ ?_) (fun Ïƒ â†¦ ?_)
  Â· exact (ProgMeasurable.stronglyMeasurable_stoppedValue_stoppedProcess hX_prog hÏ„
      (embed Ïƒ)).aestronglyMeasurable
  Â· rcases stoppedValue_stoppedProcess_dominated_le X hÏ„ (embed Ïƒ) with âŸ¨rho, h_le_sigma, h_domâŸ©
    let rho_bounded : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰¤ â†‘t} :=
      âŸ¨rho.1, rho.2.1, fun Ï‰ â†¦ le_trans (h_le_sigma Ï‰) (Ïƒ.2.2 Ï‰)âŸ©
    exact âŸ¨rho_bounded, h_domâŸ©

omit [OrderBot Î¹] in
lemma _root_.MeasureTheory.Integrable.classDL [Nonempty Î¹] [SecondCountableTopology Î¹]
    (hX1 : ProgMeasurable ğ“• X) (hX2 : âˆ€ t, Integrable (fun Ï‰ â†¦ â¨† s â‰¤ t, â€–X s Ï‰â€–â‚‘) P) :
    ClassDL X ğ“• P := by
  refine âŸ¨hX1, fun t â†¦ ?_âŸ©
  let supX_t : Î© â†’ â„â‰¥0âˆ := fun Ï‰ â†¦ â¨† s â‰¤ t, â€–X s Ï‰â€–â‚‘
  have hY : MemLp supX_t 1 P := memLp_one_iff_integrable.mpr (hX2 t)
  -- measurability of each stopped value
  have mX (Ï„ : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰¤ t}) :
      AEStronglyMeasurable (stoppedValue X Ï„.1) P :=
    ((stronglyMeasurable_stoppedValue_of_le hX1 Ï„.2.1 Ï„.2.2).mono (ğ“•.le' t)).aestronglyMeasurable
  -- pointwise domination by the supremum process
  have hDom (Ï„ : {T | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰¤ t}) (Ï‰ : Î©) :
      â€–stoppedValue X Ï„.1 Ï‰â€–â‚‘ â‰¤ supX_t Ï‰ :=
    calc â€–stoppedValue X Ï„.1 Ï‰â€–â‚‘
    _ = â€–X (Ï„.1 Ï‰).untopA Ï‰â€–â‚‘ := by simp[stoppedValue]
    _ â‰¤ supX_t Ï‰ := by
      refine le_iSup_of_le (Ï„.1 Ï‰).untopA (le_iSup_of_le ?_ le_rfl)
      exact (WithTop.untopA_le_iff (ne_of_lt (lt_of_le_of_lt (Ï„.2.2 Ï‰) (by simp)))).mpr
        (Ï„.2.2 Ï‰)
  -- apply domination lemma with p = 1
  exact uniformIntegrable_of_dominated_enorm_singleton hY mX (fun Ï„ â†¦ ae_of_all _ (hDom Ï„))

omit [OrderBot Î¹] in
lemma HasIntegrableSup.classDL [Nonempty Î¹] [SecondCountableTopology Î¹]
    (hX1 : ProgMeasurable ğ“• X) (hX2 : HasIntegrableSup X P) :
    ClassDL X ğ“• P :=
  Integrable.classDL hX1 (fun t â†¦ hX2.2 t)

lemma HasLocallyIntegrableSup.locally_classDL [SecondCountableTopology Î¹] [PseudoMetrizableSpace Î¹]
    (hX1 : Locally (ProgMeasurable ğ“• Â·) ğ“• X P) (hX2 : HasLocallyIntegrableSup X ğ“• P) :
    Locally (ClassDL Â· ğ“• P) ğ“• X P := by
  have h_and : Locally (fun X â†¦ ProgMeasurable ğ“• X âˆ§ HasIntegrableSup X P) ğ“• X P := by
    rw [locally_and]
    Â· exact âŸ¨hX1, hX2âŸ©
    Â· exact isStable_progMeasurable
    Â· exact isStable_hasIntegrableSup
  exact h_and.mono fun X âŸ¨hX_prog, hX_intâŸ© â†¦ hX_int.classDL hX_prog

/-- A process of class DL is locally of class D. -/
lemma ClassDL.locally_classD [SecondCountableTopology Î¹] [PseudoMetrizableSpace Î¹]
    (hX : ClassDL X ğ“• P) :
    Locally (ClassD Â· ğ“• P) ğ“• X P := by
  rcases topOrderOrNoTopOrder Î¹ with ha | hb
  Â· exact locally_of_prop hX.classD
  obtain âŸ¨v, hv1, hv2âŸ© := exists_seq_monotone_tendsto_atTop_atTop Î¹
  refine âŸ¨fun n Ï‰ => v n, âŸ¨âŸ¨fun n => ?_, ?_âŸ©, ?_âŸ©, fun n => âŸ¨?_, ?_âŸ©âŸ©
  Â· simp [isStoppingTime_const]
  Â· filter_upwards with Ï‰
    simp only [tendsto_atTop_atTop] at hv2
    refine tendsto_atTop_isLUB (fun _ _ h => mod_cast hv1 h) âŸ¨top_mem_upperBounds _, fun x hx => ?_âŸ©
    simp only [top_le_iff, WithTop.eq_top_iff_forall_gt]
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff] at hx
    intro a
    obtain âŸ¨c, hcâŸ© := (NoTopOrder.to_noMaxOrder Î¹).exists_gt a
    obtain âŸ¨n, hnâŸ© := hv2 c
    exact lt_of_lt_of_le (WithTop.coe_lt_coe.mpr (lt_of_lt_of_le hc (hn n le_rfl))) (hx n)
  Â· filter_upwards with Ï‰
    exact fun _ _ h => WithTop.coe_le_coe.mpr (hv1 h)
  Â· refine ProgMeasurable.stoppedProcess (fun t => ?_) (by simp [isStoppingTime_const])
    by_cases hb : âŠ¥ < (v n : WithTop Î¹)
    Â· simp [hb, hX.1 t]
    Â· simp [hb, stronglyMeasurable_const]
  Â· let A := {T : Î© â†’ WithTop Î¹ | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰  âŠ¤}
    let Y := fun T : A â†¦ stoppedValue (stoppedProcess X (fun _ â†¦ v n)) T
    refine uniformIntegrable_of_dominated (Y := Y) ?_ (fun T => ?_) (fun T => ?_)
    Â· let B := {T : Î© â†’ WithTop Î¹ | IsStoppingTime ğ“• T âˆ§ âˆ€ Ï‰, T Ï‰ â‰¤ v n}
      let f : A â†’ B := fun T => âŸ¨T.1 âŠ“ (fun Ï‰ => â†‘(v n)), âŸ¨T.2.1.min_const (v n), by simpâŸ©âŸ©
      have : Y = (fun T : B â†¦ stoppedValue X T) âˆ˜ f := by
        ext T
        simpa [Y, f] using stoppedValue_stoppedProcess_apply (T.2.2 _)
      rw [this]
      exact UniformIntegrable.comp (hX.2 (v n)) f
    Â· by_cases hb : âŠ¥ < (v n : WithTop Î¹)
      Â· simp only [hb, Set.setOf_true, Set.indicator_univ, ne_eq, Set.mem_setOf_eq]
        refine AEStronglyMeasurable.congr ?_ (stoppedValue_stoppedProcess_ae_eq ?_).symm
        Â· refine (StronglyMeasurable.mono ?_ (ğ“•.le' (v n))).aestronglyMeasurable
          refine stronglyMeasurable_stoppedValue_of_le hX.1 ((T.2.1).min_const _) (fun Ï‰ => ?_)
          grind
        Â· exact ae_of_all P T.2.2
      Â· unfold stoppedValue
        simp only [hb, Set.setOf_false, Set.indicator_empty, ne_eq, Set.mem_setOf_eq,
          stoppedProcess_const]
        fun_prop
    Â· by_cases hb : âŠ¥ < (v n : WithTop Î¹)
      Â· simpa [hb, Y] using âŸ¨T.1, T.2, ae_of_all P fun Ï‰ => rfl.leâŸ©
      Â· simpa [hb, Y, stoppedValue] using âŸ¨T.1, T.2âŸ©

lemma locally_classD_of_locally_classDL {Î¹ : Type*} [ConditionallyCompleteLinearOrderBot Î¹]
    [TopologicalSpace Î¹] [OrderTopology Î¹] [DenselyOrdered Î¹] [SecondCountableTopology Î¹]
    [NoMaxOrder Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹] [PseudoMetrizableSpace Î¹]
    {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} [IsFiniteMeasure P]
    (hX : Locally (ClassDL Â· ğ“• P) ğ“• X P) (hğ“• : ğ“•.IsRightContinuous) :
    Locally (ClassD Â· ğ“• P) ğ“• X P :=
  locally_induction hğ“• (fun _ â†¦ ClassDL.locally_classD) isStable_classD hX

end ClassDClassDL

variable {Î¹ Î² : Type*}
noncomputable def leastGE [Preorder Î¹] [OrderBot Î¹] [InfSet Î¹] [Preorder Î²]
    (f : Î¹ â†’ Î© â†’ Î²) (r : Î²) : Î© â†’ WithTop Î¹ :=
  hittingAfter f (Set.Ici r) âŠ¥

instance {Î¹ : Type*} [LE Î¹] [OrderTop Î¹] [OrderBot Î¹] : BoundedOrder Î¹ where
  top := âŠ¤
  le_top := OrderTop.le_top
  bot := âŠ¥
  bot_le := OrderBot.bot_le

-- TODO: The assumptions should be refined with those of DÃ©but theorem.
lemma isLocalizingSequence_hittingAfter_Ici {Î¹ : Type*} [ConditionallyCompleteLinearOrderBot Î¹]
    [TopologicalSpace Î¹] [OrderTopology Î¹] [CompactIccSpace Î¹]
    (ğ“• : Filtration Î¹ mÎ©) {X : Î¹ â†’ Î© â†’ â„} (hX1 : StronglyAdapted ğ“• X)
    (hX2 : âˆ€ Ï‰, IsCadlag (X Â· Ï‰)) (hğ“• : ğ“•.IsRightContinuous) :
    IsLocalizingSequence ğ“• (fun n => leastGE X n) P where
  isStoppingTime n := sorry
  mono := by filter_upwards with Ï‰ n m hnm using
    hittingAfter_anti X âŠ¥ (Set.Ici_subset_Ici.2 (Nat.cast_le.2 hnm)) Ï‰
  tendsto_top := by
    filter_upwards with Ï‰
    -- Consider two cases. If `Î¹` has a top element, then `Î¹` is compact and the range of `X Â· Ï‰` is
    -- bounded. Hence, `leastGE X n` is eventually equal to `âŠ¤`.
    rcases topOrderOrNoTopOrder Î¹ with ha | hb
    Â· have : Bornology.IsBounded (Set.range (X Â· Ï‰)) := by
        have : Set.Icc (âŠ¥ : Î¹) âŠ¤ = Set.univ := Set.Icc_bot_top
        exact Set.image_univ â–¸ this â–¸ isBounded_image_of_isCadlag_of_isCompact (hX2 Ï‰) isCompact_Icc
      obtain âŸ¨m, hmâŸ© : âˆƒ (m : â„•), âˆ€ i, X i Ï‰ â‰¤ m := by
        obtain âŸ¨x, hxâŸ© := bddAbove_def.1 this.bddAbove
        exact âŸ¨âŒˆxâŒ‰â‚Š, fun i => (hx (X i Ï‰) (Set.mem_range_self i)).trans (Nat.le_ceil x)âŸ©
      apply tendsto_nhds_of_eventually_eq
      filter_upwards [Ioi_mem_atTop m] with n hn
      simpa [leastGE, hittingAfter] using fun i => lt_of_le_of_lt (hm i) (Nat.cast_lt.2 hn)
    -- If `Î¹` does not have a top element, then it suffices to show that every `i : Î¹`,
    -- `leastGE X n` is eventually larger than `i`.
    refine nhds_top_basis.tendsto_right_iff.2 fun i hi => ?_
    obtain âŸ¨c, hcâŸ© := (NoTopOrder.to_noMaxOrder Î¹).exists_gt (i.untop (lt_top_iff_ne_top.1 hi))
    have : Bornology.IsBounded ((X Â· Ï‰) '' (Set.Icc âŠ¥ c)) :=
      isBounded_image_of_isCadlag_of_isCompact (hX2 Ï‰) isCompact_Icc
    obtain âŸ¨m, hmâŸ© : âˆƒ (m : â„•), âˆ€ j â‰¤ c, X j Ï‰ â‰¤ m := by
      obtain âŸ¨x, hxâŸ© := bddAbove_def.1 this.bddAbove
      exact âŸ¨âŒˆxâŒ‰â‚Š, fun i hi => (hx (X i Ï‰)
        (Set.mem_image_of_mem _ âŸ¨bot_le, hiâŸ©)).trans (Nat.le_ceil x)âŸ©
    filter_upwards [Ioi_mem_atTop m] with n hn
    simp only [leastGE, hittingAfter]
    by_cases hj : âˆƒ j, X j Ï‰ âˆˆ Set.Ici â†‘n
    Â· simp_all only [bot_le, true_and, â†“reduceIte]
      have : c â‰¤ sInf {j | â†‘n â‰¤ X j Ï‰} := by
        refine le_csInf hj fun k hk1 => ?_
        by_contra! hk2
        grind [Nat.cast_le.1 (hk1.trans (hm k hk2.le))]
      exact lt_of_le_of_lt' (mod_cast this) (by simp_all : i < c)
    Â· grind

lemma sup_stoppedProcess_hittingAfter_Ici_le
    {Î¹ : Type*} [ConditionallyCompleteLinearOrderBot Î¹] {X : Î¹ â†’ Î© â†’ E} (t : Î¹) (K : â„)
    (hK : 0 â‰¤ K) (Ï‰ : Î©) :
    â¨† s â‰¤ t, â€–stoppedProcess X (leastGE  (fun t Ï‰ â†¦ â€–X t Ï‰â€–) K) s Ï‰â€– â‰¤
    K + Set.indicator {Ï‰ | leastGE (fun t Ï‰ â†¦ â€–X t Ï‰â€–) K Ï‰ â‰¤ t}
      (fun Ï‰ â†¦ â€–stoppedValue X (leastGE (fun t Ï‰ â†¦ â€–X t Ï‰â€–) K) Ï‰â€–) Ï‰ := by
  let Ï„ := leastGE (fun t Ï‰ â†¦ â€–X t Ï‰â€–) K
  have bound1 (i : Î¹) (hi : i < Ï„ Ï‰) : â€–X i Ï‰â€– â‰¤ K := by
    by_contra! h
    have := Exists.intro i (p := fun j => âŠ¥ â‰¤ j âˆ§ â€–X j Ï‰â€– âˆˆ Set.Ici K) âŸ¨by simp, by grindâŸ©
    simp_all only [leastGE, hittingAfter, bot_le, Set.mem_Ici, true_and, â†“reduceIte,
      WithTop.coe_lt_coe, Ï„]
    have := csInf_le (OrderBot.bddBelow {j | K â‰¤ â€–X j Ï‰â€–}) h.le
    grind
  by_cases ht : Â¬ Ï„ Ï‰ â‰¤ t
  Â· calc
    _ â‰¤ K := by
      refine ciSup_le fun i => ?_
      by_cases hN : Nonempty (i â‰¤ t)
      Â· have hi : i < Ï„ Ï‰ := lt_of_le_of_lt (WithTop.coe_le_coe.mpr hN.some) (not_le.mp ht)
        simp_all [stoppedProcess, Ï„, min_eq_left hi.le]
      Â· simp_all
    _ â‰¤ K + Set.indicator {Ï‰ | Ï„ Ï‰ â‰¤ t} (fun Ï‰ â†¦ â€–stoppedValue X Ï„ Ï‰â€–) Ï‰ := by simp [ht]
  Â· simp only [not_le, not_lt] at ht
    have : Ï„ Ï‰ â‰  âŠ¤ := by simpa [â† WithTop.lt_top_iff_ne_top] using lt_of_le_of_lt ht (by simp)
    have bound2 (i : Î¹) : â¨† (_ : i â‰¤ (Ï„ Ï‰).untopA), â€–X i Ï‰â€– â‰¤ K + â€–X (Ï„ Ï‰).untopA Ï‰â€– := by
      by_cases hNi : Nonempty (i â‰¤ (Ï„ Ï‰).untopA)
      Â· refine ciSup_le fun q => ?_
        rcases lt_or_eq_of_le q with ha | hb
        Â· linarith [bound1 i ((WithTop.lt_untopA_iff this).mp ha), norm_nonneg (X (Ï„ Ï‰).untopA Ï‰)]
        Â· simp [hb, hK]
      Â· simp only [nonempty_prop, not_le] at hNi
        simp only [isEmpty_Prop, not_le, hNi, Real.iSup_of_isEmpty]
        positivity
    calc
    _ â‰¤ â¨† s â‰¤ (Ï„ Ï‰).untopA, â€–X s Ï‰â€– := by
      refine ciSup_le fun i => ?_
      by_cases hN : Nonempty (i â‰¤ t)
      Â· by_cases hi : i â‰¤ Ï„ Ï‰
        Â· simp_all only [nonempty_prop, stoppedProcess, inf_of_le_left, WithTop.untopD_coe,
            ciSup_unique, Ï„]
          have : i â‰¤ (Ï„ Ï‰).untopA := (WithTop.le_untopA_iff this).mpr hi
          have : â€–X i Ï‰â€– â‰¤ â¨† (_ : i â‰¤ (Ï„ Ï‰).untopA), â€–X i Ï‰â€– := by
            refine le_ciSup (f := fun h : i â‰¤ (Ï„ Ï‰).untopA => â€–X i Ï‰â€–) ?_ this
            exact âŸ¨â€–X i Ï‰â€–, fun _ _ => by grindâŸ©
          refine le_trans this (le_ciSup (f := fun i => â¨† (_ : i â‰¤ (Ï„ Ï‰).untopA), â€–X i Ï‰â€–) ?_ i)
          exact âŸ¨K + â€–X (Ï„ Ï‰).untopA Ï‰â€–, fun y âŸ¨x, hxâŸ© => hx â–¸ bound2 xâŸ©
        Â· simp_all only [nonempty_prop, not_le, stoppedProcess, Ï„]
          simp_all only [min_eq_right hi.le, ciSup_unique]
          refine le_trans (le_ciSup (f := fun h : (Ï„ Ï‰).untopA â‰¤ (Ï„ Ï‰).untopA =>
            â€–X (Ï„ Ï‰).untopA Ï‰â€–) ?_ le_rfl) (le_ciSup (f := fun i =>
            â¨† (_ : i â‰¤ (Ï„ Ï‰).untopA), â€–X i Ï‰â€–) ?_ (Ï„ Ï‰).untopA)
          Â· exact âŸ¨â€–X (Ï„ Ï‰).untopA Ï‰â€–, fun y âŸ¨x, hxâŸ© => by simp [hx]âŸ©
          Â· exact âŸ¨K + â€–X (Ï„ Ï‰).untopA Ï‰â€–, fun y âŸ¨x, hxâŸ© => hx â–¸ bound2 xâŸ©
      Â· simp_all only [nonempty_prop, Real.iSup_of_isEmpty]
        exact Real.iSup_nonneg fun i => Real.iSup_nonneg fun h => norm_nonneg _
    _ â‰¤ K + â€–X (Ï„ Ï‰).untopA Ï‰â€– := ciSup_le fun i => bound2 i
    _ = K + Set.indicator {Ï‰ | Ï„ Ï‰ â‰¤ t} (fun Ï‰ â†¦ â€–stoppedValue X Ï„ Ï‰â€–) Ï‰ := by
      simp [stoppedValue, ht]

lemma ClassDL.hasLocallyIntegrableSup {Î¹ : Type*} [Nonempty Î¹]
    [ConditionallyCompleteLinearOrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    [SecondCountableTopology Î¹] [PseudoMetrizableSpace Î¹]
    [MeasurableSpace Î¹] [BorelSpace Î¹]
    [IsFiniteMeasure P]
    {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E}
    (hX1 : âˆ€ Ï‰, IsCadlag (X Â· Ï‰)) (hX2 : ClassDL X ğ“• P) (hğ“• : ğ“•.IsRightContinuous) :
    HasLocallyIntegrableSup X ğ“• P := by
  rcases hX2 with âŸ¨hX2, hX3âŸ©
  let Y : Î¹ â†’ Î© â†’ â„ := fun t Ï‰ â†¦ â€–X t Ï‰â€–
  have hY1 : StronglyAdapted ğ“• Y := hX2.stronglyAdapted.norm
  have hY2 : âˆ€ (Ï‰ : Î©), IsCadlag (Y Â· Ï‰) := by
    refine fun Ï‰ â†¦ âŸ¨?_, fun i â†¦ ?_âŸ©
    Â· exact Function.RightContinuous.continuous_comp continuous_norm (hX1 Ï‰).1
    Â· obtain âŸ¨l, hlâŸ© := (hX1 Ï‰).2 i
      exact âŸ¨â€–lâ€–, (continuous_norm.tendsto l).comp hlâŸ©
  let Ï„ : â„• â†’ Î© â†’ WithTop Î¹ := (fun n â†¦ hittingAfter Y (Set.Ici n) âŠ¥)
  have hÏ„ : IsLocalizingSequence ğ“• Ï„ P := isLocalizingSequence_hittingAfter_Ici ğ“• hY1 hY2 hğ“•
  refine âŸ¨Ï„, hÏ„, fun n â†¦ ?_âŸ©
  have hX4 := fun (t : Î¹) (Ï‰ : Î©) â†¦ sup_stoppedProcess_hittingAfter_Ici_le (X := X) t n (by simp) Ï‰
  have hX6 :=  hX2.hasStronglyMeasurableSupProcess_of_isCadlag hX1
  let Xs : Î¹ â†’ Î© â†’ E := (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)) (Ï„ n))
  have hX1s : âˆ€ Ï‰,  IsCadlag fun t â†¦ Xs t Ï‰ := isStable_isCadlag X (hX1) (Ï„ n) (hÏ„.isStoppingTime n)
  let rhs := fun (t : Î¹) (Ï‰ : Î©) â†¦
    â†‘n + {Ï‰ | hittingAfter (fun t Ï‰ â†¦ â€–X t Ï‰â€–) (Set.Ici â†‘n) âŠ¥ Ï‰ â‰¤ â†‘t}.indicator
    (fun Ï‰ â†¦ â€–stoppedValue X (hittingAfter (fun t Ï‰ â†¦ â€–X t Ï‰â€–) (Set.Ici â†‘n) âŠ¥) Ï‰â€–) Ï‰
  constructor
  Â· refine ProgMeasurable.hasStronglyMeasurableSupProcess_of_isCadlag (ğ“• := ğ“•) ?_ hX1s
    exact isStable_progMeasurable (Î¹ := Î¹) (E := E) X hX2 (Ï„ n) (hÏ„.isStoppingTime n)
  Â· intro t
    let dom := fun Ï‰ â†¦ â†‘n + â€–stoppedValue X (Ï„ n âŠ“ fun _ â†¦ t) Ï‰â€–
    let Ïƒ : Î© â†’ WithTop Î¹ := (Ï„ n) âŠ“ (fun _ â†¦ t : Î© â†’ WithTop Î¹)
    have hÏƒ : IsStoppingTime ğ“• Ïƒ := (hÏ„.isStoppingTime n).min (isStoppingTime_const ğ“• t)
    have hÏƒ_le : Ïƒ â‰¤ (fun _ â†¦ t : Î© â†’ WithTop Î¹) := inf_le_right
    refine Integrable.mono_enorm (g := dom) ?_ ?_ ?_
    Â· change Integrable ((fun Ï‰ : Î© â†¦ (n : â„)) + (fun Ï‰ â†¦ â€–stoppedValue X (Ï„ n âŠ“ fun x â†¦ â†‘t) Ï‰â€–)) P
      refine Integrable.add (integrable_const (n : â„)) ( ?_)
      rcases hX3 t with âŸ¨h_meas, _, âŸ¨C, h_boundâŸ©âŸ©
      refine âŸ¨(h_meas âŸ¨Ïƒ, âŸ¨hÏƒ, hÏƒ_leâŸ© âŸ©).norm , ?_âŸ©
      Â· simp_rw [HasFiniteIntegral, enorm_norm, â† eLpNorm_one_eq_lintegral_enorm]
        exact lt_of_le_of_lt (h_bound âŸ¨Ïƒ, âŸ¨hÏƒ, hÏƒ_leâŸ©âŸ©) ENNReal.coe_lt_top
    Â· apply StronglyMeasurable.aestronglyMeasurable
      have h_stopped := isStable_hasStronglyMeasurableSupProcess X hX6 (Ï„ n) (hÏ„.isStoppingTime n)
      exact h_stopped.comp_measurable (measurable_const.prodMk measurable_id)
    Â· filter_upwards with Ï‰
      have h_LE (Ï‰ : Î©): 0 â‰¤ dom Ï‰ :=
        add_nonneg (Nat.cast_nonneg' n) (norm_nonneg (stoppedValue X (Ï„ n âŠ“ fun x â†¦ â†‘t) Ï‰))
      have h_bdd_subtype : BddAbove (Set.range fun (u : {x // x â‰¤ t}) â†¦
            â€–stoppedProcess X (Ï„ n) u Ï‰â€–) := by
        let S := Set.Icc âŠ¥ t
        have hS_compact : IsCompact S := isCompact_Icc
        have h_subset : (Set.range fun (u : {x // x â‰¤ t}) â†¦ â€–stoppedProcess X (Ï„ n) u Ï‰â€–) âŠ†
                        (fun x â†¦ â€–X x Ï‰â€–) '' S := by
          rintro _ âŸ¨u, rflâŸ©
          simp only [stoppedProcess, Set.mem_image]
          refine âŸ¨((Ï„ n âŠ“ fun _ â†¦ â†‘u) Ï‰).untopA, âŸ¨bot_le, ?_ âŸ©, by rw [min_comm, Pi.inf_apply]âŸ©
          Â· apply le_trans _ u.2
            rw [WithTop.untopA_eq_untop, WithTop.untop_le_iff]
            Â· exact inf_le_right
            Â· exact ne_top_of_le_ne_top (WithTop.coe_ne_top) inf_le_right
        apply BddAbove.mono h_subset
        have h_metric_bdd := isBounded_image_of_isCadlag_of_isCompact (hX1 Ï‰) hS_compact
        obtain âŸ¨C, hCâŸ© : âˆƒ C, âˆ€ x âˆˆ (X Â· Ï‰) '' S, â€–xâ€– â‰¤ C := by
          rw [Metric.isBounded_iff_subset_ball (0 : E)] at h_metric_bdd
          rcases h_metric_bdd with âŸ¨C, h_subset_ballâŸ©
          refine âŸ¨C, fun x hx â†¦ ?_âŸ©
          specialize h_subset_ball hx
          rw [Metric.mem_ball, dist_zero_right] at h_subset_ball
          exact le_of_lt h_subset_ball
        use C
        rintro y âŸ¨x, hx, rflâŸ©
        exact hC _ (Set.mem_image_of_mem _ hx)
      have h_val_le_rhs (s : Î¹) (hs : s â‰¤ t) : â€–stoppedProcess X (Ï„ n) s Ï‰â€– â‰¤ rhs t Ï‰ := by
        apply le_trans ?_ (hX4 t Ï‰)
        have h_bdd_nested :
            BddAbove (Set.range fun s â†¦ â¨† (_ : s â‰¤ t), â€–stoppedProcess X (Ï„ n) s Ï‰â€–) := by
          obtain âŸ¨M, hMâŸ© := h_bdd_subtype
          refine âŸ¨max M 0, fun y hy â†¦ ?_âŸ©
          obtain âŸ¨s', rflâŸ© := hy
          by_cases hs' : s' â‰¤ t
          Â· calc â¨† (_ : s' â‰¤ t), â€–stoppedProcess X (Ï„ n) s' Ï‰â€–
                = â€–stoppedProcess X (Ï„ n) s' Ï‰â€– := ciSup_pos hs'
              _ â‰¤ M := hM âŸ¨âŸ¨s', hs'âŸ©, rflâŸ©
              _ â‰¤ max M 0 := le_max_left M 0
          Â· show (fun s â†¦ â¨† (_ : s â‰¤ t), â€–stoppedProcess X (Ï„ n) s Ï‰â€–) s' â‰¤ max M 0
            have : IsEmpty (s' â‰¤ t) := âŸ¨fun h => hs' hâŸ©
            simp only [Real.iSup_of_isEmpty, le_sup_right]
        refine le_ciSup_of_le h_bdd_nested s ?_
        refine le_ciSup_of_le ?_ hs le_rfl
        use â€–stoppedProcess X (Ï„ n) s Ï‰â€–
        rintro _ âŸ¨_, rflâŸ©
        exact le_rfl
      have h_rhs_le_dom : rhs t Ï‰ â‰¤ dom Ï‰ := by
        simp only [rhs, dom, add_le_add_iff_left]
        rw [Set.indicator]
        split_ifs with h
        Â· simp only [Set.mem_setOf_eq] at h
          simp only [stoppedValue, Pi.inf_apply]
          rw [min_eq_left h]
        Â· simp only [norm_nonneg]
      calc
        â¨† s, â¨† (_ : s â‰¤ t), â€–stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)) (Ï„ n) s Ï‰â€–â‚‘
      _ â‰¤ â¨† s, â¨† (_ : s â‰¤ t), â€–stoppedProcess X (Ï„ n) s Ï‰â€–â‚‘ := by
        gcongr with s hs
        simp only [stoppedProcess, Set.indicator, Set.mem_setOf_eq]
        split_ifs <;> simp
      _ â‰¤ ENNReal.ofReal (rhs t Ï‰) := by
        rw [iSup_subtype']
        simp only [iSup_le_iff, Subtype.forall]
        intro s hs
        rw [â† enorm_norm, Real.enorm_of_nonneg]
        Â· exact ENNReal.ofReal_le_ofReal <| h_val_le_rhs s hs
        Â· exact norm_nonneg (stoppedProcess X (Ï„ n) s Ï‰)
      _ â‰¤ ENNReal.ofReal (dom Ï‰) := ENNReal.ofReal_le_ofReal h_rhs_le_dom
      _ â‰¤ â€–dom Ï‰â€–â‚‘ := by rw [â† Real.enorm_of_nonneg <| h_LE Ï‰]

end LinearOrder

section ConditionallyCompleteLinearOrderBot

variable [ConditionallyCompleteLinearOrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
  [MeasurableSpace Î¹] [SecondCountableTopology Î¹] [DenselyOrdered Î¹] [NoMaxOrder Î¹] [BorelSpace Î¹]
  [PseudoMetrizableSpace Î¹] [IsFiniteMeasure P] {ğ“• : Filtration Î¹ mÎ©}

lemma hasLocallyIntegrableSup_of_locally_classDL (hğ“• : ğ“•.IsRightContinuous)
    (hX1 : Locally (fun X â†¦ âˆ€ Ï‰, IsCadlag (X Â· Ï‰)) ğ“• X P) (hX2 : Locally (ClassDL Â· ğ“• P) ğ“• X P) :
    HasLocallyIntegrableSup X ğ“• P :=
  locally_inductionâ‚‚ hğ“• (fun _ hCad hDL â†¦ ClassDL.hasLocallyIntegrableSup hCad hDL hğ“•)
     isStable_isCadlag isStable_classDL isStable_hasIntegrableSup hX1 hX2

lemma locally_classDL_iff_hasLocallyIntegrableSup (hğ“• : ğ“•.IsRightContinuous)
    (hX_prog : Locally (ProgMeasurable ğ“• Â·) ğ“• X P)
    (hX1 : Locally (fun X â†¦ âˆ€ Ï‰, IsCadlag (X Â· Ï‰)) ğ“• X P) :
    Locally (ClassDL Â· ğ“• P) ğ“• X P â†” HasLocallyIntegrableSup X ğ“• P :=
  âŸ¨hasLocallyIntegrableSup_of_locally_classDL hğ“• hX1, fun h_sup â†¦ h_sup.locally_classDL hX_progâŸ©

lemma locally_classD_iff_locally_classDL (hğ“• : ğ“•.IsRightContinuous) :
    Locally (ClassD Â· ğ“• P) ğ“• X P â†” Locally (ClassDL Â· ğ“• P) ğ“• X P :=
  âŸ¨fun hD â†¦ hD.mono fun _ hXD â†¦ hXD.classDL, fun hDL â†¦ locally_classD_of_locally_classDL hDL hğ“•âŸ©

lemma locally_classD_iff_hasLocallyIntegrableSup (hğ“• : ğ“•.IsRightContinuous)
    (hX_prog : Locally (ProgMeasurable ğ“• Â·) ğ“• X P)
    (hX1 : Locally (fun X â†¦ âˆ€ Ï‰, IsCadlag (X Â· Ï‰)) ğ“• X P) :
    Locally (ClassD Â· ğ“• P) ğ“• X P â†” HasLocallyIntegrableSup X ğ“• P := by
  rw [locally_classD_iff_locally_classDL hğ“•,
      locally_classDL_iff_hasLocallyIntegrableSup hğ“• hX_prog hX1]

/-- A right-continuous, nonnegative submartingale is locally of class D. -/
lemma _root_.MeasureTheory.Submartingale.locally_classD
    [NormedSpace â„ E] [CompleteSpace E] [Lattice E] [HasSolidNorm E]
    [IsOrderedAddMonoid E] [IsOrderedModule â„ E]
    (hğ“• : ğ“•.IsRightContinuous) (hX : Submartingale X ğ“• P) (hC : âˆ€ Ï‰, RightContinuous (X Â· Ï‰))
    (hX_nonneg : 0 â‰¤ X) :
    Locally (ClassD Â· ğ“• P) ğ“• X P := by
  rw [locally_classD_iff_locally_classDL hğ“•]
  exact locally_of_prop (hX.classDL hC hX_nonneg)

/-- A nonnegative local submartingale is locally of class D. -/
lemma IsLocalSubmartingale.locally_classD [NormedSpace â„ E] [CompleteSpace E] [Lattice E]
    [HasSolidNorm E] [IsOrderedAddMonoid E] [IsOrderedModule â„ E]
    [MeasureSpace E] [BorelSpace E] [SecondCountableTopology E]
    [Approximable ğ“• P]
    (hğ“• : ğ“•.IsRightContinuous) (hX : IsLocalSubmartingale X ğ“• P) (hX_nonneg : 0 â‰¤ X) :
    Locally (ClassD Â· ğ“• P) ğ“• X P := by
  refine locally_induction hğ“• ?_ isStable_classD ?_
    (p := fun X : Î¹ â†’ Î© â†’ E â†¦ Submartingale X ğ“• P âˆ§ (âˆ€ Ï‰, IsCadlag (X Â· Ï‰)) âˆ§ 0 â‰¤ X)
  Â· intro X âŸ¨hX, hXC, hX_nonnegâŸ©
    exact hX.locally_classD hğ“• (fun Ï‰ â†¦ (hXC Ï‰).right_continuous) hX_nonneg
  Â· simp_rw [â† and_assoc]
    rw [locally_and]
    Â· constructor
      Â· exact hX
      Â· exact locally_of_prop hX_nonneg
    Â· exact isStable_submartingale
    Â· intro X hX Ï„ hÏ„
      -- todo: stoppedProcess_nonneg
      unfold stoppedProcess
      intro i Ï‰
      simp only [Pi.zero_apply, Set.indicator_apply, Set.mem_setOf_eq]
      split_ifs with h
      Â· exact hX _ _
      Â· rfl

end ConditionallyCompleteLinearOrderBot

end ProbabilityTheory
