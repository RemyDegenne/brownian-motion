/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
import Mathlib.Probability.Process.Stopping
import BrownianMotion.StochasticIntegral.Predictable
import BrownianMotion.Auxiliary.WithTop
import BrownianMotion.Auxiliary.IsStoppingTime
import BrownianMotion.Auxiliary.StoppedProcess
import BrownianMotion.StochasticIntegral.Cadlag

/-! # Localizing sequences of stopping times

-/

open MeasureTheory Filter Filtration
open scoped ENNReal Topology

namespace ProbabilityTheory

variable {Î¹ Î© E : Type*} {mÎ© : MeasurableSpace Î©} {P : Measure Î©}

/-- A pre-localizing sequence is a sequence of stopping times that tends almost surely
to infinity. -/
structure IsPreLocalizingSequence [Preorder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ mÎ©) (Ï„ : â„• â†’ Î© â†’ WithTop Î¹) (P : Measure Î© := by volume_tac) :
    Prop where
  isStoppingTime : âˆ€ n, IsStoppingTime ğ“• (Ï„ n)
  tendsto_top : âˆ€áµ Ï‰ âˆ‚P, Tendsto (Ï„ Â· Ï‰) atTop (ğ“ âŠ¤)

/-- A localizing sequence is a sequence of stopping times that is almost surely increasing and
tends almost surely to infinity. -/
structure IsLocalizingSequence [Preorder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ mÎ©) (Ï„ : â„• â†’ Î© â†’ WithTop Î¹)
    (P : Measure Î© := by volume_tac) extends IsPreLocalizingSequence ğ“• Ï„ P where
  mono : âˆ€áµ Ï‰ âˆ‚P, Monotone (Ï„ Â· Ï‰)

lemma isLocalizingSequence_const_top [Preorder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ mÎ©) (P : Measure Î©) : IsLocalizingSequence ğ“• (fun _ _ â†¦ âŠ¤) P where
  isStoppingTime n := by simp [IsStoppingTime]
  mono := ae_of_all _ fun _ _ _ _ â†¦ by simp
  tendsto_top := by filter_upwards with Ï‰ using tendsto_const_nhds

section LinearOrder

variable [LinearOrder Î¹] {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} {p q : (Î¹ â†’ Î© â†’ E) â†’ Prop}

lemma IsLocalizingSequence.min [TopologicalSpace Î¹] [OrderTopology Î¹] {Ï„ Ïƒ : â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : IsLocalizingSequence ğ“• Ïƒ P) :
    IsLocalizingSequence ğ“• (min Ï„ Ïƒ) P where
  isStoppingTime n := (hÏ„.isStoppingTime n).min (hÏƒ.isStoppingTime n)
  mono := by filter_upwards [hÏ„.mono, hÏƒ.mono] with Ï‰ hÏ„Ï‰ hÏƒÏ‰; exact hÏ„Ï‰.min hÏƒÏ‰
  tendsto_top := by
    filter_upwards [hÏ„.tendsto_top, hÏƒ.tendsto_top] with Ï‰ hÏ„Ï‰ hÏƒÏ‰ using hÏ„Ï‰.min hÏƒÏ‰

end LinearOrder

section ConditionallyCompleteLinearOrderBot

variable [ConditionallyCompleteLinearOrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
  [DenselyOrdered Î¹] [FirstCountableTopology Î¹] [NoMaxOrder Î¹]
  {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} {p q : (Î¹ â†’ Î© â†’ E) â†’ Prop}

lemma measure_iInter_of_ae_antitone {Î¹ : Type*}
    [Countable Î¹] [Preorder Î¹] [IsDirected Î¹ fun (x1 x2 : Î¹) â†¦ x1 â‰¤ x2]
    {s : Î¹ â†’ Set Î©} (hs : âˆ€áµ Ï‰ âˆ‚P, Antitone (s Â· Ï‰))
    (hsm : âˆ€ (i : Î¹), MeasureTheory.NullMeasurableSet (s i) P) (hfin : âˆƒ (i : Î¹), P (s i) â‰  âŠ¤) :
    P (â‹‚ (i : Î¹), s i) = â¨… (i : Î¹), P (s i) := by
  set t : Î¹ â†’ Set Î© := fun i â†¦ â‹‚ j â‰¤ i, s j with ht
  have hst (i : Î¹) : s i =áµ[P] t i := by
    filter_upwards [hs] with Ï‰ hÏ‰
    suffices Ï‰ âˆˆ s i â†” Ï‰ âˆˆ t i by
      exact propext this
    simp only [ht, Set.mem_iInter]
    refine âŸ¨fun (h : s i Ï‰) j hj â†¦ ?_, fun h â†¦ h i le_rflâŸ©
    change s j Ï‰
    specialize hÏ‰ hj
    simp only [le_Prop_eq] at hÏ‰
    exact hÏ‰ h
  rw [measure_congr <| EventuallyEq.countable_iInter hst, Antitone.measure_iInter]
  Â· exact iInf_congr <| fun i â†¦ measure_congr <| (hst i).symm
  Â· intros i j hij
    simp only [ht]
    rw [(_ : â‹‚ k â‰¤ j, s k = (â‹‚ k â‰¤ i, s k) âˆ© (â‹‚ k âˆˆ {k | k â‰¤ j âˆ§ Â¬ k â‰¤ i}, s k))]
    Â· exact Set.inter_subset_left
    Â· ext Ï‰
      simp only [Set.mem_iInter, Set.mem_setOf_eq, Set.mem_inter_iff, and_imp]
      grind
  Â· exact fun _ â†¦ NullMeasurableSet.iInter <| fun j â†¦ NullMeasurableSet.iInter <| fun _ â†¦ hsm j
  Â· obtain âŸ¨i, hiâŸ© := hfin
    refine âŸ¨i, (lt_of_le_of_lt ?_ <| lt_top_iff_ne_top.2 hi).neâŸ©
    rw [measure_congr (hst i)]

lemma isLocalizingSequence_of_isPreLocalizingSequence
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} (hğ“• : IsRightContinuous ğ“•) (hÏ„ : IsPreLocalizingSequence ğ“• Ï„ P) :
    IsLocalizingSequence ğ“• (fun i Ï‰ â†¦ â¨… j â‰¥ i, Ï„ j Ï‰) P where
  isStoppingTime (n : â„•) := IsStoppingTime.iInf {j | j â‰¥ n} hğ“• (fun j â†¦ hÏ„.isStoppingTime j)
  mono :=  ae_of_all _ <| fun Ï‰ n m hnm â†¦ iInf_le_iInf_of_subset <| fun k hk â†¦ hnm.trans hk
  tendsto_top := by
    filter_upwards [hÏ„.tendsto_top] with Ï‰ hÏ‰
    replace hÏ‰ := hÏ‰.liminf_eq
    rw [liminf_eq_iSup_iInf_of_nat] at hÏ‰
    rw [â† hÏ‰]
    refine tendsto_atTop_iSup ?_
    intro n m hnm
    simp only [ge_iff_le, le_iInf_iff, iInf_le_iff]
    intro k hk i hi
    grind

section

omit [DenselyOrdered Î¹] [FirstCountableTopology Î¹] [NoMaxOrder Î¹]
variable [SecondCountableTopology Î¹] [IsFiniteMeasure P]

lemma isPreLocalizingSequence_of_isLocalizingSequence_aux'
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ T : â„• â†’ Î¹, Tendsto T atTop atTop
      âˆ§ âˆ€ n, âˆƒ k, P {Ï‰ | Ïƒ n k Ï‰ < min (Ï„ n Ï‰) (T n)} â‰¤ (1 / 2) ^ n := by
  obtain âŸ¨T, -, hTâŸ© := Filter.exists_seq_monotone_tendsto_atTop_atTop Î¹
  refine âŸ¨T, hT, fun n â†¦ ?_âŸ©
  by_contra hn; push_neg at hn
  suffices (1 / 2) ^ n â‰¤ P (â‹‚ k : â„•, {Ï‰ | Ïƒ n k Ï‰ < min (Ï„ n Ï‰) (T n)}) by
    refine (by simp : Â¬ (1 / 2 : â„â‰¥0âˆ) ^ n â‰¤ 0) <| this.trans <| nonpos_iff_eq_zero.2 ?_
    rw [measure_eq_zero_iff_ae_notMem]
    filter_upwards [(hÏƒ n).tendsto_top] with Ï‰ hTop hmem
    rw [WithTop.tendsto_atTop_nhds_top_iff] at hTop
    simp only [Set.mem_iInter, Set.mem_setOf_eq] at hmem
    obtain âŸ¨N, hNâŸ© := hTop (T n)
    specialize hN N le_rfl
    specialize hmem N
    grind
  rw [measure_iInter_of_ae_antitone, le_iInf_iff]
  Â· exact fun k â†¦ (hn k).le
  Â· filter_upwards [(hÏƒ n).mono] with Ï‰ hÏ‰
    intros i j hij
    specialize hÏ‰ hij
    simp only [lt_inf_iff, le_Prop_eq] at *
    change Ïƒ n j Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n j Ï‰ < T n â†’ Ïƒ n i Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n i Ï‰ < T n
    grind
  Â· intro i
    refine MeasurableSet.nullMeasurableSet ?_
    have hMÏƒ := ((hÏƒ n).isStoppingTime i).measurable
    have hMÏ„ := (hÏ„.isStoppingTime n).measurable
    simp_rw [lt_inf_iff]
    rw [(_ : {Ï‰ | Ïƒ n i Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n i Ï‰ < T n} = {Ï‰ | Ïƒ n i Ï‰ < Ï„ n Ï‰} âˆ© {Ï‰ | Ïƒ n i Ï‰ < T n})]
    Â· exact MeasurableSet.inter
        (measurableSet_lt ((hÏƒ n).isStoppingTime i).measurable' (hÏ„.isStoppingTime n).measurable')
        <| measurableSet_lt ((hÏƒ n).isStoppingTime i).measurable' measurable_const
    Â· rfl
  Â· exact âŸ¨0, measure_ne_top P _âŸ©

/-- Auxliary defintion for `isPreLocalizingSequence_of_isLocalizingSequence` which constructs a
strictly increasing sequence from a given sequence. -/
def mkStrictMonoAux (x : â„• â†’ â„•) : â„• â†’ â„•
| 0 => x 0
| n + 1 => max (x (n + 1)) (mkStrictMonoAux x n) + 1

lemma mkStrictMonoAux_strictMono (x : â„• â†’ â„•) : StrictMono (mkStrictMonoAux x) := by
  refine strictMono_nat_of_lt_succ <| fun n â†¦ ?_
  simp only [mkStrictMonoAux]
  exact lt_of_le_of_lt (le_max_right (x (n + 1)) _) (lt_add_one (max (x (n + 1)) _))

lemma le_mkStrictMonoAux (x : â„• â†’ â„•) : âˆ€ n, x n â‰¤ mkStrictMonoAux x n
| 0 => by simp [mkStrictMonoAux]
| n + 1 => by
    simp only [mkStrictMonoAux]
    exact (le_max_left (x (n + 1)) (mkStrictMonoAux x n)).trans <|
       Nat.le_add_right (max (x (n + 1)) (mkStrictMonoAux x n)) 1

lemma isPreLocalizingSequence_of_isLocalizingSequence_aux
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ nk : â„• â†’ â„•, StrictMono nk âˆ§ âˆƒ T : â„• â†’ Î¹, Tendsto T atTop atTop
      âˆ§ âˆ€ n, P {Ï‰ | Ïƒ n (nk n) Ï‰ < min (Ï„ n Ï‰) (T n)} â‰¤ (1 / 2) ^ n := by
  obtain âŸ¨T, hT, hâŸ© := isPreLocalizingSequence_of_isLocalizingSequence_aux' hÏ„ hÏƒ
  choose nk hnk using h
  refine âŸ¨mkStrictMonoAux nk, mkStrictMonoAux_strictMono nk, T, hT, fun n â†¦
    le_trans (EventuallyLE.measure_le ?_) (hnk n)âŸ©
  filter_upwards [(hÏƒ n).mono] with Ï‰ hÏ‰
  specialize hÏ‰ (le_mkStrictMonoAux nk n)
  simp only [lt_inf_iff, le_Prop_eq]
  change Ïƒ n (mkStrictMonoAux nk n) Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n (mkStrictMonoAux nk n) Ï‰ < T n â†’
    Ïƒ n (nk n) Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n (nk n) Ï‰ < T n
  grind

lemma isPreLocalizingSequence_of_isLocalizingSequence
    [NoMaxOrder Î¹] {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ nk : â„• â†’ â„•, StrictMono nk
      âˆ§ IsPreLocalizingSequence ğ“• (fun i Ï‰ â†¦ (Ï„ i Ï‰) âŠ“ (Ïƒ i (nk i) Ï‰)) P := by
  obtain âŸ¨nk, hnk, T, hT, hPâŸ© := isPreLocalizingSequence_of_isLocalizingSequence_aux hÏ„ hÏƒ
  refine âŸ¨nk, hnk, fun n â†¦ (hÏ„.isStoppingTime n).min ((hÏƒ _).isStoppingTime _), ?_âŸ©
  have : âˆ‘' n, P {Ï‰ | Ïƒ n (nk n) Ï‰ < min (Ï„ n Ï‰) (T n)} < âˆ :=
    lt_of_le_of_lt (ENNReal.summable.tsum_mono ENNReal.summable hP)
      (tsum_geometric_lt_top.2 <| by norm_num)
  have hÏ„Top := hÏ„.tendsto_top
  filter_upwards [ae_eventually_notMem this.ne, hÏ„Top] with Ï‰ hÏ‰ hÏ‰Ï„
  replace hT := hÏ‰Ï„.min hT.tendsto_withTop_atTop_nhds_top
  simp_rw [eventually_atTop, not_lt, â† eventually_atTop] at hÏ‰
  rw [min_self] at hT
  rw [â† min_self âŠ¤]
  refine hÏ‰Ï„.min <| tendsto_of_tendsto_of_tendsto_of_le_of_le' hT tendsto_const_nhds hÏ‰ ?_
  simp only [le_top, eventually_atTop, ge_iff_le, implies_true, exists_const]

end

end ConditionallyCompleteLinearOrderBot

section cadlag

section LinearOrder

variable [LinearOrder Î¹] [OrderBot Î¹] {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} {p : (Î¹ â†’ Î© â†’ E) â†’ Prop}

open Classical in
/-- Given a property on paths which holds almost surely for a stochastic process, we construct a
localizing sequence by setting the stopping time to be âˆ whenever the property holds. -/
noncomputable
def LocalizingSequenceOfProp (X : Î¹ â†’ Î© â†’ E) (p : (Î¹ â†’ E) â†’ Prop) : â„• â†’ Î© â†’ WithTop Î¹ :=
  Function.const _ <| fun Ï‰ â†¦ if p (X Â· Ï‰) then âŠ¤ else âŠ¥

omit [OrderBot Î¹] in
lemma isStoppingTime_ae_const [HasUsualConditions ğ“• P] (Ï„ : Î© â†’ WithTop Î¹) (c : WithTop Î¹)
    (hÏ„ : Ï„ =áµ[P] Function.const _ c) :
    IsStoppingTime ğ“• Ï„ := by
  intros i
  suffices P {Ï‰ | Ï„ Ï‰ â‰¤ i} = 0 âˆ¨ P {Ï‰ | Ï„ Ï‰ â‰¤ â†‘i}á¶œ = 0 by
    obtain h | h := this
    Â· exact HasUsualConditions.IsComplete h i
    Â· exact (HasUsualConditions.IsComplete h i).of_compl
  obtain hle | hgt := le_or_gt c i
  Â· refine Or.inr <| ae_iff.1 ?_
    filter_upwards [hÏ„] with Ï‰ rfl using hle
  Â· refine Or.inl ?_
    rw [â† compl_compl {Ï‰ | Ï„ Ï‰ â‰¤ i}]
    refine ae_iff.1 ?_
    filter_upwards [hÏ„] with Ï‰ hÏ‰
    simp [hÏ‰, hgt]

variable [TopologicalSpace Î¹] [OrderTopology Î¹]

lemma isLocalizingSequence_localizingSequenceOfProp [HasUsualConditions ğ“• P] {p : (Î¹ â†’ E) â†’ Prop}
    (hpX : âˆ€áµ Ï‰ âˆ‚P, p (X Â· Ï‰)) :
    IsLocalizingSequence ğ“• (LocalizingSequenceOfProp X p) P where
  isStoppingTime n := by
    refine isStoppingTime_ae_const (P := P) _ âŠ¤ ?_
    filter_upwards [hpX] with Ï‰ hÏ‰
    rw [LocalizingSequenceOfProp, Function.const_apply, Function.const_apply, if_pos hÏ‰]
  mono := ae_of_all _ <| fun Ï‰ i j hij â†¦ by simp [LocalizingSequenceOfProp]
  tendsto_top := by
    filter_upwards [hpX] with Ï‰ hÏ‰
    simp [LocalizingSequenceOfProp, if_pos hÏ‰]

variable [Zero E]

end LinearOrder

end cadlag

end ProbabilityTheory
