/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.LpSpace.Basic
import Mathlib.Probability.Moments.Basic
import Mathlib.Analysis.SpecialFunctions.Log.ENNRealLogExp
import Mathlib.Analysis.SpecialFunctions.Log.PosLog

import Mathlib.Probability.Notation
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.Calculus.MeanValue
import Mathlib.Analysis.Convex.SpecificFunctions.Basic
import Mathlib.Analysis.Normed.Lp.WithLp

/-!
# Komlos lemmas

-/
open Filter MeasureTheory Set Topology
open scoped Topology NNReal ENNReal BigOperators

variable {E Î© : Type*} {mÎ© : MeasurableSpace Î©}

lemma komlos_convex [AddCommMonoid E] [Module â„â‰¥0 E]
  {f : â„• â†’ E} {Ï† : E â†’ â„} (hÏ†_nonneg : 0 â‰¤ Ï†)
  (hÏ†_bdd : âˆƒ M : â„, âˆ€ n, Ï† (f n) â‰¤ M) :
  âˆƒ g : â„• â†’ E, (âˆ€ n, g n âˆˆ convexHull â„â‰¥0 (Set.range fun m â†¦ f (n + m))) âˆ§
    âˆ€ Î´ > 0, âˆƒ N, âˆ€ n m, N â‰¤ n â†’ N â‰¤ m â†’
      2â»Â¹ * Ï† (g n) + 2â»Â¹ * Ï† (g m) - Ï† ((2 : â„â‰¥0)â»Â¹ â€¢ (g n + g m)) < Î´ := by
  obtain âŸ¨M, hMâŸ© := hÏ†_bdd
  let r : â„• â†’ â„ := fun n â†¦ sInf (Set.image Ï† (convexHull â„â‰¥0 (Set.range (fun m â†¦ f (n + m)))))
  have hr_nondec n : r n â‰¤ r (n + 1) := by
    apply_rules [csInf_le_csInf]
    Â· exact âŸ¨0, Set.forall_mem_image.2 fun x hx â†¦ hÏ†_nonneg xâŸ©
    Â· exact âŸ¨_, âŸ¨ _, subset_convexHull â„â‰¥0 _ âŸ¨0, rflâŸ©, rflâŸ©âŸ©
    Â· refine Set.image_mono <| convexHull_min ?_ (convex_convexHull â„â‰¥0 _)
      rintro _ âŸ¨m, rflâŸ©; exact subset_convexHull â„â‰¥0 _ âŸ¨m + 1, by simp [add_comm, add_left_comm]âŸ©
  obtain âŸ¨A, hAâŸ© : âˆƒ A, Filter.Tendsto r Filter.atTop (nhds A) := by
    refine âŸ¨_, tendsto_atTop_ciSup (monotone_nat_of_le_succ hr_nondec) ?_âŸ©
    exact âŸ¨M, Set.forall_mem_range.mpr fun n â†¦ csInf_le
      âŸ¨0, Set.forall_mem_image.mpr fun x hx â†¦ hÏ†_nonneg xâŸ©
        (Set.mem_image_of_mem _ <| subset_convexHull â„â‰¥0 _
          <| Set.mem_range_self 0) |> le_trans <| by simpa using hM nâŸ©
  obtain âŸ¨g, hgâŸ© :
      âˆƒ g : â„• â†’ E, (âˆ€ n, g n âˆˆ convexHull â„â‰¥0 (Set.range (fun m â†¦ f (n + m))))
          âˆ§ (âˆ€ n, Ï† (g n) â‰¤ A + 1 / (n + 1)) := by
    have h_exists_g :
        âˆ€ n, âˆƒ g âˆˆ convexHull â„â‰¥0 (Set.range (fun m â†¦ f (n + m))), Ï† g â‰¤ A + 1 / (n + 1) := by
      intro n
      have h_exists_g :
          âˆƒ g âˆˆ convexHull â„â‰¥0 (Set.range (fun m â†¦ f (n + m))), Ï† g < A + 1 / (n + 1) := by
        have h_exists_g : r n < A + 1 / (n + 1) := by
          exact lt_add_of_le_of_pos (le_of_tendsto_of_tendsto tendsto_const_nhds hA
            (Filter.eventually_atTop.2 âŸ¨n, fun m hm â†¦ by
              induction hm <;> [tauto; linarith [hr_nondec â€¹_â€º]]âŸ©)) (by positivity)
        contrapose! h_exists_g
        exact le_csInf âŸ¨ _, Set.mem_image_of_mem _ <| subset_convexHull â„â‰¥0 _
          <| Set.mem_range_self 0 âŸ© fun x hx â†¦ by
            rcases hx with âŸ¨ g, hg, rfl âŸ©; exact h_exists_g g hg
      exact âŸ¨h_exists_g.choose, h_exists_g.choose_spec.1, le_of_lt h_exists_g.choose_spec.2âŸ©
    exact âŸ¨fun n â†¦ Classical.choose (h_exists_g n),
      fun n â†¦ Classical.choose_spec (h_exists_g n) |>.1,
        fun n â†¦ Classical.choose_spec (h_exists_g n) |>.2âŸ©
  refine âŸ¨g, hg.1, fun Î´ Î´pos â†¦ ?_âŸ©
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© := exists_between (div_pos Î´pos zero_lt_four)
  obtain âŸ¨N, hNâŸ© : âˆƒ N, r N â‰¥ A - Îµ âˆ§ 1 / (N + 1) â‰¤ Îµ := by
    rcases Metric.tendsto_atTop.mp hA Îµ Îµpos with âŸ¨N, hNâŸ©
    exact âŸ¨N + âŒˆÎµâ»Â¹âŒ‰â‚Š, by linarith [abs_lt.mp (hN (N + âŒˆÎµâ»Â¹âŒ‰â‚Š) (by grind))], by
      simpa using inv_le_of_inv_leâ‚€ Îµpos (by linarith [Nat.le_ceil (Îµâ»Â¹)])âŸ©
  refine âŸ¨N, fun n m hn hm â†¦ ?_âŸ©
  have h_convex : Ï† ((1 / 2 : â„â‰¥0) â€¢ (g n + g m)) â‰¥ A - Îµ := by
    have h_convex :
        (1 / 2 : â„â‰¥0) â€¢ (g n + g m) âˆˆ convexHull â„â‰¥0 (Set.range (fun m â†¦ f (N + m))) := by
      simp only [one_div, gt_iff_lt, ge_iff_le, tsub_le_iff_right, smul_add] at *
      refine convex_convexHull â„â‰¥0 _ ?_ ?_ ?_ ?_ ?_ <;> norm_num
      Â· refine convexHull_mono (Set.range_subset_iff.2 fun m â†¦ ?_) (hg.1 n)
        exact âŸ¨m + (n - N), by grindâŸ©
      Â· refine convexHull_mono ?_ (hg.1 m)
        exact Set.range_subset_iff.2 fun k â†¦ âŸ¨k + (m - N), by
          simp [show N + (k + (m - N)) = m + k by grind]âŸ©
    refine le_trans hN.1 ?_
    exact csInf_le âŸ¨0, Set.forall_mem_image.2 fun x hx â†¦ hÏ†_nonneg _âŸ© âŸ¨_, h_convex, rflâŸ©
  norm_num at *
  linarith [hg.2 n, hg.2 m, inv_antiâ‚€
    (by positivity) (by norm_cast; grind : (n : â„) + 1 â‰¥ N + 1), inv_antiâ‚€
      (by positivity) (by norm_cast; grind : (m : â„) + 1 â‰¥ N + 1)]

lemma komlos_norm [NormedAddCommGroup E] [InnerProductSpace â„ E] [CompleteSpace E]
    {f : â„• â†’ E} (h_bdd : âˆƒ M : â„, âˆ€ n, â€–f nâ€– â‰¤ M) :
    âˆƒ (g : â„• â†’ E) (x : E), (âˆ€ n, g n âˆˆ convexHull â„â‰¥0 (Set.range fun m â†¦ f (n + m))) âˆ§
      Tendsto g atTop (ğ“ x) := by
  let Ï† : E â†’ â„ := fun f â†¦ â€–fâ€– ^ 2
  have Ï†_nonneg : 0 â‰¤ Ï† := fun f â†¦ sq_nonneg â€–fâ€–
  have Ï†_bdd : âˆƒ M : â„, âˆ€ n, Ï† (f n) â‰¤ M := by
    rcases h_bdd with âŸ¨M, hMâŸ©
    exact âŸ¨M ^ 2, fun n â†¦ pow_le_pow_leftâ‚€ (norm_nonneg _) (hM n) 2âŸ©
  rcases komlos_convex Ï†_nonneg Ï†_bdd with âŸ¨g, hg, hâŸ©
  use g
  have parallelogram_identity (x y : E) :
      2â»Â¹ * â€–xâ€– ^ 2 + 2â»Â¹ * â€–yâ€– ^ 2 - â€–(2 : â„â‰¥0)â»Â¹ â€¢ (x + y)â€– ^ 2 = â€–y - xâ€– ^ 2 / 4 := by
    have : (2 : â„â‰¥0)â»Â¹ â€¢ (x + y) = (2 : â„)â»Â¹ â€¢ (x + y) := by rfl
    rw [this, norm_smul_of_nonneg (by norm_num), mul_pow, add_comm x y]
    let para := parallelogram_law_with_norm â„ y x
    linear_combination - para / 4
  have g_cauchy : CauchySeq g := by
    rw [Metric.cauchySeq_iff]
    intro Î´ Î´pos
    rcases h (Î´ ^ 2 / 4) (by positivity) with âŸ¨N, hnâŸ©
    use N
    intro m mgeN n ngeN
    specialize hn n m ngeN mgeN
    dsimp [Ï†] at hn
    rw [parallelogram_identity (g n) (g m)] at hn
    have : â€–g m - g nâ€– ^ 2 < Î´ ^ 2 := by linarith
    rw [dist_eq_norm]
    exact (pow_lt_pow_iff_leftâ‚€ (norm_nonneg (g m - g n)) (by positivity) (by norm_num)).mp this
  rcases CompleteSpace.complete g_cauchy with âŸ¨x, hxâŸ©
  exact âŸ¨x, hg, hxâŸ©

/-
## Part 0: Strict Convexity under Linear Equivalence

We need a quick transport lemma: strict convexity survives precomposition by a linear
equivalence. This lets us pass from `exp` to `exp âˆ˜ (-)` in Part 1.
-/

namespace StrictConvexOn

variable {ğ•œ : Type*} {E : Type*} {F : Type*} {Î² : Type*} [Semiring ğ•œ]
  [PartialOrder ğ•œ] [AddCommMonoid E] [AddCommMonoid F] [AddCommMonoid Î²]
  [PartialOrder Î²] [Module ğ•œ E] [Module ğ•œ F]
  [SMul ğ•œ Î²]

lemma LinearEquiv {f : F â†’ Î²} {s : Set F} (hf : StrictConvexOn ğ•œ s f) (g : E â‰ƒâ‚—[ğ•œ] F) :
    StrictConvexOn  ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
  âŸ¨hf.1.linear_preimage _, fun x hx y hy hxy a b ha hb hab =>
    calc
      f (g (a â€¢ x + b â€¢ y)) = f (a â€¢ g x + b â€¢ g y) := by rw [g.map_add, g.map_smul, g.map_smul]
      _ < a â€¢ f (g x) + b â€¢ f (g y) := hf.2 hx hy (g.injective.ne hxy) ha hb habâŸ©

end StrictConvexOn

/-
## Part 1: The Reciprocal of the Real Exponential Function

We isolate analytic properties of `x â†¦ exp (-x)` (a.k.a. `expâ»Â¹`) that will be
needed later: strict convexity gives a midpoint gap inequality, and strict antitonicity
gives injectivity. These are the real-analytical inputs for the defect bounds.
-/

namespace Real

lemma exp_inv_le_one_of_nonneg {x : â„} (hx : 0 â‰¤ x) : x.expâ»Â¹ â‰¤ 1 :=
  inv_le_one_of_one_leâ‚€ (one_le_exp hx)

lemma strictConvexOn_exp_neg : StrictConvexOn â„ (Set.univ : Set â„) (fun x â†¦ Real.exp (-x)) :=
  strictConvexOn_exp.LinearEquiv (LinearEquiv.neg â„)

lemma strictConvexOn_exp_inv : StrictConvexOn â„ Set.univ rexpâ»Â¹ := by
  simp_rw [Pi.inv_def, â† Real.exp_neg]
  exact strictConvexOn_exp_neg

lemma exp_inv_midpoint_lt_avg {x y : â„} (hxy : x â‰  y) :
    (2â»Â¹ * (x + y)).expâ»Â¹ < 2â»Â¹ * (x.expâ»Â¹ + y.expâ»Â¹) := by
  have := strictConvexOn_exp_inv.2 trivial trivial hxy one_half_pos one_half_pos (add_halves 1)
  simpa only [mul_add, one_div, smul_eq_mul] using this

lemma strictAnti_exp_inv : StrictAnti rexpâ»Â¹ := by
  intro x y hxy
  simp_rw [Pi.inv_apply]
  gcongr

lemma exp_inv_injective : Function.Injective rexpâ»Â¹ := strictAnti_exp_inv.injective

end Real

/-
## Part 2: Extension to Extended Non-Negative Reals

Our random variables are `â„â‰¥0âˆ`-valued, so we extend `expâ»Â¹` and `-log` to this setting.
We package `expInv : â„â‰¥0âˆ â†’ â„` so that it lands in `[0,1]`, prove the same midpoint
inequality and strict antitone/injective/continuous properties, and build a closed
embedding `expInvIcc`. This will later let us move between convergence of `expInv âˆ˜ f`
and convergence of `f` itself.
-/

namespace ENNReal

section ExponentialTransform

/-- ExponentialTransform -/
noncomputable
def expInv : â„â‰¥0âˆ â†’ â„
  | âˆ => 0
  | (x : â„â‰¥0) => (Real.exp x)â»Â¹

@[simp] lemma expInv_top : expInv âŠ¤ = 0 := rfl

@[simp] lemma expInv_of_nnreal {x : â„â‰¥0} : expInv x = (Real.exp x)â»Â¹ := rfl

@[simp] lemma expInv_of_ne_top {x : â„â‰¥0âˆ} (hy : x â‰  âŠ¤) : expInv x = (Real.exp x.toReal)â»Â¹ := by
  lift x to NNReal using hy
  rw [coe_toReal]; rfl

lemma exp_coe_of_nonneg (x : Real) : (EReal.exp x).toReal = Real.exp x := by
  rw [EReal.exp_coe, ENNReal.toReal_ofReal_eq_iff]
  exact Real.exp_nonneg x

lemma expInv_eq_toReal_inv_exp {x : â„â‰¥0âˆ} : x.expInv = (EReal.exp x)â»Â¹.toReal:= by
  induction x with
  | top => rw [EReal.coe_ennreal_top, EReal.exp_top,inv_top,toReal_zero ]; rfl
  | coe x => rw [expInv_of_nnreal, toReal_inv, inv_inj, â† exp_coe_of_nonneg]; rfl

lemma expInv_def : expInv = fun x : ENNReal â†¦ (EReal.exp x)â»Â¹.toReal := by
  ext x
  rw [â† expInv_eq_toReal_inv_exp]

lemma expInv_nonneg (x : â„â‰¥0âˆ) : 0 â‰¤ x.expInv :=
  expInv_eq_toReal_inv_exp â–¸ toReal_nonneg

lemma expInv_le_one (x : â„â‰¥0âˆ) : x.expInv â‰¤ 1 := by
  induction x with
  | top => exact zero_le_one
  | coe x => exact Real.exp_inv_le_one_of_nonneg x.2

lemma expInv_abs_eq_self {x : â„â‰¥0âˆ} : |x.expInv| = x.expInv := abs_of_nonneg (expInv_nonneg _)

lemma expInv_mem_Icc (x : â„â‰¥0âˆ) : x.expInv âˆˆ Icc 0 1 := âŸ¨expInv_nonneg x, expInv_le_one xâŸ©

lemma strictAnti_exp_inv : StrictAnti expInv := by
  intro x y hxy
  lift x to NNReal using hxy.ne_top
  induction y with
  | top => exact inv_pos.2 (Real.exp_pos _)
  | coe y => exact Real.strictAnti_exp_inv (mod_cast hxy)

lemma expInv_injective : Function.Injective expInv :=  strictAnti_exp_inv.injective

lemma continuous_expInv : Continuous expInv := by
  rw [expInv_def, continuous_iff_continuousAt]
  intro x
  apply (continuousAt_toReal (by simp)).comp'
  exact (continuous_exp.comp' continuous_coe_ennreal_ereal).inv.continuousAt

lemma expInv_midpoint_lt_avg {x y : â„â‰¥0âˆ} (h : x â‰  y) :
    expInv (2â»Â¹ * (x + y)) < 2â»Â¹ * (x.expInv + y.expInv) := by
  rw [â† coe_inv_two]
  wlog hxy : x < y
  Â· rw [add_comm x.expInv, add_comm x]
    exact this h.symm <| lt_of_le_of_ne (not_lt.mp hxy) h.symm
  lift x to NNReal using hxy.ne_top
  induction y with
  | top => simpa using Real.exp_pos _
  | coe y =>
    exact Real.exp_inv_midpoint_lt_avg <| Subtype.coe_ne_coe.2 fun a â†¦ h (congrArg ofNNReal a)

lemma expInv_midpoint_le_avg {x y : â„â‰¥0âˆ} :
     expInv (2â»Â¹ * (x + y)) â‰¤  2â»Â¹ * (x.expInv + y.expInv) := by
  cases em (x = y) with
  | inl h => simp [h, â† two_mul, â† mul_assoc, ENNReal.inv_mul_cancel, one_mul]
  | inr h => exact (expInv_midpoint_lt_avg h).le

end ExponentialTransform

section LogarithmicTransform

/-- The inverse of expInv -/
noncomputable
def logNeg (x : â„) : â„â‰¥0âˆ := if x = 0 then âŠ¤ else ENNReal.ofReal (- Real.log x)

lemma expInv_logNeg_of_mem {y : â„} (hy : y âˆˆ Icc 0 1) : expInv (logNeg y) = y := by
  unfold logNeg
  split_ifs with h
  Â· subst h; rfl
  Â· rw [expInv_of_ne_top ofReal_ne_top, toReal_ofReal', max_eq_left]
    Â· rw [Real.exp_neg, InvolutiveInv.inv_inv, Real.exp_log <| hy.1.lt_of_ne' h]
    Â· rw [Right.nonneg_neg_iff, Real.log_nonpos_iff hy.1]
      exact hy.2

lemma measurable_logNeg : Measurable logNeg :=
  Measurable.ite measurableSet_eq measurable_const <| by measurability

end LogarithmicTransform

section Embedding

/-- Tranformation into the unit-interval -/
noncomputable
def expInvIcc (x : â„â‰¥0âˆ) : Icc (0 : â„) 1 := âŸ¨expInv x, expInv_mem_Icc xâŸ©

lemma isClosedEmbedding_expInvIcc : IsClosedEmbedding expInvIcc := by
  have : Function.Injective expInvIcc := by
    intro x y hxy
    exact expInv_injective (congrArg Subtype.val hxy)
  exact (continuous_expInv.subtype_mk expInv_mem_Icc).isClosedEmbedding this

end Embedding

/-!
## Part 3: Convexity and Quantitative Defects

We quantify the strict convexity of `expInv` via a `defect` function measuring the midpoint gap.
The key estimate says: if `expInv x` and `expInv y` are separated, then the defect is uniformly
positive. This is the bridge from pointwise separation to an integral estimate in Part 4.
-/

/-- Gap function -/
noncomputable
def defect (x y : â„â‰¥0âˆ) : â„ :=  2â»Â¹ *  (expInv x + expInv y) - expInv (2â»Â¹ * (x + y))

lemma defect_nonneg (x y : â„â‰¥0âˆ) : 0 â‰¤ defect x y :=
  sub_nonneg.mpr (expInv_midpoint_le_avg (x := x) (y := y))

lemma defect_pos_of_ne {x y : â„â‰¥0âˆ} (hxy : x â‰  y) : 0 < defect x y :=
  sub_pos.mpr (expInv_midpoint_lt_avg hxy)

lemma continuous_defect_prod : Continuous (fun (p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ) â†¦ defect p.1 p.2) := by
  have h_avg : Continuous fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => expInv p.1 + expInv p.2 :=
    (continuous_expInv.comp continuous_fst).add (continuous_expInv.comp continuous_snd)
  have h_mul : Continuous fun z : â„â‰¥0âˆ => 2â»Â¹ * z :=
    ENNReal.continuous_const_mul (inv_ne_top.mpr (NeZero.ne 2))
  have h_mid : Continuous fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ =>  2â»Â¹ * (p.1 + p.2) :=
    h_mul.comp (continuous_fst.add continuous_snd)
  exact (continuous_const.mul h_avg).sub <| continuous_expInv.comp h_mid

lemma quantitative_convexity (Îµ : â„) (hÎµ : 0 < Îµ) :
    âˆƒ Î´ > 0, âˆ€ x y : â„â‰¥0âˆ, Îµ â‰¤ |expInv x - expInv y| â†’ Î´ â‰¤ defect x y := by
  let K := {p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ | Îµ â‰¤ |expInv p.1 - expInv p.2|}
  have hK_closed : IsClosed K := by
    have h_phi_diff : Continuous fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => |expInv p.1 - expInv p.2| :=
      ((continuous_expInv.comp continuous_fst).sub (continuous_expInv.comp continuous_snd)).abs
    exact isClosed_le continuous_const h_phi_diff
  by_cases h_nonempty : K.Nonempty
  Â· rcases h_nonempty with âŸ¨pâ‚€, hpâ‚€âŸ©
    rcases hK_closed.isCompact.exists_isMinOn âŸ¨pâ‚€, hpâ‚€âŸ© continuous_defect_prod.continuousOn with
      âŸ¨âŸ¨xâ‚€, yâ‚€âŸ©, hxy_mem, h_minâŸ©
    refine âŸ¨defect xâ‚€ yâ‚€, ?_, ?_âŸ©
    Â· have h_dist_pos : expInv xâ‚€ â‰  expInv yâ‚€ := by
        intro h_eq
        have h_le_zero : Îµ â‰¤ 0 := by simpa [K, h_eq] using hxy_mem
        exact lt_irrefl _ (lt_of_le_of_lt h_le_zero hÎµ)
      exact defect_pos_of_ne fun h_eq => h_dist_pos (by rw [h_eq])
    Â· intro x y hxy
      simpa using (isMinOn_iff.1 h_min) âŸ¨x, yâŸ© hxy
  Â· exact âŸ¨1, zero_lt_one, fun x y hxy â†¦ (h_nonempty âŸ¨âŸ¨x, yâŸ©, hxyâŸ©).elimâŸ©

/-- Inverse mapping properties.
Later we will know `expInv âˆ˜ g_n â†’ expInv g` and need to recover convergence of `g_n`.
This lemma uses the closed embedding from Part 2 to transfer convergence back to `â„â‰¥0âˆ`. -/
lemma tendsto_of_expInv_tendsto {Î± : Type*} {l : Filter Î±} {f : Î± â†’ â„â‰¥0âˆ} {y : â„â‰¥0âˆ}
    (h : Tendsto (expInv âˆ˜ f) l (ğ“ (expInv y))) : Tendsto f l (ğ“ y) := by
  have hcoe : IsClosedEmbedding fun z : Icc (0 : â„) 1 => (z : â„) :=
    (isClosed_Icc : IsClosed (Icc (0 : â„) 1)).isClosedEmbedding_subtypeVal
  have hÏ† : Tendsto (expInvIcc âˆ˜ f) l (ğ“ (expInvIcc y)) := by
    refine (hcoe.tendsto_nhds_iff).2 ?_
    simpa [Function.comp, expInvIcc] using h
  exact (isClosedEmbedding_expInvIcc.tendsto_nhds_iff).2 hÏ†

/-!
## Part 4: Measure-Theoretic Stuff

We lift the pointwise theory to random variables: measurability/integrability of `expInv`
and `defect`, and a definition of the expected defect `Defect_val`.
The  probabilistic estimate bounds the probability of large separation by this expected
defect; this used in the L1 Cauchy argument in Part 5.
-/

section MeasureTheory

variable (P : Measure Î©) [IsProbabilityMeasure P]

lemma measurable_expInv : Measurable expInv := continuous_expInv.measurable

lemma integrable_expInv_comp {f : Î© â†’ â„â‰¥0âˆ} (hf : Measurable f) : Integrable (expInv âˆ˜ f) P := by
  refine âŸ¨((measurable_expInv.comp hf).aestronglyMeasurable), ?_âŸ©
  apply MeasureTheory.HasFiniteIntegral.of_bounded
  Â· filter_upwards with i
    simpa [Function.comp_apply, Real.norm_eq_abs, expInv_abs_eq_self] using expInv_le_one _

lemma integrable_defect {f g : Î© â†’ â„â‰¥0âˆ}
   (hf : Measurable f) (hg : Measurable g) : Integrable (fun Ï‰ â†¦ defect (f Ï‰) (g Ï‰)) P := by
  have h_D_meas : Measurable (fun Ï‰ â†¦ defect (f Ï‰) (g Ï‰)) := by
    refine Measurable.sub ?_ <| measurable_expInv.comp <| (hf.add hg).const_mul _
    exact measurable_const.mul ((measurable_expInv.comp hf).add (measurable_expInv.comp hg))
  use Measurable.aestronglyMeasurable h_D_meas
  apply HasFiniteIntegral.of_bounded
  change âˆ€áµ (a : Î©) âˆ‚P, â€–defect (f a) (g a)â€– â‰¤ 1
  filter_upwards with Ï‰
  rw [defect, Real.norm_eq_abs, abs_le]
  refine âŸ¨?_, ?_âŸ©
  Â· linarith [expInv_nonneg (f Ï‰), expInv_nonneg (g Ï‰), expInv_le_one (2â»Â¹ * (f Ï‰ + g Ï‰))]
  Â· linarith [expInv_le_one (f Ï‰), expInv_le_one (g Ï‰), expInv_nonneg (2â»Â¹ * (f Ï‰ + g Ï‰))]

/-- Gap value -/
noncomputable
def Defect_val (f g : Î© â†’ â„â‰¥0âˆ) := (âˆ« Ï‰, (defect (f Ï‰) (g Ï‰)) âˆ‚P)

lemma Defect_val_eq {f g : Î© â†’ â„â‰¥0âˆ} (hf : Measurable f) (hg : Measurable g) :
   Defect_val P f g =  2â»Â¹ * (âˆ« (Ï‰ : Î©), (f Ï‰).expInv âˆ‚P + âˆ« (Ï‰ : Î©), (g Ï‰).expInv âˆ‚P) -
    âˆ« (Ï‰ : Î©), (2â»Â¹ * (f Ï‰ + g Ï‰)).expInv âˆ‚P := by
  have hfx : Integrable (fun Ï‰ â†¦ (f Ï‰).expInv) P := integrable_expInv_comp P hf
  have hgx : Integrable (fun a â†¦ (g a).expInv) P := integrable_expInv_comp P hg
  have hmid : Integrable (fun Ï‰ â†¦ expInv (2â»Â¹ * (f Ï‰ + g Ï‰))) P :=
    integrable_expInv_comp  _ <| (Measurable.add hf hg).const_mul 2â»Â¹
  have hsum : Integrable (fun Ï‰ â†¦ expInv (f Ï‰) + expInv (g Ï‰)) P := hfx.add hgx
  have hmul : Integrable (fun Ï‰ â†¦ 2â»Â¹ * (expInv (f Ï‰) + expInv (g Ï‰))) P := hsum.const_mul _
  dsimp [Defect_val, defect]
  rwa [integral_sub hmul hmid, integral_const_mul, integral_add hfx]

omit [IsProbabilityMeasure P] in
lemma defect_val_nonneg (f g : Î© â†’ â„â‰¥0âˆ) : 0 â‰¤ Defect_val P f g  := by
  apply integral_nonneg
  intro Ï‰
  exact defect_nonneg (f Ï‰) (g Ï‰)

lemma prob_large_diff_le_defect (Îµ : â„) (hÎµ : 0 < Îµ) :
    âˆƒ Î´ > 0, âˆ€ (f g : Î© â†’ â„â‰¥0âˆ), Measurable f â†’ Measurable g â†’
      P {Ï‰ | Îµ â‰¤ dist (f Ï‰).expInv (g Ï‰).expInv} â‰¤ ENNReal.ofReal (Î´â»Â¹ * (Defect_val P f g)) := by
  obtain âŸ¨Î´, hÎ´_pos, hÎ´âŸ© := quantitative_convexity Îµ hÎµ
  use Î´, hÎ´_pos
  intro f g hf hg
  let S := {Ï‰ | Îµ â‰¤ dist (f Ï‰).expInv (g Ï‰).expInv}
  let D := fun Ï‰ â†¦ defect (f Ï‰) (g Ï‰)
  have hS_meas : MeasurableSet S := by
    apply measurableSet_le measurable_const
    exact (measurable_expInv.comp hf).dist (measurable_expInv.comp hg)
  have h_ineq_ptwise : âˆ€ Ï‰, Î´ * (S.indicator (fun _ â†¦ 1) Ï‰) â‰¤ D Ï‰ := by
    intro Ï‰
    by_cases hÏ‰ : Ï‰ âˆˆ S
    Â· rw [indicator_of_mem hÏ‰, mul_one]
      exact hÎ´ (f Ï‰) (g Ï‰) hÏ‰
    Â· rw [indicator_of_notMem hÏ‰, mul_zero]
      exact defect_nonneg (f Ï‰) (g Ï‰)
  have h_integrable_indicator : Integrable (S.indicator (fun _ â†¦ (1 : Real))) P :=
    (integrable_const 1).indicator hS_meas
  have h_integral_ineq : âˆ« Ï‰, Î´ * (S.indicator (fun _ â†¦ 1) Ï‰) âˆ‚P â‰¤ âˆ« Ï‰, D Ï‰ âˆ‚P :=
    integral_mono (h_integrable_indicator.const_mul Î´) (integrable_defect _ hf hg) h_ineq_ptwise
  simp only [integral_const_mul, integral_indicator hS_meas, integral_const, MeasurableSet.univ,
    measureReal_restrict_apply, univ_inter, smul_eq_mul, mul_one] at h_integral_ineq
  rw [â† Defect_val] at h_integral_ineq
  rw [le_ofReal_iff_toReal_le]
  Â· rwa [â† div_eq_inv_mul, le_div_iffâ‚€' hÎ´_pos]
  Â· exact measure_ne_top P _
  Â· exact mul_nonneg (inv_nonneg.mpr (le_of_lt hÎ´_pos)) <| defect_val_nonneg P _ _

lemma convexHull_real_subset_convexHull_ennreal (f : â„• â†’ Î© â†’ â„â‰¥0âˆ) (n : â„•) :
    convexHull â„â‰¥0 (Set.range (fun m => (f (n + m)))) âŠ†
      convexHull â„â‰¥0âˆ (Set.range fun m => f (n + m)) := by
  intro i hg_mem
  rw [mem_convexHull_iff]
  intro S hS hS_conv
  exact (mem_convexHull_iff (ğ•œ := â„â‰¥0)).1 hg_mem _ hS (Convex.lift â„â‰¥0 hS_conv)

end MeasureTheory

open ContinuousLinearMap PiLp WithLp


end ENNReal



open ENNReal
/-
## Part 5: KomlÃ³s Lemma for ENNReal

We combine the convex KomlÃ³s lemma with the defect bounds: first show `expInv âˆ˜ g_n`
is Cauchy in L1, then extract a subsequence with a.e. convergence, and finally invert
`expInv` using `logNeg` to obtain a.e. convergence of `g_n` itself.
-/

lemma komlos_ennreal (X : â„• â†’ Î© â†’ â„â‰¥0âˆ) (hX : âˆ€ n, Measurable (X n))
    {P : Measure Î©} [IsProbabilityMeasure P] :
    âˆƒ (Y : â„• â†’ Î© â†’ â„â‰¥0âˆ) (Y_lim : Î© â†’ â„â‰¥0âˆ),
      (âˆ€ n, Y n âˆˆ convexHull â„â‰¥0âˆ (Set.range fun m â†¦ X (n + m))) âˆ§ Measurable Y_lim âˆ§
      âˆ€áµ Ï‰ âˆ‚P, Tendsto (Y Â· Ï‰) atTop (ğ“ (Y_lim Ï‰)) := by
  /-
  ### Step 1: Set up the `expInv` transform and basic bounds
  We move to the bounded transform `Z = expInv âˆ˜ X` so that integrability and L1 estimates
  are straightforward, and we record uniform bounds that will be used repeatedly.
  -/
  letI : MeasureSpace Î© := { toMeasurableSpace := mÎ©, volume := P }
  let Z : (Î© â†’ â„â‰¥0âˆ) â†’ Î© â†’ â„ := fun X Ï‰ => (X Ï‰).expInv
  let Ï† : (Î© â†’ â„â‰¥0âˆ) â†’ â„ := fun X => âˆ« Ï‰, Z X Ï‰ âˆ‚P
  have hZ_le_one (X : Î© â†’ â„â‰¥0âˆ) (Ï‰ : Î©) : (Z X) Ï‰ â‰¤ 1 := expInv_le_one (X Ï‰)
  have hZ_norm_le_one (X : Î© â†’ â„â‰¥0âˆ) : âˆ€áµ Ï‰ : Î©, â€–(X Ï‰).expInvâ€– â‰¤ 1 := by
    simp_rw [Real.norm_eq_abs, expInv_abs_eq_self]
    filter_upwards with Ï‰ using expInv_le_one _
  have hZ_integrable {W : Î© â†’ â„â‰¥0âˆ} (hX : Measurable W) : Integrable (Z W) :=
    âŸ¨(measurable_expInv.comp hX).aestronglyMeasurable, HasFiniteIntegral.of_bounded
      (hZ_norm_le_one _)âŸ©
  have hÏ†_le_one (n : â„•) : Ï† (X n) â‰¤ 1 := by
    have hf_int : Integrable (Z (X n)) P := integrable_expInv_comp _ (hX n)
    have h_int := integral_mono (hf_int) (integrable_const _) (hZ_le_one _)
    have hconst : âˆ« Ï‰, (1 : â„) âˆ‚P = 1 := by
      rw [integral_const 1, smul_eq_mul, mul_one, MeasureTheory.probReal_univ]
    simpa [Ï†,  Function.comp, hconst] using h_int
  have hÏ†_nonneg : 0 â‰¤ Ï† := by
    intro X
    apply integral_nonneg
    intro Ï‰
    apply expInv_nonneg _
  have hÏ†_nonneg_X (X : Î© â†’ â„â‰¥0âˆ) : 0 â‰¤ Ï† X := hÏ†_nonneg X
  have hÏ†_bdd : âˆƒ M : â„, âˆ€ n, Ï† (X n) â‰¤ M := âŸ¨1, hÏ†_le_oneâŸ©
  /-
  ### Step 2: Apply abstract KomlÃ³s with the defect functional
  We apply the convex KomlÃ³s lemma to the functional `Ï†` to obtain convex combinations `g n`
  with a Cauchy-type control in terms of the defect.
  -/
  have âŸ¨g, hg_convex, hg_cauchy_defectâŸ© : âˆƒ g : â„• â†’ Î© â†’ â„â‰¥0âˆ,
    (âˆ€ n, g n âˆˆ convexHull â„â‰¥0âˆ (Set.range fun m â†¦ X (n + m))) âˆ§
    âˆ€ Î´ > 0, âˆƒ N, âˆ€ n m, N â‰¤ n â†’ N â‰¤ m â†’
       2â»Â¹ *(Ï† (g n) + Ï† (g m)) - Ï† (fun Ï‰ â†¦  2â»Â¹ * (g n Ï‰ + g m Ï‰)) < Î´ := by
    obtain âŸ¨g, hg_conv_rge0, hg_cauchyâŸ© := komlos_convex (fun f â†¦ hÏ†_nonneg f) hÏ†_bdd
    refine âŸ¨g, fun n â†¦ convexHull_real_subset_convexHull_ennreal X n (hg_conv_rge0 n), ?_âŸ©
    intro Î´ Î´pos
    obtain âŸ¨N, hNâŸ© := hg_cauchy Î´ Î´pos
    refine âŸ¨N, fun n m hn hm â†¦ ?_âŸ©
    simp_rw [mul_add]
    convert hN n m hn hm with z
    simp only [Pi.smul_apply, Pi.add_apply, smul_add]
    congr
    Â· simp
    Â· simp
  have h_measurable_convex : Convex â„â‰¥0âˆ {h : Î© â†’ â„â‰¥0âˆ | Measurable h} := by
    intro f hf g hg a b ha hb hab
    exact (measurable_const.mul hf).add (measurable_const.mul hg)
  have hg_meas (n : â„•) : Measurable (g n) := by
    have h_range :  Set.range (fun m => X (n + m)) âŠ† {h : Î© â†’ â„â‰¥0âˆ | Measurable h} :=
      range_subset_iff.mpr fun y â†¦ hX (n + y)
    have hsubset : convexHull â„â‰¥0âˆ (Set.range fun m => X (n + m)) âŠ† {h : Î© â†’ â„â‰¥0âˆ | Measurable h} :=
      convexHull_min h_range h_measurable_convex
    exact hsubset (hg_convex n)
  /-
  ### Step 3: Convert defect control into an L1 Cauchy estimate
  Using `prob_large_diff_le_defect`, we show `Z âˆ˜ g n` is Cauchy in L1. This is the
  analytic heart of the argument: boundedness plus defect control yields L1 Cauchy.
  -/
  have h_exp_cauchy : âˆ€ Î·>0, âˆƒ N, âˆ€ n m, N â‰¤ n â†’ N â‰¤ m â†’ âˆ« Ï‰, |Z (g n) Ï‰ - Z (g m) Ï‰| âˆ‚P < Î· := by
    intro Îµ hÎµ
    let Î´' := 2â»Â¹ * Îµ
    have hÎ´'_pos : 0 < Î´' := by positivity
    obtain âŸ¨Î´_defect, hÎ´_defect_pos, h_measure_boundâŸ© := prob_large_diff_le_defect P Î´' hÎ´'_pos
    let target_defect := (Îµ / 4) * Î´_defect
    have h_target_defect_pos : 0 < target_defect := by
      rw [mul_pos_iff_of_pos_right hÎ´_defect_pos]
      positivity
    obtain âŸ¨N, hN_defectâŸ© := hg_cauchy_defect target_defect h_target_defect_pos
    use N
    intro m n hm hn
    set Y := fun n â†¦ expInv âˆ˜ (g n)
    let S := {Ï‰ | Î´' â‰¤ |Y m Ï‰ - Y n Ï‰|}
    have hS_meas : MeasurableSet S := by
      apply measurableSet_le measurable_const
      exact (measurable_expInv.comp (hg_meas m)).dist (measurable_expInv.comp (hg_meas n))
    have h_abs_integrable : Integrable (fun Ï‰ â†¦ |Y m Ï‰ - Y n Ï‰|) P :=
      ((hZ_integrable  (hg_meas m)).sub (hZ_integrable  (hg_meas n))).abs
    have h_eval_constants :
        (âˆ« Ï‰ in S, 2 âˆ‚P) + (âˆ« Ï‰ in Sá¶œ, Î´' âˆ‚P) = 2 * (P S).toReal + Î´' * (P Sá¶œ).toReal := by
      simp [smul_eq_mul, smul_eq_mul, mul_comm]
      rfl
    have h_prob_compl_le_one : 2 * (P S).toReal + Î´' * (P Sá¶œ).toReal â‰¤ 2 * (P S).toReal + Î´' := by
      have : (P Sá¶œ).toReal â‰¤ 1 := (measureReal_def P Sá¶œ).symm â–¸ measureReal_le_one
      simpa only [add_le_add_iff_left, ge_iff_le] using ((mul_le_iff_le_one_right hÎ´'_pos).2 this)
    have h_measure_by_defect : 2 * (P S).toReal + Î´'
        â‰¤ 2 * (Î´_defectâ»Â¹ * (Defect_val P (g m) (g n))) + Î´' := by
      have h_measure : (P S).toReal â‰¤ Î´_defectâ»Â¹ * (Defect_val P (g m) (g n)) := by
        rw [â† ENNReal.le_ofReal_iff_toReal_le (measure_ne_top P S)]
        Â· exact h_measure_bound (g m) (g n) (hg_meas m) (hg_meas n)
        Â· exact mul_nonneg (Right.inv_nonneg.mpr hÎ´_defect_pos.le) (defect_val_nonneg P (g m) (g n))
      simpa using mul_le_mul_of_nonneg_left h_measure zero_le_two
    have h_integral_bound : âˆ« Ï‰ in S, |Y m Ï‰ - Y n Ï‰| âˆ‚P + âˆ« Ï‰ in Sá¶œ, |Y m Ï‰ - Y n Ï‰| âˆ‚P â‰¤
        âˆ« Ï‰ in S, 2 âˆ‚P + âˆ« Ï‰ in Sá¶œ, Î´' âˆ‚P := by
      have hY_le_one (k) (Ï‰) : |Y k Ï‰| â‰¤ 1 := by
        simpa only [Y, Function.comp, expInv_abs_eq_self] using (expInv_le_one _)
      have h_abs_le (Ï‰ : Î©) : |Y m Ï‰ - Y n Ï‰| â‰¤ 2 := by
        rw [â† one_add_one_eq_two]
        exact (abs_sub _ _ ).trans <| add_le_add (hY_le_one m Ï‰) (hY_le_one n Ï‰)
      have h_abs_le_delta {Ï‰} (hÏ‰ : Ï‰ âˆˆ Sá¶œ) : |Y m Ï‰ - Y n Ï‰| â‰¤ Î´' := by
        have : Â¬ Î´' â‰¤ |Y m Ï‰ - Y n Ï‰| := hÏ‰
        exact (lt_of_not_ge this).le
      have hone : (âˆ« Ï‰ in S, |Y m Ï‰ - Y n Ï‰| âˆ‚P) â‰¤ (âˆ« Ï‰ in S, 2 âˆ‚P)  :=
        setIntegral_mono h_abs_integrable.integrableOn (integrable_const 2).integrableOn h_abs_le
      have htwo : (âˆ« Ï‰ in Sá¶œ, |Y m Ï‰ - Y n Ï‰| âˆ‚P) â‰¤ (âˆ« Ï‰ in Sá¶œ, Î´' âˆ‚P)  := by
        apply setIntegral_mono_on h_abs_integrable.integrableOn (integrable_const Î´').integrableOn
        Â· rwa [@MeasurableSet.compl_iff]
        apply h_abs_le_delta
      exact add_le_add hone htwo
    have h_defect_lt_target_two :
        2 * (Î´_defectâ»Â¹ * ((Defect_val P (g m) (g n)))) + Î´' <
          2 * (Î´_defectâ»Â¹ * target_defect) + Î´' := by
      simpa [Defect_val_eq P (hg_meas m) (hg_meas n)] using
        mul_lt_mul_of_pos_left (hN_defect m n hm hn) <| Right.inv_pos.mpr hÎ´_defect_pos
    have h_dist_lt_Îµ : âˆ« Ï‰, |Y m Ï‰ - Y n Ï‰| âˆ‚P < Îµ := by
      calc
      âˆ« Ï‰, |Y m Ï‰ - Y n Ï‰| âˆ‚P =
          (âˆ« Ï‰ in S, |Y m Ï‰ - Y n Ï‰| âˆ‚P) + (âˆ« Ï‰ in Sá¶œ, |Y m Ï‰ - Y n Ï‰| âˆ‚P) :=
            (integral_add_compl hS_meas h_abs_integrable).symm
      _ â‰¤ (âˆ« Ï‰ in S, 2 âˆ‚P) + (âˆ« Ï‰ in Sá¶œ, Î´' âˆ‚P) := h_integral_bound
      _ = 2 * (P S).toReal + Î´' * (P Sá¶œ).toReal := h_eval_constants
      _ â‰¤ 2 * (P S).toReal + Î´' := h_prob_compl_le_one
      _ â‰¤ 2 * (Î´_defectâ»Â¹ * (Defect_val P (g m) (g n))) + Î´' := h_measure_by_defect
      _ < 2 * (Î´_defectâ»Â¹ * target_defect) + Î´' := h_defect_lt_target_two
      _ = Îµ :=  by field
    exact lt_of_le_of_lt (le_refl _) h_dist_lt_Îµ
  /-
  ### Step 4: Extract a subsequence with a.e. convergence in the `expInv` chart
  The L1 Cauchy property gives L1 convergence, hence convergence in measure. We then
  extract a subsequence converging almost surely pointwise in the `expInv` chart.
  -/
  have hZ_int (n : â„•) : Integrable (Z (g n)) P := hZ_integrable (hg_meas n)
  let C : â„• â†’ Lp â„ 1 P := fun n => (hZ_int n).toL1 (Z (g n))
  have hY_mem_L1' : CauchySeq C := by
    refine Metric.cauchySeq_iff'.2 <| fun Îµ hÎµ â†¦ ?_
    obtain âŸ¨N, hNâŸ© := h_exp_cauchy Îµ hÎµ
    refine âŸ¨N, fun n hn => ?_âŸ©
    rw [dist_eq_norm, â† Integrable.toL1_sub, L1.norm_of_fun_eq_integral_norm]
    exact hN n N hn le_rfl
  obtain âŸ¨h, h_L1âŸ©  := cauchySeq_tendsto_of_complete hY_mem_L1'
  have h_step_mon : âˆƒ ns : â„• â†’ â„•, StrictMono ns âˆ§
    âˆ€áµ Ï‰ âˆ‚P, Tendsto (fun k => Z (g (ns k)) Ï‰) atTop (ğ“ (h Ï‰)) := by
    have h_in_measure : TendstoInMeasure P (fun n => C n) atTop h :=
       tendstoInMeasure_of_tendsto_Lp h_L1
    obtain âŸ¨ns, hns_mono, hns_tendstoâŸ© := h_in_measure.exists_seq_tendsto_ae
    have hZR_eq_all : âˆ€áµ Ï‰ âˆ‚P, âˆ€ n, C n Ï‰ = Z (g n) Ï‰ := by
      rw [ae_all_iff]
      intro n
      exact Integrable.coeFn_toL1 (hZ_integrable (hg_meas n))
    refine âŸ¨ns, hns_mono, ?_âŸ©
    filter_upwards [hZR_eq_all, hns_tendsto] with Ï‰ hÏ‰_eq hÏ‰_tend
    simpa [hÏ‰_eq] using hÏ‰_tend
  obtain âŸ¨ns, hns_mono, hns_tendstoâŸ© := h_step_mon
  /-
  ### Step 5: Invert the transform and finish
  Use `logNeg` to recover an `â„â‰¥0âˆ`-valued limit and transfer a.e. convergence back
  through `expInv`, yielding the KomlÃ³s limit for the convex averages.
  -/
  have h_step_lim : âˆƒ g_lim : Î© â†’ â„â‰¥0âˆ, Measurable g_lim âˆ§
      âˆ€áµ Ï‰ âˆ‚P, Tendsto (fun k => g (ns k) Ï‰) atTop (ğ“ (g_lim Ï‰)) := by
    have h_range : âˆ€áµ Ï‰ âˆ‚P, h Ï‰ âˆˆ Icc 0 1 := by
      filter_upwards [hns_tendsto] with Ï‰ hÏ‰_tend
      exact isClosed_Icc.mem_of_tendsto hÏ‰_tend (by filter_upwards with k using expInv_mem_Icc _)
    let g_lim0 : Î© â†’ â„â‰¥0âˆ := fun Ï‰ => logNeg (h Ï‰)
    have hg_lim0_aemeasurable : AEMeasurable g_lim0 P :=
      measurable_logNeg.comp_aemeasurable ((Lp.aestronglyMeasurable h).aemeasurable)
    have h_phi_eq : âˆ€áµ Ï‰ âˆ‚P, expInv (g_lim0 Ï‰) = h Ï‰ := by
      filter_upwards [h_range] with Ï‰ hÏ‰_range using expInv_logNeg_of_mem hÏ‰_range
    have hg_lim0_tendsto : âˆ€áµ Ï‰ âˆ‚P, Tendsto (fun k => g (ns k) Ï‰) atTop (ğ“ (g_lim0 Ï‰)) := by
      filter_upwards [hns_tendsto, h_phi_eq] with Ï‰ hÏ‰_tend hÏ‰_eq
        using tendsto_of_expInv_tendsto (by rwa [hÏ‰_eq])
    have hg_lim_eq : hg_lim0_aemeasurable.mk g_lim0 =áµ[P] g_lim0 :=
      EventuallyEq.symm (AEMeasurable.ae_eq_mk hg_lim0_aemeasurable)
    refine âŸ¨hg_lim0_aemeasurable.mk g_lim0, hg_lim0_aemeasurable.measurable_mk, ?_âŸ©
    filter_upwards [hg_lim0_tendsto, hg_lim_eq] with Ï‰ hÏ‰_tend hÏ‰_eq
    rwa [hÏ‰_eq]
  obtain âŸ¨g_lim, mea_glim, tend_glimâŸ© := h_step_lim
  refine âŸ¨g âˆ˜ ns, g_lim, ?_, mea_glim, tend_glimâŸ©
  have h_ns_ge : âˆ€ n, n â‰¤ ns n := by
    refine Nat.rec (Nat.zero_le _) (fun n hn â†¦ ?_)
    exact Nat.succ_le_of_lt (lt_of_le_of_lt hn (hns_mono (n.lt_succ_self)))
  intro N
  refine convexHull_mono (fun _ â†¦ ?_) (hg_convex (ns N))
  simp only [mem_range, forall_exists_index]
  intro n nh
  use (ns N - N) + n
  rw [â† Nat.add_sub_cancel' (h_ns_ge N)] at nh
  rwa [â† add_assoc]
