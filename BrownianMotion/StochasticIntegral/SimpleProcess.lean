/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Process.Predictable
import Mathlib.Probability.Process.Stopping
import BrownianMotion.Auxiliary.StoppedProcess
import BrownianMotion.Gaussian.BrownianMotion

/-! # Simple processes and elementary stochastic integral

## Main definitions

- `SimpleProcess`: the type of simple processes, as a Module over â„
- `SimpleProcess.toFun`: simple process interpreted as a stochastic process, with a CoeFun instance
- `SimpleProcess.integral`: elementary stochastic integral
- `SimpleProcess.isPredictable`: simple processes are predictable

## Implementation notes

`SimpleProcess` consists of a value function as a `Finsupp`: `value : Î¹ Ã— Î¹ â†’â‚€ Î© â†’ F` and a
value at âŠ¥: `valueBot : Î© â†’ F`. This allows the definition of operations like addition to be
defined naturally.

However, this makes the function `SimpleProcess.toFun` non-injective, so `SimpleProcess` is not
`FunLike`.

This should not be a problem for defining the dense-inducing map of simple processes into $L^2(M)$,
because `IsDenseInducing.extend` in Mathlib does not require injectivity.

## TODO

- Generalize instance variables.
-/

open MeasureTheory Filter Finset
open scoped ENNReal Topology

noncomputable section

namespace ProbabilityTheory

variable {Î¹ Î© E F G : Type*} [LinearOrder Î¹] [OrderBot Î¹] {mÎ© : MeasurableSpace Î©} {P : Measure Î©}
  [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [MeasurableSpace F]
  [NormedAddCommGroup G] [NormedSpace â„ G]
  {ğ“• : Filtration Î¹ mÎ©}

open scoped Function

/-- A simple process. -/
@[ext]
structure SimpleProcess (Î¹ F : Type*) [LinearOrder Î¹] [OrderBot Î¹] {mÎ© : MeasurableSpace Î©}
    [NormedAddCommGroup F] [MeasurableSpace F] (ğ“• : Filtration Î¹ mÎ©) where
  /-- The value on each interval. -/
  value : Î¹ Ã— Î¹ â†’â‚€ Î© â†’ F
  /-- The value at âŠ¥. -/
  valueBot : Î© â†’ F
  /-- The value on each interval is measurable with respect to the filtration at the left endpoint.

  Do not use this lemma directly. Use `SimpleProcess.measurable_value` instead. -/
  measurable_value' : âˆ€ p âˆˆ value.support, Measurable[ğ“• p.1] (value p) := by
    -- Note: Most of the time we need to eta-expand to make `fun_prop` find the right lemma,
    -- such as `Measurability.neg` that can only recognize `fun x â†¦ -f x` rather than `-f`.
    -- On the other hand, some other lemmas like `Measurable.const_smul` can only recognize `c â€¢ f`
    -- rather than `fun x â†¦ c â€¢ f x`, so we also need to try directly applying `measurability`.
    first | measurability | eta_expand; measurability
  /-- The value at âŠ¥ is measurable with respect to the filtration at âŠ¥. -/
  measurable_valueBot : Measurable[ğ“• âŠ¥] valueBot := by
    first | measurability | eta_expand; measurability

attribute [fun_prop] SimpleProcess.measurable_valueBot

/-- The value of the simple process at the left endpoint of an interval is measurable
with respect to the filtration at the left endpoint.

Note that we do not require `p âˆˆ V.value.support`, because the value is 0 otherwise,
which is measurable. -/
@[fun_prop]
lemma measurable_value (V : SimpleProcess Î¹ F ğ“•) (p : Î¹ Ã— Î¹) : Measurable[ğ“• p.1] (V.value p) := by
  by_cases hp : p âˆˆ V.value.support
  Â· exact V.measurable_value' p hp
  Â· rw [show V.value p = 0 by simpa using hp]
    exact measurable_const

namespace SimpleProcess

section Module

-- These are needed for e.g. `ContinuousAdd.measurableMulâ‚‚` (which, by the way, has the wrong name).
-- Alternatively, for more generalized instances, we could just use `[Module â„ F]`, with
-- `[MeasurableNeg F]`, `[MeasurableAddâ‚‚ F]`, `[MeasurableSubâ‚‚ F]`, and/or
-- `[MeasurableConstSMul â„ F]` directly.
variable [NormedSpace â„ F] [BorelSpace F] [SecondCountableTopology F]

@[simps]
instance instZero : Zero (SimpleProcess Î¹ F ğ“•) where
  zero := { value := 0, valueBot := 0 }

@[simps]
instance instNeg : Neg (SimpleProcess Î¹ F ğ“•) where
  neg V := { value := -V.value, valueBot := -V.valueBot }

@[simps]
instance instAdd : Add (SimpleProcess Î¹ F ğ“•) where
  add V W := { value := V.value + W.value, valueBot := V.valueBot + W.valueBot }

@[simps]
instance instSub : Sub (SimpleProcess Î¹ F ğ“•) where
  sub V W := { value := V.value - W.value, valueBot := V.valueBot - W.valueBot }

@[simps]
instance instSMul : SMul â„ (SimpleProcess Î¹ F ğ“•) where
  smul c V := { value := c â€¢ V.value, valueBot := c â€¢ V.valueBot }

instance instAddCommGroup : AddCommGroup (SimpleProcess Î¹ F ğ“•) where
  sub_eq_add_neg U V := by ext <;> apply sub_eq_add_neg
  add_assoc U V W := by ext <;> apply add_assoc
  add_comm U V := by ext <;> apply add_comm
  zero_add V := by ext <;> apply zero_add
  add_zero V := by ext <;> apply add_zero
  neg_add_cancel V := by ext <;> apply neg_add_cancel
  nsmul := nsmulRec
  zsmul := zsmulRec

instance instModule : Module â„ (SimpleProcess Î¹ F ğ“•) where
  zero_smul V := by ext <;> apply zero_smul
  smul_zero V := by ext <;> apply smul_zero
  add_smul U V W := by ext <;> apply add_smul
  smul_add U V W := by ext <;> apply smul_add
  one_smul V := by ext <;> apply one_smul
  mul_smul U V W := by ext <;> apply mul_smul

end Module

section ToFun

/-- Coercion from a simple process to a function. Note that this is not injective. -/
@[coe]
def toFun (V : SimpleProcess Î¹ F ğ“•) (i : Î¹) (Ï‰ : Î©) : F :=
  ({âŠ¥} : Set Î¹).indicator (fun _ â†¦ V.valueBot Ï‰) i
    + V.value.sum fun p v => (Set.Ioc p.1 p.2).indicator (fun _ â†¦ v Ï‰) i

instance instCoeFun : CoeFun (SimpleProcess Î¹ F ğ“•) (fun _ â†¦ Î¹ â†’ Î© â†’ F) where
  coe := toFun

@[simp] lemma coe_apply (V : SimpleProcess Î¹ F ğ“•) (i : Î¹) (Ï‰ : Î©) :
  â‡‘V i Ï‰ = ({âŠ¥} : Set Î¹).indicator (fun _ â†¦ V.valueBot Ï‰) i
    + V.value.sum fun p v => (Set.Ioc p.1 p.2).indicator (fun _ â†¦ v Ï‰) i := rfl

@[simp] lemma coe_zero : â‡‘(0 : SimpleProcess Î¹ F ğ“•) = 0 := by ext; simp

variable [BorelSpace F]

@[simp] lemma coe_neg (V : SimpleProcess Î¹ F ğ“•) : â‡‘(-V) = -â‡‘V := by
  ext; simp [Set.indicator_neg, Finsupp.sum_neg_index]; abel

@[simp] lemma coe_add [SecondCountableTopology F] (V W : SimpleProcess Î¹ F ğ“•) :
   â‡‘(V + W) = â‡‘V + â‡‘W := by
  ext; simp [Set.indicator_add, Finsupp.sum_add_index]; abel

@[simp] lemma coe_sub [SecondCountableTopology F] (V W : SimpleProcess Î¹ F ğ“•) :
   â‡‘(V - W) = â‡‘V - â‡‘W := by
  ext; simp [Set.indicator_sub, Finsupp.sum_sub_index]; abel

@[simp] lemma coe_smul [NormedSpace â„ F] (c : â„) (V : SimpleProcess Î¹ F ğ“•) :
   â‡‘(c â€¢ V) = c â€¢ â‡‘V := by
  ext; simp [Set.indicator_smul, Finsupp.sum_smul_index', Finsupp.smul_sum]

end ToFun

section Predictable

lemma isPredictable (V : SimpleProcess Î¹ F ğ“•) : IsPredictable ğ“• V := by
  sorry

end Predictable

section Integral

variable {E F G : Type*}
variable [NormedAddCommGroup E] [NormedSpace â„ E]
variable [NormedAddCommGroup F] [NormedSpace â„ F] [MeasurableSpace F]
variable [NormedAddCommGroup G] [NormedSpace â„ G]

/-- The elementary stochastic integral. -/
def integral (B : E â†’L[â„] F â†’L[â„] G) (X : Î¹ â†’ Î© â†’ E) (V : SimpleProcess Î¹ F ğ“•) :
    Î¹ â†’ Î© â†’ G :=
  fun i Ï‰ â†¦ V.value.sum fun p v =>
    B (stoppedProcess X (fun _ â†¦ i) p.2 Ï‰ - stoppedProcess X (fun _ â†¦ i) p.1 Ï‰) (v Ï‰)

-- TODO: possible notation Vâ—X, possibly for more general integrals

@[simp] lemma integral_zero_left {B : E â†’L[â„] F â†’L[â„] G} (V : SimpleProcess Î¹ F ğ“•) :
    integral B (fun _ â†¦ 0) V = fun _ â†¦ 0 := by
  ext; simp [integral]

@[simp] lemma integral_zero_right {B : E â†’L[â„] F â†’L[â„] G} (X : Î¹ â†’ Î© â†’ E) :
    integral B X (0 : SimpleProcess Î¹ F ğ“•) = fun _ â†¦ 0 := by
  ext; simp [integral]

@[simp] lemma integral_add_left {B : E â†’L[â„] F â†’L[â„] G} (X Y : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess Î¹ F ğ“•) :
    integral B (X + Y) V = integral B X V + integral B Y V := by
  ext; simp [integral]; abel

@[simp] lemma integral_sub_left {B : E â†’L[â„] F â†’L[â„] G} (X Y : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess Î¹ F ğ“•) :
    integral B (X - Y) V = integral B X V - integral B Y V := by
  ext; simp [integral]; abel

@[simp] lemma integral_smul_left {B : E â†’L[â„] F â†’L[â„] G} (c : â„) (X : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess Î¹ F ğ“•) :
    integral B (c â€¢ X) V = c â€¢ integral B X V := by
  ext; simp [integral, Finsupp.smul_sum, smul_sub]

variable [BorelSpace F]

@[simp] lemma integral_add_right [SecondCountableTopology F] {B : E â†’L[â„] F â†’L[â„] G} (X : Î¹ â†’ Î© â†’ E)
    (V W : SimpleProcess Î¹ F ğ“•) :
    integral B X (V + W) = integral B X V + integral B X W := by
  ext; simp [integral, Finsupp.sum_add_index]; abel

@[simp] lemma integral_sub_right [SecondCountableTopology F] {B : E â†’L[â„] F â†’L[â„] G}
    (X : Î¹ â†’ Î© â†’ E) (V W : SimpleProcess Î¹ F ğ“•) :
    integral B X (V - W) = integral B X V - integral B X W := by
  ext; simp [integral, Finsupp.sum_sub_index]; abel

@[simp] lemma integral_smul_right {B : E â†’L[â„] F â†’L[â„] G} (c : â„) (X : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess Î¹ F ğ“•) :
    integral B X (c â€¢ V) = c â€¢ integral B X V := by
  ext; simp [integral, Finsupp.sum_smul_index', Finsupp.smul_sum, smul_sub]

end Integral

end SimpleProcess

end ProbabilityTheory
