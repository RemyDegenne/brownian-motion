/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Thomas Zhu
-/
import Mathlib.Data.Finsupp.Pointwise
import Mathlib.Probability.Process.Predictable
import Mathlib.Probability.Process.Stopping
import BrownianMotion.Auxiliary.StoppedProcess
import BrownianMotion.Gaussian.BrownianMotion

/-! # Simple processes and elementary stochastic integral

## Main definitions

- `ElementaryPredictableSet`: the type of elementary predictable sets
- `SimpleProcess`: the type of simple processes, as a Module over â„
- `SimpleProcess.toFun`: simple process interpreted as a stochastic process, with a CoeFun instance
- `SimpleProcess.integral`: elementary stochastic integral
- `SimpleProcess.isPredictable`: simple processes are predictable

## Implementation notes

`SimpleProcess` consists of a value function as a `Finsupp`: `value : Î¹ Ã— Î¹ â†’â‚€ Î© â†’ F` and a
value at âŠ¥: `valueBot : Î© â†’ F`. This allows the definition of operations like addition to be
defined naturally.

However, this makes the function `SimpleProcess.toFun` non-injective, so `SimpleProcess` is not
`FunLike`. In other words, two distinct elements `X Y : SimpleProcess F ğ“•` may produce the same
function `(â‡‘X : Î¹ â†’ Î© â†’ F) = (â‡‘Y : Î¹ â†’ Î© â†’ F)`.

There are subtleties that are caused by this: for example, by a nonnegative simple
process, we mean `X : SimpleProcess F ğ“•` with `0 â‰¤ X.valueBot` and `0 â‰¤ X.value`, which is not the
same as `0 â‰¤ (â‡‘X : Î¹ â†’ Î© â†’ F)`.

Similarly, `ElementaryPredictableSet` is a data type that has a coercion to `Set (Î¹ Ã— Î©)`, but
this coercion is not injective, so it is not `SetLike`. This makes it easy to define the indicator
function of an elementary predictable set as a simple process by mapping respective datas
(which is why it also requires disjoint unions).

## TODO

- Generalize instance variables.
-/

open MeasureTheory Filter Finset
open scoped ENNReal Topology

noncomputable section

namespace ProbabilityTheory

variable {Î¹ Î© F : Type*} [LinearOrder Î¹] [OrderBot Î¹] {mÎ© : MeasurableSpace Î©}
variable [SeminormedRing F] [mF : MeasurableSpace F]
-- These are needed for e.g. `ContinuousAdd.measurableMulâ‚‚` (which, by the way, has the wrong name).
variable [NormedAlgebra â„ F] [BorelSpace F] [SecondCountableTopology F]
variable {ğ“• : Filtration Î¹ mÎ©}

attribute [local measurability]
  measurableSet_predictable_singleton_bot_prod
  measurableSet_predictable_Ioi_prod
  measurableSet_predictable_Ioc_prod

/-- An **elementary predictable set** is a finite disjoint union of sets of the form `{âŠ¥} Ã— B` for
`B âˆˆ ğ“• âŠ¥` and of the form `(s, t] Ã— B` for `s < t` in `Î¹` and `B âˆˆ ğ“• s`.

Note that we require the union to be disjoint. This is not necessary, but makes it easier to define
the indicator function of an elementary predictable set as a `SimpleProcess`. -/
structure ElementaryPredictableSet (ğ“• : Filtration Î¹ mÎ©) where
  /-- The set over `âŠ¥`. -/
  setBot : Set Î©
  /-- The finite index for sets over `(s, t]`. -/
  I : Finset (Î¹ Ã— Î¹)
  /-- The sets over `(s, t]`. -/
  set : Î¹ Ã— Î¹ â†’ Set Î©
  le_of_mem_I : âˆ€ p âˆˆ I, p.1 â‰¤ p.2
  measurableSet_setBot : MeasurableSet[ğ“• âŠ¥] setBot
  measurableSet_set : âˆ€ p âˆˆ I, MeasurableSet[ğ“• p.1] (set p)
  pairwiseDisjoint : Set.PairwiseDisjoint â†‘I (fun p : Î¹ Ã— Î¹ â†¦ Set.Ioc p.1 p.2 Ã—Ë¢ set p)

namespace ElementaryPredictableSet

attribute [measurability] measurableSet_setBot measurableSet_set

/-- Coercion from an `ElementaryPredictableSet ğ“•` to a `Set (Î¹ Ã— Î©)`. -/
@[coe] def toSet (S : ElementaryPredictableSet ğ“•) : Set (Î¹ Ã— Î©) :=
    {âŠ¥} Ã—Ë¢ S.setBot âˆª â‹ƒ p âˆˆ S.I, (Set.Ioc p.1 p.2) Ã—Ë¢ S.set p

instance : CoeOut (ElementaryPredictableSet ğ“•) (Set (Î¹ Ã— Î©)) where
  coe := toSet

/-- The set `{âŠ¥} Ã— Bâ‚€` as an `ElementaryPredictableSet`. -/
def singletonBotProd {Bâ‚€ : Set Î©} (hBâ‚€ : MeasurableSet[ğ“• âŠ¥] Bâ‚€) :
    ElementaryPredictableSet ğ“• where
  setBot := Bâ‚€
  I := âˆ…
  set := default
  le_of_mem_I := by simp
  measurableSet_setBot := hBâ‚€
  measurableSet_set := by simp
  pairwiseDisjoint := by simp

@[simp] lemma coe_singletonBotProd {Bâ‚€ : Set Î©} (hBâ‚€ : MeasurableSet[ğ“• âŠ¥] Bâ‚€) :
    â†‘(singletonBotProd hBâ‚€) = {(âŠ¥ : Î¹)} Ã—Ë¢ Bâ‚€ := by
  simp [toSet, singletonBotProd]

/-- The set `(i, j] Ã— B` as an `ElementaryPredictableSet`. -/
def IocProd (i j : Î¹) {B : Set Î©} (hB : MeasurableSet[ğ“• i] B) :
    ElementaryPredictableSet ğ“• where
  setBot := âˆ…
  I := if i â‰¤ j then {(i, j)} else âˆ…
  set := fun p â†¦ B
  le_of_mem_I := by
    split_ifs
    Â· simpa
    Â· simp
  measurableSet_setBot := by simp
  measurableSet_set := by
    split_ifs
    Â· simpa
    Â· simp
  pairwiseDisjoint := by split_ifs <;> simp

@[simp] lemma coe_IocProd (i j : Î¹) {B : Set Î©} (hB : MeasurableSet[ğ“• i] B) :
    â†‘(IocProd i j hB) = (Set.Ioc i j) Ã—Ë¢ B := by
  unfold IocProd
  split_ifs with h
  Â· simp [toSet]
  Â· simp [toSet, Set.Ioc_eq_empty_of_le (not_le.1 h).le]

end ElementaryPredictableSet

/-- A **simple process** is defined as a finite sum of indicator functions of intervals `(s, t]`,
each assigned to a bounded `ğ“• s`-measurable random variable `value`, plus a `valueBot` at âŠ¥. -/
@[ext]
structure SimpleProcess (F : Type*) [SeminormedRing F] [MeasurableSpace F] [NormedAlgebra â„ F]
    [BorelSpace F] [SecondCountableTopology F] (ğ“• : Filtration Î¹ mÎ©) where
  /-- The value at âŠ¥. -/
  valueBot : Î© â†’ F
  /-- The value on each interval. Note that intervals are not necessarily disjoint. -/
  value : Î¹ Ã— Î¹ â†’â‚€ Î© â†’ F
  /-- The intervals in the support of `value` are ordered. -/
  le_of_mem_support_value : âˆ€ p âˆˆ value.support, p.1 â‰¤ p.2
  /-- The value at âŠ¥ is measurable with respect to the filtration at âŠ¥. -/
  measurable_valueBot : Measurable[ğ“• âŠ¥] valueBot := by
    first | measurability | eta_expand; measurability
  /-- The value on each interval is measurable with respect to the filtration at the left endpoint.

  Do not use this lemma directly. Use `SimpleProcess.measurable_value` instead. -/
  measurable_value' : âˆ€ p âˆˆ value.support, Measurable[ğ“• p.1] (value p) := by
    -- Note: Most of the time we need to eta-expand to make `fun_prop` find the right lemma,
    -- such as `Measurability.neg` that can only recognize `fun x â†¦ -f x` rather than `-f`.
    -- On the other hand, some other lemmas like `Measurable.const_smul` can only recognize `c â€¢ f`
    -- rather than `fun x â†¦ c â€¢ f x`, so we also need to try directly applying `measurability`.
    first | measurability | eta_expand; measurability
  /-- The value at âŠ¥ is bounded. -/
  bounded_valueBot : âˆƒ C : â„, âˆ€ Ï‰ : Î©, â€–valueBot Ï‰â€– â‰¤ C
  /-- The value on each interval is bounded. -/
  bounded_value : âˆƒ C : â„, âˆ€ p âˆˆ value.support, âˆ€ Ï‰ : Î©, â€–value p Ï‰â€– â‰¤ C

namespace SimpleProcess

attribute [fun_prop] measurable_valueBot

/-- A bound on the value at âŠ¥. -/
noncomputable def valueBotBound (V : SimpleProcess F ğ“•) : â„ := max 0 V.bounded_valueBot.choose

/-- The value at âŠ¥ is bounded by `valueBotBound`. -/
@[simp] lemma valueBot_le_valueBotBound (V : SimpleProcess F ğ“•) (Ï‰ : Î©) :
    â€–V.valueBot Ï‰â€– â‰¤ V.valueBotBound :=
  le_max_of_le_right (V.bounded_valueBot.choose_spec Ï‰)

/-- The value of the simple process at the left endpoint of an interval is measurable
with respect to the filtration at the left endpoint.

Note that we do not require `p âˆˆ V.value.support`, because the value is 0 otherwise,
which is measurable. -/
@[fun_prop]
lemma measurable_value (V : SimpleProcess F ğ“•) (p : Î¹ Ã— Î¹) : Measurable[ğ“• p.1] (V.value p) := by
  by_cases hp : p âˆˆ V.value.support
  Â· exact V.measurable_value' p hp
  Â· rw [show V.value p = 0 by simpa using hp]
    exact measurable_const

/-- A nonnegative bound on the value on each interval. -/
noncomputable def valueBound (V : SimpleProcess F ğ“•) : â„ := max 0 (V.bounded_value.choose)

/-- The value on each interval is bounded by `valueBound`. Note that we do not require
`p âˆˆ V.value.support`. -/
@[simp] lemma value_le_valueBound (V : SimpleProcess F ğ“•) (p : Î¹ Ã— Î¹) (Ï‰ : Î©) :
    â€–V.value p Ï‰â€– â‰¤ V.valueBound := by
  by_cases hp : p âˆˆ V.value.support
  Â· exact le_max_of_le_right (V.bounded_value.choose_spec p hp Ï‰)
  Â· apply le_max_of_le_left
    rw [show V.value p = 0 by simpa using hp]
    simp

section Module

@[simps]
instance instZero : Zero (SimpleProcess F ğ“•) where
  zero := {
    valueBot := 0,
    value := 0,
    le_of_mem_support_value := by simp,
    bounded_valueBot := âŸ¨0, by simpâŸ©,
    bounded_value := âŸ¨0, by simpâŸ© }

@[simps]
instance instNeg : Neg (SimpleProcess F ğ“•) where
  neg V := {
    valueBot := -V.valueBot,
    value := -V.value,
    le_of_mem_support_value := by simpa using V.le_of_mem_support_value,
    bounded_valueBot := âŸ¨V.valueBotBound, by simpâŸ©,
    bounded_value := âŸ¨V.valueBound, by simpâŸ© }

@[simps]
instance instAdd : Add (SimpleProcess F ğ“•) where
  add V W := {
    valueBot := V.valueBot + W.valueBot,
    value := V.value + W.value,
    le_of_mem_support_value := fun p hp â†¦ (mem_union.1 (Finsupp.support_add hp)).elim
      (V.le_of_mem_support_value p) (W.le_of_mem_support_value p),
    bounded_valueBot := âŸ¨V.valueBotBound + W.valueBotBound, fun Ï‰ â†¦ by
      dsimp
      grw [norm_add_le, V.valueBot_le_valueBotBound, W.valueBot_le_valueBotBound]âŸ©,
    bounded_value := âŸ¨V.valueBound + W.valueBound, fun p _ Ï‰ â†¦ by
      dsimp
      grw [norm_add_le, V.value_le_valueBound, W.value_le_valueBound]âŸ© }

@[simps]
instance instSub : Sub (SimpleProcess F ğ“•) where
  sub V W := {
    valueBot := V.valueBot - W.valueBot,
    value := V.value - W.value,
    le_of_mem_support_value := fun p hp â†¦ (mem_union.1 (Finsupp.support_sub hp)).elim
      (V.le_of_mem_support_value p) (W.le_of_mem_support_value p),
    bounded_valueBot := âŸ¨V.valueBotBound + W.valueBotBound, fun Ï‰ â†¦ by
      dsimp
      grw [norm_sub_le, V.valueBot_le_valueBotBound, W.valueBot_le_valueBotBound]âŸ©,
    bounded_value := âŸ¨V.valueBound + W.valueBound, fun p _ Ï‰ â†¦ by
      dsimp
      grw [norm_sub_le, V.value_le_valueBound, W.value_le_valueBound]âŸ© }

@[simps]
instance instSMul : SMul â„ (SimpleProcess F ğ“•) where
  smul c V := {
    valueBot := c â€¢ V.valueBot,
    value := c â€¢ V.value,
    le_of_mem_support_value := by simpa using fun p _ â†¦ V.le_of_mem_support_value p,
    bounded_valueBot := âŸ¨|c| * V.valueBotBound, fun Ï‰ â†¦ by
      dsimp
      grw [norm_smul, V.valueBot_le_valueBotBound, Real.norm_eq_abs]âŸ©,
    bounded_value := âŸ¨|c| * V.valueBound, fun p _ Ï‰ â†¦ by
      dsimp
      grw [norm_smul, V.value_le_valueBound, Real.norm_eq_abs]âŸ© }

instance instAddCommGroup : AddCommGroup (SimpleProcess F ğ“•) where
  sub_eq_add_neg U V := by ext <;> apply sub_eq_add_neg
  add_assoc U V W := by ext <;> apply add_assoc
  add_comm U V := by ext <;> apply add_comm
  zero_add V := by ext <;> apply zero_add
  add_zero V := by ext <;> apply add_zero
  neg_add_cancel V := by ext <;> apply neg_add_cancel
  nsmul := nsmulRec
  zsmul := zsmulRec

instance instModule : Module â„ (SimpleProcess F ğ“•) where
  zero_smul V := by ext <;> simp
  smul_zero V := by ext <;> simp
  add_smul U V W := by ext <;> simp [add_smul]
  smul_add U V W := by ext <;> simp [smul_add]
  one_smul V := by ext <;> simp
  mul_smul U V W := by ext <;> simp [mul_smul]

-- TODO: Mathlib's Finset.measurable_prod is wrong because it is currently an exact duplicate of
-- Finset.measurable_fun_sum; we want the following version instead.
@[to_additive (attr := measurability, fun_prop)]
theorem Finset.measurable_prod' {M Î¹ Î± : Type*} [CommMonoid M] [MeasurableSpace M]
    [MeasurableMulâ‚‚ M] {m : MeasurableSpace Î±} {f : Î¹ â†’ Î± â†’ M} (s : Finset Î¹)
    (hf : âˆ€ i âˆˆ s, Measurable (f i)) :
    Measurable (âˆ i âˆˆ s, f i) := by
  change Measurable (fun a â†¦ (âˆ i âˆˆ s, f i) a)
  measurability

@[simps]
instance instMul : Mul (SimpleProcess F ğ“•) where
  mul V W := {
    valueBot := V.valueBot * W.valueBot,
    value := V.value.sum fun p v => W.value.sum fun q w =>
      Finsupp.single (p.1 âŠ” q.1, p.2 âŠ“ q.2) (if q.1 â‰¤ p.2 âˆ§ p.1 â‰¤ q.2 then v * w else 0),
    le_of_mem_support_value := by
      intro p' hp'
      obtain âŸ¨p, hp, hâŸ© := mem_biUnion.1 (Finsupp.support_sum hp')
      obtain âŸ¨q, hq, hâŸ© := mem_biUnion.1 (Finsupp.support_sum h)
      split_ifs at h with h_le
      Â· simpa [(Finsupp.mem_support_single _ _ _).1 h] using
          âŸ¨âŸ¨V.le_of_mem_support_value p hp, h_le.1âŸ©, âŸ¨h_le.2, W.le_of_mem_support_value q hqâŸ©âŸ©
      Â· simp at h
    bounded_valueBot := by
      refine âŸ¨V.valueBotBound * W.valueBotBound, fun Ï‰ â†¦ ?_âŸ©
      dsimp
      grw [norm_mul_le, V.valueBot_le_valueBotBound, W.valueBot_le_valueBotBound]
      apply le_max_left
    bounded_value := by
      refine âŸ¨#V.value.support â€¢ #W.value.support â€¢ (V.valueBound * W.valueBound), fun p _ Ï‰ â†¦ ?_âŸ©
      simp only [Finsupp.sum, Finsupp.single_eq_indicator, Finsupp.coe_finset_sum, sum_apply,
        Finsupp.indicator_apply, mem_singleton, dite_eq_ite]
      grw [norm_sum_le, Finset.sum_le_card_nsmul]
      intro p hp
      grw [norm_sum_le, Finset.sum_le_card_nsmul]
      intro q hq
      split_ifs
      Â· dsimp
        grw [norm_mul_le, V.value_le_valueBound, W.value_le_valueBound]
        apply le_max_left
      Â· simpa using mul_nonneg (le_max_left _ _) (le_max_left _ _)
      Â· simpa using mul_nonneg (le_max_left _ _) (le_max_left _ _)
    measurable_value' := by
      simp only [Finsupp.sum_apply]
      refine fun p' hp' â†¦ Finset.measurable_sum' _ fun p hp â†¦ Finset.measurable_sum' _ fun q hq â†¦ ?_
      simp_rw [Finsupp.single_apply]
      split_ifs with h h_le
      Â· exact ((V.measurable_value p).mono (ğ“•.mono (by simp [â† h])) le_rfl).mul
          ((W.measurable_value q).mono (ğ“•.mono (by simp [â† h])) le_rfl)
      Â· exact measurable_const
      Â· exact measurable_const }

end Module

section ToFun

/-- Coercion from a simple process to a function. Note that this is not injective. -/
@[coe] def toFun (V : SimpleProcess F ğ“•) (i : Î¹) (Ï‰ : Î©) : F :=
  ({âŠ¥} : Set Î¹).indicator (fun _ â†¦ V.valueBot Ï‰) i
    + V.value.sum fun p v => (Set.Ioc p.1 p.2).indicator (fun _ â†¦ v Ï‰) i

instance instCoeFun : CoeFun (SimpleProcess F ğ“•) (fun _ â†¦ Î¹ â†’ Î© â†’ F) where
  coe := toFun

lemma apply_eq (V : SimpleProcess F ğ“•) (i : Î¹) (Ï‰ : Î©) :
  â‡‘V i Ï‰ = ({âŠ¥} : Set Î¹).indicator (fun _ â†¦ V.valueBot Ï‰) i
    + V.value.sum fun p v => (Set.Ioc p.1 p.2).indicator (fun _ â†¦ v Ï‰) i := rfl

@[simp] lemma coe_zero : â‡‘(0 : SimpleProcess F ğ“•) = 0 := by ext; simp [apply_eq]

@[simp] lemma coe_neg (V : SimpleProcess F ğ“•) : â‡‘(-V) = -â‡‘V := by
  ext; simp [apply_eq, Set.indicator_neg, Finsupp.sum_neg_index]; abel

@[simp] lemma coe_add (V W : SimpleProcess F ğ“•) :
   â‡‘(V + W) = â‡‘V + â‡‘W := by
  ext; simp [apply_eq, Set.indicator_add, Finsupp.sum_add_index]; abel

@[simp] lemma coe_sub (V W : SimpleProcess F ğ“•) :
   â‡‘(V - W) = â‡‘V - â‡‘W := by
  ext; simp [apply_eq, Set.indicator_sub, Finsupp.sum_sub_index]; abel

@[simp] lemma coe_smul (c : â„) (V : SimpleProcess F ğ“•) :
   â‡‘(c â€¢ V) = c â€¢ â‡‘V := by
  ext; simp [apply_eq, Set.indicator_smul, Finsupp.sum_smul_index', Finsupp.smul_sum]

@[simp] lemma coe_mul (V W : SimpleProcess F ğ“•) :
   â‡‘(V * W) = â‡‘V * â‡‘W := by
  ext i Ï‰
  calc
    _ = ({âŠ¥} : Set Î¹).indicator (fun _ â†¦ V.valueBot Ï‰ * W.valueBot Ï‰) i +
      V.value.sum fun p v â†¦ W.value.sum fun q w â†¦
        (Finsupp.single (p.1 âŠ” q.1, p.2 âŠ“ q.2) (if q.1 â‰¤ p.2 âˆ§ p.1 â‰¤ q.2 then v * w else 0)).sum
          fun p' v' â†¦ (Set.Ioc p'.1 p'.2).indicator (fun _ â†¦ v' Ï‰) i := by
      simp [-Finsupp.single_mul, apply_eq, Set.indicator_mul, Finsupp.sum_sum_index,
        Set.indicator_add]
    _ = ({âŠ¥} : Set Î¹).indicator (fun _ â†¦ V.valueBot Ï‰ * W.valueBot Ï‰) i +
      V.value.sum fun p v â†¦ W.value.sum fun q w â†¦
        (Set.Ioc (p.1 âŠ” q.1) (p.2 âŠ“ q.2)).indicator (fun _ â†¦ v Ï‰ * w Ï‰) i := by
      congr! with p v q w
      split_ifs with h_le
      Â· simp [-Finsupp.single_mul]
      Â· have : p.2 < q.1 âˆ¨ q.2 < p.1 := by contrapose! h_le; exact h_le
        have : p.2 âŠ“ q.2 < p.1 âŠ” q.1 := by simp; tauto
        simp [Set.Ioc_eq_empty_of_le this.le]
    _ = _ := by
      have h0 (f g : Î¹ â†’ F) (i j : Î¹) (t : Î¹) :
          (Set.Ioc i j).indicator f t * ({âŠ¥} : Set Î¹).indicator g t = 0 := by
        simp [â† Set.inter_indicator_mul, Set.inter_singleton_eq_empty.mpr]
      have h0' (f g : Î¹ â†’ F) (i j : Î¹) (t : Î¹) :
          ({âŠ¥} : Set Î¹).indicator g t * (Set.Ioc i j).indicator f t = 0 := by
        simp +contextual [â† Set.inter_indicator_mul]
      simpa [apply_eq, Set.indicator_mul, Finsupp.sum_mul, Finsupp.mul_sum, add_mul, mul_add,
        h0, h0', â† Set.Ioc_inter_Ioc, Set.inter_indicator_mul] using Finsupp.sum_comm ..

lemma coe_bounded (V : SimpleProcess F ğ“•) :
    âˆƒ C : â„, âˆ€ i : Î¹, âˆ€ Ï‰ : Î©, â€–â‡‘V i Ï‰â€– â‰¤ C := by
  use V.valueBotBound + #V.value.support â€¢ V.valueBound
  intro i Ï‰
  dsimp [apply_eq]
  grw [norm_add_le, Finsupp.sum, norm_sum_le, norm_indicator_le_norm_self,
    V.valueBot_le_valueBotBound, Finset.sum_le_card_nsmul]
  Â· intro p hp
    grw [norm_indicator_le_norm_self, V.value_le_valueBound]

end ToFun

section Integral

variable {E : Type*} [AddCommGroup E] [Module F E]

/-- The elementary stochastic integral. -/
def integral (X : Î¹ â†’ Î© â†’ E) (V : SimpleProcess F ğ“•) :
    WithTop Î¹ â†’ Î© â†’ E :=
  fun i Ï‰ â†¦ V.value.sum fun p v =>
    v Ï‰ â€¢ (stoppedProcess X (fun _ â†¦ i) p.2 Ï‰ - stoppedProcess X (fun _ â†¦ i) p.1 Ï‰)

-- TODO: possible notation Vâ—X, possibly for more general integrals

@[simp] lemma integral_zero_left (V : SimpleProcess F ğ“•) :
    integral (fun _ â†¦ (0 : Î© â†’ E)) V = fun _ â†¦ 0 := by
  ext; simp [integral]

@[simp] lemma integral_neg_left (X : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess F ğ“•) :
    integral (-X) V = -integral X V := by
  ext; simp [integral, smul_sub]; abel

@[simp] lemma integral_add_left (X Y : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess F ğ“•) :
    integral (X + Y) V = integral X V + integral Y V := by
  ext; simp [integral, smul_sub]; abel

@[simp] lemma integral_sub_left (X Y : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess F ğ“•) :
    integral (X - Y) V = integral X V - integral Y V := by
  ext; simp [integral, smul_sub]; abel

@[simp] lemma integral_smul_left [Module â„ E] [SMulCommClass â„ F E] (c : â„) (X : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess F ğ“•) :
    integral (c â€¢ X) V = c â€¢ integral X V := by
  ext; simp [integral, Finsupp.smul_sum, smul_sub, smul_comm c]

@[simp] lemma integral_zero_right (X : Î¹ â†’ Î© â†’ E) :
    integral X (0 : SimpleProcess F ğ“•) = fun _ â†¦ 0 := by
  ext; simp [integral]

@[simp] lemma integral_neg_right (X : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess F ğ“•) :
    integral X (-V) = -integral X V := by
  ext; simp [integral, Finsupp.sum_neg_index]

@[simp] lemma integral_add_right (X : Î¹ â†’ Î© â†’ E)
    (V W : SimpleProcess F ğ“•) :
    integral X (V + W) = integral X V + integral X W := by
  ext; simp [integral, Finsupp.sum_add_index, add_smul, smul_sub]; abel

@[simp] lemma integral_sub_right (X : Î¹ â†’ Î© â†’ E)
    (V W : SimpleProcess F ğ“•) :
    integral X (V - W) = integral X V - integral X W := by
  ext; simp [integral, Finsupp.sum_sub_index, sub_smul, smul_sub]; abel

@[simp] lemma integral_smul_right [Module â„ E] [IsScalarTower â„ F E] (c : â„) (X : Î¹ â†’ Î© â†’ E)
    (V : SimpleProcess F ğ“•) :
    integral X (c â€¢ V) = c â€¢ integral X V := by
  ext; simp [integral, Finsupp.sum_smul_index', Finsupp.smul_sum, smul_sub]

@[simp] lemma integral_top (X : Î¹ â†’ Î© â†’ E) (V : SimpleProcess F ğ“•) (Ï‰ : Î©) :
    integral X V âŠ¤ Ï‰ = V.value.sum fun p v â†¦ v Ï‰ â€¢ (X p.2 Ï‰ - X p.1 Ï‰) := by simp [integral]

theorem stoppedProcess_integral (X : Î¹ â†’ Î© â†’ E) (V : SimpleProcess F ğ“•) (Ï„ : Î© â†’ WithTop Î¹) :
    stoppedProcess (integral X V âˆ˜ WithTop.some) Ï„ =
      integral (stoppedProcess X Ï„) V âˆ˜ WithTop.some := by
  ext i Ï‰
  rw [stoppedProcess]
  dsimp [integral]
  conv_rhs => rw [stoppedProcess_stoppedProcess]
  simp [stoppedProcess, WithTop.untopA_eq_untop]

theorem integral_integral (X : Î¹ â†’ Î© â†’ E) (V W : SimpleProcess F ğ“•) :
    integral (integral X W âˆ˜ WithTop.some) V = integral X (V * W) := by
  ext i Ï‰
  let Xi := stoppedProcess X (fun _ â†¦ i)
  calc
    _ = V.value.sum fun p v â†¦ W.value.sum fun q w â†¦ (v Ï‰ * w Ï‰) â€¢
        ((Xi (p.2 âŠ“ q.2) Ï‰ - Xi (p.2 âŠ“ q.1) Ï‰) -
          (Xi (p.1 âŠ“ q.2) Ï‰ - Xi (p.1 âŠ“ q.1) Ï‰)) := by
      simp only [integral, stoppedProcess_integral, Function.comp_apply,
        stoppedProcess_stoppedProcess, â† Finsupp.sum_sub, â† smul_sub, Finsupp.smul_sum, smul_smul]
      congr! 9 with p v q w <;> simp [Xi, stoppedProcess, min_left_comm, min_assoc]
    _ = V.value.sum fun p v â†¦ W.value.sum fun q w â†¦ if q.1 â‰¤ p.2 âˆ§ p.1 â‰¤ q.2 then
        (v Ï‰ * w Ï‰) â€¢ (Xi (p.2 âŠ“ q.2) Ï‰ - Xi (p.1 âŠ” q.1) Ï‰) else 0 := by
      refine Finsupp.sum_congr fun p hp â†¦ ?_
      refine Finsupp.sum_congr fun q hq â†¦ ?_
      split_ifs with h_le
      Â· by_cases h_le' : p.1 â‰¤ q.1
        Â· simp [h_le, h_le']
        Â· simp [h_le, (not_le.1 h_le').le]
      Â· have : p.2 â‰¤ q.1 âˆ¨ q.2 â‰¤ p.1 := by contrapose! h_le; exact âŸ¨h_le.1.le, h_le.2.leâŸ©
        rcases this with h_le | h_le
        Â· have h : p.1 â‰¤ p.2 âˆ§ p.2 â‰¤ q.1 âˆ§ q.1 â‰¤ q.2 := by
            simp [h_le, V.le_of_mem_support_value p hp, W.le_of_mem_support_value q hq]
          simp [h, h.2.1.trans h.2.2, h.1.trans h.2.1, (h.1.trans h.2.1).trans h.2.2]
        Â· have h : q.1 â‰¤ q.2 âˆ§ q.2 â‰¤ p.1 âˆ§ p.1 â‰¤ p.2 := by
            simp [h_le, W.le_of_mem_support_value q hq, V.le_of_mem_support_value p hp]
          simp [h, h.2.1.trans h.2.2, h.1.trans h.2.1, (h.1.trans h.2.1).trans h.2.2]
    _ = _ := by
      simp [integral, Finsupp.sum_sum_index, add_smul, ite_apply, Xi]

end Integral

end SimpleProcess

section Indicator

namespace ElementaryPredictableSet

variable (F)

/-- The indicator function of an elementary predictable set as a simple process. -/
def indicator (S : ElementaryPredictableSet ğ“•) :
    SimpleProcess F ğ“• where
  valueBot := S.setBot.indicator 1
  value := Finsupp.onFinset S.I (fun p â†¦ if p âˆˆ S.I then (S.set p).indicator 1 else 0)
    (by simp +contextual)
  le_of_mem_support_value := fun p hp â†¦ S.le_of_mem_I p (Finsupp.support_onFinset_subset hp)
  bounded_valueBot := âŸ¨â€–(1 : F)â€–, fun _ â†¦ norm_indicator_le_norm_self _ _âŸ©
  bounded_value := âŸ¨â€–(1 : F)â€–, fun _ _ _ â†¦ by
    rw [Finsupp.onFinset_apply]
    split_ifs
    Â· exact norm_indicator_le_norm_self _ _
    Â· simpâŸ©
  measurable_value' := fun p hp â†¦ by
    rw [Finsupp.onFinset_apply]
    measurability

@[simp] lemma coe_indicator (S : ElementaryPredictableSet ğ“•) :
    â‡‘(S.indicator F) = Function.curry ((S : Set (Î¹ Ã— Î©)).indicator 1) := by
  classical
  ext i Ï‰
  rw [ElementaryPredictableSet.toSet, Set.indicator_union_of_disjoint, Finset.indicator_biUnion]
  Â· simp only [ElementaryPredictableSet.indicator, SimpleProcess.apply_eq, Pi.zero_apply,
      Set.indicator_zero, implies_true, Finsupp.onFinset_sum, Function.curry_apply]
    congr 1
    Â· simp [Set.indicator, ite_and]
    Â· apply Finset.sum_congr rfl
      intro p hp
      simp [Set.indicator, ite_and, hp]
  Â· exact S.pairwiseDisjoint
  Â· rw [Set.disjoint_iff]
    intro (i, Ï‰)
    simp +contextual

variable {E : Type*} [AddCommGroup E] [Module F E]

/-- Explicit formula for `1_S â— X` where `S` is an elementary predictable set. -/
lemma integral_indicator_apply (S : ElementaryPredictableSet ğ“•)
    (X : Î¹ â†’ Î© â†’ E) (i : Î¹) (Ï‰ : Î©) :
    (S.indicator F).integral X i Ï‰ = âˆ‘ p âˆˆ S.I, (S.set p).indicator
      (fun Ï‰ â†¦ stoppedProcess X (fun _ â†¦ i) p.2 Ï‰ - stoppedProcess X (fun _ â†¦ i) p.1 Ï‰) Ï‰ := by
  rw [SimpleProcess.integral, indicator, Finsupp.onFinset_sum]
  Â· apply Finset.sum_congr rfl
    intro p hp
    rw [if_pos hp, Set.indicator, Set.indicator]
    split_ifs <;> simp
  simp

end ElementaryPredictableSet

end Indicator

section Predictable

namespace ElementaryPredictableSet

@[measurability]
theorem measurableSet_predictable (S : ElementaryPredictableSet ğ“•) :
    MeasurableSet[ğ“•.predictable] â†‘S := by
  apply MeasurableSet.union
  Â· measurability
  Â· apply MeasurableSet.biUnion (Finset.countable_toSet _)
    measurability

variable (Î¹ Î©) in
/-- The elementary predictable sets generate the predictable Ïƒ-algebra. Note that we require the
time domain to have countably generated `atTop` so that each `(t, âˆ]` can be written as a countable
union of intervals `(t, s]`. -/
theorem generateFrom_eq_predictable [(atTop : Filter Î¹).IsCountablyGenerated] :
    MeasurableSpace.generateFrom {â†‘S | S : ElementaryPredictableSet ğ“•} = ğ“•.predictable := by
  apply le_antisymm
  Â· apply MeasurableSpace.generateFrom_le
    rintro _ âŸ¨S, rflâŸ©
    exact S.measurableSet_predictable
  Â· apply measurableSpace_le_predictable_of_measurableSet
    Â· intro Bâ‚€ hBâ‚€
      apply MeasurableSpace.measurableSet_generateFrom
      use singletonBotProd hBâ‚€, coe_singletonBotProd hBâ‚€
    Â· intro t B hB
      obtain âŸ¨seq, _, tendstoâŸ© := Filter.exists_seq_monotone_tendsto_atTop_atTop Î¹
      have : Set.Ioi t = â‹ƒ n : â„•, Set.Ioc t (seq n) := by
        ext s
        suffices âˆƒ n, s â‰¤ seq n by simpa using fun _ â†¦ this
        rw [Filter.tendsto_atTop_atTop] at tendsto
        obtain âŸ¨n, hâŸ© := tendsto s
        exact âŸ¨n, h n le_rflâŸ©
      rw [this, Set.iUnion_prod_const]
      refine MeasurableSet.iUnion fun n â†¦ MeasurableSpace.measurableSet_generateFrom ?_
      use IocProd t (seq n) hB, coe_IocProd _ _ hB

end ElementaryPredictableSet

namespace SimpleProcess

theorem isPredictable (V : SimpleProcess F ğ“•) : IsPredictable ğ“• V := by
  apply Measurable.stronglyMeasurable
  apply Measurable.add
  Â· apply Measurable.indicator
    Â· intro s hs
      suffices MeasurableSet[ğ“•.predictable] (({âŠ¥} âˆª Set.Ioi âŠ¥) Ã—Ë¢ (V.valueBot â»Â¹' s)) by
        convert this
        ext; simp
      rw [Set.union_prod]
      measurability
    Â· change MeasurableSet[ğ“•.predictable] {a | a.1 = âŠ¥}
      suffices MeasurableSet[ğ“•.predictable] ({âŠ¥} Ã—Ë¢ Set.univ) by
        convert this
        ext; simp
      measurability
  Â· apply measurable_sum
    intro p hp s hs
    suffices MeasurableSet[ğ“•.predictable]
        ((Set.Ioc p.1 p.2)á¶œ Ã—Ë¢ ((fun _ â†¦ 0) â»Â¹' s) âˆª Set.Ioc p.1 p.2 Ã—Ë¢ (V.value p â»Â¹' s)) by
      convert this
      ext âŸ¨i, Ï‰âŸ©
      simp only [Set.indicator, Set.mem_preimage, Set.mem_union, Set.mem_prod, Set.mem_compl_iff]
      split_ifs with h <;> simp [h]
    apply MeasurableSet.union
    Â· have : (Set.Ioc p.1 p.2)á¶œ = {âŠ¥} âˆª Set.Ioc âŠ¥ p.1 âˆª Set.Ioi p.2 := by
        rw [Set.compl_Ioc, â† Set.Icc_bot, â† Set.Ioc_union_left bot_le, Set.union_comm {âŠ¥}]
      rw [this, Set.union_prod, Set.union_prod]
      measurability
    Â· measurability

variable (F ğ“•) in
theorem iSup_comap_eq_predictable [(atTop : Filter Î¹).IsCountablyGenerated]
    [MeasurableSingletonClass F] [Nontrivial F] :
    (â¨† V : SimpleProcess F ğ“•, mF.comap (Function.uncurry â‡‘V)) = ğ“•.predictable := by
  apply le_antisymm
  Â· rw [iSup_le_iff]
    intro V
    simp [(isPredictable V).measurable.comap_le]
  Â· rw [â† ElementaryPredictableSet.generateFrom_eq_predictable]
    apply MeasurableSpace.generateFrom_le
    rintro _ âŸ¨S, rflâŸ©
    simp_rw [MeasurableSpace.measurableSet_iSup, MeasurableSpace.measurableSet_comap]
    apply MeasurableSpace.GenerateMeasurable.basic
    use S.indicator F, {1}, measurableSet_singleton 1
    simp only [ElementaryPredictableSet.coe_indicator, ElementaryPredictableSet.toSet,
      Function.uncurry_curry]
    classical erw [Set.indicator_const_preimage_eq_union]
    simpa using fun h â†¦ nomatch h

end SimpleProcess

end Predictable

variable {P : Measure Î©} [SigmaFiniteFiltration P ğ“•]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
variable [Module F E] [IsScalarTower â„ F E]

section Submartingale

/-- A stochastic process `X` is a submartingale if and only if for all nonnegative simple processes
`V`, their integral `V â— X` evaluated at time âŠ¤ is nonnegative.
Note that by nonnegative, we mean `V.value` and `V.valueBot` are nonnegative, and not that
`â‡‘V` is nonnegative. -/
lemma Submartingale.simpleProcess_integral_nonneg {X : Î¹ â†’ Î© â†’ â„} (h : Submartingale X ğ“• P)
    {V : SimpleProcess â„ ğ“•} (hv : 0 â‰¤ V.value) (hvB : 0 â‰¤ V.valueBot) (i : WithTop Î¹) :
    0 â‰¤ P[V.integral X i] := by
  erw [integral_finset_sum]
  refine Finset.sum_nonneg fun p hp â†¦ ?_
  rw [â† integral_condExp (ğ“•.le ((p.1 : WithTop Î¹) âŠ“ i).untopA)]
  apply integral_nonneg_of_ae
  dsimp [stoppedProcess]
  change 0 â‰¤áµ[P] P[V.value p * _ | _]
  have := h.condExp_sub_nonneg
    (show ((p.1 : WithTop Î¹) âŠ“ i).untopA â‰¤ ((p.2 : WithTop Î¹) âŠ“ i).untopA by
      simp [WithTop.untopA_mono, V.le_of_mem_support_value p hp])
  all_goals sorry
  -- grw [condExp_mul_of_stronglyMeasurable_left]
  -- have := condExp_mul_of_stronglyMeasurable_left (V.measurable_value p).stronglyMeasurable
  --   ?_ ?_
  -- apply EventuallyLE.mul_nonneg (.of_forall (hv p))


end Submartingale

end ProbabilityTheory
