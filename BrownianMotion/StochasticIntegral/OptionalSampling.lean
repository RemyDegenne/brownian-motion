/-
Copyright (c) 2025 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import BrownianMotion.StochasticIntegral.ApproxSeq
import BrownianMotion.Auxiliary.Adapted

open Filter TopologicalSpace Function
open scoped NNReal ENNReal Topology

namespace MeasureTheory

namespace Martingale

variable {Î¹ Î© E : Type*} [LinearOrder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
  [OrderBot Î¹] [MeasurableSpace Î¹] [SecondCountableTopology Î¹] [BorelSpace Î¹] [MetrizableSpace Î¹]
  [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E]
  {mÎ© : MeasurableSpace Î©} {ğ“• : Filtration Î¹ mÎ©} {Î¼ : Measure Î©} [IsFiniteMeasure Î¼]
  {X : Î¹ â†’ Î© â†’ E} {Ï„ Ïƒ : Î© â†’ WithTop Î¹} {n : Î¹}

theorem condExp_stoppedValue_stopping_time_ae_eq_restrict_le_of_countable_range
    (h : Martingale X ğ“• Î¼) (hRC : âˆ€ Ï‰, RightContinuous (X Â· Ï‰)) {i : Î¹} (hÏ„_le : âˆ€ x, Ï„ x â‰¤ i)
    (hÏ„ : IsStoppingTime ğ“• Ï„) (hÏƒ : IsStoppingTime ğ“• Ïƒ)
    (hÏ„_countable_range : (Set.range Ï„).Countable) :
    Î¼[stoppedValue X Ï„|hÏƒ.measurableSpace] =áµ[Î¼.restrict {x : Î© | Ï„ x â‰¤ Ïƒ x}] stoppedValue X Ï„ := by
  rw [ae_eq_restrict_iff_indicator_ae_eq
    (hÏ„.measurableSpace_le _ (hÏ„.measurableSet_le_stopping_time hÏƒ))]
  refine (condExp_indicator
    (h.integrable_stoppedValue_of_countable_range Ï„ hÏ„ hÏ„_le hÏ„_countable_range)
    (hÏ„.measurableSet_stopping_time_le hÏƒ)).symm.trans ?_
  have h_int :
      Integrable ({Ï‰ : Î© | Ï„ Ï‰ â‰¤ Ïƒ Ï‰}.indicator (stoppedValue X Ï„)) Î¼ :=
    Integrable.indicator
      (h.integrable_stoppedValue_of_countable_range Ï„ hÏ„ hÏ„_le hÏ„_countable_range)
      <| hÏ„.measurableSpace_le _ (hÏ„.measurableSet_le_stopping_time hÏƒ)
  have h_meas : AEStronglyMeasurable[hÏƒ.measurableSpace]
      ({Ï‰ : Î© | Ï„ Ï‰ â‰¤ Ïƒ Ï‰}.indicator (stoppedValue X Ï„)) Î¼ := by
    refine StronglyMeasurable.aestronglyMeasurable ?_
    refine StronglyMeasurable.stronglyMeasurable_of_measurableSpace_le_on
      (hÏ„.measurableSet_le_stopping_time hÏƒ) ?_ ?_ ?_
    Â· intro t ht
      rw [Set.inter_comm _ t] at ht âŠ¢
      rw [hÏ„.measurableSet_inter_le_iff hÏƒ, IsStoppingTime.measurableSet_min_iff hÏ„ hÏƒ] at ht
      exact ht.2
    Â· exact (measurable_stoppedValue (h.stronglyAdapted.progMeasurable_of_rightContinuous hRC)
        hÏ„).stronglyMeasurable.indicator (hÏ„.measurableSet_le_stopping_time hÏƒ)
    Â· intro x hx
      simp only [hx, Set.indicator_of_notMem, not_false_iff]
  exact condExp_of_aestronglyMeasurable' hÏƒ.measurableSpace_le h_meas h_int

theorem stoppedValue_min_ae_eq_condExp_of_countable_range
    (h : Martingale X ğ“• Î¼) (hRC : âˆ€ Ï‰, RightContinuous (X Â· Ï‰))
    (hÏ„ : IsStoppingTime ğ“• Ï„) (hÏƒ : IsStoppingTime ğ“• Ïƒ) {n : Î¹} (hÏ„_le : âˆ€ x, Ï„ x â‰¤ n)
    (hÏ„_countable_range : (Set.range Ï„).Countable) (hÏƒ_countable_range : (Set.range Ïƒ).Countable) :
    (stoppedValue X fun x â†¦ min (Ïƒ x) (Ï„ x)) =áµ[Î¼] Î¼[stoppedValue X Ï„|hÏƒ.measurableSpace] := by
  refine
    (h.stoppedValue_ae_eq_condExp_of_le_of_countable_range hÏ„
      (hÏƒ.min hÏ„) (fun x â†¦ min_le_right _ _) hÏ„_le hÏ„_countable_range ?_).trans ?_
  Â· exact (hÏ„_countable_range.union hÏƒ_countable_range).mono <| by grind
  refine ae_of_ae_restrict_of_ae_restrict_compl {x | Ïƒ x â‰¤ Ï„ x} ?_ ?_
  Â· exact condExp_min_stopping_time_ae_eq_restrict_le hÏƒ hÏ„
  Â· suffices Î¼[stoppedValue X Ï„|(hÏƒ.min hÏ„).measurableSpace] =áµ[Î¼.restrict {x | Ï„ x â‰¤ Ïƒ x}]
        Î¼[stoppedValue X Ï„|hÏƒ.measurableSpace] by
      rw [ae_restrict_iff' (hÏƒ.measurableSpace_le _ (hÏƒ.measurableSet_le_stopping_time hÏ„).compl)]
      rw [Filter.EventuallyEq, ae_restrict_iff'] at this
      swap; Â· exact hÏ„.measurableSpace_le _ (hÏ„.measurableSet_le_stopping_time hÏƒ)
      filter_upwards [this] with x hx hx_mem
      simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_le] at hx_mem
      exact hx hx_mem.le
    apply Filter.EventuallyEq.trans _ ((condExp_min_stopping_time_ae_eq_restrict_le hÏ„ hÏƒ).trans _)
    Â· exact stoppedValue X Ï„
    Â· rw [IsStoppingTime.measurableSpace_min hÏƒ hÏ„,
        IsStoppingTime.measurableSpace_min hÏ„ hÏƒ, inf_comm]
    Â· have h1 : Î¼[stoppedValue X Ï„|hÏ„.measurableSpace] = stoppedValue X Ï„ := by
        apply condExp_of_stronglyMeasurable hÏ„.measurableSpace_le
        Â· exact Measurable.stronglyMeasurable <|
            measurable_stoppedValue (h.stronglyAdapted.progMeasurable_of_rightContinuous hRC) hÏ„
        Â· exact h.integrable_stoppedValue_of_countable_range Ï„ hÏ„ hÏ„_le hÏ„_countable_range
      rw [h1]
      exact (h.condExp_stoppedValue_stopping_time_ae_eq_restrict_le_of_countable_range hRC hÏ„_le
        hÏ„ hÏƒ hÏ„_countable_range).symm

/-- **Optional sampling theorem** for general time indices
(assuming existence of `DiscreteApproxSequence`). -/
theorem stoppedValue_min_ae_eq_condExp_of_discreteApproxSequence
    (h : Martingale X ğ“• Î¼) (hRC : âˆ€ Ï‰, RightContinuous (X Â· Ï‰))
    (hÏ„ : IsStoppingTime ğ“• Ï„) (hÏƒ : IsStoppingTime ğ“• Ïƒ) {n : Î¹} (hÏ„_le : âˆ€ x, Ï„ x â‰¤ n)
    (Ï„n : DiscreteApproxSequence ğ“• Ï„ Î¼) (Ïƒn : DiscreteApproxSequence ğ“• Ïƒ Î¼) :
    (stoppedValue X fun x â†¦ min (Ï„ x) (Ïƒ x)) =áµ[Î¼] Î¼[stoppedValue X Ï„|hÏƒ.measurableSpace] := by
  set Ï„n' := (discreteApproxSequence_of ğ“• hÏ„_le Ï„n).inf Ïƒn
  have hint (m : â„•) : stoppedValue X (Ï„n' m) =áµ[Î¼]
      Î¼[stoppedValue X (discreteApproxSequence_of ğ“• hÏ„_le Ï„n m) |
        (Ïƒn.isStoppingTime m).measurableSpace] := by
    refine EventuallyEq.trans (Eq.eventuallyEq ?_)
      (h.stoppedValue_min_ae_eq_condExp_of_countable_range hRC
        ((discreteApproxSequence_of ğ“• hÏ„_le Ï„n).isStoppingTime m)
        (Ïƒn.isStoppingTime m) (discreteApproxSequence_of_le hÏ„_le Ï„n m)
        (DiscreteApproxSequence.countable _ _) (Ïƒn.countable m))
    congr 1; ext Ï‰; rw [min_comm]; rfl
  have hintgbl : Integrable (stoppedValue X Ï„) Î¼ :=
    integrable_stoppedValue_of_discreteApproxSequence' h hRC hÏ„_le Ï„n
  refine ae_eq_condExp_of_forall_setIntegral_eq _ hintgbl ?_ ?_
    ((measurable_stoppedValue (h.stronglyAdapted.progMeasurable_of_rightContinuous hRC)
      (hÏ„.min hÏƒ)).mono ((hÏ„.min hÏƒ).measurableSpace_mono hÏƒ <| fun Ï‰ â†¦ min_le_right _ _)
      le_rfl).aestronglyMeasurable
  Â· exact fun s hs _ â†¦ (integrable_stoppedValue_of_discreteApproxSequence' h hRC
      (fun _ â†¦ min_le_of_left_le <| hÏ„_le _) <| Ï„n.inf Ïƒn).integrableOn
  rintro s hs -
  have : (fun m â†¦ âˆ« Ï‰ in s, stoppedValue X (Ï„n' m) Ï‰ âˆ‚Î¼) =
    fun m â†¦ âˆ« Ï‰ in s, stoppedValue X (discreteApproxSequence_of ğ“• hÏ„_le Ï„n m) Ï‰ âˆ‚Î¼ := by
    ext m
    rw [setIntegral_congr_ae (g := Î¼[stoppedValue X (discreteApproxSequence_of ğ“• hÏ„_le Ï„n m) |
        (Ïƒn.isStoppingTime m).measurableSpace]) (hÏƒ.measurableSpace_le _ hs)
        (by filter_upwards [hint m] with Ï‰ hÏ‰ _ using hÏ‰)]
    exact setIntegral_condExp _
      (h.integrable_stoppedValue_of_countable_range _
        (DiscreteApproxSequence.isStoppingTime _ _) (discreteApproxSequence_of_le hÏ„_le Ï„n m)
        (DiscreteApproxSequence.countable _ m))
      (hÏƒ.measurableSpace_mono (Ïƒn.isStoppingTime m) (Ïƒn.le m) _ hs)
  refine tendsto_nhds_unique (f := (fun m â†¦ âˆ« (Ï‰ : Î©) in s, stoppedValue X (Ï„n' m) Ï‰ âˆ‚Î¼))
    (l := atTop) ?_ (this â–¸ ?_)
  Â· refine tendsto_setIntegral_of_L1' _ (integrable_stoppedValue_of_discreteApproxSequence' h hRC
        (fun _ â†¦ min_le_of_left_le <| hÏ„_le _) Ï„n') ?_
      (tendsto_eLpNorm_stoppedValue_of_discreteApproxSequence_of_le h hRC Ï„n'
        (Ï„n.discreteApproxSequence_of_le_inf_le_of_left Ïƒn hÏ„_le)) _
    rw [eventually_atTop]
    exact âŸ¨0, fun m _ â†¦ (h.integrable_stoppedValue_of_countable_range _
      (DiscreteApproxSequence.isStoppingTime _ _)
      (Ï„n.discreteApproxSequence_of_le_inf_le_of_left Ïƒn hÏ„_le m)
      (DiscreteApproxSequence.countable _ m))âŸ©
  Â· refine tendsto_setIntegral_of_L1' _ hintgbl ?_
      (tendsto_eLpNorm_stoppedValue_of_discreteApproxSequence h hRC hÏ„_le Ï„n) _
    rw [eventually_atTop]
    exact âŸ¨0, fun m _ â†¦ (h.integrable_stoppedValue_of_countable_range _
        (DiscreteApproxSequence.isStoppingTime _ _) (discreteApproxSequence_of_le hÏ„_le Ï„n m)
        (DiscreteApproxSequence.countable _ m))âŸ©

-- TODO: change name of `stoppedValue_min_ae_eq_condExp` in mathlib and remove the prime here
/-- **Optional sampling theorem** for approximable time indices. -/
theorem stoppedValue_min_ae_eq_condExp'
    [Approximable ğ“• Î¼] (h : Martingale X ğ“• Î¼) (hRC : âˆ€ Ï‰, RightContinuous (X Â· Ï‰))
    (hÏ„ : IsStoppingTime ğ“• Ï„) (hÏƒ : IsStoppingTime ğ“• Ïƒ) {n : Î¹} (hÏ„_le : âˆ€ x, Ï„ x â‰¤ n) :
    (stoppedValue X fun x â†¦ min (Ï„ x) (Ïƒ x)) =áµ[Î¼] Î¼[stoppedValue X Ï„|hÏƒ.measurableSpace] :=
  stoppedValue_min_ae_eq_condExp_of_discreteApproxSequence h hRC hÏ„ hÏƒ hÏ„_le
    (hÏ„.discreteApproxSequence Î¼) (hÏƒ.discreteApproxSequence Î¼)

theorem stoppedValue_ae_eq_condExp_of_le_const'
    [Approximable ğ“• Î¼] (h : Martingale X ğ“• Î¼) (hRC : âˆ€ Ï‰, RightContinuous (X Â· Ï‰))
    (hÏ„ : IsStoppingTime ğ“• Ï„) (hÏ„_le : âˆ€ x, Ï„ x â‰¤ n) :
    stoppedValue X Ï„ =áµ[Î¼] Î¼[X n|hÏ„.measurableSpace] := by
  convert stoppedValue_min_ae_eq_condExp_of_discreteApproxSequence h hRC
    (isStoppingTime_const ğ“• n) hÏ„ (fun _ â†¦ le_rfl) (discreteApproxSequence_const ğ“• n)
      (hÏ„.discreteApproxSequence Î¼) using 2
  ext Ï‰
  rw [eq_comm, min_eq_right_iff]
  exact hÏ„_le Ï‰

theorem condExp_stoppedValue_ae_eq_stoppedProcess [Approximable ğ“• Î¼] {n : Î¹}
    (h : Martingale X ğ“• Î¼) (hRC : âˆ€ Ï‰, RightContinuous (X Â· Ï‰))
    (hÏ„ : IsStoppingTime ğ“• Ï„) (hÏ„_le : âˆ€ x, Ï„ x â‰¤ n) (i : Î¹) :
    Î¼[stoppedValue X Ï„|ğ“• i] =áµ[Î¼] stoppedProcess X Ï„ i := by
  simp_rw [stoppedProcess_eq_stoppedValue, min_comm]
  exact EventuallyEq.trans (Eq.eventuallyEq <| by simp)
    (stoppedValue_min_ae_eq_condExp' h hRC hÏ„ (isStoppingTime_const ğ“• i) hÏ„_le).symm

end Martingale

section subsupermartingale

variable {Î© E : Type*} {mÎ© : MeasurableSpace Î©} {P : Measure Î©}
    [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]

section Nat

variable {Ïƒ Ï„ : Î© â†’ WithTop â„•} {X : â„• â†’ Î© â†’ E} (ğ“• : Filtration â„• mÎ©)

theorem Submartingale.stoppedValue_min_ae_le_condExp_nat [PartialOrder E] [OrderClosedTopology E]
    [IsOrderedModule â„ E] [IsOrderedAddMonoid E]
    (hX : Submartingale X ğ“• P) {k : â„•} (hÏ„k : âˆ€áµ Ï‰ âˆ‚P, Ï„ Ï‰ â‰¤ k)
    (hÏƒ : IsStoppingTime ğ“• Ïƒ) (hÏ„ : IsStoppingTime ğ“• Ï„) :
    stoppedValue X (Ï„ âŠ“ Ïƒ) â‰¤áµ[P] P[stoppedValue X Ï„|hÏƒ.measurableSpace] := by
  sorry

theorem Supermartingale.condExp_ae_le_stoppedValue_min_nat [PartialOrder E] [OrderClosedTopology E]
    [IsOrderedModule â„ E] [IsOrderedAddMonoid E]
    (hX : Supermartingale X ğ“• P) {k : â„•} (hÏ„k : âˆ€áµ Ï‰ âˆ‚P, Ï„ Ï‰ â‰¤ k)
    (hÏƒ : IsStoppingTime ğ“• Ïƒ) (hÏ„ : IsStoppingTime ğ“• Ï„) :
    P[stoppedValue X Ï„|hÏƒ.measurableSpace] â‰¤áµ[P] stoppedValue X (Ï„ âŠ“ Ïƒ) := by
  have hXneg : Submartingale (-X) ğ“• P := hX.neg
  have h1 := hXneg.stoppedValue_min_ae_le_condExp_nat ğ“• hÏ„k hÏƒ hÏ„
  have hsvn : âˆ€ Ï„', stoppedValue (-X) Ï„' = -stoppedValue X Ï„' := fun Ï„' => by
    ext Ï‰; simp [stoppedValue]
  rw [hsvn, hsvn] at h1
  exact (h1.trans (condExp_neg (stoppedValue X Ï„) hÏƒ.measurableSpace).le).mono
    fun Ï‰ hÏ‰ => neg_le_neg_iff.mp hÏ‰

end Nat

variable {Î¹ : Type*} [LinearOrder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
  [OrderBot Î¹] [MeasurableSpace Î¹] [SecondCountableTopology Î¹] [BorelSpace Î¹] [MetrizableSpace Î¹]
  {Ïƒ Ï„ : Î© â†’ WithTop Î¹} {X : Î¹ â†’ Î© â†’ E} (ğ“• : Filtration Î¹ mÎ©)

theorem Submartingale.stoppedValue_min_ae_le_condExp [PartialOrder E] [OrderClosedTopology E]
    [IsOrderedModule â„ E] [IsOrderedAddMonoid E]
    (hX1 : Submartingale X ğ“• P) (hX2 : âˆ€ Ï‰, RightContinuous (X Â· Ï‰)) {k : Î¹}
    (hÏ„k : âˆ€áµ Ï‰ âˆ‚P, Ï„ Ï‰ â‰¤ k) (hÏƒ : IsStoppingTime ğ“• Ïƒ) (hÏ„ : IsStoppingTime ğ“• Ï„) :
    stoppedValue X (Ï„ âŠ“ Ïƒ) â‰¤áµ[P] P[stoppedValue X Ï„|hÏƒ.measurableSpace] := by
  sorry

end subsupermartingale

end MeasureTheory
